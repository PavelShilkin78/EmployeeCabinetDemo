импорт Компания
импорт КонтрольДоступа

//КонтрольДоступа

@Обработчик
метод ВычислитьРазрешенияДоступа(): Массив<РазрешениеДоступа>
    возврат КонтрольДоступа.РегистрСведенийРедактированиеАдминистраторуЗагружаемый()
;

//КонтрольДоступа

@ВПодсистеме
метод ДанныеУсловийТрудаСотрудников(
    СотрудникиСсылки: Массив<Сотрудники.Ссылка>): ЧитаемоеСоответствие<Сотрудники.Ссылка, ДанныеУсловийТрудаСотрудника>
    
    пер ДанныеУсловийТрудаСотрудников: Соответствие<Сотрудники.Ссылка, ДанныеУсловийТрудаСотрудника>
    пер Запрос = Запрос{
        ВЫБРАТЬ
            УсловияТрудаСотрудников.Сотрудник,
            УсловияТрудаСотрудников.РабочееМесто.ЗаменитьNull(Неопределено) КАК РабочееМесто,
            УсловияТрудаСотрудников.РабочееМесто.Номер.ЗаменитьNull("") КАК НомерРабочегоМеста,
            УсловияТрудаСотрудников.КлассУсловийТруда.ЗаменитьNull(Неопределено) КАК КлассУсловийТруда
        ПОРОДИТЬ
            ДанныеУсловийТрудаСотрудника
        ИЗ
            УсловияТрудаСотрудников КАК УсловияТрудаСотрудников
        ГДЕ
            УсловияТрудаСотрудников.Сотрудник В (%СотрудникиСсылки)
    }
    исп Результат = Запрос.Выполнить()
    для РезультатСтрока из Результат
        ДанныеУсловийТрудаСотрудников.Вставить(РезультатСтрока.Сотрудник, РезультатСтрока)
    ;
    возврат ДанныеУсловийТрудаСотрудников
;