импорт ИнтерфейсныеКомпонентыСтандартные
импорт Компания
импорт ФизическиеЛица

@ВПроекте
@Реализация
метод Инициализировать()
    если ФизическоеЛицо == Неопределено или не ОхранаТруда.ИспользованиеРаздела()
        возврат
    ;
    Данные = ПолучитьДанныеКарточки(ФизическоеЛицо!)
;

метод Заголовок(): Строка
    если ПоказыватьЗаголовок
        возврат "Охрана труда"
    иначе
        возврат ""
    ;
;

метод КлассУсловийТрудаВидимость(): Булево
    возврат не Данные.КлассУсловийТруда.Пусто()
;

@ВПодсистеме
@НаКлиенте @НаСервере
структура ДанныеРабочегоМестаОхраныТруда
    пер НомерРабочегоМеста: Строка
    пер КлассУсловийТруда: Строка
;

@НаСервере
@ДоступноСКлиента
статический метод ПолучитьДанныеКарточки(ФизическоеЛицо: ФизическиеЛица.Ссылка?): ДанныеРабочегоМестаОхраныТруда
    знч ДанныеКарточки: ДанныеРабочегоМестаОхраныТруда
    если ФизическоеЛицо != Неопределено
        знч Сотрудник: Сотрудники.Ссылка? = Сотрудники.ОсновнойСотрудникФизическогоЛица(ФизическоеЛицо!)
        если Сотрудник != Неопределено
            знч ДанныеУсловийТрудаСотрудников = УсловияТрудаСотрудников.ДанныеУсловийТрудаСотрудников([Сотрудник])
            если не ДанныеУсловийТрудаСотрудников.Пусто()
                знч ДанныеУсловийТрудаСотрудника = ДанныеУсловийТрудаСотрудников[Сотрудник]
                ДанныеКарточки.НомерРабочегоМеста = ДанныеУсловийТрудаСотрудника.НомерРабочегоМеста
                если ДанныеУсловийТрудаСотрудника.КлассУсловийТруда != Неопределено
                    ДанныеКарточки.КлассУсловийТруда = "${ДанныеУсловийТрудаСотрудника.КлассУсловийТруда}"
                ;
            ;
        ;
    ;
    возврат ДанныеКарточки
;