импорт КонтрольДоступа

//Область РазрешенияДоступа

@Обработчик
метод ВычислитьРазрешенияДоступа(): Массив<РазрешениеДоступа>
    возврат КонтрольДоступа.РегистрСведенийРедактированиеАдминистратору()
;

//КонецОбласти

@ВПодсистеме
метод ФункциональныеОпции(): ОхранаТруда.ФункциональныеОпции
    знч ФункциональныеОпции: ОхранаТруда.ФункциональныеОпции
    знч Запрос = Запрос{
        ВЫБРАТЬ
            Функциональность,
            Использование
        ИЗ
            ИспользуемаяФункциональность
    }
    исп Результат = Запрос.Выполнить()
    для РезультатСтрока из Результат
        УстановитьЗначение(ФункциональныеОпции, РезультатСтрока.Функциональность, РезультатСтрока.Использование)
    ;
    возврат ФункциональныеОпции
;

метод УстановитьЗначение(ФункциональныеОпции: ОхранаТруда.ФункциональныеОпции, Функциональность: Функциональность,
    Значение: Булево)
    выбор Функциональность
    когда ИнформацияОбОхранеТруда
        ФункциональныеОпции.ИнформацияОбОхранеТруда = Значение
    ;
;

@ВПодсистеме
метод ЗаписатьФункциональныеОпции(ФункциональныеОпции: ОхранаТруда.ФункциональныеОпции)
    ЗаписатьЗначение(Функциональность.ИнформацияОбОхранеТруда, ФункциональныеОпции.ИнформацияОбОхранеТруда)
;

метод ЗаписатьЗначение(Функциональность: Функциональность, Значение: Булево?)
    если Значение == Неопределено
        возврат
    ;

    знч НаборЗаписей = новый ИспользуемаяФункциональность.НаборЗаписей()
    НаборЗаписей.Фильтр.Функциональность.Установить(Функциональность)
    НаборЗаписей.ДобавитьЗапись(
            Функциональность = Функциональность,
            Использование = Значение)
    НаборЗаписей.Записать()
;