импорт ВладельцыКабинетов
импорт Компания
импорт КонтрольДоступа
импорт ЛичныеКабинетыПользователей
импорт ОбменДанными
импорт СтандартноеПриложение

@ВПодсистеме
конст ИМЯ_РАЗДЕЛА_ОХРАНА_ТРУДА = "Охрана труда"

@ВПроекте
конст ИД_ПОДСИСТЕМЫ = "ОхранаТруда"

@ВПроекте
конст ТИП_DTO_РАБОЧИЕ_МЕСТА = "РабочиеМестаОхраныТруда"

@ВПроекте
@НаКлиенте
метод ИспользованиеРаздела(): Булево
    знч ФункциональныеОпции = ПолучитьФункциональныеОпции()
    знч РазделПодключен = ФункциональныеОпции.ИнформацияОбОхранеТруда
    возврат РазделПодключен и ПараметрыСеанса().РабочееМестоОхраныТрудаЗаполнено и ЛичныеКабинетыПользователей
        .ПараметрыСеанса().ТекущееФизическоеЛицо != Неопределено и (Компания.ПараметрыСеанса()
        .ТекущийПользовательСотрудник или ВладельцыКабинетов.ПараметрыСеанса().ДнейДоОтключенияКабинета != Неопределено)
;

// Область ПараметрыСеанса

@ВПодсистеме
конст КЛЮЧ_ПАРАМЕТРОВ_СЕАНСА: Строка = "ОхранаТруда"

@ВПроекте
структура ПараметрыСеанса
    обз знч ФункциональныеОпции: ФункциональныеОпции
    обз знч РабочееМестоОхраныТрудаЗаполнено: Булево
;

@ВПроекте
@НаКлиенте
метод ПараметрыСеанса(): ПараметрыСеанса
    возврат ПараметрыСеансаСервис.ПолучитьСервис().ПараметрПоКлючу(КЛЮЧ_ПАРАМЕТРОВ_СЕАНСА) как ПараметрыСеанса
;

// Конец ПараметрыСеанса

// Область ФункциональныеОпции

@ВПроекте
структура ФункциональныеОпции
    пер ИнформацияОбОхранеТруда: Булево
;

@ВПроекте
@НаСервере
@ДоступноСКлиента
метод ПолучитьФункциональныеОпции(): ФункциональныеОпции?
    возврат ИспользуемаяФункциональность.ФункциональныеОпции()
;

@ВПроекте
@НаСервере
@ДоступноСКлиента
метод ЗаписатьФункциональныеОпции(ФункциональныеОпции: ФункциональныеОпции)
    ИспользуемаяФункциональность.ЗаписатьФункциональныеОпции(ФункциональныеОпции)
;

// Конец ФункциональныеОпции

@ВПодсистеме
@НаСервере
метод ДобавитьТипDtoОбъекта()
    ДобавитьТипDtoОбъектаРабочиеМестаОхраныТруда()
;

@НаСервере
метод ДобавитьТипDtoОбъектаРабочиеМестаОхраныТруда()
    ТипыDtoОбъектов.ДобавитьТипDtoОбъекта(
        "РабочиеМестаОхраныТруда",
        ТИП_DTO_РАБОЧИЕ_МЕСТА)
;

@ВПодсистеме
@НаСервере
метод РабочееМестоОхраныТрудаЗаполнено(): Булево
    знч ТекущееФизическоеЛицо = ПользователиСистемы.ТекущееФизическоеЛицо()
    если ТекущееФизическоеЛицо != Неопределено
        знч Сотрудник: Сотрудники.Ссылка? = Сотрудники.ОсновнойСотрудникФизическогоЛица(ТекущееФизическоеЛицо!)
        если Сотрудник != Неопределено
            знч ДанныеУсловийТрудаСотрудников = УсловияТрудаСотрудников.ДанныеУсловийТрудаСотрудников([Сотрудник])
            если не ДанныеУсловийТрудаСотрудников.Пусто()
                знч ДанныеУсловийТрудаСотрудника = ДанныеУсловийТрудаСотрудников[Сотрудник]
                возврат ДанныеУсловийТрудаСотрудника.РабочееМесто != Неопределено
            ;
        ;
    ;
    возврат Ложь
;