импорт ВебApi
импорт Компания

@ВПроекте
структура РабочееМестоОхраныТруда_V3_0
    @JsonСвойство(Имя = "ID")
    обз знч Идентификатор: Строка
    @JsonСвойство(Имя = "name")
    обз знч Наименование: Строка
    @JsonСвойство(Имя = "number")
    обз знч Номер: Строка
    @JsonСвойство(Имя = "employerID")
    обз знч Организация: Строка
    @JsonСвойство(Имя = "positionID")
    обз знч Должность: Строка
    @JsonСвойство(Имя = "inUse")
    обз знч Используется: Булево?
;

@ВПодсистеме
метод ТипОбъектаJson(): Строка
    возврат "workplacesSafety"
;

@ВПодсистеме
метод ТипДанныхJson(ВерсияФорматаОбмена: Версия?): Тип
    если ВерсияФорматаОбмена == Неопределено или ВерсияФорматаОбмена < Версия{3.0}
        выбросить новый ИсключениеВыполнения("Неверный формат")
    ;
    возврат Тип<РабочееМестоОхраныТруда_V3_0>
;

@ВПодсистеме
метод ТипКоллекцииДанныхJson(ВерсияФорматаОбмена: Версия?): Тип
    если ВерсияФорматаОбмена == Неопределено или ВерсияФорматаОбмена < Версия{3.0}
        выбросить новый ИсключениеВыполнения("Неверный формат")
    ;
    возврат Тип<Массив<РабочееМестоОхраныТруда_V3_0>>
;

@ВПодсистеме
метод ЗагрузитьОбъектИзJson(ТипДанных: Тип, ДанныеJson: Объект?, Сервисы: ВебApiСервер.СервисыЗагрузкиJson)
    выбор ТипДанных
    когда Тип<РабочееМестоОхраныТруда_V3_0>
        ЗагрузитьРабочееМесто(ДанныеJson как РабочееМестоОхраныТруда_V3_0, Сервисы)
    ;
;

@ВПодсистеме
метод ЗагрузитьРабочееМесто(ДанныеJson: РабочееМестоОхраныТруда_V3_0, Сервисы: ВебApiСервер.СервисыЗагрузкиJson)
    исп Транзакции.Начать()

    знч РабочееМесто =
        Сервисы.ПрочитатьОбъект(ДанныеJson.Идентификатор, &РабочиеМестаОхраныТруда.Объект.новый(Ууид),
        Тип<РабочиеМестаОхраныТруда.Ссылка>) как РабочиеМестаОхраныТруда.Объект
    знч ВнешниеСсылки: Множество<ВебApiСервер.ВнешняяСсылка>
    ВнешниеСсылки.Добавить(Организации.ВнешняяСсылка(ДанныеJson.Организация))
    ВнешниеСсылки.Добавить(Должности.ВнешняяСсылка(ДанныеJson.Должность))
    Сервисы.ПолучитьСсылки(ВнешниеСсылки)

    РабочееМесто.Наименование = ДанныеJson.Наименование
    РабочееМесто.Номер = ДанныеJson.Номер
    РабочееМесто.Организация = Сервисы.ПолучитьСсылку(
        Организации.ВнешняяСсылка(ДанныеJson.Организация)) как Организации.Ссылка
    РабочееМесто.Должность = Сервисы.ПолучитьСсылку(
        Должности.ВнешняяСсылка(ДанныеJson.Должность)) как Должности.Ссылка
    РабочееМесто.Используется = ДанныеJson.Используется ?? Истина

    Сервисы.ЗаписатьОбъект(новый ВебApiСервер.ПараметрыЗаписиВходящихДанных(РабочееМесто, ДанныеJson.Идентификатор),
        Сервисы.ИнструментыМенеджераОбъекта)
;

@ВПроекте
метод ВнешняяСсылка(Идентификатор: Строка): ВебApiСервер.ВнешняяСсылка
    возврат новый ВебApiСервер.ВнешняяСсылка(Идентификатор, (Ууид) -> РабочиеМестаОхраныТруда.ПолучитьСсылку(Ууид))
;