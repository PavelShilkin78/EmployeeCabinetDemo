импорт Уведомления
импорт ИнтерфейсныеКомпонентыСтандартные

@Обработчик
метод ПослеСоздания()
    ДанныеНастроек = ПоставляемыеНастройкиПриложения()
;

метод ПоляПроверки(): Множество<ПолеВвода<Строка>|ПолеВвода<Число>>
    возврат {
        Компоненты.ИдентификаторPushNotifications, 
        Компоненты.ИдEssWelcome
    }
;

метод ВыборФайловПриИзменении(Источник: ВыборФайлов, Событие: СобытиеПриИзменении<ЧитаемыйМассив<ВыбранныйФайл>>)
    пер Загрузка = ЗагрузкаФайлов.НачатьЗагрузку(Событие.НовоеЗначение[0])
    пер ПоставляемыеНастройки = Загрузка.ПолучитьРезультат()
    попытка 
        ДанныеНастроек = ПриложениеНастройки.ЗагрузитьПоставляемыеНастройки(ПоставляемыеНастройки)
        СобственнаяМодифицированность = Истина
        ПоказыватьПодсказкуФайлаНастроек = Ложь
    поймать Ошибка: Исключение
        ПоказыватьПодсказкуФайлаНастроек = Истина
    ;
    Компоненты.НастройкиEssm.Заполнить(ДанныеНастроек.ПараметрыДоступаКEssm)
    Компоненты.НастройкиСтандартногоПриложения.Заполнить(ДанныеНастроек.Приложение)
    Компоненты.НастройкиОтправкиSms.Заполнить(ДанныеНастроек.ОтправкаSms)
;

метод СохранениеНастроекПриНажатии(Источник: Кнопка, Событие: СобытиеПриНажатии)
    если не Компоненты.НастройкиEssm.Записать() 
            или не Компоненты.НастройкиСтандартногоПриложения.Записать()
            или не Компоненты.НастройкиОтправкиSms.Записать()
        возврат
    ;
    если СтандартныеФормы.ПроверкаЗаполенияПолей(ПоляПроверки())
        ЗаписатьНастройки(ДанныеНастроек)
        СтандартныеФормы.ПоказатьУведомление("Сохранение настроек", "Все настройки успешно сохранены", ОценкаИнформации.Положительная)        
        СобственнаяМодифицированность = Ложь
    ;
;

@НаСервере @ДоступноСКлиента
статический метод ПоставляемыеНастройкиПриложения(): ПриложениеНастройки.ПоставляемыеНастройкиПриложения
    пер ДанныеНастроек = новый ПриложениеНастройки.ПоставляемыеНастройкиПриложения(
        ИдентификаторPushNotifications = Уведомления.Идентификаторы().ИдентификаторPushNotifications,
        ИдEssWelcome = МенеджерНастроекПриложения.НастройкиПриложения().ИдEssWelcome
    )
    возврат ДанныеНастроек
;

@ВПроекте
@НаСервере @ДоступноСКлиента
статический метод ЗаписатьНастройки(ДанныеНастроек: ПриложениеНастройки.ПоставляемыеНастройкиПриложения)
    пер Настройки = МенеджерНастроекПриложения.НастройкиПриложения()
    Настройки.ИдEssWelcome = ДанныеНастроек.ИдEssWelcome
    МенеджерНастроекПриложения.СохранитьНастройки(Настройки)
    
    пер Идентификаторы = новый Уведомления.ИдентификаторыПриложения()
    Идентификаторы.ИдентификаторPushNotifications = ДанныеНастроек.ИдентификаторPushNotifications
    Уведомления.УстановитьИдентификаторыПриложения(Идентификаторы, ["pushNotificationId"])
;