импорт КонтрольДоступа

@Обработчик
метод ВычислитьРазрешенияДоступа(): Массив<РазрешениеДоступа>
    возврат КонтрольДоступа.РазрешенияДоступаТаблицыСервисныхДанныхПриложения()
;

@ВПроекте
структура ДанныеДоступностиПриложения
    пер ДоступПользователей: СостоянияДоступности = СостоянияДоступности.Разрешено
    пер ДоступИнформационныхСистем: СостоянияДоступности = СостоянияДоступности.Разрешено
;

@ВПроекте
метод ДанныеДоступностиПриложения(): ДоступностьПриложения.ДанныеДоступностиПриложения
    
    пер ДанныеДоступности = новый ДоступностьПриложения.ДанныеДоступностиПриложения()
    
    знч Запрос = Запрос{
        Выбрать Первые 1
            ДоступПользователей,
            ДоступИнформационныхСистем
        Из 
            ДоступностьПриложения}
    исп РезультатЗапроса = Запрос.Выполнить()
    знч РезультатЗапросаСтроки = новый ЧитаемыйМассив(РезультатЗапроса)
    если (не РезультатЗапросаСтроки.Пусто())
        ДанныеДоступности.ДоступПользователей = РезультатЗапросаСтроки[0].ДоступПользователей
        ДанныеДоступности.ДоступИнформационныхСистем = РезультатЗапросаСтроки[0].ДоступИнформационныхСистем
    ;
    
    возврат ДанныеДоступности
;

@ВПроекте
метод УстановитьДоступностьПриложения(ДанныеДоступности: ДоступностьПриложения.ДанныеДоступностиПриложения)

    знч Запрос = Запрос{
        Выбрать Первые 1
            Ссылка
        Из 
            ДоступностьПриложения}
    исп РезультатЗапроса = Запрос.Выполнить()
    
    пер ДоступностьОбъект: ДоступностьПриложения.Объект | Неопределено
    знч РезультатЗапросаСтроки = новый ЧитаемыйМассив(РезультатЗапроса)
    если (не РезультатЗапросаСтроки.Пусто())
        ДоступностьОбъект = РезультатЗапросаСтроки[0].Ссылка.ЗагрузитьОбъект()
    ;
    если ДоступностьОбъект == Неопределено
        ДоступностьОбъект = новый ДоступностьПриложения.Объект()
    ;
    
    ДоступностьОбъект.ДоступПользователей = ДанныеДоступности.ДоступПользователей
    ДоступностьОбъект.ДоступИнформационныхСистем = ДанныеДоступности.ДоступИнформационныхСистем
    ДоступностьОбъект.Записать()
;