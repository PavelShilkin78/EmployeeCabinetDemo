импорт Приложение
импорт КонтрольДоступа
импорт ЛичныеКабинетыПользователей
импорт Криптография

//Область РазрешенияДоступа

@Обработчик
метод ВычислитьРазрешенияДоступа(): Массив<РазрешениеДоступа>
    возврат КонтрольДоступа.РегистрСведенийРедактированиеАдминистратору()
;

//КонецОбласти

@ВПодсистеме
метод ФункциональныеОпции(): Приложение.ФункциональныеОпции
    пер ФункциональныеОпции: Приложение.ФункциональныеОпции
    знч Запрос = Запрос{
        Выбрать 
            Функциональность, 
            Использование
        Из ИспользуемаяФункциональность}
    исп Результат = Запрос.Выполнить() 
    для РезультатСтрока из Результат
        УстановитьЗначение(ФункциональныеОпции, РезультатСтрока.Функциональность, РезультатСтрока.Использование)
    ;
    
    возврат ФункциональныеОпции
;

метод УстановитьЗначение(ФункциональныеОпции: Приложение.ФункциональныеОпции, 
    Функциональность: Функциональность, Значение: Булево)
    
    выбор Функциональность
    когда ИспользуетсяКэдо
        ФункциональныеОпции.ИспользуетсяКэдо = Значение
    когда ИспользоватьОтказПодписывать
        ФункциональныеОпции.ИспользоватьОтказПодписывать = Значение
    когда ИнтеграцияС1СПерсонал
        ФункциональныеОпции.ИнтеграцияС1СПерсонал = Значение
    ;
;

@ВПодсистеме
метод ЗаписатьФункциональныеОпции(ФункциональныеОпции: Приложение.ФункциональныеОпции)
    ЗаписатьЗначение(Функциональность.ИспользуетсяКэдо, ФункциональныеОпции.ИспользуетсяКэдо) 
    ЗаписатьЗначение(Функциональность.ИспользоватьОтказПодписывать, ФункциональныеОпции.ИспользоватьОтказПодписывать)
    ЗаписатьЗначение(Функциональность.ИнтеграцияС1СПерсонал, ФункциональныеОпции.ИнтеграцияС1СПерсонал)
;

@ВПодсистеме
метод ЗаписатьЗначение(ФункциональнаяОпция: Функциональность, Значение: Булево?)
    если Значение == Неопределено
        возврат
    ;
    знч НаборЗаписей = новый ИспользуемаяФункциональность.НаборЗаписей()
	НаборЗаписей.Фильтр.Функциональность.Установить(ФункциональнаяОпция)
	НаборЗаписей.ДобавитьЗапись(
        Функциональность = ФункциональнаяОпция,
        Использование = Значение)
    НаборЗаписей.Записать()      
;

@ВПодсистеме
метод ПослеВключенияИспользованияКЭДОВФоне()
    исп ПривилегированныйРежим: КонтекстДоступа = КонтекстДоступа.Привилегированный()
    знч НастройкиКабинетов: ЛичныеКабинетыПользователей.НастройкиЛичныхКабинетов = ЛичныеКабинетыПользователей.ТекущиеНастройкиСозданияУчетныхЗаписей()
    РаботаСПодписями.СоздатьЦифровойСертификатУдостоверяющегоЦентра(НастройкиКабинетов.АдресСервиса)
    РаботаСПодписями.ОбновитьИнфомрациюКорневогоСертификата()
    ПривилегированныйРежим.Закрыть()
;