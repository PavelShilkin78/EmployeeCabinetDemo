импорт КонтрольДоступа
импорт ОбменДанными
импорт ПриложениеНастройки
импорт ВебApi
импорт КонтрольДоступаСтандартные

@НаСервере
статический метод ПослеНастроекОбмена(ИнформационнаяСистема: ИнформационныеСистемы.Ссылка)
    для Сервис из ОбменДаннымиОбработчик.ПолучитьСервисы()
        Сервис.ПослеНастройкиОбмена(ИнформационнаяСистема)
    ;
;

метод СформироватьФайлПриНажатии(Источник: Кнопка, Событие: СобытиеПриНажатии)
    СоздатьИнформационнуюСистему()
    ВыгрузкаФайлов.НачатьВыгрузку(ДанныеФайлаНастроек(), "Настройки подключения для 1С-Персонал.json")
;

@НаСервере @ДоступноСКлиента
статический метод ДанныеФайлаНастроек(): ДвоичныйОбъект.Ссылка
    возврат МенеджерНастроекПриложения.ДанныеФайлаНастроек("1С:Персонал")
;

@НаСервере @ДоступноСКлиента
статический метод СоздатьПользователяИнформационнойСистемы(): ИдПользователя
    пер СвойстваПользователя = новый КонтрольДоступа.СвойстваПользователя()
    СвойстваПользователя.Представление = "HrSystem"
    СвойстваПользователя.РолиДоступа.Добавить(РолиДоступа.МенеджерСервиса)
    СвойстваПользователя.РолиДоступа.Добавить(РолиДоступа.ИнформационнаяСистема)
    СвойстваПользователя.РолиДоступа.Добавить(РолиДоступа.АдминистраторАбонента)
        
    МенеджерПравДоступа.ПроверитьПраваАдминистратора()
    исп КонтекстДоступа.Привилегированный()
    пер СписокБезПодтвержденияУчетнойЗаписи = ПользователиСистемы.СписокБезПодтвержденияУчетнойЗаписи()
    пер Пользователь: Пользователи.Объект = ПользователиСистемы.СоздатьПользователя(СвойстваПользователя, СписокБезПодтвержденияУчетнойЗаписи)
    возврат Пользователь.Ид
;

@НаСервере @ДоступноСКлиента
статический метод СоздатьИнформационнуюСистему()
    если ИнформационныеСистемы.НайтиПоНаименованию("1С:Персонал") != Неопределено
        возврат 
    ;
    
    исп Транзакции.Начать() 

    знч ИдПользователя = СоздатьПользователяИнформационнойСистемы()
    пер ДанныеИнформационнойСистемы = новый ИнформационныеСистемы.ДанныеИнформационнойСистемы()
    ДанныеИнформационнойСистемы.Наименование = "1С:Персонал"
    ДанныеИнформационнойСистемы.ИдПользователя = ИдПользователя
    ДанныеИнформационнойСистемы.ВидПриложения = ВидПриложения.Hr
    знч ИнформационнаяСистема = ИнформационныеСистемы.СоздатьИнформационнуюСистему(ДанныеИнформационнойСистемы)
    ПослеНастроекОбмена(ИнформационнаяСистема.Ссылка)
;