импорт СтандартноеПриложение
импорт ЛичныеКабинетыПользователей
импорт ИнтерфейсныеКомпонентыСтандартные

@Обработчик
метод ПослеЧтения()
    если ЭтоНовый()
        Объект.ФизическоеЛицо = ЛичныеКабинетыПользователей.ПараметрыСеанса().ТекущееФизическоеЛицо
        СобственнаяМодифицированность = Ложь
    ;
    МобОценкаНачальная = Объект.МобОценка
    ВебОценкаНачальная = Объект.ВебОценка
    если КлиентскоеУстройство.ВидИнтерфейса == ВидИнтерфейса.Телефон и МобОценкаНачальная == Неопределено
        Объект.МобОценка = ОбщаяОценкаПриложения.Нравится
    иначе если КлиентскоеУстройство.ВидИнтерфейса == ВидИнтерфейса.Компьютер и ВебОценкаНачальная == 0
        Объект.ВебОценка = 5 
    ;
;

метод МобВидимость(): Булево
    возврат КлиентскоеУстройство.ВидИнтерфейса == ВидИнтерфейса.Телефон 
;

метод ВебВидимость(): Булево
    возврат не МобВидимость() 
;

метод ПанельОсновнаяЗаписатьИЗакрыть(Источник: КоманднаяПанельФорм, Событие: СобытиеКомпонента)
    если СобственнаяМодифицированность
        УстановитьДатуОценки()
        СобственнаяМодифицированность = Ложь
        если Объект.МобОценка != МобОценкаНачальная
            Объект.МобПредыдущаяОценка = МобОценкаНачальная
        ;
        если Объект.ВебОценка != ВебОценкаНачальная
            Объект.ВебПредыдущаяОценка = ВебОценкаНачальная
        ;
    ;
    Записать()
    если КлиентскоеУстройство.ВидИнтерфейса == ВидИнтерфейса.Телефон и Объект.МобОценка == ОбщаяОценкаПриложения.Нравится
        ПоказатьИнтерфейсОценкиПриложения()
    ;
    Закрыть()
;

метод ПанельОсновнаяОтменить(Источник: КоманднаяПанельФорм, Событие: СобытиеКомпонента)
    СобственнаяМодифицированность = Ложь
    Закрыть()
;

метод ЗаголовокКнопкиОтменить(): Строка
    если не ОценкаЗаполнена()
        возврат "Пока не готов"
    иначе если не СобственнаяМодифицированность  
        возврат "Оценка не изменилась"
    иначе
        возврат "Отменить отправку"
    ;
;

метод ОценкаЗаполнена(): Булево
    если КлиентскоеУстройство.ВидИнтерфейса == ВидИнтерфейса.Телефон
        возврат МобОценкаНачальная != Неопределено
    иначе
        возврат ВебОценкаНачальная != 0 
    ;
;

метод УстановитьДатуОценки()
    если КлиентскоеУстройство.ВидИнтерфейса == ВидИнтерфейса.Телефон
        Объект.МобДатаОценки = Дата.Сейчас() 
    иначе
        Объект.ВебДатаОценки = Дата.Сейчас() 
    ;
;

метод ОбратнаяСвязьВысота(): Число
    возврат ОсновнойИнтерфейс.ВысотаФормыДокумента(100)
;

метод ОценкаПодсказкаЗаголовок(): Строка
    если не ОценкаЗаполнена()
        возврат "Если готовы - оцените приложение. Позже можно будет изменить свою оценку."
    иначе   
        возврат "Если ваша оценка приложения изменилась, вы можете это отметить."
    ;
;

метод ПятьАдресКартинки(): ДвоичныйОбъект.Ссылка?
    возврат АдресКартинкиПоОценке(5)
;

метод ЧетыреАдресКартинки(): ДвоичныйОбъект.Ссылка?
    возврат АдресКартинкиПоОценке(4)
;

метод ТриАдресКартинки(): ДвоичныйОбъект.Ссылка?
    возврат АдресКартинкиПоОценке(3)
;

метод ДваАдресКартинки(): ДвоичныйОбъект.Ссылка?
    возврат АдресКартинкиПоОценке(2)
;

метод ОдинАдресКартинки(): ДвоичныйОбъект.Ссылка?
    возврат АдресКартинкиПоОценке(1)
;

метод АдресКартинкиПоОценке(Оценка: Число): ДвоичныйОбъект.Ссылка?
    если Объект.ВебОценка == Оценка
        возврат Оценка1БаллВыбранная
    иначе
        возврат Оценка1Балл
    ;
;

метод ПодсказкаОтзываЗаголовок(): Строка
    если КлиентскоеУстройство.ВидИнтерфейса == ВидИнтерфейса.Телефон
        пер МобОценка: ОбщаяОценкаПриложения? = Объект.МобОценка
        выбор МобОценка
        когда НравитсяНоСОговорками
            возврат "Напишите, чего не хватает в приложении, чтобы оно стало более полезным для вас."
        когда НеНравится 
            возврат "Напишите, что вас не устроило. Что нам переделать или улучшить, чтобы вы дали позитивную оценку?"
        ;
        возврат ""
    иначе
        возврат "Напишите, что понравилось или, наоборот, вызвало затруднения. Что нам улучшить, в общем – любые пожелания разработчикам приложения."
    ;    
;

метод ОтзывВидимость(): Булево
    если КлиентскоеУстройство.ВидИнтерфейса == ВидИнтерфейса.Компьютер
        возврат Истина
    иначе
        пер МобОценка: ОбщаяОценкаПриложения? = Объект.МобОценка
        возврат МобОценка == ОбщаяОценкаПриложения.НеНравится или МобОценка == ОбщаяОценкаПриложения.НравитсяНоСОговорками
    ;
;