импорт ПриложениеНастройки
импорт СтандартноеПриложение
импорт ВзаимодействиеСEssm
импорт Уведомления
импорт ЛичныеКабинетыПользователей
импорт ФизическиеЛица

@ВПроекте
@НаКлиенте
метод ПараметрыСеанса(): ПараметрыСеанса
    возврат ПараметрыСеансаСервис.ПолучитьСервис().ПараметрПоКлючу(КЛЮЧ_ПАРАМЕТРОВ_СЕАНСА) как ПараметрыСеанса
; 

@ВПодсистеме
конст КЛЮЧ_ПАРАМЕТРОВ_СЕАНСА: Строка = "ПриложениеПараметрыСеанса" 

@ВПодсистеме конст ИД_ПОДСИСТЕМЫ = "Приложение"
@ВПодсистеме конст ИД_НАСТРОЕК_КОМПАНИИ = "НастройкиКомпании"
@ВПодсистеме конст ИД_НАСТРОЕК_СОГЛАСОВАНИЯ = "НастройкиСогласования"
@ВПодсистеме конст ИД_НАСТРОЕК_УВЕДОМЛЕНИЙ = "НастройкиУведомлений"
@ВПодсистеме конст ИД_НАСТРОЙКИ_ОБМЕНА = "НастройкиОбмена"

@ВПроекте
структура ФункциональныеОпции
    пер ИспользуетсяКэдо: Булево
    пер ИнформацияОКомпании: Булево = Истина
    пер ИнформацияОПолученныхДокументах: Булево = Истина
    пер ИспользоватьОтказПодписывать: Булево
    пер ИнтеграцияС1СПерсонал: Булево
;

@ВПроекте
структура ПараметрыСеанса
    пер ДоступностьПриложения: Булево = Истина
    пер РежимРаботы: РежимыРаботы
    пер ПриложениеЗарегистрировано: Булево
    пер ФункциональныеОпции: ФункциональныеОпции
    пер ИсходДействияУспешно: ДвоичныйОбъект.Ссылка?
    пер СборАналитики: Булево
    пер МаксимальныйРазмерФайла: Число
    пер ПользовательСВАвторизован: Булево
;

@ВПроекте
@НаСервере
структура ПараметрыСеансаПодсистемы
    обз пер Ключ: Строка
    обз пер Данные: Объект
;

@ВПроекте
@НаСервере @ДоступноСКлиента
метод ИнтернетДоступОткрыт(Настройки: ПриложениеНастройки.ПоставляемыеНастройкиПриложения): Булево
    возврат ИнтернетДоступОткрыт(Настройки.ПараметрыДоступаКEssm, Настройки.ОтправкаSms.НастройкиУчетнойЗаписи)
;

@ВПроекте
@НаСервере @ДоступноСКлиента
метод ИнтернетДоступОткрыт(ПараметрыДоступа: ВзаимодействиеСEssm.ПараметрыДоступаКEssm? = Неопределено, 
            УчетнаяЗаписьSms: Уведомления.НастройкиУчетнойЗаписиОтправкиSms? = Неопределено): Булево
    если ЛичныеКабинетыПользователей.СервисWelcomeДоступен()
        возврат Истина
    ;
    если КлиентEssm.СервисДоступен(ПараметрыДоступа != Неопределено ? ПараметрыДоступа : ВзаимодействиеСEssm.ПараметрыДоступаКEssm())
        возврат Истина
    ;
    возврат УведомленияСервер.СервисОтправкиSmsДоступен(УчетнаяЗаписьSms != Неопределено ? УчетнаяЗаписьSms : Уведомления.НастройкиУчетнойЗаписи())
;

@ВПроекте
@НаСервере @ДоступноСКлиента
метод Пусто(): Булево
    знч Запрос = Запрос{
        Выбрать
            Ссылка Как Ссылка
        Из
            ФизическиеЛица
    }
    пер Результат = новый ЧитаемыйМассив(Запрос.Выполнить())
    возврат Результат.Пусто()
;

@ВПодсистеме
@НаСервере @ДоступноСКлиента
статический метод ПолучитьИдентификаторПриложения(): Строка
    возврат МенеджерНастроекПриложения.ОбщедоступныеНастройкиПриложения().ИдПриложения
;