импорт ФизическиеЛица
импорт КонтрольДоступа
импорт ОбменДанными

//Область РазрешенияДоступа

@Обработчик
метод ВычислитьРазрешенияДоступа(): Массив<РазрешениеДоступа>
    возврат КонтрольДоступа.РазрешенияДоступаТаблицыКонтентДанныхАбонентаЧтениеВсем()
;

//КонецОбласти

//Область СлужебныйПрограммныйИнтерфейс

@ВПодсистеме
метод ОбновитьИнформационнуюСистемуФизическогоЛица(ФизическоеЛицо: ФизическиеЛица.Ссылка, ИнформационнаяСистема: ИнформационныеСистемы.Ссылка)

    знч Запрос = Запрос{
        Выбрать Первые 1 
            Истина Как Значение
        Из 
            ИнформационныеСистемыФизическихЛиц
        Где
            ФизическоеЛицо == %ФизическоеЛицо
            И ИнформационнаяСистема == %ИнформационнаяСистема}
    исп Результат = Запрос.Выполнить() 
    знч РезультатСтроки = новый ЧитаемыйМассив(Результат)
    если (не РезультатСтроки.Пусто())
        возврат
    ;
    пер НовыйЭлемент = новый ИнформационныеСистемыФизическихЛиц.Объект()
    НовыйЭлемент.ФизическоеЛицо = ФизическоеЛицо
    НовыйЭлемент.ИнформационнаяСистема = ИнформационнаяСистема
    НовыйЭлемент.Записать()
;

@ВПодсистеме
метод ИнформационныеСистемыФизическогоЛица(ФизическоеЛицо: ФизическиеЛица.Ссылка): Массив<ИнформационныеСистемы.Ссылка>
    пер ИнформационныеСистемы: Массив<ИнформационныеСистемы.Ссылка>
    знч Запрос = Запрос{
        Выбрать 
            ИнформационнаяСистема 
         Из 
            ИнформационныеСистемыФизическихЛиц
         Где
            ФизическоеЛицо == %ФизическоеЛицо}
    исп Результат = Запрос.Выполнить() 
    для РезультатСтрока из Результат
        ИнформационныеСистемы.Добавить(РезультатСтрока.ИнформационнаяСистема)
    ;
    возврат ИнформационныеСистемы
;

@ВПодсистеме
метод ЗаполнитьРазделителиВыгружаемыхДанных()
    знч Запрос = Запрос{
        Выбрать
            ФизическоеЛицо,
            ИнформационнаяСистема
        Из
            ИнформационныеСистемыФизическихЛиц
    }
    исп Результат = Запрос.Выполнить()
    для СтрокаРезультата из Результат
        ИнформационныеСистемыРазделителейДанных.ОбновитьИнформационныеСистемыРазделителейДанных(
            СтрокаРезультата.ФизическоеЛицо,
            СтрокаРезультата.ИнформационнаяСистема)
    ;
;

//КонецОбласти
