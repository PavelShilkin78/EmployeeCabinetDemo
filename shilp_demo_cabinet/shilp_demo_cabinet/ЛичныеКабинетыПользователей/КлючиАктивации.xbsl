импорт КонтрольДоступа
импорт ФизическиеЛица

//Область РазрешенияДоступа

@Обработчик
метод ВычислитьРазрешенияДоступа(): Массив<РазрешениеДоступа>
    возврат КонтрольДоступа.РегистрСведенийРедактированиеАдминистратору()
;

@Обработчик
метод ВычислитьРазрешенияДоступаДляОбъектов(Записи: ЧитаемыйМассив<КлючиАктивации.ДанныеРасчетаРазрешений>): ЧитаемоеСоответствие<КлючиАктивации.ДанныеРасчетаРазрешений, ЧитаемаяКоллекция<РазрешениеДоступа>>
    знч Разрешения: Соответствие<КлючиАктивации.ДанныеРасчетаРазрешений, ЧитаемаяКоллекция<РазрешениеДоступа>>
    для Элемент из Записи
        знч РазрешенияЭлемента: Множество<РазрешениеДоступа>
        РазрешенияЭлемента.ДобавитьВсе(КонтрольДоступа.ЧтениеДанныхРегистровПерсонально([Элемент.ФизическоеЛицо]))
        Разрешения.Вставить(Элемент, РазрешенияЭлемента)
    ;
    возврат Разрешения
;

//КонецОбласти

@ВПроекте
метод КлючАктивацииФизическогоЛица(ФизическоеЛицо: ФизическиеЛица.Ссылка?): Строка?
    пер Ключ: Строка?
    если ФизическоеЛицо != Неопределено
        знч Запрос = Запрос{
            Выбрать
                КлючАктивации
            Из  
                КлючиАктивации
            Где
                ФизическоеЛицо == %ФизическоеЛицо}
        исп Результат = Запрос.Выполнить()
        для СтрокаРезультата из Результат
            Ключ = СтрокаРезультата.КлючАктивации
        ;
    ;
    возврат Ключ
;

@ВПроекте
метод ДобавитьКлючАктивации(ФизическоеЛицо: ФизическиеЛица.Ссылка, КлючАктивации:Строка)
    исп КонтекстДоступа.Привилегированный()
    знч НаборЗаписей = новый КлючиАктивации.НаборЗаписей()
    НаборЗаписей.Фильтр.ФизическоеЛицо.Установить(ФизическоеЛицо)
    НаборЗаписей.ДобавитьЗапись(
        ФизическоеЛицо = ФизическоеЛицо,
        КлючАктивации = КлючАктивации
    )
    НаборЗаписей.Записать()
;