импорт ВебApi
импорт ОбщиеСтандартные
импорт ФизическиеЛица
импорт КонтрольДоступа

@ВПроекте
структура ДанныеУчетнойЗаписиADСервиса_V1_0
    знч personID: Строка
    пер serviceName: Строка
    пер accountServiceName: Строка
;

//Область РазрешенияДоступа

@Обработчик
метод ВычислитьРазрешенияДоступа(): Массив<РазрешениеДоступа>
    возврат КонтрольДоступа.РазрешенияДоступаТаблицыСервисныхДанныхАбонента()
;

//КонецОбласти

@ВПроекте
метод ТипДанныхJson(ВерсияФорматаОбмена: Версия?): Тип
    если ВерсияФорматаОбмена != Неопределено и ВерсияФорматаОбмена >= Версия{1.0}
        возврат Тип<ДанныеУчетнойЗаписиADСервиса_V1_0>
    ;
;

@ВПодсистеме
метод ТипОбъектаJson(): Строка
    возврат "accountsADService"
;

@ВПроекте
метод ТипКоллекцииДанныхJson(ВерсияФорматаОбмена: Версия?): Тип
    если ВерсияФорматаОбмена != Неопределено и ВерсияФорматаОбмена >= Версия{1.0}
        возврат Тип<Массив<ДанныеУчетнойЗаписиADСервиса_V1_0>>
    ;
;

@ВПодсистеме
метод Заполнить(УчетнаяЗаписьADСервиса: УчетныеЗаписиADСервиса.Объект, ДанныеУчетнойЗаписиADСервиса: ДанныеУчетнойЗаписиADСервиса_V1_0)
    УчетнаяЗаписьADСервиса.ИмяСервиса = ДанныеУчетнойЗаписиADСервиса.serviceName
    УчетнаяЗаписьADСервиса.ИмяУчетнойЗаписи = ДанныеУчетнойЗаписиADСервиса.accountServiceName
;

@ВПроекте
метод ДанныеУчетнойЗаписиADСервиса(ФизическоеЛицо: ФизическиеЛица.Ссылка?): УчетныеЗаписиADСервиса.Ссылка?
    пер СсылкаПоФизлицу: УчетныеЗаписиADСервиса.Ссылка?
    знч Запрос = Запрос{
        Выбрать Ссылка
        Из УчетныеЗаписиADСервиса
        Где Владелец == %ФизическоеЛицо}
    исп Результат = Запрос.Выполнить()
    для СтрокаРезультата из Результат
        СсылкаПоФизлицу = СтрокаРезультата.Ссылка
    ;
    возврат СсылкаПоФизлицу
;

@ВПроекте
метод ЗагрузитьОбъектИзJson(ДанныеJson: ДанныеУчетнойЗаписиADСервиса_V1_0, Сервисы: ВебApiСервер.СервисыЗагрузкиJson)
    знч Ссылка = ВебApiСервер.НайтиСсылку(ДанныеJson.personID)
    если Ссылка == Неопределено или не МенеджерСущности.СсылкаСуществует(Ссылка как Справочник.Ссылка)
        выбросить новый СервисыHttp.ИсключениеОбъектНеНайден("Физическое лицо не обнаружено", "Владелец", ДанныеJson.personID)
    ;
    
    пер ПодчиненнаяСсылка = ДанныеУчетнойЗаписиADСервиса(Ссылка как ФизическиеЛица.Ссылка)
    пер УчетнаяЗаписьADСервиса: УчетныеЗаписиADСервиса.Объект?
        
    если ПодчиненнаяСсылка == Неопределено
        УчетнаяЗаписьADСервиса = новый УчетныеЗаписиADСервиса.Объект()
        УчетнаяЗаписьADСервиса.Владелец = Ссылка как ФизическиеЛица.Ссылка
    иначе
        УчетнаяЗаписьADСервиса = ПодчиненнаяСсылка.ЗагрузитьОбъект()
        если УчетнаяЗаписьADСервиса == Неопределено
            УчетнаяЗаписьADСервиса = новый УчетныеЗаписиADСервиса.Объект()
            УчетнаяЗаписьADСервиса.Владелец = Ссылка как ФизическиеЛица.Ссылка
        ;
    ;
    Заполнить(УчетнаяЗаписьADСервиса, ДанныеJson)
    УчетнаяЗаписьADСервиса.Записать()
    исп ПривилегированныйРежим: КонтекстДоступа = КонтекстДоступа.Привилегированный()
    ЛичныеКабинетыПользователей.ОбновитьADУчетнуюЗапись(УчетнаяЗаписьADСервиса, (Ссылка как ФизическиеЛица.Ссылка))
    ПривилегированныйРежим.Закрыть()
;
