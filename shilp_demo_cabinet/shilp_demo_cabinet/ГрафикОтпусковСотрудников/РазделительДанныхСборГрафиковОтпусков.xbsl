импорт ОбменДанными
импорт ВебApi
импорт КонтрольДоступа

@ВПроекте конст ИД_РАЗДЕЛИТЕЛЬ_СБОР_ГРАФИКОВ_HRM = "7af27b5a-77c1-4559-b6eb-ef2f11bc7c23"

конст НАИМЕНОВАНИЯ_ПРЕДОПРЕДЕЛЕННЫХ_ЭЛЕМЕНТОВ = {
    ИД_РАЗДЕЛИТЕЛЬ_СБОР_ГРАФИКОВ_HRM: "Сбор графика отпусков предприятия HRM"
}

//Область РазрешенияДоступа
@Обработчик
метод ВычислитьРазрешенияДоступа(): Массив<РазрешениеДоступа>
    возврат [КонтрольДоступа.ПолныйДоступАутентифицированным(этот)] 
;

//КонецОбласти

@ВПодсистеме
метод ПровестиПервоначальноеЗаполнение()
    знч ДанныеИнформационныхСистем = ИнформационныеСистемы.ДанныеИнформационныхСистем()
    для ДанныеИнформационнойСистемы из ДанныеИнформационныхСистем
        // наличие разделителя актуально для обмена с HR
        если ДанныеИнформационнойСистемы.Значение.ВидПриложения == ВидПриложения.Hr
            пер ИдПредопределенных = новый Массив(НАИМЕНОВАНИЯ_ПРЕДОПРЕДЕЛЕННЫХ_ЭЛЕМЕНТОВ.Ключи())     
            знч Запрос = Запрос{
                Выбрать
                    ИдПредопределенного
                Из
                    РазделительДанныхСборГрафиковОтпусков
                Где
                    ИдПредопределенного != Неопределено  
            }
            исп РезультатЗапроса = Запрос.Выполнить()
            для СтрокаРезультата из РезультатЗапроса
                знч ИдПредопределенного = СтрокаРезультата.ИдПредопределенного.ВСтроку()
                если ИдПредопределенных.Содержит(ИдПредопределенного)
                    ИдПредопределенных.Удалить(ИдПредопределенного)
                ;    
            ;
            для ИдПредопределенного из ИдПредопределенных
                пер Разделитель: РазделительДанныхСборГрафиковОтпусков.Объект
                Разделитель.Наименование = НАИМЕНОВАНИЯ_ПРЕДОПРЕДЕЛЕННЫХ_ЭЛЕМЕНТОВ[ИдПредопределенного]
                Разделитель.ИдПредопределенного = новый Ууид(ИдПредопределенного)
                Разделитель.Записать()  
                ИнформационныеСистемыРазделителейДанных.ОбновитьИнформационныеСистемыРазделителейДанных(
                    Разделитель.Ссылка,
                    ДанныеИнформационнойСистемы.Ключ
                )  
            ;
        ;
    ;
;

@ВПроекте
метод ПредопределенныйЭлемент(ИдПредопределенного: Строка): РазделительДанныхСборГрафиковОтпусков.Ссылка?
    знч Запрос = Запрос{
        Выбрать
            Ссылка
        Из 
            РазделительДанныхСборГрафиковОтпусков
        Где
            ИдПредопределенного == %{новый Ууид(ИдПредопределенного)}   
    }    
    исп РезультатЗапроса = Запрос.Выполнить()
    для СтрокаРезультата из РезультатЗапроса 
        возврат СтрокаРезультата.Ссылка    
    ;
    возврат Неопределено
;