импорт Компания
импорт ФизическиеЛица
импорт ЛичныеКабинетыПользователей
импорт ГрафикОтпусков
импорт ИнтерфейсныеКомпонентыСтандартные

метод ОбновитьОтображениеКомпонентовГрафикОтпусковСотрудников()
    ПоказАдминистратору = ГрафикОтпусковСотрудников.ПоказатьСоставлениеГрафикаОтпусковАдминистраторуИИнициатору()
    ПоказОтветственному = ГрафикОтпусковСотрудников.ПоказатьСоставлениеГрафикаОтпусковОтветственному()

    знч ДанныеПоПодразделениям = ПрочитатьДанные().Фильтровать(Данные -> Данные.КоличествоСотрудниковПодразделения > 0)
    знч ДанныеПодразделения = ДанныеПоПодразделениям.ПервыйИлиУмолчание()
    если ДанныеПодразделения == Неопределено
        Содержимое.Добавить(
            новый Надпись(Значение = "Отсутствуют доступные графики подразделений"))
    иначе
        знч ГрафикОтпусковСотрудников = новый ГрафикОтпусковСотрудниковКомпонент(
                                        КомпонентВладелец = этот,
                                        ПоказРуководителю = ПоказРуководителю,
                                        ПоказАдминистратору = ПоказАдминистратору,
                                        ПоказОтветственному = ПоказОтветственному,
                                        СтруктурнаяЕдиница = ДанныеПодразделения.СтруктурнаяЕдиница,
                                        ГрафикОтпусковПодразделения = ДанныеПодразделения.ГрафикОтпусковПодразделения,
                                        ОтветственныйЗаСбор = ДанныеПодразделения.ОтветственныйЗаСбор,
                                        ЗаполнениеГрафикаЯвляетсяСкрытым = ДанныеПодразделения.ЗаполнениеГрафикаЯвляетсяСкрытым,
                                        ПервичноеСостояниеКампанииПодразделения = ДанныеПодразделения.ПервичноеСостояниеКампанииПодразделения,
                                        РастягиватьПоВертикали = Истина,
                                        МинимальнаяВысота = 400,
                                        ЗафиксироватьСокращенныйФормФактор = Ложь,
                                        ДанныеПоПодразделениям = ДанныеПоПодразделениям)
        Содержимое.Добавить(ГрафикОтпусковСотрудников)    
        ГрафикОтпусковСотрудников.Инициализировать()
    ;
    
;


метод ПрочитатьДанные(): Массив<СборГрафиковОтпусков.ДанныеКампанииПодразделения>
    знч Данные: Массив<СборГрафиковОтпусков.ДанныеКампанииПодразделения>
    пер ГрафикОтпусковПредприятияТекущейКампании: ГрафикОтпусковПредприятия.Ссылка? = 
        ГрафикОтпусковСотрудников.ПараметрыСеанса().ГрафикОтпусковПредприятияТекущейКампании
    если ПоказРуководителю и Компания.ПараметрыСеанса().ТекущийПользовательРуководитель
        Данные.ДобавитьВсе(ПрочитатьДанныеПоПодразделениям(Компания.ПараметрыСеанса().ПодразделенияРуководителя, ГрафикОтпусковПредприятияТекущейКампании))
    иначе если ПоказАдминистратору
        Данные.ДобавитьВсе(ПрочитатьДанныеПоОтветственным(Неопределено, ГрафикОтпусковПредприятияТекущейКампании))
    иначе если ПоказОтветственному
        Данные.ДобавитьВсе(ПрочитатьДанныеПоОтветственным(ЛичныеКабинетыПользователей.ПараметрыСеанса().ТекущееФизическоеЛицо, ГрафикОтпусковПредприятияТекущейКампании))
    ;
    возврат Данные        
;


//Область СлужебныеПроцедурыИФункции

@ВПодсистеме
@НаСервере @ДоступноСКлиента
статический метод ПрочитатьДанныеПоПодразделениям(ПараметрУсловия: Массив<СтруктураПредприятия.Ссылка>, ГрафикОтпусковПредприятияТекущейКампании: ГрафикОтпусковПредприятия.Ссылка?): Массив<СборГрафиковОтпусков.ДанныеКампанииПодразделения>
    возврат ПрочитатьДанные(
        ГрафикОтпусковПредприятияТекущейКампании,
        ПараметрУсловия,
        Истина,
        Ложь,
        Ложь)
;

@ВПодсистеме
@НаСервере @ДоступноСКлиента
статический метод ПрочитатьДанныеПоОтветственным(ПараметрУсловия: ФизическиеЛица.Ссылка?, ГрафикОтпусковПредприятияТекущейКампании: ГрафикОтпусковПредприятия.Ссылка?): Массив<СборГрафиковОтпусков.ДанныеКампанииПодразделения>
    возврат ПрочитатьДанные(
        ГрафикОтпусковПредприятияТекущейКампании,
        ПараметрУсловия,
        Ложь,
        Истина,
        ПараметрУсловия == Неопределено)
;

@НаСервере
статический метод ПрочитатьДанные(ГрафикОтпусковПредприятияТекущейКампании: ГрафикОтпусковПредприятия.Ссылка?, ПараметрУсловия: Массив<СтруктураПредприятия.Ссылка>|ФизическиеЛица.Ссылка?, УсловиеПоПодразделениям: Булево, УсловиеПоОтветственным: Булево, ЭтоОтветственныйАдминистратор: Булево): Массив<СборГрафиковОтпусков.ДанныеКампанииПодразделения>
    пер ДанныеПоПодразделениям = новый Массив<СборГрафиковОтпусков.ДанныеКампанииПодразделения>()    
    пер Запрос =  Запрос{
        Выбрать 
            СтруктураПредприятия.Ссылка Как СтруктурнаяЕдиница,
            ГрафикОтпусковСтруктурнойЕдиницы.Состояние.ЗаменитьNull(Неопределено) Как СостояниеГрафикаСтруктурнойЕдиницы,
            ГрафикОтпусковСтруктурнойЕдиницы.ГрафикОтпусковПредприятия.ЗаменитьNull(Неопределено) Как ГрафикОтпусковПредприятия,
            ГрафикОтпусковСтруктурнойЕдиницы.Ссылка.ЗаменитьNull(Неопределено) Как ГрафикОтпусковСтруктурнойЕдиницы,
            ГрафикОтпусковСтруктурнойЕдиницы.Ответственный.ЗаменитьNull(Неопределено) Как ОтветственныйЗаСбор,
            ГрафикОтпусковСтруктурнойЕдиницы.ЗаполнениеГрафикаЯвляетсяСкрытым.ЗаменитьNull(Неопределено) Как ЗаполнениеГрафикаЯвляетсяСкрытым,
            Минимум(ПериодыОтпуска.Состояние.ЗаменитьNull(СостояниеГрафикаОтпусков.Черновик)) Как Состояние,
            Количество(Различные Выбор Когда Сотрудники.ОсновноеМестоРаботы Тогда Сотрудники.Ссылка Конец) Как КоличествоСотрудниковПодразделения
        Из 
            СтруктураПредприятия Как СтруктураПредприятия
            Левое Соединение ГрафикОтпусковСтруктурнойЕдиницы Как ГрафикОтпусковСтруктурнойЕдиницы
                Левое Соединение ГрафикОтпусковСотрудника Как ГрафикОтпусковСотрудника
                    Левое Соединение ПериодыОтпуска Как ПериодыОтпуска 
                    По ГрафикОтпусковСотрудника.ФизическоеЛицо == ПериодыОтпуска.ФизическоеЛицо
                По ГрафикОтпусковСтруктурнойЕдиницы.Ссылка == ГрафикОтпусковСотрудника.ГрафикОтпусковПодразделения
            По СтруктураПредприятия.Ссылка == ГрафикОтпусковСтруктурнойЕдиницы.СтруктурнаяЕдиница
            Левое Соединение ПодразделенияИСотрудники Как Сотрудники
                По СтруктураПредприятия.Ссылка == Сотрудники.Подразделение
        Где 
            ГрафикОтпусковСтруктурнойЕдиницы.ГрафикОтпусковПредприятия == %ГрафикОтпусковПредприятияТекущейКампании
            И ((%УсловиеПоПодразделениям и СтруктураПредприятия.Ссылка В(%ПараметрУсловия))
                или (%УсловиеПоОтветственным 
                    и (%ЭтоОтветственныйАдминистратор или ГрафикОтпусковСтруктурнойЕдиницы.Ответственный.ЗаменитьNull(Неопределено) В(%ПараметрУсловия))))
        Сгруппировать по 
            СтруктураПредприятия.Ссылка,
            ГрафикОтпусковСтруктурнойЕдиницы.Ссылка,
            ГрафикОтпусковСтруктурнойЕдиницы.ГрафикОтпусковПредприятия,
            ГрафикОтпусковСтруктурнойЕдиницы.Состояние,                     
            ГрафикОтпусковСтруктурнойЕдиницы.Ответственный,
            ГрафикОтпусковСтруктурнойЕдиницы.ЗаполнениеГрафикаЯвляетсяСкрытым
        }     
    исп Результат = Запрос.Выполнить() 
    для СтрокаРезультата из Результат
        ДанныеПоПодразделениям.Добавить(
            новый СборГрафиковОтпусков.ДанныеКампанииПодразделения(
                ГрафикОтпусковПредприятия = СтрокаРезультата.ГрафикОтпусковПредприятия,
                ГрафикОтпусковПодразделения = СтрокаРезультата.ГрафикОтпусковСтруктурнойЕдиницы,
                СтруктурнаяЕдиница = СтрокаРезультата.СтруктурнаяЕдиница,
                ОтветственныйЗаСбор = СтрокаРезультата.ОтветственныйЗаСбор,
                ПервичноеСостояниеКампанииПодразделения = СтрокаРезультата.СостояниеГрафикаСтруктурнойЕдиницы,
                КоличествоСотрудниковПодразделения = СтрокаРезультата.КоличествоСотрудниковПодразделения,
                ЗаполнениеГрафикаЯвляетсяСкрытым = СтрокаРезультата.ЗаполнениеГрафикаЯвляетсяСкрытым
            )
        )      
    ;
    возврат ДанныеПоПодразделениям
;

// КонецОбласти

// Контракт ИнициализируемыйКомпонент

@ВПроекте
@Реализация
метод Инициализировать()
    ОбновитьОтображениеКомпонентовГрафикОтпусковСотрудников()    
;

// Конец ИнициализируемыйКомпонент