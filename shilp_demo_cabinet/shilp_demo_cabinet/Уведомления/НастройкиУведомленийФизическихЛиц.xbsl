импорт КонтрольДоступа
импорт ФизическиеЛица

//Область РазрешенияДоступа

@Обработчик
метод ВычислитьРазрешенияДоступа(): Массив<РазрешениеДоступа>
    возврат [КонтрольДоступа.ЧтениеИРедактированиеАутентифицированным(этот)]
;

@Обработчик
метод ВычислитьРазрешенияДоступаДляОбъектов(Элементы: ЧитаемыйМассив<НастройкиУведомленийФизическихЛиц.ДанныеРасчетаРазрешений>): ЧитаемоеСоответствие<НастройкиУведомленийФизическихЛиц.ДанныеРасчетаРазрешений, ЧитаемаяКоллекция<РазрешениеДоступа>>
    знч Разрешения: Соответствие<НастройкиУведомленийФизическихЛиц.ДанныеРасчетаРазрешений, ЧитаемаяКоллекция<РазрешениеДоступа>>
    для Элемент из Элементы
        знч РазрешенияЭлемента: Множество<РазрешениеДоступа>
        РазрешенияЭлемента.ДобавитьВсе(КонтрольДоступа.ЧтениеРедактированиеДанныхСправочниковПерсонально([Элемент.Владелец]))
        Разрешения.Вставить(Элемент, РазрешенияЭлемента)
    ;
    возврат Разрешения
;

//КонецОбласти

@ВПодсистеме
метод УстановитьНастройки(ФизическоеЛицо: ФизическиеЛица.Ссылка, Настройки: Уведомления.НастройкиУведомленийФизическихЛиц)
    знч Запрос = Запрос{
        Выбрать
            Ссылка
        Из
            НастройкиУведомленийФизическихЛиц
        Где
            Владелец == %ФизическоеЛицо
    }
    
    исп Результат = Запрос.Выполнить()
    знч НастройкиОбъект = Результат.ПервыйИлиУмолчание()?.Ссылка.ЗагрузитьОбъект() ?? новый НастройкиУведомленийФизическихЛиц.Объект()
    НастройкиОбъект.Владелец = ФизическоеЛицо
    НастройкиОбъект.ПредпочтительныйСпособУведомления = Настройки.ПредпочтительныйСпособУведомления
    НастройкиОбъект.ВремяНачалаПолученияУведомлений = Настройки.ВремяНачалаПолученияУведомлений
    НастройкиОбъект.ВремяОкончанияПолученияУведомлений = Настройки.ВремяОкончанияПолученияУведомлений
    НастройкиОбъект.ТекущееСмещениеКлиента = Настройки.ТекущееСмещениеКлиента
    
    НастройкиОбъект.Записать()
;

@ВПодсистеме
метод УстановитьПредпочтительныйСпособУведомления(ФизическоеЛицо: ФизическиеЛица.Ссылка, ПредпочтительныйСпособУведомления: СпособыУведомления)
    знч Запрос = Запрос{
        Выбрать
            Ссылка
        Из
            НастройкиУведомленийФизическихЛиц
        Где
            Владелец == %ФизическоеЛицо
    }
    
    исп Результат = Запрос.Выполнить()
    знч НастройкиОбъект = Результат.ПервыйИлиУмолчание()?.Ссылка.ЗагрузитьОбъект() ?? новый НастройкиУведомленийФизическихЛиц.Объект()
    НастройкиОбъект.Владелец = ФизическоеЛицо
    НастройкиОбъект.ПредпочтительныйСпособУведомления = ПредпочтительныйСпособУведомления
    НастройкиОбъект.Записать()
;

@ВПодсистеме
метод ЗаменитьПредпочтительныйСпособУведомления(ЗаменяемыйСпособУведомления: СпособыУведомления, НовыйСпособУведомления: СпособыУведомления)
    знч Запрос = Запрос{
        Выбрать
            Ссылка
        Из
            НастройкиУведомленийФизическихЛиц
        Где
            ПредпочтительныйСпособУведомления == %ЗаменяемыйСпособУведомления
    }
    
    исп Результат = Запрос.Выполнить()
    для СтрокаРезультата из Результат
        знч НастройкиОбъект = СтрокаРезультата.Ссылка.ЗагрузитьОбъект()
        НастройкиОбъект.ПредпочтительныйСпособУведомления = НовыйСпособУведомления
        НастройкиОбъект.Записать()
    ;
;

@ВПодсистеме
метод УстановитьВремяПолученияУведомлений(ФизическоеЛицо: ФизическиеЛица.Ссылка, ВремяНачалаПолученияУведомлений: Время?, ВремяОкончанияПолученияУведомлений: Время?)
    знч Запрос = Запрос{
        Выбрать
            Ссылка
        Из
            НастройкиУведомленийФизическихЛиц
        Где
            Владелец == %ФизическоеЛицо
    }
    
    исп Результат = Запрос.Выполнить()
    знч НастройкиОбъект = Результат.ПервыйИлиУмолчание()?.Ссылка.ЗагрузитьОбъект() ?? новый НастройкиУведомленийФизическихЛиц.Объект()
    НастройкиОбъект.Владелец = ФизическоеЛицо
    НастройкиОбъект.ВремяНачалаПолученияУведомлений = ВремяНачалаПолученияУведомлений
    НастройкиОбъект.ВремяОкончанияПолученияУведомлений = ВремяОкончанияПолученияУведомлений
    НастройкиОбъект.Записать()
;

@ВПодсистеме
метод УстановитьТекущееСмещениеКлиента(ФизическоеЛицо: ФизическиеЛица.Ссылка, ТекущееСмещениеКлиента: Число)
    знч Запрос = Запрос{
        Выбрать
            Ссылка
        Из
            НастройкиУведомленийФизическихЛиц
        Где
            Владелец == %ФизическоеЛицо
    }
    
    исп Результат = Запрос.Выполнить()
    знч НастройкиОбъект = Результат.ПервыйИлиУмолчание()?.Ссылка.ЗагрузитьОбъект() ?? новый НастройкиУведомленийФизическихЛиц.Объект()
    НастройкиОбъект.Владелец = ФизическоеЛицо
    НастройкиОбъект.ТекущееСмещениеКлиента = ТекущееСмещениеКлиента
    НастройкиОбъект.Записать()
;

@ВПодсистеме
метод Настройки(ФизическиеЛица: ЧитаемаяКоллекция<ФизическиеЛица.Ссылка>): ЧитаемоеСоответствие<ФизическиеЛица.Ссылка, Уведомления.НастройкиУведомленийФизическихЛиц>
    знч НастройкиФизическихЛиц: Соответствие<ФизическиеЛица.Ссылка, Уведомления.НастройкиУведомленийФизическихЛиц>
    знч Запрос = Запрос{
        Выбрать
            Владелец,
            ПредпочтительныйСпособУведомления,
            ВремяНачалаПолученияУведомлений,
            ВремяОкончанияПолученияУведомлений,
            ТекущееСмещениеКлиента
        Из
            НастройкиУведомленийФизическихЛиц
        Где
            Владелец в (%ФизическиеЛица)
    }
    
    исп Результат = Запрос.Выполнить()
    для СтрокаРезультата из Результат
        НастройкиФизическихЛиц.Вставить(СтрокаРезультата.Владелец,
            новый Уведомления.НастройкиУведомленийФизическихЛиц(
                СтрокаРезультата.ПредпочтительныйСпособУведомления,
                СтрокаРезультата.ВремяНачалаПолученияУведомлений,
                СтрокаРезультата.ВремяОкончанияПолученияУведомлений,
                СтрокаРезультата.ТекущееСмещениеКлиента))
    ;
    
    возврат НастройкиФизическихЛиц
;