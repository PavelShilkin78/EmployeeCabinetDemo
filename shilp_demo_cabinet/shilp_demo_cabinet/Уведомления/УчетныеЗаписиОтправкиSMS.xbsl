импорт КонтрольДоступа

//Область РазрешенияДоступа

@Обработчик
метод ВычислитьРазрешенияДоступа(): Массив<РазрешениеДоступа>
    возврат КонтрольДоступа.РазрешенияДоступаТаблицыСервисныхДанныхАбонентаЧтениеВсем()
;

//КонецОбласти

//Область СлужебныйПрограммныйИнтерфейс

@ВПодсистеме
метод НастройкиУчетнойЗаписи(): Уведомления.НастройкиУчетнойЗаписиОтправкиSms
    знч Настройки: Уведомления.НастройкиУчетнойЗаписиОтправкиSms
    знч Запрос = Запрос{
        Выбрать Первые 1
           Сервер,
           URLСервиса
        Из 
           УчетныеЗаписиОтправкиSMS
        Где
           ИспользоватьПоУмолчанию}
    
    исп Результат = Запрос.Выполнить()
    знч РезультатСтроки = новый ЧитаемыйМассив(Результат)
    если (не РезультатСтроки.Пусто())
        Настройки.Сервер = РезультатСтроки[0].Сервер
        Настройки.URLСервиса = РезультатСтроки[0].URLСервиса
    ;
    возврат Настройки
;

@ВПодсистеме
метод УстановитьНастройкиУчетнойЗаписиОтправкиSms(СохраняемыеНастройки: Уведомления.НастройкиУчетнойЗаписиОтправкиSms,
    ИспользоватьПоУмолчанию: Булево = Истина)
    
    исп Транзакции.Начать()
    знч Запрос = Запрос{
        Выбрать Первые 1 
            Ссылка 
        Из УчетныеЗаписиОтправкиSMS Где ИспользоватьПоУмолчанию}
    исп Результат = Запрос.Выполнить()
    пер НастройкиОбъект: УчетныеЗаписиОтправкиSMS.Объект?
    знч РезультатСтроки = новый ЧитаемыйМассив(Результат)
    если (не РезультатСтроки.Пусто())
        НастройкиОбъект = РезультатСтроки[0].Ссылка.ЗагрузитьОбъект(Истина)
    иначе
        НастройкиОбъект = новый УчетныеЗаписиОтправкиSMS.Объект()
    ;
    НастройкиОбъект!.Сервер = СохраняемыеНастройки.Сервер
    НастройкиОбъект!.URLСервиса = СохраняемыеНастройки.URLСервиса
    НастройкиОбъект!.ИспользоватьПоУмолчанию = ИспользоватьПоУмолчанию
    НастройкиОбъект!.Записать()
;

//КонецОбласти