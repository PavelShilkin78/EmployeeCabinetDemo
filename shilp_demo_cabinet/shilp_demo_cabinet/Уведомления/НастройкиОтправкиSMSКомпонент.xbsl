// Блок инициализации
импорт КонтрольДоступа
импорт ИнтерфейсныеКомпонентыСтандартные

@Обработчик
метод ПослеСоздания()
    ДанныеНастроек = НастройкиОтправки()
;

// Конец инициализации

// Блок программного интерфейса

@ВПроекте
метод Записать(): Булево
    возврат ЗаписатьСПроверкой()
;

@ВПроекте
метод Заполнить(ДанныеНастроек: Уведомления.НастройкиОтправкиSms)
    этот.ДанныеНастроек = ДанныеНастроек
;

// Конец программного интерфейса 

// Блок обработки событий клиента

метод СохранениеНастроекПриНажатии(Источник: Кнопка, Событие: СобытиеПриНажатии)
    ЗаписатьСПроверкой()
;

// Конец обработки событий клиента

// Блок проверки данных

метод ПоляПроверки(): Множество<ПолеВвода<Строка>|ПолеВвода<Число>>
    возврат {
        Компоненты.ИдентификаторSmsGate, 
        Компоненты.СерверSmsGate,
        Компоненты.UrlСервисаSmsGate
    }
;

// Конец проверки данных

// Блок чтения и записи данных

метод ЗаписатьСПроверкой(): Булево
    пер ПоляКарточкиЗаполнены = СтандартныеФормы.ПроверкаЗаполенияПолей(ПоляПроверки())
    если ПоляКарточкиЗаполнены
        ЗаписатьНастройкиОтправкиSms(ДанныеНастроек)
        если ОбязательноеЗаполнение
            УстановитьНастройкиШлюзовSms(ДанныеНастроек)
        ;
    ;
    возврат ПоляКарточкиЗаполнены
;

@НаСервере @ДоступноСКлиента
статический метод НастройкиОтправки(): Уведомления.НастройкиОтправкиSms
    пер УчетнаяЗаписьSms = Уведомления.НастройкиУчетнойЗаписи()
    пер НастройкиОтправки = новый Уведомления.НастройкиОтправкиSms(
        Уведомления.Идентификаторы().ИдентификаторSmsGate,
        новый Уведомления.НастройкиУчетнойЗаписиОтправкиSms(
            УчетнаяЗаписьSms.Сервер,
            УчетнаяЗаписьSms.URLСервиса
        )
    )
    возврат НастройкиОтправки
;

@НаСервере @ДоступноСКлиента
статический метод ЗаписатьНастройкиОтправкиSms(ДанныеНастроек: Уведомления.НастройкиОтправкиSms)
    Уведомления.УстановитьНастройкиУчетнойЗаписиОтправкиSms(ДанныеНастроек.НастройкиУчетнойЗаписи)

    пер Идентификаторы = новый Уведомления.ИдентификаторыПриложения()
    Идентификаторы.ИдентификаторSmsGate = ДанныеНастроек.ИдентификаторSmsGate
    Уведомления.УстановитьИдентификаторыПриложения(Идентификаторы, ["smsGate"])
;

@НаСервере @ДоступноСКлиента
статический метод УстановитьНастройкиШлюзовSms(ДанныеНастроек: Уведомления.НастройкиОтправкиSms)
    пер НастройкиШлюза = Уведомления.НастройкиШлюза(ДанныеНастроек)
    ПользователиСистемы.УстановитьШлюзSms(НастройкиШлюза)
;

// Конец чтения и записи данных