импорт КонтрольДоступа

//Область РазрешенияДоступа

@Обработчик
метод ВычислитьРазрешенияДоступа(): Массив<РазрешениеДоступа>
    возврат КонтрольДоступа.РазрешенияДоступаТаблицыСервисныхДанныхАбонентаЧтениеВсем()
;

//КонецОбласти

//Область СлужебныеПроцедурыИФункции

@ВПодсистеме
метод Настройки(): Соответствие<СпособыУведомления, Уведомления.НастройкиУведомленийПриложения>
    пер Настройки = новый Соответствие<СпособыУведомления, Уведомления.НастройкиУведомленийПриложения>()
    знч Запрос = Запрос{
        Выбрать
            ВремяНачалаПолученияУведомлений,
            ВремяОкончанияПолученияУведомлений,
            Способ
        Из 
            НастройкиУведомлений
    }
    исп Результат = Запрос.Выполнить()
        для РезультатСтрока из Результат
            пер НастрокаПоСпособу = новый Уведомления.НастройкиУведомленийПриложения(
                новый ЗакрытыйДиапазон<Время>(
                    РезультатСтрока.ВремяНачалаПолученияУведомлений, 
                    РезультатСтрока.ВремяОкончанияПолученияУведомлений
                )
            )
            Настройки.Вставить(РезультатСтрока.Способ, НастрокаПоСпособу)
        ;

    возврат Настройки
;

@ВПодсистеме
метод ЗаписатьВремяПолученияУведомлений(Способ: СпособыУведомления, ВремяПолучения: ЗакрытыйДиапазон<Время>, Замещать: Булево = Истина)
    пер Настройки = новый НастройкиУведомлений.Объект()
    знч Запрос = Запрос{
        Выбрать
            Ссылка
        Из 
            НастройкиУведомлений
        Где
            Способ == %Способ
    }
    исп РезультатЗапроса = Запрос.Выполнить() 
    знч РезультатЗапросаСтроки = новый ЧитаемыйМассив(РезультатЗапроса)
    если не РезультатЗапросаСтроки.Пусто()
        если Замещать
            Настройки = РезультатЗапросаСтроки[0].Ссылка.ЗагрузитьОбъект()
        иначе
            возврат
        ;
    ;
    Настройки.Способ = Способ
    Настройки.ВремяНачалаПолученияУведомлений = ВремяПолучения.От
    Настройки.ВремяОкончанияПолученияУведомлений = ВремяПолучения.До
    Настройки.Записать()
;

@ВПодсистеме
метод ПрименитьВремяПолученияУведомлений(ВремяПолучения: ЗакрытыйДиапазон<Время>)
    знч Запрос = Запрос{
        Выбрать
            Ссылка
        Из 
            НастройкиУведомлений
    }
    исп РезультатЗапроса = Запрос.Выполнить() 
    для РезультатСтрока из РезультатЗапроса
        пер Настройки = РезультатСтрока.Ссылка.ЗагрузитьОбъект()
        Настройки.ВремяНачалаПолученияУведомлений = ВремяПолучения.От
        Настройки.ВремяОкончанияПолученияУведомлений = ВремяПолучения.До
        Настройки.Записать()
    ;
;

@ВПодсистеме
метод Удалить(Способ: СпособыУведомления)
    знч Запрос = Запрос{
        Выбрать
            Ссылка
        Из 
            НастройкиУведомлений
        Где
            Способ == %Способ
    }
    исп РезультатЗапроса = Запрос.Выполнить() 
    знч РезультатЗапросаСтроки = новый ЧитаемыйМассив(РезультатЗапроса)
    если не РезультатЗапросаСтроки.Пусто()
        РезультатЗапросаСтроки[0].Ссылка.ЗагрузитьОбъект().Удалить()
    ;
;

//КонецОбласти