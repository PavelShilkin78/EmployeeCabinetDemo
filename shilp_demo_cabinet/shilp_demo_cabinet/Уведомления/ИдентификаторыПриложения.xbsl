@ВПодсистеме
исключение ИсключениеЗаписьСИдентификаторамиУжеСуществует
;

@ВПодсистеме
метод ИдентификаторSmsGate(): Строка
    возврат Идентификаторы().ИдентификаторSmsGate
;

@ВПодсистеме
метод УстановитьИдентификаторыПриложения(Идентификаторы: Уведомления.ИдентификаторыПриложения, Ключи: ЧитаемаяКоллекция<Строка>? = Неопределено)
    знч Запрос = Запрос{
        Выбрать Первые 1
            ИдентификаторыПриложения.Ссылка Как Ссылка
        Из ИдентификаторыПриложения Как ИдентификаторыПриложения}
    исп Транзакции.Начать()
    исп Результат = Запрос.Выполнить()
    пер Объект: ИдентификаторыПриложения.Объект?
    знч РезультатСтроки = новый ЧитаемыйМассив(Результат)
    если (не РезультатСтроки.Пусто())
        Объект = РезультатСтроки[0].Ссылка.ЗагрузитьОбъект(Истина)
    иначе
        Объект = новый ИдентификаторыПриложения.Объект()
    ;
    
    если Ключи == Неопределено или Ключи!.Содержит("smsGate")
        Объект!.ИдентификаторSmsGate = Идентификаторы.ИдентификаторSmsGate
    ;
    
    если Ключи == Неопределено или Ключи!.Содержит("pushNotificationId")
        Объект!.ИдентификаторPushNotifications = Идентификаторы.ИдентификаторPushNotifications
    ;
    
    если Ключи == Неопределено или Ключи!.Содержит("tokenFcm")
        Объект!.КлючFcm = Идентификаторы.КлючFcm
    ;
    
    если Ключи == Неопределено или Ключи!.Содержит("certApns")
        Объект!.СертификатApns = Идентификаторы.СертификатApns
    ;
    
    если Ключи == Неопределено или Ключи!.Содержит("certFcm")
        Объект!.КлючCервисаFirebase = Идентификаторы.КлючCервисаFirebase
    ;
    
    если Ключи == Неопределено или Ключи!.Содержит("certHms")
        Объект!.КлючCервисаHms = Идентификаторы.КлючCервисаHms
    ;
    
    Объект!.Записать()

;

@ВПодсистеме
метод Идентификаторы(): Уведомления.ИдентификаторыПриложения
    пер Идентификаторы = новый Уведомления.ИдентификаторыПриложения() 
    знч Запрос = Запрос{
        Выбрать Первые 1
            ИдентификаторыПриложения.ИдентификаторSmsGate Как ИдентификаторSmsGate,
            ИдентификаторыПриложения.ИдентификаторPushNotifications Как ИдентификаторPushNotifications,
            ИдентификаторыПриложения.КлючFcm Как КлючFcm,
            ИдентификаторыПриложения.СертификатApns Как СертификатApns,
            ИдентификаторыПриложения.КлючCервисаFirebase Как КлючCервисаFirebase,
            ИдентификаторыПриложения.КлючCервисаHms Как КлючCервисаHms
        Из
            ИдентификаторыПриложения Как ИдентификаторыПриложения}
    исп Результат = Запрос.Выполнить()
    знч РезультатСтроки = новый ЧитаемыйМассив(Результат)
    если (не РезультатСтроки.Пусто())
        Идентификаторы.ИдентификаторSmsGate = РезультатСтроки[0].ИдентификаторSmsGate
        Идентификаторы.ИдентификаторPushNotifications = РезультатСтроки[0].ИдентификаторPushNotifications
        Идентификаторы.КлючFcm = РезультатСтроки[0].КлючFcm
        Идентификаторы.СертификатApns = РезультатСтроки[0].СертификатApns
        Идентификаторы.КлючCервисаFirebase = РезультатСтроки[0].КлючCервисаFirebase
        Идентификаторы.КлючCервисаHms = РезультатСтроки[0].КлючCервисаHms
    ;
    возврат Идентификаторы
;