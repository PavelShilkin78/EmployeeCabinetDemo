импорт ВзаимодействиеССервисомПодписания
импорт ОбщиеСтандартные
импорт КонтрольДоступа

//КонтрольДоступа

@Обработчик
метод ВычислитьРазрешенияДоступа(): ЧитаемаяКоллекция<РазрешениеДоступа>
    возврат [КонтрольДоступа.ЧтениеИРедактированиеАутентифицированным(этот)]
;

@Обработчик
метод ВычислитьРазрешенияДоступаДляОбъектов(Объекты: ЧитаемыйМассив<ЗапросыВыпускаСертификатаПодписи.ДанныеРасчетаРазрешений>): ЧитаемоеСоответствие<ЗапросыВыпускаСертификатаПодписи.ДанныеРасчетаРазрешений, ЧитаемаяКоллекция<РазрешениеДоступа>>
    возврат КлиентСервисаПодписанияКонтрольДоступаПоставщик.ПолучитьСервис().ВычислитьРазрешенияДоступаДляЗапросовВыпускаСертификатаПодписи(Объекты)
;

//КонтрольДоступа

@ВПодсистеме
метод ЗапросВыпускаСертификатаВладельца(ВладелецУнэп: ВладелецСертификатовСервисаПодписания.Ссылка): ЗапросыВыпускаСертификатаПодписи.Ссылка?
    знч Запрос = Запрос{
        Выбрать Первые 1
            Ссылка
        Из
            ЗапросыВыпускаСертификатаПодписи
        Где
           ВладелецУнэп == %ВладелецУнэп
        Упорядочить По
            МоментСоздания Убыв}
    исп Результат = Запрос.Выполнить()
    возврат Результат.ЕдинственныйИлиУмолчание()?.Ссылка
;

@ВПроекте
метод ВладельцыЗапросовВыпусковСертификатов(Запросы: ЧитаемаяКоллекция<ЗапросыВыпускаСертификатаПодписи.Ссылка?>): ЧитаемоеСоответствие<ЗапросыВыпускаСертификатаПодписи.Ссылка, ВладелецСертификатовСервисаПодписания.Ссылка?>
    знч ВладельцыЗапросовВыпусковСертификатов: Соответствие<ЗапросыВыпускаСертификатаПодписи.Ссылка, ВладелецСертификатовСервисаПодписания.Ссылка?>
    знч Запрос = Запрос{
        Выбрать
            Ссылка,
            ВладелецУнэп
        Из
            ЗапросыВыпускаСертификатаПодписи
        Где
           Ссылка В (%Запросы)}
    исп Результат = Запрос.Выполнить()
    для СтрокаРезультата из Результат
        ВладельцыЗапросовВыпусковСертификатов.Вставить(СтрокаРезультата.Ссылка, СтрокаРезультата.ВладелецУнэп)
    ;
    
    возврат ВладельцыЗапросовВыпусковСертификатов
;

@ВПодсистеме
метод ДанныеЗапроса(ЗапросВыпускаСертификата: ЗапросыВыпускаСертификатаПодписи.Ссылка): 
    ЧитаемоеСоответствие<Строка, Объект?>
    
    возврат ДанныеЗапросовНаПодключение([ЗапросВыпускаСертификата])[ЗапросВыпускаСертификата]
;

@ВПодсистеме
метод ДанныеЗапросовНаПодключение(ЗапросыВыпускаСертификата: ЧитаемаяКоллекция<ЗапросыВыпускаСертификатаПодписи.Ссылка>): 
    ЧитаемоеСоответствие<ЗапросыВыпускаСертификатаПодписи.Ссылка, ЧитаемоеСоответствие<Строка, Объект?>>
    
    пер ДанныеДокументов = новый Соответствие<ЗапросыВыпускаСертификатаПодписи.Ссылка, ЧитаемоеСоответствие<Строка, Объект?>>()
    знч Запрос = Запрос{
        Выбрать
            Ссылка,
            ВладелецУнэп,
            ЕстьУчетнаяЗаписьНаПорталеГосулуг,
            Телефон,
            Почта,
            Фамилия,
            Имя,
            Отчество,
            Снилс,
            Инн,
            Пол,
            Гражданство,
            ДокументСерия,
            ДокументНомер,
            ДокументКодПодразделения,
            ДокументДатаВыдачи,
            ДокументКемВыдан,
            ДокументДатаРождения,
            ДокументМестоРождения,
            ИдТелеграмЧата,
            СпособДоставкиКода
        Из
            ЗапросыВыпускаСертификатаПодписи
        Где
            Ссылка В (%ЗапросыВыпускаСертификата)}
    исп Результат = Запрос.Выполнить()
    для СтрокаРезультата из Результат
        ДанныеДокументов.Вставить(СтрокаРезультата.Ссылка,
            МенеджерЗапросов.СтрокаРезультатаЗапросаВСоответствие(СтрокаРезультата, Результат))
    ;
    возврат ДанныеДокументов
;

@ВПодсистеме
метод ЕстьЗапросВРаботе(ВладелецУнэп: ВладелецСертификатовСервисаПодписания.Ссылка): Булево
    знч Запрос = Запрос{
        Выбрать Первые 1
            ЗапросыВыпускаСертификатаПодписи.Ссылка Как Ссылка,
            СостоянияЗапросовВыпускаСертификатаПодписи.СостояниеЗапроса.ЗаменитьNull(СостоянияЗапросаВыпускаСертификата.Обрабатывается) Как Состояние
        Из
            ЗапросыВыпускаСертификатаПодписи Как ЗапросыВыпускаСертификатаПодписи
                Левое Соединение СостоянияЗапросовВыпускаСертификатаПодписи Как СостоянияЗапросовВыпускаСертификатаПодписи
                По ЗапросыВыпускаСертификатаПодписи.Ссылка == СостоянияЗапросовВыпускаСертификатаПодписи.ЗапросВыпускаСертификата
        Где
            ЗапросыВыпускаСертификатаПодписи.ВладелецУнэп == %ВладелецУнэп
        Упорядочить По
            ЗапросыВыпускаСертификатаПодписи.МоментСоздания Убыв}
    исп Результат = Запрос.Выполнить()
    если не Результат.Пусто()
        знч СтрокаРезультата = Результат.ЕдинственныйИлиУмолчание()
        возврат СтрокаРезультата.Состояние == СостоянияЗапросаВыпускаСертификата.Обрабатывается
            или СтрокаРезультата.Состояние == СостоянияЗапросаВыпускаСертификата.ТребуетсяДополнительнаяИнформация
    ;
    возврат Ложь
;