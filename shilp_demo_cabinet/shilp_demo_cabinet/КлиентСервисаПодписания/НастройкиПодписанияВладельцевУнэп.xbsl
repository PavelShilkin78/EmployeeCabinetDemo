импорт ВзаимодействиеССервисомПодписания
импорт КонтрольДоступа

//КонтрольДоступа

@Обработчик
метод ВычислитьРазрешенияДоступа(): ЧитаемаяКоллекция<РазрешениеДоступа>
    возврат [КонтрольДоступа.ПолныйДоступАутентифицированным(этот)]
;

@Обработчик
метод ВычислитьРазрешенияДоступаДляОбъектов(Записи: ЧитаемыйМассив<НастройкиПодписанияВладельцевУнэп.ДанныеРасчетаРазрешений>): ЧитаемоеСоответствие<НастройкиПодписанияВладельцевУнэп.ДанныеРасчетаРазрешений, ЧитаемаяКоллекция<РазрешениеДоступа>>
    возврат КлиентСервисаПодписанияКонтрольДоступаПоставщик.ПолучитьСервис().ВычислитьРазрешенияДоступаДляНастроекПодписанияВладельцевУнэп(Записи)
;

//КонтрольДоступа

@ВПодсистеме
метод НастройкиПодписания(ВладелецУнэп: ВладелецСертификатовСервисаПодписания.Ссылка): КлиентСервисаПодписания.ПараметрыСеанса
    исп КонтекстДоступа.Привилегированный()
    знч Запрос = Запрос{
        Выбрать
            ВремяОжиданияДоставкиКода,
            СпособДоставкиКодаПодтверждения
        Из
            НастройкиПодписанияВладельцевУнэп
        Где
            ВладелецУнэп == %ВладелецУнэп}
    исп Результат = Запрос.Выполнить()
    пер СтрокаРезультата = Результат.ЕдинственныйИлиУмолчание()
    если СтрокаРезультата == Неопределено
        возврат новый КлиентСервисаПодписания.ПараметрыСеанса()
    ;
    возврат новый КлиентСервисаПодписания.ПараметрыСеанса(
        СтрокаРезультата.ВремяОжиданияДоставкиКода,
        СтрокаРезультата.СпособДоставкиКодаПодтверждения)
;

@ВПодсистеме
метод ОбновитьСпособДоставкиКодаПодтверждения(ВладелецУнэп: ВладелецСертификатовСервисаПодписания.Ссылка, СпособДоставкиКодаПодтверждения: СпособыДоставкиКодаПодтверждения)
    знч НаборЗаписей = новый НастройкиПодписанияВладельцевУнэп.НаборЗаписей()
    НаборЗаписей.Фильтр.ВладелецУнэп.Установить(ВладелецУнэп)
    НаборЗаписей.Прочитать()
    если НаборЗаписей.Пусто() или НаборЗаписей[0].СпособДоставкиКодаПодтверждения != СпособДоставкиКодаПодтверждения
        если НаборЗаписей.Пусто()
            НаборЗаписей.ДобавитьЗапись(ВладелецУнэп = ВладелецУнэп)
        ;
        НаборЗаписей[0].СпособДоставкиКодаПодтверждения = СпособДоставкиКодаПодтверждения
        НаборЗаписей.Записать()
    ;
;