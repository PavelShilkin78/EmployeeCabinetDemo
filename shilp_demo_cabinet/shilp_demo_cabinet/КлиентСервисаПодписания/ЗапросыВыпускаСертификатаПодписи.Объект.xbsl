импорт ВзаимодействиеССервисомПодписания
импорт Классификаторы
импорт КонтрольДоступа
импорт ФизическиеЛица
импорт ОбщиеСтандартные

@Обработчик
метод ПриЗаполнении()
    ВладелецУнэп = ПользователиСистемы.ТекущееФизическоеЛицо()
    если ВладелецУнэп == Неопределено
        возврат
    ;
    этот.Дата = Дата.Сейчас(МенеджерДат.ЧасовойПоясТекущий())
    этот.ЕстьУчетнаяЗаписьНаПорталеГосулуг = Истина
    знч ЗапрашиваемыеДанные: ФизЛица.ЗапрашиваемыеДанныеФизическихЛиц
    ЗапрашиваемыеДанные.ЛичныеДанные = Истина
    ЗапрашиваемыеДанные.УдостоверениеЛичности = Истина
    ЗапрашиваемыеДанные.РабочаяКонтактнаяИнформация = Истина
    ЗапрашиваемыеДанные.ПерсональныеДанные = Истина
    пер ДанныеФизическогоЛица = ФизЛица.ДанныеФизическогоЛица(ВладелецУнэп как ФизическиеЛица.Ссылка, ЗапрашиваемыеДанные)
    этот.Фамилия = ДанныеФизическогоЛица.ЛичныеДанные.ФИО.Фамилия
    этот.Имя = ДанныеФизическогоЛица.ЛичныеДанные.ФИО.Имя
    этот.Отчество = ДанныеФизическогоЛица.ЛичныеДанные.ФИО.Отчество
    этот.Снилс = ДанныеФизическогоЛица.ПерсональныеДанные.СНИЛС
    этот.Инн = ДанныеФизическогоЛица.ПерсональныеДанные.ИНН
    этот.Пол = ДанныеФизическогоЛица.ЛичныеДанные.Пол
    если ДанныеФизическогоЛица.УдостоверениеЛичности.Вид == ДокументыУдостоверяющиеЛичность.Паспорт
        или ДанныеФизическогоЛица.УдостоверениеЛичности.Вид == ДокументыУдостоверяющиеЛичность.ИностранныйПаспорт
        
        этот.ДокументВид = ДанныеФизическогоЛица.УдостоверениеЛичности.Вид
        этот.ДокументСерия = ДанныеФизическогоЛица.УдостоверениеЛичности.Серия
        этот.ДокументНомер = ДанныеФизическогоЛица.УдостоверениеЛичности.Номер
        этот.ДокументКодПодразделения = ДанныеФизическогоЛица.УдостоверениеЛичности.КодВыдавшегоОргана
        этот.ДокументДатаВыдачи = ДанныеФизическогоЛица.УдостоверениеЛичности.ДатаВыдачи
        этот.ДокументКемВыдан = ДанныеФизическогоЛица.УдостоверениеЛичности.ВыдавшийОрган
        этот.Гражданство = ДанныеФизическогоЛица.УдостоверениеЛичности.Страна
    иначе
        этот.ДокументВид = ДокументыУдостоверяющиеЛичность.Паспорт
        этот.Гражданство = СтраныМира.Россия()
    ;
    этот.Телефон = ДанныеФизическогоЛица.РабочаяКонтактнаяИнформация.Телефон
    этот.Почта = ДанныеФизическогоЛица.РабочаяКонтактнаяИнформация.ЭлектроннаяПочта
    если этот.Телефон.Пусто() и не этот.Почта.Пусто()
        этот.СпособДоставкиКода = СпособыДоставкиКодаПодтверждения.НаАдресЭлектроннойПочты
    ;
    этот.ДокументДатаРождения = ДанныеФизическогоЛица.ПерсональныеДанные.ДатаРождения
    этот.ДокументМестоРождения = ДанныеФизическогоЛица.ПерсональныеДанные.МестоРождения
    этот.СервисПодписания = СервисыПодписания.АстралПлатформаУнэп
;

@Обработчик
метод ПередЗаписью(ПредыдущееЗначение: ЗапросыВыпускаСертификатаПодписи.Данные, ПараметрыЗаписи: ЗапросыВыпускаСертификатаПодписи.ПараметрыЗаписи)
    если МоментСоздания == Неопределено
        МоментСоздания = Момент.Сейчас()
    ;
    Наименование = "%{ВладелецУнэп.Представление()} от %{МенеджерДат.ФорматДаты(Дата)}"
;

@Обработчик
@ВыполнятьПриЗагрузкеДанных
метод ПередУдалением(ПараметрыУдаления: ЗапросыВыпускаСертификатаПодписи.ПараметрыУдаления)
    пер НаборЗаписейСостоянияЗапросовВыпускаСертификатаПодписи = новый СостоянияЗапросовВыпускаСертификатаПодписи.НаборЗаписей()
    НаборЗаписейСостоянияЗапросовВыпускаСертификатаПодписи.Фильтр.ЗапросВыпускаСертификата.Установить(Ссылка)
    НаборЗаписейСостоянияЗапросовВыпускаСертификатаПодписи.Записать()
    
    пер НаборЗаписейИдентификаторыСертификатовУнэп = новый ИдентификаторыСертификатовУнэп.НаборЗаписей()
    НаборЗаписейИдентификаторыСертификатовУнэп.Фильтр.ЗапросВыпускаСертификата.Установить(Ссылка)
    НаборЗаписейИдентификаторыСертификатовУнэп.Записать()
    
    пер НаборЗаписейОжидаемыеДокументыЗапросовВыпускаСертификатаПодписи = новый ОжидаемыеДокументыЗапросовВыпускаСертификатаПодписи.НаборЗаписей()
    НаборЗаписейОжидаемыеДокументыЗапросовВыпускаСертификатаПодписи.Фильтр.ЗапросВыпускаСертификата.Установить(Ссылка)
    НаборЗаписейОжидаемыеДокументыЗапросовВыпускаСертификатаПодписи.Записать()
    
;