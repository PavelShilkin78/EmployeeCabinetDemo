импорт ВзаимодействиеССервисомПодписания
импорт ОбщиеСтандартные

@ВПодсистеме
метод ЗарегистрироватьИдентификаторыФайла(Файл: ФайлСервисаПодписания.Ссылка, Идентификатор: Строка, СрокХранения: Момент)
    исп КонтекстДоступа.Привилегированный()
    пер НаборЗаписей = новый ИдентификаторыФайлов.НаборЗаписей()
    НаборЗаписей.Фильтр.Файл.Установить(Файл)
    НаборЗаписей.ДобавитьЗапись(
        Файл = Файл,
        Идентификатор = Идентификатор,
        СрокХранения = СрокХранения
    )
    НаборЗаписей.Записать()
;

@ВПодсистеме
метод ИдентификаторФайла(Файл: ФайлСервисаПодписания.Ссылка): Строка?
    знч ПредельныйСрокХранения = МенеджерДат.ТекущаяДатаВремя() + 1д
    знч Запрос = Запрос{
        Выбрать Первые 1
            Файл,
            Идентификатор
        Из
            ИдентификаторыФайлов
        Где
            Файл == %Файл
            и СрокХранения >= %ПредельныйСрокХранения}
    исп Результат = Запрос.Выполнить()
    возврат Результат.ЕдинственныйИлиУмолчание()?.Идентификатор ?? Неопределено
 ;

@ВПодсистеме
метод УдалитьФайл(Файл: ФайлСервисаПодписания.Ссылка)
    пер НаборЗаписей = новый ИдентификаторыФайлов.НаборЗаписей()
    НаборЗаписей.Фильтр.Файл.Установить(Файл)
    НаборЗаписей.Записать()
;
