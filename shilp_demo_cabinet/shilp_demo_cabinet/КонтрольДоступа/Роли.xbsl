
@ВПодсистеме
метод ЗаполнитьПредопределенныеДанные()
    для Сервис из КонтрольДоступаПоПолномочиямСервис.ПолучитьСервисы()
        Сервис.ЗаполнитьПредопределенныеРоли()
    ;
;

@ВПодсистеме
метод ДобавитьРоль(Наименование: Строка, ИмяПредопределенныхДанных: Строка, Полномочия: ЧитаемыйМассив<Полномочия.Ссылка>, Описание: Строка): Роли.Ссылка
    
    знч СуществующаяРоль = Роль(ИмяПредопределенныхДанных)
    знч РольОбъект = СуществующаяРоль?.ЗагрузитьОбъект() ?? новый Роли.Объект()
    
    РольОбъект.Наименование = Наименование
    РольОбъект.ИмяПредопределенныхДанных = ИмяПредопределенныхДанных
    РольОбъект.Полномочия.Очистить()
    РольОбъект.Полномочия.ДобавитьВсе(Полномочия)
    РольОбъект.Описание = Описание
    РольОбъект.Записать()
    
    возврат РольОбъект.Ссылка
;

@ВПодсистеме
метод Роль(ИмяПредопределенныхДанных: Строка): Роли.Ссылка?
    если ИмяПредопределенныхДанных.Пусто()
        возврат Неопределено
    ;
    
    знч Запрос = Запрос{
        Выбрать
            Ссылка
        из
            Роли
        Где
            ИмяПредопределенныхДанных == %ИмяПредопределенныхДанных}
    исп Результат = Запрос.Выполнить()
    возврат Результат.ПервыйИлиУмолчание()?.Ссылка
;

@ВПодсистеме
метод ПолномочияРоли(Роль: Роли.Ссылка): ЧитаемоеМножество<Полномочия.Ссылка>
    знч ПолномочияРоли: Множество<Полномочия.Ссылка>
    знч Запрос = Запрос{
        Выбрать
            Элемент как Полномочие
        Из
            Роли.Полномочия
        Где
            Владелец в (%Роль)
    }
    исп Результат = Запрос.Выполнить()
    для СтрокаРезультата из Результат
        ПолномочияРоли.Добавить(СтрокаРезультата.Полномочие)
    ;
    
    возврат ПолномочияРоли
;

