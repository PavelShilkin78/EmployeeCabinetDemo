импорт ОбщиеСтандартные


@ВПодсистеме
метод ЗаполнитьПредопределенныеДанные()
    для Сервис из КонтрольДоступаПоПолномочиямСервис.ПолучитьСервисы()
        Сервис.ЗаполнитьПредопределенныеТипыАтрибутов()
    ;
;

@ВПодсистеме
метод ДобавитьТипАтрибута(Наименование: Строка, ИмяПредопределенныхДанных: Строка): ТипыАтрибутов.Ссылка
    
    знч СуществующийТипАтрибута = ТипАтрибута(ИмяПредопределенныхДанных)
    знч ТипАтрибутаОбъект = СуществующийТипАтрибута?.ЗагрузитьОбъект() ?? новый ТипыАтрибутов.Объект()
    
    ТипАтрибутаОбъект.Наименование = Наименование
    ТипАтрибутаОбъект.ИмяПредопределенныхДанных = ИмяПредопределенныхДанных
    ТипАтрибутаОбъект.Записать()
    
    возврат ТипАтрибутаОбъект.Ссылка
;

@ВПодсистеме
метод ТипАтрибута(ИмяПредопределенныхДанных: Строка): ТипыАтрибутов.Ссылка?
    если ИмяПредопределенныхДанных.Пусто()
        возврат Неопределено
    ;
    
    знч Запрос = Запрос{
        Выбрать
            Ссылка
        из
            ТипыАтрибутов
        Где
            ИмяПредопределенныхДанных == %ИмяПредопределенныхДанных}
    исп Результат = Запрос.Выполнить()
    возврат Результат.ПервыйИлиУмолчание()?.Ссылка
;

@ВПодсистеме
метод ТипыАтрибутовПоТипамЗначений(): ЧитаемоеСоответствие<Тип, ТипыАтрибутов.Ссылка>
    знч ТипыАтрибутовПоТипамЗначений: Соответствие<Тип, ТипыАтрибутов.Ссылка>
    
    для Сервис из КонтрольДоступаПоПолномочиямСервис.ПолучитьСервисы()
        ТипыАтрибутовПоТипамЗначений.ВставитьВсе(Сервис.ТипыАтрибутовПоТипамЗначений())
    ;
    
    возврат ТипыАтрибутовПоТипамЗначений
;

@ВПроекте
метод ПолучитьИмяИсточникаДанных(ОбъектПолномочий: ТипыАтрибутов.Ссылка?): Строка?
    знч ТипыАтрибутовПоТипамЗначений: Соответствие<ТипыАтрибутов.Ссылка, Тип>
    
    для Сервис из КонтрольДоступаПоПолномочиямСервис.ПолучитьСервисы()
        ТипыАтрибутовПоТипамЗначений.ВставитьВсе(
            Сервис.ТипыАтрибутовПоТипамЗначений().ВСоответствие(Запись -> Запись.Значение, Запись -> Запись.Ключ))
    ;
    
    возврат МенеджерСущности.ИмяТаблицыПоТипу(ТипыАтрибутовПоТипамЗначений[ОбъектПолномочий])  
;