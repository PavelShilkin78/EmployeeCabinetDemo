@ВПодсистеме
метод ЗаполнитьПредопределенныеДанные()
    для Сервис из КонтрольДоступаПоПолномочиямСервис.ПолучитьСервисы()
        Сервис.ЗаполнитьПредопределенныеПолномочия()
    ;
;

@ВПодсистеме
метод ДобавитьПолномочие(Наименование: Строка, ИмяПредопределенныхДанных: Строка, Описание: Строка,
    ТипАтрибута: ТипыАтрибутов.Ссылка? = Неопределено): Полномочия.Ссылка
    
    знч СуществующееПолномочие = Полномочие(ИмяПредопределенныхДанных)
    знч ПолномочиеОбъект = СуществующееПолномочие?.ЗагрузитьОбъект() ?? новый Полномочия.Объект()
    
    ПолномочиеОбъект.Наименование = Наименование
    ПолномочиеОбъект.ИмяПредопределенныхДанных = ИмяПредопределенныхДанных
    ПолномочиеОбъект.Описание = Описание
    ПолномочиеОбъект.ТипАтрибута = ТипАтрибута
    ПолномочиеОбъект.Записать()
    
    возврат ПолномочиеОбъект.Ссылка
;

@ВПодсистеме
метод Полномочие(ИмяПредопределенныхДанных: Строка): Полномочия.Ссылка?
    если ИмяПредопределенныхДанных.Пусто()
        возврат Неопределено
    ;
    
    знч Запрос = Запрос{
        Выбрать
            Ссылка
        из
            Полномочия
        Где
            ИмяПредопределенныхДанных == %ИмяПредопределенныхДанных}
    исп Результат = Запрос.Выполнить()
    возврат Результат.ПервыйИлиУмолчание()?.Ссылка
;