импорт ФизическиеЛица

@Обработчик
метод ВычислитьРазрешенияДоступа(): Массив<РазрешениеДоступа>
    знч Разрешения: Массив<РазрешениеДоступа>
    Разрешения.ДобавитьВсе(КонтрольДоступа.РазрешенияДоступаТаблицыКонтентДанныхАбонентаСДоступомАдминистратору())
    Разрешения.ДобавитьВсе(КонтрольДоступа.ЧтениеРедактированиеУдалениеДанныхСправочниковПоПолномочиям(
        [КонтрольДоступа.ПолномочиеЧтениеИРедактированиеНастроекПользователей()],
        [КонтрольДоступа.ПолномочиеЧтениеИРедактированиеНастроекПользователей()]
    ))
    возврат Разрешения
;

@ВПодсистеме
метод ЗаписатьНаборОграниченийДоступа(ТипАтрибута: ТипыАтрибутов.Ссылка, ПараметрФильтра: ПараметрыФильтровОграниченийДоступа.Ссылка, Наименование: Строка,
    ЗначенияОграничений: ЧитаемыйМассив<ЗначенияОграничений.Ссылка|ФизическиеЛица.Ссылка>)
    
    знч НаборОграничений = НаборОграниченийДоступаПоПараметру(ТипАтрибута, ПараметрФильтра)?.ЗагрузитьОбъект() ?? новый НаборыОграниченийДоступа.Объект()
    НаборОграничений.Наименование = Наименование
    НаборОграничений.ТипАтрибута = ТипАтрибута
    НаборОграничений.ПараметрФильтра = ПараметрФильтра
    НаборОграничений.ЗначенияОграничений.Очистить()
    НаборОграничений.ЗначенияОграничений.ДобавитьВсе(ЗначенияОграничений)
    
    НаборОграничений.Записать()
;

@ВПодсистеме
метод ОбновитьЗначенияОграничений(НаборОграниченийСсылка: НаборыОграниченийДоступа.Ссылка, ЗначенияОграничений: ЧитаемыйМассив<ЗначенияОграничений.Ссылка|ФизическиеЛица.Ссылка>)
    
    знч НаборОграничений = НаборОграниченийСсылка.ЗагрузитьОбъект()
    НаборОграничений.ЗначенияОграничений.Очистить()
    НаборОграничений.ЗначенияОграничений.ДобавитьВсе(ЗначенияОграничений)
    
    НаборОграничений.Записать()
;

@ВПодсистеме
метод ЗначенияОграниченийНаборов(НаборыОграничений: ЧитаемаяКоллекция<НаборыОграниченийДоступа.Ссылка>): ЧитаемоеМножество<ЗначенияОграничений.Ссылка|ФизическиеЛица.Ссылка>
    знч ЗначенияОграниченийНаборов: Множество<ЗначенияОграничений.Ссылка|ФизическиеЛица.Ссылка>
    
    знч Запрос = Запрос{
        Выбрать
            Элемент как Значение
        Из
            НаборыОграниченийДоступа.ЗначенияОграничений
        Где
            Владелец в (%НаборыОграничений)
    }
    
    исп Результат = Запрос.Выполнить()
    для СтрокаРезультата из Результат
        ЗначенияОграниченийНаборов.Добавить(СтрокаРезультата.Значение)
    ;
    возврат ЗначенияОграниченийНаборов
;

@ВПодсистеме
метод НаборОграниченийДоступаПоПараметру(ТипАтрибута: ТипыАтрибутов.Ссылка, ПараметрФильтра: ПараметрыФильтровОграниченийДоступа.Ссылка): НаборыОграниченийДоступа.Ссылка?
    знч Запрос = Запрос{
        Выбрать
            Ссылка
        из
            НаборыОграниченийДоступа
        Где
            ТипАтрибута == %ТипАтрибута
            и ПараметрФильтра == %ПараметрФильтра
    }
    
    исп Результат = Запрос.Выполнить()
    возврат Результат.ПервыйИлиУмолчание()?.Ссылка
;

@ВПодсистеме
метод НаборыОграниченийДоступаПоПараметрам(ТипАтрибута: ТипыАтрибутов.Ссылка, ПараметрыФильтра: ЧитаемыйМассив<ПараметрыФильтровОграниченийДоступа.Ссылка>):
    ЧитаемоеСоответствие<ПараметрыФильтровОграниченийДоступа.Ссылка, НаборыОграниченийДоступа.Ссылка>
    
    знч НаборыОграничений: Соответствие<ПараметрыФильтровОграниченийДоступа.Ссылка, НаборыОграниченийДоступа.Ссылка>
    знч Запрос = Запрос{
        Выбрать
            Ссылка,
            ПараметрФильтра
        из
            НаборыОграниченийДоступа
        Где
            ТипАтрибута == %ТипАтрибута
            и ПараметрФильтра в (%ПараметрыФильтра)
    }
    
    исп Результат = Запрос.Выполнить()
    для СтрокаРезультата из Результат
        НаборыОграничений.Вставить(СтрокаРезультата.ПараметрФильтра, СтрокаРезультата.Ссылка)
    ;
    возврат НаборыОграничений
;

@ВПодсистеме
метод СоздатьНаборыОграниченийДоступа()
    для Сервис из КонтрольДоступаПоПолномочиямСервис.ПолучитьСервисы()
        Сервис.СоздатьНаборыОграниченийДоступа()
    ;
;