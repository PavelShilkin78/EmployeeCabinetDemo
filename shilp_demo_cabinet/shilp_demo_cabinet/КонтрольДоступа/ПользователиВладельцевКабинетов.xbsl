импорт ФизическиеЛица

//Область РазрешенияДоступа

@Обработчик
метод ВычислитьРазрешенияДоступа(): Массив<РазрешениеДоступа>
    возврат КонтрольДоступа.РегистрСведенийЧтениеВсемРедактированиеАдминистратору()
;

//КонецОбласти РазрешенияДоступа

@ВПроекте
метод ПользовательПоФизлицу(Физлицо: ФизическиеЛица.Ссылка): Пользователи.Ссылка?
    пер Пользователь: Пользователи.Ссылка?
    знч Запрос = Запрос{
        Выбрать Пользователь
        Из ПользователиВладельцевКабинетов как ПользователиВладельцевКабинетов
        Где ПользователиВладельцевКабинетов.ФизическоеЛицо == %Физлицо}
    исп Результат = Запрос.Выполнить()
    для СтрокаРезультата из Результат
        Пользователь = СтрокаРезультата.Пользователь
    ;
    возврат Пользователь
;

@ВПодсистеме
метод ПользователиФизическихЛиц(ФизическиеЛица: ЧитаемаяКоллекция<ФизическиеЛица.Ссылка>): ЧитаемоеСоответствие<ФизическиеЛица.Ссылка, Пользователи.Ссылка?>
    знч ПользователиФизическихЛиц: Соответствие<ФизическиеЛица.Ссылка, Пользователи.Ссылка?>
    знч Запрос = Запрос{
        Выбрать 
            Пользователь.ЗаменитьNull(Неопределено) Как Пользователь,
            ФизическоеЛицо
        Из ПользователиВладельцевКабинетов как ПользователиВладельцевКабинетов
        Где ПользователиВладельцевКабинетов.ФизическоеЛицо В (%ФизическиеЛица)}
    исп РезультатЗапроса = Запрос.Выполнить()
    для СтрокаРезультата из РезультатЗапроса
        ПользователиФизическихЛиц.Вставить(СтрокаРезультата.ФизическоеЛицо, СтрокаРезультата.Пользователь)
    ;
    возврат ПользователиФизическихЛиц
;

@ВПодсистеме
метод ФизическоеЛицоПользователя(Пользователь: Пользователи.Ссылка): ФизическиеЛица.Ссылка?
    возврат ФизическиеЛицаПользователей([Пользователь]).ПолучитьИлиУмолчание(Пользователь)
;

@ВПодсистеме
метод ФизическиеЛицаПользователей(Пользователи: ЧитаемаяКоллекция<Пользователи.Ссылка>):
    ЧитаемоеСоответствие<Пользователи.Ссылка, ФизическиеЛица.Ссылка>
    
    знч ФизическиеЛицаПользователей: Соответствие<Пользователи.Ссылка, ФизическиеЛица.Ссылка>
    знч Запрос = Запрос{
        Выбрать 
            ФизическоеЛицо,
            Пользователь
        Из
            ПользователиВладельцевКабинетов
        Где
            Пользователь в (%Пользователи)
            и ФизическоеЛицо != Неопределено}
    исп РезультатЗапроса = Запрос.Выполнить()
    для СтрокаРезультата из РезультатЗапроса
        ФизическиеЛицаПользователей.Вставить(СтрокаРезультата.Пользователь, СтрокаРезультата.ФизическоеЛицо)
    ;
    возврат ФизическиеЛицаПользователей
;

@ВПодсистеме
метод ПослеЗаписиВФоне(ФизическиеЛица: ЧитаемаяКоллекция<ФизическиеЛица.Ссылка>)
    знч Сервисы = ПользователиВладельцевКабинетовОбрабочикСобытий.ПолучитьСервисы()
    для Сервис из Сервисы
        Сервис.ПользователиВладельцевКабинетовПослеЗаписи(ФизическиеЛица)
    ;    
;