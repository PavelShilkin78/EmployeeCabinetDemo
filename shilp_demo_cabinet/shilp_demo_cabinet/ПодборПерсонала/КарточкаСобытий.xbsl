импорт ИнтерфейсныеКомпонентыСтандартные
импорт КонтрольДоступа
импорт ФизическиеЛица

@ВПроекте
@Реализация
метод Обновить()
    ЗаполнитьДанные()
;

@Обработчик
метод ПослеСоздания()
    ЗаполнитьДанные()
;

@ВПроекте
@НаКлиенте @НаСервере
структура ДанныеСобеседования
    обз знч Ссылка: Собеседования.Ссылка
    обз знч ПредставлениеКандидата: Строка
    обз знч Формат: ФорматСобеседования
    обз знч ДатаПроведения: ДатаВремя
;

@НаСервере @ДоступноСКлиента
статический метод Данные(Участник: ФизическиеЛица.Ссылка): Массив<ДанныеСобеседования>
    знч Данные = новый Массив<ДанныеСобеседования>()
    знч Запрос = Запрос{
        выбрать первые 6
            СобеседованияУчастника.Владелец как Собеседование
            поместить СобеседованияУчастника
            из 
                Собеседования.Участники как СобеседованияУчастника
            
            где
            СобеседованияУчастника.Элемент == %Участник
            ;
            выбрать
                Собеседования.Ссылка,
                Собеседования.Кандидат.Представление.ЗаменитьNull("") как Кандидат,
                Собеседования.Формат,
                Собеседования.ДатаПроведения
            из
                Собеседования как Собеседования
            где
                Собеседования.Ссылка в(
                    выбрать Собеседование из СобеседованияУчастника
                )
                И Собеседования.ДатаПроведения >= %{ДатаВремя.Сейчас()}
            упорядочить по 
                ДатаПроведения
    }
    исп РезультатЗапроса = Запрос.Выполнить()
    для Элемент из РезультатЗапроса
        Данные.Добавить(новый ДанныеСобеседования(
            Элемент.Ссылка,
            Элемент.Кандидат,
            Элемент.Формат,
            Элемент.ДатаПроведения))
    ;
    возврат Данные
;

метод ЗаполнитьДанные()
    Данные.Очистить()
    знч ТекущийПользователь = КонтрольДоступа.ПараметрыСеанса().ФизлицоТекущегоПользователя
    если ТекущийПользователь != Неопределено
       Данные.ДобавитьВсе(Данные(ТекущийПользователь)) 
    ; 
;

метод ПриНажатииСтроки(Источник: Список<ИсточникДанныхМассив<ДанныеСобеседования>>, Событие: СобытиеСДанными<СтрокаСписка<ДанныеСобеседования>>)
    знч ФормаСобеседования = новый ФормаСобеседования()
    ФормаСобеседования.Прочитать(Событие.Данные.ДанныеСтроки.Ссылка)
    ФормаСобеседования.ОткрытьВМодальномОкне()
;