импорт ОбменДанными
импорт СтандартноеПриложение

@Обработчик
@ВыполнятьПриЗагрузкеДанных
метод ПередЗаписью(Замещать: Булево, ПараметрыЗаписи: СтатусыОффера.ПараметрыЗаписи)
    знч Оффер: Офферы.Ссылка? = Фильтр.Оффер.Значение
    если Оффер != Неопределено
        ПараметрыЗаписи.АктуальнаяЗапись = СтатусыОффера.АктуальнаяЗапись(Оффер)
    ;
;

@Обработчик
@ВыполнятьПриЗагрузкеДанных
метод ПослеЗаписи(Замещать: Булево, ПараметрыЗаписи: СтатусыОффера.ПараметрыЗаписи)
    исп КонтекстДоступа.Привилегированный()
    РегистрацияИзменений(ПараметрыЗаписи)
    ОбработкаИзмененияСтатуса(ПараметрыЗаписи)
;

метод РегистрацияИзменений(ПараметрыЗаписи: СтатусыОффера.ПараметрыЗаписи)
    знч СервисРегистрацииИзменений = ПодборПерсоналаРегистрацияИзменений.ПолучитьСервис()
    для Запись из этот
        если СервисРегистрацииИзменений.ОфферыТребуетсяРегистрация(Запись.Статус, ПараметрыЗаписи.РежимЗагрузкиДанных)
            ОбменДанными.ЗарегистрироватьОтправляемыйДокумент(Запись.Оффер, Истина)
            для Разделитель из СервисРегистрацииИзменений.ОфферыРазделителиДанных(Запись.Статус)
                РегистрацияИзменений.ЗарегистрироватьИзменение(Запись.Оффер, Разделитель)
            ;    
        ;        
    ;
;

метод ОбработкаИзмененияСтатуса(ПараметрыЗаписи: СтатусыОффера.ПараметрыЗаписи)
    если Фильтр.Оффер.Значение == Неопределено
        возврат
    ;
    знч ПервичнаяЗапись = (ПараметрыЗаписи.АктуальнаяЗапись == Неопределено)
    пер АктуальныйСтатус: СтатусОффера? = ПараметрыЗаписи.АктуальнаяЗапись?.Статус
    для Запись из этот
        если ПервичнаяЗапись
            ПриИзмененииСтатусаОффера(Запись)
        иначе 
            если Запись.Период > ПараметрыЗаписи.АктуальнаяЗапись!.Период и Запись.Статус != АктуальныйСтатус!
                ПриИзмененииСтатусаОффера(Запись)
                АктуальныйСтатус = Запись.Статус
            ;
        ;
    ;
;

метод ПриИзмененииСтатусаОффера(Запись: СтатусыОффера.Запись)
    знч СервисыСобытийОффера = СобытияОффера.ПолучитьСервисы()
    для Сервис из СервисыСобытийОффера
        попытка 
            Сервис.ПриИзмененииСтатусаОффера(Запись.Оффер, Запись.Статус)            
        поймать Ошибка: Исключение
            Журналирование.ЗарегистрироватьОшибку(Ошибка, ВажностьСобытияЖурналаСобытий.Высокая)
        ;
    ;
;