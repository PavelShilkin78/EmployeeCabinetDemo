импорт КонтрольДоступа


//КонтрольДоступа
@Обработчик
метод ВычислитьРазрешенияДоступа(): ЧитаемаяКоллекция<РазрешениеДоступа>
    возврат КонтрольДоступа.РазрешенияДоступаТаблицыРегистраСведений()
;

@Обработчик
метод ВычислитьРазрешенияДоступаДляОбъектов(Записи: ЧитаемыйМассив<СтатусыКандидатов.ДанныеРасчетаРазрешений>): ЧитаемоеСоответствие<СтатусыКандидатов.ДанныеРасчетаРазрешений, ЧитаемаяКоллекция<РазрешениеДоступа>>
    возврат ПодборПерсоналаКонтрольДоступаПоставщик.ПолучитьСервис().ВычилитьРазрешенияДляЧтенияСтатусовКандидатов(Записи)
;
//КонтрольДоступа


@ВПроекте
@НаСервере @ДоступноСКлиента
метод Получить(Кандидат: Кандидаты.Ссылка): СтатусыКандидата
    знч ЗапросСтатус = Запрос{
        ВЫБРАТЬ
            СтатусыКандидатов.Статус
        ИЗ 
            СтатусыКандидатов КАК СтатусыКандидатов
        ГДЕ
            СтатусыКандидатов.Кандидат == %Кандидат
    }
    
    для Элемент из ЗапросСтатус.Выполнить()
        возврат Элемент.Статус
    ;
    
    возврат СтатусыКандидата.Зарегистрирован
;

@ВПроекте
@НаСервере @ДоступноСКлиента
метод Установить(Кандидат: Кандидаты.Ссылка, Статус: СтатусыКандидата)
    знч Запись = новый СтатусыКандидатов.Запись(Кандидат = Кандидат, Статус = Статус)
    Записать(Запись)
;

@ВПроекте
@НаСервере @ДоступноСКлиента
метод Установить(Кандидаты: Массив<Кандидаты.Ссылка>, Статус: СтатусыКандидата)
    для Элемент из Кандидаты
        Установить(Элемент, Статус)
    ;
;

метод УстановитьАктуальныйСтатус(Кандидат: Кандидаты.Ссылка, ТекущийСтатус: СтатусыКандидата?, Рассматривается: Булево)
    пер АктуальныйСтатус: СтатусыКандидата
    
    если Рассматривается
        АктуальныйСтатус = СтатусыКандидата.ПрикрепленКВакансии
    иначе если ТекущийСтатус == Неопределено
        АктуальныйСтатус = СтатусыКандидата.Зарегистрирован
    ;
    
    если АктуальныйСтатус != ТекущийСтатус
        Установить(Кандидат, АктуальныйСтатус)   
    ;
;

@ВПроекте
метод УстановитьАктуальныйСтатус(РассматриваемыйКандидат: РассматриваемыеКандидаты.Ссылка)
    исп ГрупповаяОперация.Начать(Транзакция = Истина, ПривилегированныйРежим = Истина)
    знч Кандидат = РассматриваемыеКандидаты.Кандидат(РассматриваемыйКандидат)
    исп ОбластьВидимостиВременныхТаблиц.Открыть()
    РассматриваемыеКандидаты.СоздатьВТДетальнаяИнформация(Кандидаты = [Кандидат])
    исп Результат = Запрос{
        ВЫБРАТЬ
            РассматриваемыеКандидаты.Кандидат КАК Кандидат,
		    СтатусыКандидатов.Статус.ЗаменитьNull(Неопределено) КАК Статус,
            Максимум(РассматриваемыеКандидаты.Рассматривается.ЗаменитьNull(Ложь)) КАК Рассматривается
        ИЗ
            РассматриваемыеКандидаты.ДетальнаяИнформация КАК РассматриваемыеКандидаты
            ЛЕВОЕ СОЕДИНЕНИЕ
                СтатусыКандидатов КАК СтатусыКандидатов
                    ПО 
                        СтатусыКандидатов.Кандидат == РассматриваемыеКандидаты.Кандидат
        СГРУППИРОВАТЬ ПО 
            РассматриваемыеКандидаты.Кандидат, 
            СтатусыКандидатов.Статус
    }.Выполнить()
    для Элемент из Результат
        если Элемент.Статус != Неопределено
            УстановитьАктуальныйСтатус(Элемент.Кандидат, Элемент.Статус, Элемент.Рассматривается)
        ;
    ;
;

@ВПроекте
метод ОбновитьСтатусыВсехКандидатов()
    исп ГрупповаяОперация.Начать(Транзакция = Истина, ПривилегированныйРежим = Истина)
    исп ОбластьВидимостиВременныхТаблиц.Открыть()
    РассматриваемыеКандидаты.СоздатьВТДетальнаяИнформация()
    исп Результат = Запрос{
        ВЫБРАТЬ
            Кандидаты.Ссылка КАК Кандидат,
		    СтатусыКандидатов.Статус.ЗаменитьNull(Неопределено) КАК Статус,
            Максимум(РассматриваемыеКандидаты.Рассматривается.ЗаменитьNull(Ложь)) КАК Рассматривается
        ИЗ
	        Кандидаты КАК Кандидаты
            ЛЕВОЕ СОЕДИНЕНИЕ
                РассматриваемыеКандидаты.ДетальнаяИнформация КАК РассматриваемыеКандидаты
                    ПО Кандидаты.Ссылка == РассматриваемыеКандидаты.Кандидат
            ЛЕВОЕ СОЕДИНЕНИЕ
                СтатусыКандидатов КАК СтатусыКандидатов
                    ПО 
                        СтатусыКандидатов.Кандидат == Кандидаты.Ссылка
        СГРУППИРОВАТЬ ПО 
            Кандидаты.Ссылка, 
            СтатусыКандидатов.Статус
    }.Выполнить()
    для Элемент из Результат
        УстановитьАктуальныйСтатус(Элемент.Кандидат, Элемент.Статус, Элемент.Рассматривается)
    ;
;