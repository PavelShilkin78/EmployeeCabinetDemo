импорт ОбщиеСтандартные

@ВПодсистеме
@НаСервере @ДоступноСКлиента
статический метод СписокВыбора(): Массив<ЭлементСпискаЗначений<СрочностьВакансии.Ссылка?>>
    знч Список: Массив<ЭлементСпискаЗначений<СрочностьВакансии.Ссылка?>>
    исп Результат = Запрос{
        выбрать 
            Ссылка, 
            Наименование, 
            СрокПодбораВДнях, 
            Приоритет 
        из СрочностьВакансии
        упорядочить по
            Приоритет}.Выполнить()
    
    для Элемент из Результат
        знч ПредставлениеДней = МенеджерСтрок.ТекстПоКоличеству("день", Элемент.СрокПодбораВДнях)
        пер Представление: Строка
        если Элемент.СрокПодбораВДнях != 0
            Представление = "%{Элемент.Наименование} (закрыть за %{Элемент.СрокПодбораВДнях} %ПредставлениеДней)"
        иначе
            Представление = Элемент.Наименование
        ;
        Список.Добавить(новый ЭлементСпискаЗначений<СрочностьВакансии.Ссылка?>(
            Значение = Элемент.Ссылка,
            Представление = Представление,
            ИзображениеАктивного = Элемент.Приоритет.Изображение(),
            ИзображениеНеактивного = Элемент.Приоритет.Изображение()))
    ;
    возврат Список
;