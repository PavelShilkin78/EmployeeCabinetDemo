импорт ОбщиеСтандартные

метод ЗаписатьОбработчик(Команда: ОбычнаяКоманда)
    УстановитьОшибкиЗаполнения()
    если ЕстьОшибкиЗаполнения()
        возврат
    ;
    Записать()
    Закрыть()
;

метод ОтменаОбработчик(Команда: ОбычнаяКоманда)
    СобственнаяМодифицированность = Ложь
    Закрыть()
;

метод УдалитьОбработчик(Команда: ОбычнаяКоманда)
    знч СсылкиНаСущность = МенеджерСущности.СсылкиНаСущность(КлючОбъекта)
    если СсылкиНаСущность.Размер() > 0
        МенеджерСущности.УведомлениеЕстьСсылкиНаОбъекты(КлючОбъекта, СсылкиНаСущность)
    иначе
        Удалить()
    ;
;

метод ВидимостьДополнительныеКоманды(): Булево
    возврат ВидимостьКомандыУдалить()
;

метод ВидимостьКомандыУдалить(): Булево
    возврат КлючОбъекта != Неопределено
;

метод ЕстьОшибкиЗаполнения(): Булево
    знч ПоляДляПроверки = [Компоненты.Наименование.РезультатПроверкиДанных, Компоненты.СрокПодбора.РезультатПроверкиДанных]
    возврат ПоляДляПроверки.Содержит(РезультатПроверкиДанных.Ошибка)
;

метод УстановитьОшибкиЗаполнения()    
    Компоненты.Наименование.РезультатПроверкиДанных = Объект.Наименование.Пусто() ? РезультатПроверкиДанных.Ошибка : РезультатПроверкиДанных.Отсутствует
    Компоненты.СрокПодбора.РезультатПроверкиДанных = Объект.СрокПодбораВДнях < 1 ? РезультатПроверкиДанных.Ошибка : РезультатПроверкиДанных.Отсутствует
;

метод НаименованиеПриИзмененииТекстаРедактирования(Источник: ПолеВвода<Строка>, Событие: СобытиеСДанными<Строка>)
    Источник.РезультатПроверкиДанных = Событие.Данные.Пусто() ? РезультатПроверкиДанных.Ошибка : РезультатПроверкиДанных.Отсутствует
;
