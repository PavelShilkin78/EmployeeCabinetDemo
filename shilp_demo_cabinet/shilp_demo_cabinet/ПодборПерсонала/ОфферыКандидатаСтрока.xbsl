импорт ОбщиеСтандартные
импорт УниверсальныеМеханизмы


метод ЗаголовокОффера(): Строка
    если РежимКандидата
        возврат ПредставлениеВакансии
    иначе
        знч ДатаПредложения = ДанныеСтроки.Данные.Получить("ДатаПредложения") как Дата
        возврат ДанныеСтроки.Данные.Получить("Активен") == Истина ? "Активный оффер" : "Предложение от %{ДатаПредложения}"
    ;
;

метод СтатусЦвет(): Цвет|Авто
    знч Статус = ДанныеСтроки.Данные.Получить("Статус") как СтатусОффера?
    возврат Статус?.Цвет() ?? Авто
;

метод РежимРаботы(): Строка
    знч РежимРаботы = ДанныеСтроки.Данные.Получить("РежимРаботы") как Строка
    возврат РежимРаботы.Пусто() ? "Не указан" : РежимРаботы
;

метод СодержимоеПрисоединенныеФайлы(): Массив<Компонент>
    знч СодержимоеПрисоединенныеФайлы: Массив<Компонент>
    если ПрисоединенныеФайлы != Неопределено
        для Файл из ПрисоединенныеФайлы.Файлы
            СодержимоеПрисоединенныеФайлы.Добавить(
                новый Надпись(
                    Значение = Файл.ПолноеНаименование(),
                    Изображение = Файл.АдресКартинкиРасширения,
                    Ссылка = Файлы.СсылкаНаСкачивание(Файл.Ссылка)))
        ;
    ;
    возврат СодержимоеПрисоединенныеФайлы
;

@НаСервере @ДоступноСКлиента
статический метод СохранитьPdfПредставление(Оффер: Офферы.Ссылка): ДвоичныйОбъект.Ссылка
    Оффер.ЗагрузитьОбъект().ПроверитьЗаполнение()
    знч ОфферPdf = Офферы.СформироватьPdfПредставление(Оффер)
    Офферы.СохранитьPdfПредставление(Оффер, ОфферPdf)
    возврат ОфферPdf.Ссылка
;

метод СформироватьPdfПредставление(): ДвоичныйОбъект.Ссылка?
    знч Оффер = ДанныеСтроки.Ключ как Офферы.Ссылка
    попытка
        возврат СохранитьPdfПредставление(Оффер)
    поймать Исключение: ПроверкаЗаполнения.ИсключениеПроверкиЗаполнения
        ПроверкаЗаполнения.ОбработатьОшибкиЗаполнения(Исключение, {:})
    ;
    возврат Неопределено
;

метод СоздатьПредложениеПовторноВидимость(): Булево
    знч Статус = ДанныеСтроки.Данные.Получить("Статус") как СтатусОффера?
    возврат не ТолькоЧтение и не РежимКандидата и ЕстьПолномочиеИзменениеОффера и (Статус == СтатусОффера.Отклонен или Статус == СтатусОффера.Отозван)
;

метод СоздатьПредложениеПовторноОбработчик(Команда: ОбычнаяКоманда)
    если Диалог.Вопрос(
        Текст = "Создать для кандидата новое предложение?",
        Кнопки = [КнопкаДиалога.Да, КнопкаДиалога.Отмена]) != КнопкаДиалога.Да
        возврат
    ;
    знч Кандидат = ДанныеСтроки.Данные.Получить("Кандидат") как РассматриваемыеКандидаты.Ссылка
    знч Оффер = ДанныеСтроки.Ключ как Офферы.Ссылка
    Офферы.СоздатьОфферПовторно(Оффер)
    ОфферПослеИзменения.Оповестить(Кандидат, Оффер)
;

метод ОтправитьКандидатуВидимость(): Булево
    знч Статус = ДанныеСтроки.Данные.Получить("Статус") как СтатусОффера?
    возврат не ТолькоЧтение и не РежимКандидата и ЕстьПолномочиеИзменениеОффера и Статус == СтатусОффера.Черновик
;

метод ОтправитьКандидатуОбработчик(Команда: ОбычнаяКоманда)
    если СформироватьPdfПредставление() != Неопределено
        если Диалог.Вопрос(
            Текст = "Вы уверены, что хотите отправить оффер без предпросмотра?",
            Кнопки = [КнопкаДиалога.Да, КнопкаДиалога.Отмена]) != КнопкаДиалога.Да
            возврат
        ;
        знч Кандидат = ДанныеСтроки.Данные.Получить("Кандидат") как РассматриваемыеКандидаты.Ссылка
        знч Оффер = ДанныеСтроки.Ключ как Офферы.Ссылка
        Офферы.ОтправитьКандидату(Оффер)
        ОфферПослеИзменения.Оповестить(Кандидат, Оффер)
        СтатусРассматриваемогоКандидатаПослеИзменения.Оповестить(Кандидат)
    ;
;

метод ОткрытьПредпросмотр(Команда: ОбычнаяКоманда)
    знч ПредставлениеОффера = СформироватьPdfПредставление()
    если ПредставлениеОффера == Неопределено
        возврат
    ;
    знч Результат = ФормаПредпросмотрОффера.ОткрытьВМодальномОкне(
        ПредставлениеОффераPdf = ПредставлениеОффера,
        ТолькоЧтение = Истина)
    если Результат == Истина
        знч Кандидат = ДанныеСтроки.Данные.Получить("Кандидат") как РассматриваемыеКандидаты.Ссылка
        знч Оффер = ДанныеСтроки.Ключ как Офферы.Ссылка
        Офферы.ОтправитьКандидату(Оффер)
        ОфферПослеИзменения.Оповестить(Кандидат, Оффер)
        СтатусРассматриваемогоКандидатаПослеИзменения.Оповестить(Кандидат)
    ;
;

метод ОтозватьВидимость(): Булево
    знч Статус = ДанныеСтроки.Данные.Получить("Статус") как СтатусОффера?
    возврат не ТолькоЧтение и (не РежимКандидата и ЕстьПолномочиеИзменениеОффера) и (Статус == СтатусОффера.ОтправленКандидату или Статус == СтатусОффера.Принят)
;

метод ОтозватьОбработчик(Команда: ОбычнаяКоманда)
    если Диалог.Вопрос(
        Текст = "Вы уверены, что хотите отозвать оффер?",
        Кнопки = [КнопкаДиалога.Да, КнопкаДиалога.Отмена]) != КнопкаДиалога.Да
        возврат
    ;
    знч Кандидат = ДанныеСтроки.Данные.Получить("Кандидат") как РассматриваемыеКандидаты.Ссылка
    знч Оффер = ДанныеСтроки.Ключ как Офферы.Ссылка
    Офферы.Отозвать(Оффер)
    ОфферПослеИзменения.Оповестить(Кандидат, Оффер)
;

метод ПринятьВидимость(): Булево
    знч Статус = ДанныеСтроки.Данные.Получить("Статус") как СтатусОффера?
    возврат не ТолькоЧтение и (РежимКандидата или ЕстьПолномочиеИзменениеОффера) и Статус == СтатусОффера.ОтправленКандидату
;

метод ПринятьОбработчик(Команда: ОбычнаяКоманда)
    если Диалог.Вопрос(
        Текст = "Вы уверены, что хотите принять оффер?",
        Кнопки = [КнопкаДиалога.Да, КнопкаДиалога.Отмена]) != КнопкаДиалога.Да
        возврат
    ;
    знч Кандидат = ДанныеСтроки.Данные.Получить("Кандидат") как РассматриваемыеКандидаты.Ссылка
    знч Оффер = ДанныеСтроки.Ключ как Офферы.Ссылка
    Офферы.Принять(Оффер)
    ОфферПослеИзменения.Оповестить(Кандидат, Оффер)
    СтатусРассматриваемогоКандидатаПослеИзменения.Оповестить(Кандидат)
;

метод ОтклонитьВидимость(): Булево
    знч Статус = ДанныеСтроки.Данные.Получить("Статус") как СтатусОффера?
    возврат не ТолькоЧтение и (РежимКандидата или ЕстьПолномочиеИзменениеОффера) и Статус == СтатусОффера.ОтправленКандидату
;

метод ОтклонитьОбработчик(Команда: ОбычнаяКоманда)
    знч Результат = ФормаВыбораПричинПрекращенияРассмотрения.Выбрать(ВидыПрекращенияРассмотрения.ОтказКандидата)
    если Результат != Неопределено
        знч Кандидат = ДанныеСтроки.Данные.Получить("Кандидат") как РассматриваемыеКандидаты.Ссылка
        знч Оффер = ДанныеСтроки.Ключ как Офферы.Ссылка
        Офферы.Отклонить(Оффер, Результат.Причина, Результат.Комментарий)
        ОфферПослеИзменения.Оповестить(Кандидат, Оффер)
        СтатусРассматриваемогоКандидатаПослеИзменения.Оповестить(Кандидат)
    ;
;

метод РедактироватьВидимость(): Булево
    знч Статус = ДанныеСтроки.Данные.Получить("Статус") как СтатусОффера?
    возврат не ТолькоЧтение и (РежимКандидата или ЕстьПолномочиеИзменениеОффера) и Статус == СтатусОффера.Черновик
;

метод РедактироватьОбработчик(Команда: ОбычнаяКоманда)
    ОфферыФормаОбъекта.ОткрытьВМодальномОкне(КлючОбъекта = ДанныеСтроки.Ключ как Офферы.Ссылка)
;