импорт ЛичныеКабинетыПользователей
импорт ФизическиеЛица
импорт КонтрольДоступа
импорт ПодборПерсонала
импорт Компания
импорт Компания::Согласования

@ВПодсистеме
@НаСервере @НаКлиенте
структура ДополнительныеДанные
    пер ЭтоРуководитель: Булево
    пер КоличествоКандидатов: Число
    пер КоличествоВакансий: Число
    пер КоличествоОткликов: Число
    пер КоличествоЗаявок: Число
    пер ПояснениеЗаявки: Строка
    пер МаршрутыСогласованияСотрудника: Множество<ПравилаСогласования.Ссылка>
    пер ПолномочиеУправлениеПодборомПерсонала: Булево
    пер ПолномочиеЧтениеВакансии: Булево
    пер ПолномочиеИзменениеСтатусаОтлика: Булево
    пер ПолномочиеЧтениеЗаявкиНаПодбор: Булево
;

метод ЗаявкиВидимость(): Булево
    возврат ДополнительныеДанные.КоличествоЗаявок > 0
;

метод ОткликиВидимость(): Булево
    возврат ДополнительныеДанные.КоличествоОткликов > 0
;

метод ВакансииВидимость(): Булево
    возврат 
        ДополнительныеДанные.ЭтоРуководитель и 
        ДополнительныеДанные.КоличествоВакансий > 0 и 
        ((ЗаявкиВидимость() ? 1 : 0) + (ОткликиВидимость() ? 1 : 0)) < 2
;

метод КандидатыВидимость(): Булево
    возврат 
        ДополнительныеДанные.ЭтоРуководитель и 
        ДополнительныеДанные.КоличествоКандидатов > 0 и 
        ((ЗаявкиВидимость() ? 1 : 0) + (ОткликиВидимость() ? 1 : 0) + (ВакансииВидимость() ? 1 : 0)) < 2
;

@НаСервере @ДоступноСКлиента
статический метод ДополнительныеДанные(ФизическоеЛицо: ФизическиеЛица.Ссылка, ЭтоРуководитель: Булево): ДополнительныеДанные

    знч ДополнительныеДанные = новый ДополнительныеДанные()
    ДополнительныеДанные.ЭтоРуководитель = ЭтоРуководитель
    ДополнительныеДанные.ПолномочиеУправлениеПодборомПерсонала = КонтрольДоступа.ЕстьПравоНаПолномочие(ПодборПерсонала.ПолномочиеУправлениеПодборомПерсонала())
    ДополнительныеДанные.ПолномочиеЧтениеВакансии = КонтрольДоступа.ЕстьПравоНаПолномочие(ПодборПерсонала.ПолномочиеЧтениеВакансии())
    ДополнительныеДанные.ПолномочиеИзменениеСтатусаОтлика = КонтрольДоступа.ЕстьПравоНаПолномочие(ПодборПерсонала.ПолномочиеИзменениеСтатусаОтлика())
    ДополнительныеДанные.ПолномочиеЧтениеЗаявкиНаПодбор = КонтрольДоступа.ЕстьПравоНаПолномочие(ПодборПерсонала.ПолномочиеЧтениеЗаявкиНаПодбор())
    
    если ДополнительныеДанные.ПолномочиеУправлениеПодборомПерсонала 
            или ДополнительныеДанные.ПолномочиеЧтениеЗаявкиНаПодбор
                        
        знч Запрос = Запрос{
                выбрать
                    Количество(Заявка) как КоличествоЗаявок
                из
                    ЗаявкиНаПодбор как ЗаявкиНаПодбор
                    внутреннее соединение СтатусыЗаявокНаПодбор как СтатусыЗаявокНаПодбор
                        по ЗаявкиНаПодбор.Ссылка == СтатусыЗаявокНаПодбор.Заявка
                            и (%{ДополнительныеДанные.ПолномочиеУправлениеПодборомПерсонала} или ЗаявкиНаПодбор.Рекрутер == %ФизическоеЛицо)
                            и СтатусыЗаявокНаПодбор.Статус 
                                в (СтатусыЗаявкиНаПодбор.Проверка, 
                                    СтатусыЗаявкиНаПодбор.Доработка, 
                                    СтатусыЗаявкиНаПодбор.Согласована)
        }
     
        исп Результат = Запрос.Выполнить()      
        ДополнительныеДанные.КоличествоЗаявок = Результат.ЕдинственныйИлиУмолчание()?.КоличествоЗаявок ?? 0 
        ДополнительныеДанные.ПояснениеЗаявки = "У вас есть заявки требующие внимания."
        
    иначе если ДополнительныеДанные.ЭтоРуководитель
        знч Запрос = Запрос{
            выбрать
                    Количество(Заявка) как КоличествоЗаявок
                из
                    ЗаявкиНаПодбор как ЗаявкиНаПодбор
                    внутреннее соединение СтатусыЗаявокНаПодбор как СтатусыЗаявокНаПодбор
                        по ЗаявкиНаПодбор.Ссылка == СтатусыЗаявокНаПодбор.Заявка
                            и ЗаявкиНаПодбор.Автор == %ФизическоеЛицо
                            и СтатусыЗаявокНаПодбор.Статус == СтатусыЗаявкиНаПодбор.Доработка
        }
        исп Результат = Запрос.Выполнить()      
        ДополнительныеДанные.КоличествоЗаявок = Результат.ЕдинственныйИлиУмолчание()?.КоличествоЗаявок ?? 0
        ДополнительныеДанные.КоличествоЗаявок += ПодборПерсонала.ДокументыНаСогласовании(ФизическоеЛицо).Размер()
        ДополнительныеДанные.ПояснениеЗаявки = "У вас есть заявки требующие внимания."
    иначе
        ДополнительныеДанные.КоличествоЗаявок = ПодборПерсонала.ДокументыНаСогласовании(ФизическоеЛицо).Размер() 
        ДополнительныеДанные.ПояснениеЗаявки = "У вас есть заявки на согласовании."
        ДополнительныеДанные.МаршрутыСогласованияСотрудника.ДобавитьВсе(ПравилаСогласования.ПравилаСогласованияУчастника(ФизическоеЛицо))
    ; 

    пер ДоступныеВакансии: Массив<Вакансии.Ссылка>
    если ДополнительныеДанные.ПолномочиеУправлениеПодборомПерсонала
        ДоступныеВакансии = Вакансии.АктивныеВакансии()
    иначе если ДополнительныеДанные.ПолномочиеЧтениеВакансии
        ДоступныеВакансии = Вакансии.ВакансииРекрутера(ФизическоеЛицо)
    иначе если ДополнительныеДанные.ЭтоРуководитель
        ДоступныеВакансии.ДобавитьВсе(Вакансии.ВакансииИнициатора(ФизическоеЛицо)) 
    ;
    ДополнительныеДанные.КоличествоВакансий = ДоступныеВакансии.Размер()
    
    если ДополнительныеДанные.ПолномочиеИзменениеСтатусаОтлика      
        ДополнительныеДанные.КоличествоОткликов = Отклики.ОткликиНаВакансии(ДоступныеВакансии).Размер()
    ;
    
    ДополнительныеДанные.КоличествоКандидатов = РассматриваемыеКандидаты.РассматриваемыеКандидатыВакансии(
        Вакансии = ДоступныеВакансии, 
        Статус = СтатусыВоронки.СтатусНаправленЗаказчику()).Размер()
        
    возврат ДополнительныеДанные
;

@Обработчик
метод ПослеСоздания()
    если ДополнительныеДанные.МаршрутыСогласованияСотрудника.Размер() > 0 
        Компоненты.Заявки.ПриНажатии = &ЗаявкиПриНажатии
    ;
    Компоненты.Кандидаты.ПриНажатии = &КандидатыПриНажатии
    Компоненты.Вакансии.ПриНажатии = &ВакансииПриНажатии
    Компоненты.Отклики.ПриНажатии = ПодборПерсоналаКомпонентыИнтерфейса.ПолучитьСервис().ВиджетОткликиПриНажатии()
    знч ФизЛицо = КонтрольДоступа.ПараметрыСеанса().ФизлицоТекущегоПользователя
    если ФизЛицо == Неопределено
        возврат
    ;
    ДополнительныеДанные = ДополнительныеДанные(ФизЛицо, Компания.ПараметрыСеанса().ТекущийПользовательРуководитель)
;

метод КандидатыПриНажатии()
    СписокРассматриваемыхКандидатов.Открыть(
        Заголовок = "Кандидаты на согласовании",
        Инициатор = КонтрольДоступа.ПараметрыСеанса().ФизлицоТекущегоПользователя,
        Статусы = [СтатусыВоронки.СтатусНаправленЗаказчику()])  
;

метод ВакансииПриНажатии()
    ВакансииФормаСписка.Открыть() 
;

метод ЗаявкиПриНажатии()
    знч Форма = новый ЗаявкиНаПодборФормаСписка()
    если ДополнительныеДанные.ПолномочиеУправлениеПодборомПерсонала или ДополнительныеДанные.ПолномочиеЧтениеЗаявкиНаПодбор
        Форма.ФильтрСтатус.ДобавитьВсе([СтатусыЗаявкиНаПодбор.Проверка, СтатусыЗаявкиНаПодбор.Согласована])
        если не ДополнительныеДанные.ПолномочиеУправлениеПодборомПерсонала
            Форма.ФильтрРекрутер = ЛичныеКабинетыПользователей.ПараметрыСеанса().ТекущееФизическоеЛицо
        ;
    иначе если ДополнительныеДанные.МаршрутыСогласованияСотрудника.Размер() > 0 
        Форма.ФильтрМаршрутыСогласований = новый Множество<ПравилаСогласования.Ссылка>(ДополнительныеДанные.МаршрутыСогласованияСотрудника)
        Форма.ФильтрСтатус.Добавить(СтатусыЗаявкиНаПодбор.Согласование)
    ;
    Форма.Открыть()
;