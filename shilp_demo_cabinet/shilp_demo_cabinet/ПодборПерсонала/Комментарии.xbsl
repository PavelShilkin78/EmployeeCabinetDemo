импорт ФизическиеЛица
импорт УниверсальныеМеханизмы
импорт СтандартноеПриложение
импорт КонтрольДоступа



// КонтрольДоступа
@Обработчик
метод ВычислитьРазрешенияДоступа(): ЧитаемаяКоллекция<РазрешениеДоступа>
    возврат КонтрольДоступа.РазрешенияДоступаТаблицыСФизическимЛицомСПравомУдаления()
;

@Обработчик
метод ВычислитьРазрешенияДоступаДляОбъектов(Объекты: ЧитаемыйМассив<Комментарии.ДанныеРасчетаРазрешений>): ЧитаемоеСоответствие<Комментарии.ДанныеРасчетаРазрешений, ЧитаемаяКоллекция<РазрешениеДоступа>>
    возврат ПодборПерсоналаКонтрольДоступаПоставщик.ПолучитьСервис().ВычислитьРазрешенияДоступаКомментарии(Объекты)
;

// КонтрольДоступа

@ВПроекте 
@НаСервере @ДоступноСКлиента
метод КомментарииСобеседований(Собеседования: Массив<Собеседования.Ссылка>): Соответствие<Собеседования.Ссылка, Массив<ДанныеКомментария>>
    знч Результат = новый Соответствие<Собеседования.Ссылка, Массив<ДанныеКомментария>>()
    знч Запрос = Запрос{
        ВЫБРАТЬ
            Комментарии.Ссылка КАК Ссылка,
            Комментарии.ВладелецКомментария КАК ВладелецКомментария,
            Комментарии.Автор КАК Автор,
            Комментарии.МеткаВремени КАК МеткаВремени,
            Комментарии.Комментарий КАК Комментарий,
            Комментарии.Автор.Представление.ЗаменитьNull("") КАК ПредставлениеАвтора,
            Файлы.Хранилище.ЗаменитьNull(Неопределено) как Миниатюра
        ИЗ
            Комментарии КАК Комментарии
        ЛЕВОЕ СОЕДИНЕНИЕ ФизическиеЛица КАК ФизическиеЛица
            ПО Комментарии.Автор == ФизическиеЛица.Ссылка
            И Комментарии.ВладелецКомментария В(%Собеседования)
        ЛЕВОЕ СОЕДИНЕНИЕ Файлы КАК Файлы
            ПО ФизическиеЛица.Миниатюра == Файлы.Ссылка
        УПОРЯДОЧИТЬ ПО   
            Комментарии.ВладелецКомментария,
            Комментарии.МеткаВремени возр        
    }
    исп РезультатЗапроса = Запрос.Выполнить()
    для Элемент из РезультатЗапроса
        если не Результат.СодержитКлюч(Элемент.ВладелецКомментария)
            Результат.Вставить(Элемент.ВладелецКомментария, новый Массив<ДанныеКомментария>())
        ;
        знч Миниатюра = Элемент.Миниатюра == Неопределено ? Ресурс{ФотографияПоУмолчанию.svg}.Ссылка : Элемент.Миниатюра
        Результат[Элемент.ВладелецКомментария].Добавить(
            новый ДанныеКомментария(Элемент.Ссылка, Элемент.ВладелецКомментария, Элемент.Автор, 
                Элемент.МеткаВремени, Элемент.Комментарий, Элемент.ПредставлениеАвтора, Миниатюра))
    ;
    возврат Результат
;

@ВПроекте 
@НаСервере @ДоступноСКлиента
метод Добавить(ВладелецКомментария: Собеседования.Ссылка, Автор: ФизическиеЛица.Ссылка, Комментарий: Строка)
    знч КомментарийОбъект = новый Комментарии.Объект(
        ВладелецКомментария = ВладелецКомментария,
        Автор = Автор,
        Комментарий = Комментарий,
        МеткаВремени = Момент.Сейчас()
    )   
    КомментарийОбъект.Записать()
;

@ВПроекте
@НаСервере @ДоступноСКлиента
метод Удалить(Комментарий: Комментарии.Ссылка)
    знч КомментарийОбъект = Комментарий.ЗагрузитьОбъект()
    КомментарийОбъект.Удалить()
;

@ВПроекте
@НаСервере @ДоступноСКлиента
метод УдалитьКомментарии(УдаляемыеКомментарии: Массив<Комментарии.Ссылка>)
    для Элемент из УдаляемыеКомментарии
        Удалить(Элемент)
    ;
;