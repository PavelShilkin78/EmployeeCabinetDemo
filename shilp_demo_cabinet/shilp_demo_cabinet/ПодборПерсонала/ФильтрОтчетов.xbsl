импорт ФизическиеЛица
импорт Отчеты

метод ВидимостьФильтровПриНажатии(Источник: Кнопка, Событие: СобытиеПриНажатии)
    ВидимостьФильтров = не ВидимостьФильтров
    ОчиститьПараметрыОтчетов()
    УстановитьПараметрыОтчетов()
;

метод ПериодОтбораПриИзменении(Источник: ПолеВвода<ЗакрытыйДиапазон<Дата>?>, Событие: СобытиеПриИзменении<ЗакрытыйДиапазон<Дата>?>)
    УстановитьПараметрыОтчетов() 
;

метод ВакансииПриИзменении(Источник: ПолеВвода<Вакансии.Ссылка?>, Событие: СобытиеПриИзменении<Вакансии.Ссылка?>)
    УстановитьПараметрыОтчетов() 
;

метод РекрутерПриИзменении(Источник: ПолеВвода<ФизическиеЛица.Ссылка?>, Событие: СобытиеПриИзменении<ФизическиеЛица.Ссылка?>)
    УстановитьПараметрыОтчетов() 
;

метод СтатусыВоронкиПриИзменении(Источник: ПолеВвода<СтатусыВоронки.Ссылка?>, Событие: СобытиеПриИзменении<СтатусыВоронки.Ссылка?>)
    УстановитьПараметрыОтчетов() 
;

метод СтатусЗакрытияПриИзменении(Источник: Флажок, Событие: СобытиеПриИзменении<Булево?>)
    УстановитьПараметрыОтчетов() 
;

метод ИзображениеФильтра(): ДвоичныйОбъект.Ссылка
    возврат ВидимостьФильтров ? Ресурс{Фильтр.svg}.Ссылка : Ресурс{ФильтрКонтур.svg}.Ссылка
;

метод УстановитьПараметрыОтчетов()
    знч ОтборОт = (ПериодОтбора == Неопределено) ? Неопределено : ПериодОтбора.От
    знч ОтборДо = (ПериодОтбора == Неопределено) ? Неопределено : ПериодОтбора.До
    
    для КомпонентОтчет из ПанельОтчетов
        если ФильтрПоЭтапу
            Отчеты.УстановитьПараметр(КомпонентОтчет, "Этап", Этап)
        ;
        если ФильтрПоВакансии
            Отчеты.УстановитьПараметр(КомпонентОтчет, "Вакансия", Вакансия)
        ;
        если ФильтрПоСтатусуЗакрытия
            Отчеты.УстановитьПараметр(КомпонентОтчет, "СтатусЗакрытия", СтатусЗакрытия)
        ;
        Отчеты.УстановитьПараметр(КомпонентОтчет, "Рекрутер", Рекрутер)
        Отчеты.УстановитьПараметр(КомпонентОтчет, "ПериодОт", ОтборОт)
        Отчеты.УстановитьПараметр(КомпонентОтчет, "ПериодДо", ОтборДо)
    ;
;

метод ОчиститьПараметрыОтчетов()
    Рекрутер = Неопределено
    Этап = Неопределено
    Вакансия = Неопределено
    ПериодОтбора = Неопределено
    СтатусЗакрытия = Ложь
;

метод СписокРекрутеров(): Массив<ЭлементСпискаЗначений<ФизическиеЛица.Ссылка?>>
    знч СписокРекрутеров = Вакансии.РекрутерыВсехВакансий()
    знч Результат = новый Массив<ЭлементСпискаЗначений<ФизическиеЛица.Ссылка?>>()
    для Рекрутер из СписокРекрутеров
        Результат.Добавить(
            новый ЭлементСпискаЗначений<ФизическиеЛица.Ссылка?>(
                Значение = Рекрутер,
                Представление = ФизЛица.ПредставлениеИмени(Рекрутер, ФизЛица.ВариантПредставленияИмени.ФамилияИмяОтчество)
            )
        )
    ;
    возврат Результат
;
