Выбрать
	Заявки.Номер как Номер,
	Заявки.Подразделение как Подразделение,
    Вакансии.Рекрутер как Рекрутер,
    Вакансии.Ссылка как Вакансия,
    ДанныеЭтапов.РассматриваемыйКандидат как РассматриваемыйКандидат,
	Заявки.КоличествоШтатныхЕдиниц как КоличествоШтатныхЕдиниц,
	ТекущийСтатусВакансии.Статус как Статус,
    Выбор
        Когда ПоследнийСтатус.ЭтапВоронки Есть Не NULL
        Тогда 1
        Иначе 0
    Конец Как ЗакрытоШтатныхЕдиниц,
    ОткрытыеВакансии.Период как ДатаСозданияВакансии,
    Заявки.ПлановаяДатаЗакрытия как ПлановаяДатаЗакрытия,
    Выбор
        Когда ТекущийСтатусВакансии.Статус == СтатусВакансии.Закрыта
        Тогда ТекущийСтатусВакансии.Период
        Иначе Неопределено
    Конец Как ДатаФактическогоЗакрытия,
    Отклики.КолОткликовВакансии как КолОткликовВакансии
Из
    Вакансии как Вакансии
    Левое Соединение ДанныеВоронкиПодбора как ДанныеЭтапов
    По Вакансии.Ссылка == ДанныеЭтапов.Вакансия
    Внутреннее Соединение ЗаявкиНаПодбор как Заявки
    По Вакансии.ЗаявкаНаПодбор == Заявки.Ссылка
    Внутреннее Соединение СтатусыВакансии.СрезПоследних() как ТекущийСтатусВакансии
    По Вакансии.Ссылка == ТекущийСтатусВакансии.Вакансия
    Внутреннее Соединение СтатусыВакансии как ОткрытыеВакансии
    По Вакансии.Ссылка == ОткрытыеВакансии.Вакансия
    и ОткрытыеВакансии.Статус == СтатусВакансии.Открыта
    Левое Соединение (
        Выбрать Первые 1
		СтатусыВоронки.Ссылка как ЭтапВоронки 
	Из
		СтатусыВоронки как СтатусыВоронки
	Упорядочить По 
		СтатусыВоронки.Порядок Убыв
    ) как ПоследнийСтатус
    по ДанныеЭтапов.ЭтапВоронки == ПоследнийСтатус.ЭтапВоронки 
    Левое Соединение (
        Выбрать 
            Отклики.Вакансия как Вакансия,
            Количество(Отклики.Наименование) как КолОткликовВакансии
        Из 
            Отклики как Отклики
            Внутреннее Соединение СтатусыОткликов как СтатусыОткликов
            По СтатусыОткликов.Отклик == Отклики.Ссылка
            и СтатусыОткликов.Статус == СтатусОтклика.ПринятКРассмотрению
        Сгруппировать По
            Отклики.Вакансия
    ) как Отклики
    По Вакансии.Ссылка == Отклики.Вакансия
Где 
	(&Рекрутер == Неопределено Или ДанныеЭтапов.Рекрутер == &Рекрутер)
И	(&Вакансия == Неопределено Или ДанныеЭтапов.Вакансия == &Вакансия)
И	(&Этап == Неопределено Или ДанныеЭтапов.ЭтапВоронки == &Этап)
И   (&ПериодОт == Неопределено Или ОткрытыеВакансии.Период.ВДатаВремя(ЧасовойПояс{UTC+3}).Дата >= &ПериодОт)
И   (&ПериодДо == Неопределено Или ОткрытыеВакансии.Период.ВДатаВремя(ЧасовойПояс{UTC+3}).Дата <= &ПериодДо)