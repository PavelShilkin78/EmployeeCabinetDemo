импорт КонтрольДоступа
импорт ВебApi
импорт ФизическиеЛица

// КонтрольДоступа
@Обработчик
метод ВычислитьРазрешенияДоступа(): ЧитаемаяКоллекция<РазрешениеДоступа>
    возврат КонтрольДоступа.РазрешенияДоступаТаблицыРегистраСведений()
;

@Обработчик
метод ВычислитьРазрешенияДоступаДляОбъектов(Записи: ЧитаемыйМассив<ОценкиУчастниковСобеседования.ДанныеРасчетаРазрешений>): ЧитаемоеСоответствие<ОценкиУчастниковСобеседования.ДанныеРасчетаРазрешений, ЧитаемаяКоллекция<РазрешениеДоступа>>
    возврат ПодборПерсоналаКонтрольДоступаПоставщик.ПолучитьСервис().ВычилитьРазрешенияДляОценокУчастниковСобеседований(Записи)
;
// КонтрольДоступа

// JSON
@ВПодсистеме
структура ОценкаУчастниковСобеседования_2_0
    @JsonСвойство(Имя = "member")
    обз знч Участник: Строка?
    @JsonСвойство(Имя = "grade")
    обз знч Оценка: Число   
    @JsonСвойство(Имя = "comment")
    обз знч Комментарий: Строка 
;

@ВПодсистеме
метод ОценкиВJson_V2_0(Собеседование: Собеседования.Ссылка?, Сервисы: ВебApiСервер.СервисыУпаковкиJson): Массив<ОценкаУчастниковСобеседования_2_0>
    знч Запрос = Запрос{
        выбрать
            ОценкиУчастниковСобеседования.Участник как Участник,
            ОценкиУчастниковСобеседования.Оценка как Оценка,
            ОценкиУчастниковСобеседования.Комментарий как Комментарий
        из
            ОценкиУчастниковСобеседования как ОценкиУчастниковСобеседования
        где
            ОценкиУчастниковСобеседования.Собеседование == Собеседование
    }
    знч Статусы = новый Массив<ОценкаУчастниковСобеседования_2_0>()
    для Элемент из Запрос.Выполнить()
        Статусы.Добавить(новый ОценкаУчастниковСобеседования_2_0(
           Участник = Сервисы.ПолучитьИдентификатор(Элемент.Участник),
           Оценка = Элемент.Оценка,
           Комментарий = Элемент.Комментарий
        ))
    ;
    возврат Статусы
;

@ВПодсистеме
метод ЗаписатьСтатусыJson_V2_0(Собеседование: Собеседования.Ссылка, Данные: Массив<ОценкаУчастниковСобеседования_2_0>, Сервисы: ВебApiСервер.СервисыЗагрузкиJson)
    знч НаборЗаписей = новый ОценкиУчастниковСобеседования.НаборЗаписей()
    НаборЗаписей.Фильтр.Собеседование.Установить(Собеседование)
    для Элемент из Данные
        НаборЗаписей.Добавить(
            новый ОценкиУчастниковСобеседования.Запись(
                Собеседование = Собеседование,
                Участник = Сервисы.ПолучитьСсылку(ФизическиеЛица.ВнешняяСсылка(Элемент.Участник)) как ФизическиеЛица.Ссылка,
                Оценка = Элемент.Оценка,
                Комментарий = Элемент.Комментарий))
    ;
    НаборЗаписей.Записать(Истина)
;
// JSON