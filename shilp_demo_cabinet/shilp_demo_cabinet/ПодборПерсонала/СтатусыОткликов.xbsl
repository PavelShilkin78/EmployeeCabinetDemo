
// КонтрольДоступа
@Обработчик
метод ВычислитьРазрешенияДоступа(): ЧитаемаяКоллекция<РазрешениеДоступа>
    возврат ПодборПерсоналаКонтрольДоступаПоставщик.ПолучитьСервис().ВычислитьРазрешенияДоступаСтатусыОткликов()
;
// КонтрольДоступа

@ВПроекте
@НаСервере @ДоступноСКлиента
метод Получить(Отклик: Отклики.Ссылка): СтатусОтклика
    
    знч Запрос = Запрос{
        ВЫБРАТЬ
            СтатусыОткликов.Статус КАК Статус
        ИЗ 
            СтатусыОткликов КАК СтатусыОткликов
        ГДЕ 
            СтатусыОткликов.Отклик == %Отклик
    }
     исп РезультатЗапроса = Запрос.Выполнить()
    возврат РезультатЗапроса.ЕдинственныйИлиУмолчание()?.Статус ?? СтатусОтклика.Новый
;

@ВПроекте
@НаСервере @ДоступноСКлиента
метод Получить(Отклики: ЧитаемаяКоллекция<Отклики.Ссылка>): Соответствие<Отклики.Ссылка,СтатусОтклика>
    знч Результат = новый Соответствие<Отклики.Ссылка,СтатусОтклика>()
    знч Запрос = Запрос{
        ВЫБРАТЬ
            СтатусыОткликов.Отклик КАК Отклик,
            СтатусыОткликов.Статус КАК Статус
        ИЗ 
            СтатусыОткликов КАК СтатусыОткликов
        ГДЕ 
            СтатусыОткликов.Отклик В(%Отклики)
    }
    исп РезультатЗапроса = Запрос.Выполнить()
    для Элемент из РезультатЗапроса
        Результат.Вставить(Элемент.Отклик, Элемент.Статус)     
    ;
    возврат Результат
;

@ВПроекте
@НаСервере @ДоступноСКлиента
метод Установить(Отклик: Отклики.Ссылка, Статус: СтатусОтклика)
    знч Запись = новый СтатусыОткликов.Запись(Отклик = Отклик, Статус = Статус)
    Записать(Запись)
;

@ВПроекте
@НаСервере @ДоступноСКлиента
метод Установить(Отклики: Массив<Объект?>, Статус: СтатусОтклика)
    для Ссылка из Отклики
        Установить(Ссылка как Отклики.Ссылка, Статус)
    ;
;
