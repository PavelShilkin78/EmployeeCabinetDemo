импорт ИнтерфейсныеКомпонентыСтандартные

@ВПроекте
@Реализация
метод Инициализировать()
   СтатусСогласия = СтатусПоКандидату(Кандидат)
;

@НаСервере @ДоступноСКлиента
статический метод СтатусПоКандидату(Кандидат: Кандидаты.Ссылка?): СтатусыСогласийНаОбработкуПдн?
    возврат СогласияНаОбработкуПдн.СтатусПоСубъекту(Кандидат)
;

метод ИконкаСтатусаВидЦвета(): БиблиотекаЦветов.ВидыЦвета
    выбор СтатусСогласия
    когда Сформировано
        возврат БиблиотекаЦветов.ВидыЦвета.Оранжевый
    когда Подписано
        возврат БиблиотекаЦветов.ВидыЦвета.Зеленый
    когда Отклонено
        возврат БиблиотекаЦветов.ВидыЦвета.Красный
    иначе
        возврат БиблиотекаЦветов.ВидыЦвета.Серый
    ;
;

метод ИконкаСтатусаИзображение(): ДвоичныйОбъект.Ссылка
    выбор СтатусСогласия
    когда Сформировано
        возврат Ресурс{ЧемоданБезСтатуса.svg}.Ссылка
    когда Подписано
        возврат Ресурс{ЧемоданПодтверждение.svg}.Ссылка
    когда Отклонено
        возврат Ресурс{ЧемоданОтказ.svg}.Ссылка
    иначе
        возврат Ресурс{ЧемоданБезСтатуса.svg}.Ссылка
    ;
;

метод ПоясненияСтатусаЗначение(): Строка

    выбор СтатусСогласия
    когда Сформировано
        возврат "Согласие на обработку персональных данных находится на рассмотрении"
    когда Подписано
        возврат "Согласие на обработку персональных данных получено"
    когда Отклонено
        возврат "Согласие на обработку персональных данных отклонено"
    иначе
        возврат "Согласие на обработку персональных данных еще не сформировано"        
    ;

;

метод СформироватьСогласие(Источник: Надпись, Событие: СобытиеПриНажатии)
    СформироватьСогласие(Кандидат)
    СтандартныеФормы.ПоказатьУведомление("Создано", "Согласие на обработку персональных данных создано", ОценкаИнформации.Положительная)
    Инициализировать()
;

@НаСервере @ДоступноСКлиента
статический метод СформироватьСогласие(Кандидат: Кандидаты.Ссылка?)
    СогласияНаОбработкуПдн.СформироватьСогласиеЕслиНеСоздано(Субъект = Кандидат, ИдПользователя = Неопределено, ПодписатьСразу = Ложь)
;