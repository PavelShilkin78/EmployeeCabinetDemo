импорт КонтрольДоступа
импорт УниверсальныеМеханизмы::Взаимодействия

@ВПроекте
@Реализация
метод Инициализировать()
    ПрочитатьДанные()
;


@ВПодсистеме
@НаСервере @НаКлиенте
структура ДополнительныеДанные
    знч ЕстьПолномочияИзмененияВзаимодействия: Булево
    знч ДанныеСписка: Массив<ДанныеСтроки>
;

@ВПодсистеме
@НаСервере @НаКлиенте
структура ДанныеСтроки
    обз знч Ссылка: Взаимодействие.Ссылка
    обз знч ТипВзаимодействия: ТипВзаимодействия
    обз знч МоментВзаимодействия: Момент
    обз знч Заголовок: Строка
    обз знч Тело: Строка
    обз знч Участник: УчастникВзаимодействия.Ссылка?
    обз знч Аватар: ДвоичныйОбъект.Ссылка?
    пер ЛоготипСервиса: ДвоичныйОбъект.Ссылка?
;

метод ПрочитатьДанные()
    ДополнительныеДанные = ДополнительныеДанные(ВладелецВзаимодействия)
;

@НаСервере @ДоступноСКлиента
статический метод ДополнительныеДанные(ВладелецВзаимодействия: ВладелецВзаимодействия.Ссылка?): ДополнительныеДанные
    знч Данные = новый ДополнительныеДанные(
        ЕстьПолномочияИзмененияВзаимодействия = КонтрольДоступа.ЕстьПравоНаПолномочие(ПодборПерсонала.ПолномочиеИзменениеРассматриваемогоКандидата()),
        ДанныеСписка = ДанныеСписка(ВладелецВзаимодействия))
   возврат Данные       
;

метод КомандыДобавления(): ГруппаКомандногоИнтерфейса<Команда>
    знч КомандыДобавления: Массив<Команда|ФрагментКомандногоИнтерфейса<Команда>|ГруппаКомандногоИнтерфейса<Команда>>
    для Сервис из  ВзаимодействиеПоставщик.ПолучитьСервисы()
        для СоответствиеКоманды из Сервис.КомандыДобавленияВзаимодействия(ВладелецВзаимодействия)
            пер КомандаСозданияВнешняя = СоответствиеКоманды.Значение как ОбычнаяКоманда
            КомандыДобавления.Добавить(
                новый ОбычнаяКоманда(
                    Представление = КомандаСозданияВнешняя.Представление,
                    Изображение = КомандаСозданияВнешняя.Изображение,
                    Важность = КомандаСозданияВнешняя.Важность,
                    ОпасностьДействия = КомандаСозданияВнешняя.ОпасностьДействия,
                    Обработчик = метод(Команда)-> 
                            КомандаСозданияВнешняя.Выполнить()
                            ПрочитатьДанные()
                        ;))
        ;
    ;
    возврат новый ГруппаКомандногоИнтерфейса<Команда>(
        Элементы = КомандыДобавления, 
        Представление = "Написать кандидату", 
        Изображение = Ресурс{Отправить.svg}.Ссылка,
        Видимость = ДоступноДобавлениеИзменениВзаимодействий(),
        Важность = ВажностьКоманды.Высокая)
;

метод ОбновитьПриНажатии(Источник: Кнопка, Событие: СобытиеПриНажатии)
    ПрочитатьДанные()
;

@НаСервере @ДоступноСКлиента
статический метод ДанныеСписка(ВладелецВзаимодействия: ВладелецВзаимодействия.Ссылка?): Массив<ДанныеСтроки>
    знч Данные: Массив<ДанныеСтроки>
    если ВладелецВзаимодействия == Неопределено
        возврат Данные
    ;
    
    исп Результат = Запрос{
        Выбрать 
            Взаимодействие.Ссылка как Ссылка,
            Взаимодействие.ТипВзаимодействия как ТипВзаимодействия,
            Взаимодействие.МоментВзаимодействия как МоментВзаимодействия,
            Взаимодействие.Заголовок как Заголовок,
            Взаимодействие.Тело как Тело,
            Взаимодействие.Участник как Участник
        Из 
            Взаимодействие Как Взаимодействие
        Где
            Взаимодействие.ВладелецВзаимодействия == %ВладелецВзаимодействия
    }.Выполнить()
    
    для Элемент из Результат
        Данные.Добавить(
            новый ДанныеСтроки(
                Ссылка = Элемент.Ссылка,
                ТипВзаимодействия = Элемент.ТипВзаимодействия,
                МоментВзаимодействия = Элемент.МоментВзаимодействия,
                Заголовок = Элемент.Заголовок,
                Тело = Элемент.Тело,
                Участник = Элемент.Участник,
                Аватар = Элемент.Участник?.ЗагрузитьОбъект().Аватар()))
    ;
    возврат Данные
;

метод ПриНажатииСтроки(Источник: Список<ИсточникДанныхМассив<ДанныеСтроки>>, Событие: СобытиеСДанными<СтрокаСписка<ДанныеСтроки>>)
    если ДополнительныеДанные.ЕстьПолномочияИзмененияВзаимодействия
        знч КомандыОткрытия: Соответствие<Тип, (Взаимодействие.Ссылка)->Булево>
        для Сервис из  ВзаимодействиеПоставщик.ПолучитьСервисы()
            КомандыОткрытия.ВставитьВсе(Сервис.КомандыОткрытияВзаимодействия())
        ;
        
        знч Ссылка = Событие.Данные.ДанныеСтроки.Ссылка
        знч Команда = КомандыОткрытия.ПолучитьИлиУмолчание(Ссылка.ПолучитьТип())
        если Команда != Неопределено
            Команда(Ссылка)
            ПрочитатьДанные()
        ;
    ;
;

метод ДоступноДобавлениеИзменениВзаимодействий(): Булево
    возврат ДополнительныеДанные.ЕстьПолномочияИзмененияВзаимодействия и не ТолькоЧтение
;