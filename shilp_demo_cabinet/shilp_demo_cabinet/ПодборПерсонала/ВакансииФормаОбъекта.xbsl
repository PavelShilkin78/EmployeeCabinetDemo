импорт ИнтерфейсныеКомпонентыСтандартные
импорт ФизическиеЛица

@ВПроекте
@Реализация
метод Заголовок(): Строка
    возврат ЭтоНовый() ? "Новая вакансия" : Объект.Наименование
;

@ВПроекте
@Реализация
метод КомандыСоздания(): Команда|ГруппаКомандногоИнтерфейса<Команда>|Неопределено
    возврат Неопределено
;

@ВПроекте
@Реализация
метод ДополнительныеКоманды(): ФрагментКомандногоИнтерфейса<Команда>?
    возврат Неопределено
;

@ВПроекте
@Реализация
метод ОсновнаяКоманда(): Команда?
    возврат ТолькоЧтение ? Неопределено : Записать
;

@ВПроекте
@Реализация
метод ОбычныеКоманды(): ФрагментКомандногоИнтерфейса<Команда>?
    возврат Неопределено
;

@Обработчик
метод ПослеЧтения()
    Компоненты.КлючевыеНавыки.УстановитьЗначения(Объект.КлючевыеНавыки)
    ИнтерфейсныеКомпоненты.ИнициализироватьПодчиненныеКомпоненты(Компоненты)
    ДоступныеДействияРедактирования = ЭтоНовый() ? Неопределено : ДоступныеДействияРедактирования(КлючОбъекта)
    если ДоступныеДействияРедактирования.Пусто()
        ТолькоЧтение = Истина 
    ;
    если КлючОбъекта != Неопределено
        знч Статус = СтатусыВакансии.Получить(КлючОбъекта)
        если Статус == СтатусВакансии.Закрыта
            ТолькоЧтение = Истина   
        ;
    ;
;

@Обработчик
метод ПередЗаписьюОбъекта()
    Объект.КлючевыеНавыки.Очистить()
    для Элемент из Компоненты.КлючевыеНавыки.ПолучитьЗначения()
        Объект.КлючевыеНавыки.Добавить(Элемент как КлючевыеНавыки.Ссылка)
    ;
;

@Обработчик
метод ПослеЗаписиОбъекта()
    ВакансияПослеИзменения.Оповестить(КлючОбъекта)
    СобственнаяМодифицированность = Ложь
    СтандартныеФормы.ПоказатьУведомление("Сохранение!", "Изменения успешно сохранены")    
;

@НаСервере @ДоступноСКлиента
статический метод ДоступныеДействияРедактирования(Ссылка: Вакансии.Ссылка?): Множество<Вакансии.ДоступныеДействия>?
    если Ссылка == Неопределено
        возврат Неопределено
    ;    
    возврат Вакансии.ДоступныеДействия(Ссылка, СтатусыВакансии.Получить(Ссылка))
;

метод РекрутерДоступность(): Булево
    если ЭтоНовый()
        возврат Истина
    ;
    если ДоступныеДействияРедактирования == Неопределено
        возврат Истина
    ;
    возврат ДоступныеДействияРедактирования.Содержит(Вакансии.ДоступныеДействия.РедактированиеРекрутера)
;

метод РекрутерыСписокВыбора():  Массив<ЭлементСпискаЗначений<ФизическиеЛица.Ссылка?>>
    возврат ПодборПерсонала.Рекрутеры().Преобразовать(ФизическоеЛицо -> новый ЭлементСпискаЗначений<ФизическиеЛица.Ссылка?>(ФизическоеЛицо))
;
                                                            
метод МобильныйРежим(): Булево
    возврат КлиентскоеУстройство.ВидИнтерфейса == ВидИнтерфейса.Телефон
;

метод ПроцентЗаполненияОбщаяИнформация(): Число
    знч РезультатыПроверкиЗаполнения =[
        не Объект.Наименование.Пусто(),
        Объект.Рекрутер != Неопределено]
    возврат Рубрикатор.ПроцентЗаполнения(РезультатыПроверкиЗаполнения)
;

метод ПроцентЗаполненияОписаниеВакансии(): Число
    знч РезультатыПроверкиЗаполнения =[
        не Объект.ОписаниеВакансии.Пусто()]
    возврат Рубрикатор.ПроцентЗаполнения(РезультатыПроверкиЗаполнения)
;

метод ПолеВыбораЗначенийИзСпискаПриИзменении(Источник: ПолеВыбораЗначенийИзСписка, Событие: СобытиеСДанными<Массив<Представляемое>>)
    СобственнаяМодифицированность = Истина
;