импорт Компания
импорт ИнтерфейсныеКомпонентыСтандартные

@Обработчик
метод ПослеСоздания()
    Прочитать(КлючОбъекта())
    ДанныеОрганизаций = ДанныеОрганизаций()
;

@НаСервере @ДоступноСКлиента
статический метод ДанныеОрганизаций(): Соответствие<Организации.Ссылка, ДанныеОрганизации>
    знч Обработчики: Соответствие<Организации.Ссылка, ДанныеОрганизации>
    знч Запрос = Запрос{
        Выбрать
            Ссылка как Организация,
            НаименованиеСокращенное как Наименование,
            ИНН,
            ОГРН
        Породить ДанныеОрганизации
        Из
            Организации Как Организации
    }
    знч Результат = Запрос.Выполнить()
    для СтрокаРезультата из Результат
        Обработчики.Вставить(СтрокаРезультата.Организация, СтрокаРезультата)
    ;
    возврат Обработчики
;

@НаСервере @ДоступноСКлиента
статический метод КлючОбъекта(): НастройкиПерсональныхДанных.Ссылка?
    возврат НастройкиПерсональныхДанных.Настройка()
;

@Обработчик
метод ПослеЧтения()
    если ЭтоНовый()
        УстановитьЗначениеПоУмолчанию()
    ;    
;

метод СохранитьПриНажатии(Команда: ОбычнаяКоманда)
    если не ПроверитьЗаполнение()
        возврат
    ;
    Записать()
    СтандартныеФормы.ПоказатьУведомление("Успешно", "Настройка сохранена", ОценкаИнформации.Положительная)
;

метод ПроверитьЗаполнение(): Булево
    пер ПроверкаПройдена = Истина
    если не Объект.СобиратьПерсональныеДанные
        возврат ПроверкаПройдена
    ;
    
    если Объект.ЮридическийАдресОператора.Пусто()
        Компоненты.АдресОператора.РезультатПроверкиДанных = РезультатПроверкиДанных.Ошибка
        ПроверкаПройдена = Ложь
    иначе
        Компоненты.АдресОператора.РезультатПроверкиДанных = РезультатПроверкиДанных.Отсутствует
    ;
    
    если Объект.Оператор == Неопределено
        Компоненты.Оператор.РезультатПроверкиДанных = РезультатПроверкиДанных.Ошибка
        ПроверкаПройдена = Ложь
    иначе
        Компоненты.Оператор.РезультатПроверкиДанных = РезультатПроверкиДанных.Отсутствует
    ;
    
    если Объект.АдресПочтыДляОтзывовСогласий.Пусто()
        Компоненты.ПочтаОтзываСогласий.РезультатПроверкиДанных = РезультатПроверкиДанных.Ошибка
        Компоненты.ПочтаОтзываСогласий.Активировать()
        ПроверкаПройдена = Ложь
    иначе
        Компоненты.ПочтаОтзываСогласий.РезультатПроверкиДанных = РезультатПроверкиДанных.Отсутствует
    ;
    
    возврат ПроверкаПройдена
;

метод УстановитьЗначениеПоУмолчанию()
    Объект.СпособФормированияСогласия = СпособыФормированияСогласияНаОбработкуПдн.Вручную
    Объект.СпособУказанияОтветственногоЗаПдн = СпособыУказанияОтветственногоЗаПдн.РекрутерПоВакансии
    Объект.Наименование = "Настройка персональных данных"
;

метод ДобавитьПриНажатии(Команда: ОбычнаяКоманда)
    знч Результат = ФормаДобавленияОбработчикаПерсональныхДанных.ОткрытьВМодальномОкне(
                    ДанныеОрганизаций = ДанныеОрганизаций)
    если Результат == Неопределено
        возврат
    ;
    
    Объект.Обработчики.Добавить(Результат)
;