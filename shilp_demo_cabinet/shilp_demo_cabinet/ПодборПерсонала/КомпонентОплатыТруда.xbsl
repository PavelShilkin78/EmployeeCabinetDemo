импорт ИнтерфейсныеКомпонентыСтандартные
импорт ОбщиеСтандартные
импорт УправлениеПерсоналом

@ВПроекте
@Реализация
метод Инициализировать()
    Компоненты.СуммаОт.Значение = Значение.СуммаОт
    Компоненты.СуммаДо.Значение = Значение.СуммаДо
    Компоненты.СуммаНетто.Значение = Значение.СуммаНетто
    если Значение.Валюта == Неопределено
        Значение.Валюта = Валюты.ВалютаПоУмолчанию()
    ;
    Компоненты.Валюта.Значение = Значение.Валюта
;

метод СуммаОтПриИзменении(Источник: ПолеВвода<Число>, Событие: СобытиеПриИзменении<Число>)
    знч СуммаОт = Событие.НовоеЗначение
    Значение.СуммаОт = МенеджерЧисел.Модуль(СуммаОт)
    ПроверитьЗаполнение()
;

метод СуммаДоПриИзменении(Источник: ПолеВвода<Число>, Событие: СобытиеПриИзменении<Число>)
    знч СуммаДо = Событие.НовоеЗначение
    Значение.СуммаДо = МенеджерЧисел.Модуль(СуммаДо)
    ПроверитьЗаполнение()
;

метод СуммаНеттоПриИзменении(Источник: Флажок, Событие: СобытиеПриИзменении<Булево?>)
    Значение.СуммаНетто = Событие.НовоеЗначение
    ПроверитьЗаполнение()
;

метод ВалютаПриИзменении(Источник: ПолеВвода<Валюты.Ссылка?>, Событие: СобытиеПриИзменении<Валюты.Ссылка?>)
    Значение.Валюта = Событие.НовоеЗначение
    ПроверитьЗаполнение()
;

@ВПроекте
метод ПроверитьЗаполнение()
    Компоненты.СуммаОт.РезультатПроверкиДанных = РезультатПроверкиДанных.Отсутствует
    Компоненты.Валюта.РезультатПроверкиДанных = РезультатПроверкиДанных.Отсутствует
    
    знч Ошибки = Значение.ПроверитьЗаполнение(Обязательное = Ложь, ДиапазонСумм = Истина)
    знч ПредставленияОшибокВводаЧисла: Массив<Строка>
    для Ошибка из Ошибки
        выбор Ошибка
        когда СуммаНеЗаполнена
            ПредставленияОшибокВводаЧисла.Добавить("введите значение")
        когда СуммыНеСогласованы
            ПредставленияОшибокВводаЧисла.Добавить("значение 'От' должно быть меньше значения 'До'")
        когда ВалютаНеЗаполнена
            Компоненты.Валюта.РезультатПроверкиДанных = РезультатПроверкиДанных.Ошибка
        ;
    ;
    
    если не ПредставленияОшибокВводаЧисла.Пусто()
        пер СообщениеОшибкаВводаЧисла = ПредставленияОшибокВводаЧисла.Соединить(",")
        СообщениеОшибкаВводаЧисла = МенеджерСтрок.НачатьСЗаглавной(СообщениеОшибкаВводаЧисла)
        Компоненты.СуммаОт.СообщениеОшибка =  СообщениеОшибкаВводаЧисла
        Компоненты.СуммаОт.РезультатПроверкиДанных = РезультатПроверкиДанных.Ошибка
    ;
;