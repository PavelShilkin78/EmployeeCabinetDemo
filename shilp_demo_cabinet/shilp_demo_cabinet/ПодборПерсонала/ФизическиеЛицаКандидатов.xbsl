импорт ФизическиеЛица
импорт КонтрольДоступа

@Обработчик
метод ВычислитьРазрешенияДоступа(): ЧитаемаяКоллекция<РазрешениеДоступа>
    возврат КонтрольДоступа.РазрешенияДоступаТаблицыРегистраСведений()
;

@Обработчик
метод ВычислитьРазрешенияДоступаДляОбъектов(Записи: ЧитаемыйМассив<ФизическиеЛицаКандидатов.ДанныеРасчетаРазрешений>): ЧитаемоеСоответствие<ФизическиеЛицаКандидатов.ДанныеРасчетаРазрешений, ЧитаемаяКоллекция<РазрешениеДоступа>>
    возврат ПодборПерсоналаКонтрольДоступаПоставщик.ПолучитьСервис().ВычислитьРазрешенияДоступаФизическиеЛицаКандидатов(Записи)
;

@ВПроекте
метод ПолучитьИлиУстановитьЕслиОтсутствует(Кандидат: Кандидаты.Ссылка, ПолучитьФизическоеЛицо: (Кандидаты.Ссылка)->ФизическиеЛица.Ссылка): ФизическиеЛица.Ссылка
    исп Транзакции.Начать()
    Блокировки.Заблокировать(
        новый ФизическиеЛицаКандидатов.Блокировки.КлючЗаписи(Кандидат = Кандидат, РежимБлокировки = РежимБлокировки.Исключительный)
    )
    возврат Получить(Кандидат) ?? Установить(Кандидат, ПолучитьФизическоеЛицо(Кандидат))
;

@ВПроекте
метод Установить(Кандидат: Кандидаты.Ссылка, ФизическоеЛицо: ФизическиеЛица.Ссылка): ФизическиеЛица.Ссылка 
    знч Запись = новый ФизическиеЛицаКандидатов.Запись(
        Кандидат = Кандидат,
        ФизическоеЛицо = ФизическоеЛицо
    )
    Записать(Запись)
    возврат ФизическоеЛицо
;

@ВПроекте
метод УдалитьЗапись(Кандидат: Кандидаты.Ссылка) 
    знч НаборЗаписей = новый ФизическиеЛицаКандидатов.НаборЗаписей()
    НаборЗаписей.Фильтр.Кандидат.Установить(Кандидат)
    НаборЗаписей.Записать()
;

метод Получить(Кандидат: Кандидаты.Ссылка): ФизическиеЛица.Ссылка?
    знч Запрос = Запрос{
        ВЫБРАТЬ
            ФизическиеЛицаКандидатов.ФизическоеЛицо КАК ФизическоеЛицо
        ИЗ
            ФизическиеЛицаКандидатов КАК ФизическиеЛицаКандидатов
        ГДЕ 
            ФизическиеЛицаКандидатов.Кандидат == %Кандидат
    }
    исп РезультатЗапроса = Запрос.Выполнить()
    возврат РезультатЗапроса.ЕдинственныйИлиУмолчание()?.ФизическоеЛицо
;