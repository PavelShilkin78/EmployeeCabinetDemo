импорт КонтрольДоступа

//КонтрольДоступа
@Обработчик
метод ВычислитьРазрешенияДоступа(): ЧитаемаяКоллекция<РазрешениеДоступа>
    возврат КонтрольДоступа.РазрешенияДоступаТаблицыРегистраСведений()
;

@Обработчик
метод ВычислитьРазрешенияДоступаДляОбъектов(Записи: ЧитаемыйМассив<СтатусыЗаявокНаПодбор.ДанныеРасчетаРазрешений>): ЧитаемоеСоответствие<СтатусыЗаявокНаПодбор.ДанныеРасчетаРазрешений, ЧитаемаяКоллекция<РазрешениеДоступа>>
    возврат ПодборПерсоналаКонтрольДоступаПоставщик.ПолучитьСервис().ВычилитьРазрешенияДляЧтенияСтатусовЗаявокНаПодбор(Записи)
;
//КонтрольДоступа

@ВПроекте
@НаСервере @ДоступноСКлиента
метод Получить(Заявка: ЗаявкиНаПодбор.Ссылка?): СтатусыЗаявкиНаПодбор
    
    знч Запрос = Запрос{
        ВЫБРАТЬ
            СтатусыЗаявокНаПодбор.Статус КАК Статус
        ИЗ 
            СтатусыЗаявокНаПодбор КАК СтатусыЗаявокНаПодбор
        ГДЕ 
            СтатусыЗаявокНаПодбор.Заявка == %Заявка
    }
    исп РезультатЗапроса = Запрос.Выполнить()
    возврат РезультатЗапроса.ЕдинственныйИлиУмолчание()?.Статус ?? СтатусыЗаявкиНаПодбор.Черновик
;


@ВПроекте
@НаСервере @ДоступноСКлиента
метод Установить(Заявка: ЗаявкиНаПодбор.Ссылка, Статус: СтатусыЗаявкиНаПодбор, Комментарий: Строка = "")
    исп ГрупповаяОперация.Начать(Транзакция = Истина)
    знч ПредыдущийСтатус = Получить(Заявка)
    знч Запись = новый СтатусыЗаявокНаПодбор.Запись(Заявка = Заявка, Статус = Статус, Комментарий = Комментарий)
    Записать(Запись)
    для Сервис из СобытияВакансии.ПолучитьСервисы()
        Сервис.ПослеИзмененияСтатусаЗаявкиНаПодбор(Заявка, ПредыдущийСтатус, Статус)
    ;
;

@ВПодсистеме
@НаСервере
метод Удалить(Заявка: ЗаявкиНаПодбор.Ссылка)
    знч НаборЗаписей = новый СтатусыЗаявокНаПодбор.НаборЗаписей()
    НаборЗаписей.Фильтр.Заявка.Установить(Заявка)
    НаборЗаписей.Записать()
;

//Область JSON
@ВПодсистеме
@НаСервере
структура ОписаниеСтатусаV3_0
    @JsonСвойство(Имя = "ID")
    обз знч Статус: ПодборПерсонала.СтатусыЗаявкиНаПодборJson_V3_0
    @JsonСвойство(Имя = "comment")
    знч Комментарий: Строка
;
//Конец JSON