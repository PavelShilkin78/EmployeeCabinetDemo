импорт ИнтерфейсныеКомпонентыСтандартные
импорт КонтрольДоступа
импорт ФизическиеЛица
импорт Компания

@ВПроекте
@Реализация
метод Заголовок(): Строка
    возврат "Заявки на подбор"
;

@ВПроекте
@Реализация
метод КомандыСоздания(): Команда|ГруппаКомандногоИнтерфейса<Команда>|Неопределено
    возврат КомандаДобавленияИлиКопированияВидимость({ВидИнтерфейса.Телефон}) ? КомандаДобавления : Неопределено
;

метод КомандаДобавленияИлиКопированияВидимость(ДляВидовИнтерфейса: Множество<ВидИнтерфейса>? = Неопределено): Булево
    если не ДополнительныеОбъект.ПолномочиеИзменениеЗаявкиНаПодбор
        и не Компания.ПараметрыСеанса().ТекущийПользовательРуководитель
        возврат Ложь
    ;
    если ДляВидовИнтерфейса != Неопределено и не ДляВидовИнтерфейса.Содержит(КлиентскоеУстройство.ВидИнтерфейса)
        возврат Ложь
    ;
    возврат Истина
;

@ВПроекте
@Реализация
метод ДополнительныеКоманды(): ФрагментКомандногоИнтерфейса<Команда>?
    возврат Неопределено
;

@ВПроекте
@Реализация
метод ОсновнаяКоманда(): Команда?
    возврат Неопределено
;

@ВПроекте
@Реализация
метод ОбычныеКоманды(): ФрагментКомандногоИнтерфейса<Команда>?
    возврат Неопределено
;

@Обработчик
метод ПослеСоздания()
    ДополнительныеОбъект = ДополнительныеОбъект()
    ИнтерфейсныеКомпоненты.ИнициализироватьПодчиненныеКомпоненты(Компоненты)
;

@ВПодсистеме
@НаСервере @НаКлиенте
структура ДополнительныеОбъект
    пер ПриоритетыВакансии: Массив<СрочностьВакансии.Ссылка>
    пер Рекрутеры: Множество<ФизическиеЛица.Ссылка>
    пер ПолномочиеЧтениеЗаявкиНаПодбор: Булево
    пер ПолномочиеИзменениеЗаявкиНаПодбор: Булево
;

@НаСервере @ДоступноСКлиента
статический метод ДополнительныеОбъект() : ДополнительныеОбъект
    знч ДополнительныеОбъект = новый ДополнительныеОбъект()
    ДополнительныеОбъект.ПолномочиеЧтениеЗаявкиНаПодбор = КонтрольДоступа.ЕстьПравоНаПолномочие(ПодборПерсонала.ПолномочиеЧтениеЗаявкиНаПодбор())
    ДополнительныеОбъект.ПолномочиеИзменениеЗаявкиНаПодбор = КонтрольДоступа.ЕстьПравоНаПолномочие(ПодборПерсонала.ПолномочиеИзменениеЗаявкиНаПодбор())
    ДополнительныеОбъект.ПриоритетыВакансии.ДобавитьВсе(СрочностьВакансии.ДоступныеЗначения())
    ДополнительныеОбъект.Рекрутеры.ДобавитьВсе(КонтрольДоступа.ФизическиеЛицаПоПолномочию(ПодборПерсонала.ПолномочиеЧтениеЗаявкиНаПодбор()))
    возврат ДополнительныеОбъект
;

метод СостояниеЦветФона(ДанныеЭлемента: СтрокаДинамическогоСписка): Цвет | Авто
    знч Состояние = ДанныеЭлемента.Данные["Статус"] как СтатусыЗаявкиНаПодбор
    возврат БиблиотекаЦветов.Цвет(Состояние.ВидЦвета())
;

метод ПредставлениеВыбранныхСтатусов(): Строка
    если ФильтрСтатус.Пусто()
        возврат ПредставлениеСтатуса(Неопределено)
    иначе
        пер Представления: Строка
        для Элемент из ФильтрСтатус
            Представления = "%Представления%{Элемент.Представление()}, "
        ;
        Представления = Представления.ПодстрокаСНачала(Представления.Длина()-2)
        возврат Представления
    ;
;

метод ПредставлениеСтатуса(Статус: СтатусыЗаявкиНаПодбор?): Строка
    если Статус == Неопределено
        возврат "Любой статус"
    иначе
        возврат Статус.Представление()
    ;
;

метод ВыбораРекрутераВидимость(): Булево
    возврат ДополнительныеОбъект.ПолномочиеЧтениеЗаявкиНаПодбор
        или ФильтрРекрутер != Неопределено
;

метод ПриНажатииСтроки(Источник: Список<ДинамическийСписок>, Событие: СобытиеСДанными<СтрокаСписка<СтрокаДинамическогоСписка>>)
    КарточкаЗаявкиНаПодбор.Показать(Событие.Данные.ДанныеСтроки.Ключ как ЗаявкиНаПодбор.Ссылка, этот)
;

метод ДобавитьОбработчик(Команда: ОбычнаяКоманда)
    ЗаявкиНаПодборФормаОбъекта.Показать(Неопределено, этот)
;

метод КопироватьЗаявкуОбработчик(Команда: КомандаСПараметром<Сущность.Ключ>, Параметр: Сущность.Ключ)
    ЗаявкиНаПодборФормаОбъекта.ПоказатьКопию(Параметр как ЗаявкиНаПодбор.Ссылка, этот)
;

статический метод РежимДесктоп(): Булево
    возврат КлиентскоеУстройство.ВидИнтерфейса != ВидИнтерфейса.Телефон   
;