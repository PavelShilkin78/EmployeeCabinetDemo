импорт КонтрольДоступа
импорт УниверсальныеМеханизмы

@Реализация
@ВПроекте
метод Инициализировать()
    Прочитать()
    ОфферПослеИзменения.ПодключитьОбработчик(&ПослеИзмененияОффера)
;

метод ПослеИзмененияОффера(КандидатОффера: РассматриваемыеКандидаты.Ссылка, Оффер: Офферы.Ссылка)
    если КандидатОффера == Кандидат
        Прочитать()
    ;
;

метод Прочитать()
    КоличествоСтрок = 0
    ДополнительныеДанные = ДополнительныеДанные(Кандидат)
    Компоненты.Список.Обновить()
;

@ВПодсистеме
@НаСервере @НаКлиенте
структура ДополнительныеДанные
    знч ЕстьПолномочиеИзменениеОффера: Булево
    знч ПредставлениеВакансии: Строка
    знч АктивныйОфферКандидата: Офферы.Ссылка?
    знч СтатусАктивногоОффера: СтатусОффера?
;

@НаСервере @ДоступноСКлиента
статический метод ДополнительныеДанные(Кандидат: РассматриваемыеКандидаты.Ссылка): ДополнительныеДанные
    исп КонтекстДоступа.Привилегированный()
    исп Результат = Запрос{
        Выбрать
            РассматриваемыеКандидаты.Вакансия.Представление.ЗаменитьNull("") как ПредставлениеВакансии
        Из 
            РассматриваемыеКандидаты как РассматриваемыеКандидаты
        Где 
            РассматриваемыеКандидаты.Ссылка == %Кандидат
    }.Выполнить()
    знч ДанныеАктивногоОффера = Офферы.ДанныеАктивногоОффера(Кандидат)
    возврат новый ДополнительныеДанные(
        ЕстьПолномочиеИзменениеОффера = КонтрольДоступа.ЕстьПравоНаПолномочие(ПодборПерсонала.ПолномочиеИзменениеОффера()),
        ПредставлениеВакансии = Результат.ЕдинственныйИлиУмолчание()?.ПредставлениеВакансии ?? "",
        АктивныйОфферКандидата = ДанныеАктивногоОффера?.Оффер,
        СтатусАктивногоОффера = ДанныеАктивногоОффера?.Статус)
;

@НаСервере @ДоступноСКлиента
статический метод ПрисоединенныеФайлы(Оффер: Офферы.Ссылка): ПрисоединенныеФайлы
    знч ВидыФайлов = [ВидыФайла.Вложение, ВидыФайла.ДополнительноеПредставлениеЭлектронногоДокумента]
    возврат ПрисоединенныеФайлыМенеджер.ПрисоединенныеФайлы(Оффер, ВидыФайлов)
;

метод ПослеСозданияСтроки(Источник: Список<ДинамическийСписок>, Событие: СобытиеСДанными<СтрокаСписка<СтрокаДинамическогоСписка>>)
    КоличествоСтрок += 1
    знч КомпонентСтроки = Событие.Данные как ОфферыКандидатаСтрока
    КомпонентСтроки.ТолькоЧтение = ТолькоЧтение
    КомпонентСтроки.РежимКандидата = РежимКандидата
    КомпонентСтроки.ЕстьПолномочиеИзменениеОффера = ДополнительныеДанные.ЕстьПолномочиеИзменениеОффера
    КомпонентСтроки.ПредставлениеВакансии = ДополнительныеДанные.ПредставлениеВакансии
    КомпонентСтроки.ПрисоединенныеФайлы = ПрисоединенныеФайлы(Событие.Данные.ДанныеСтроки.Ключ как Офферы.Ссылка)
    если ДополнительныеДанные.СтатусАктивногоОффера == Неопределено
        КомпонентСтроки.ДоступноСозданиеЗаново = Истина
    иначе
        КомпонентСтроки.ДоступноСозданиеЗаново = {СтатусОффера.Отозван, СтатусОффера.Отклонен}.Содержит(ДополнительныеДанные.СтатусАктивногоОффера)
    ;
;

метод Заголовок(): Строка
    возврат ДополнительныеДанные.АктивныйОфферКандидата == Неопределено ? "Офферы кандидата" : ""
;

метод СделатьПредложениеВидимость(): Булево
    возврат не ТолькоЧтение и ДополнительныеДанные.ЕстьПолномочиеИзменениеОффера 
        и ДополнительныеДанные.АктивныйОфферКандидата == Неопределено
;

метод СделатьПредложениеОбработчик(Команда: ОбычнаяКоманда)
    ОфферыФормаОбъекта.ОткрытьВМодальномОкне(Кандидат = Кандидат)
;