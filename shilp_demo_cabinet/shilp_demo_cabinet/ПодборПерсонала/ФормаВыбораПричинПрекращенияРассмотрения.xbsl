@ВПроекте
структура РезультатВыбора
    обз знч Вид: ВидыПрекращенияРассмотрения
    обз знч Причина: ПричиныОтказа.Ссылка|ПричиныОтклонения.Ссылка?
    обз знч Комментарий: Строка
;


@ВПроекте
статический метод Выбрать(Вид: ВидыПрекращенияРассмотрения): РезультатВыбора?
    знч Форма = новый ФормаВыбораПричинПрекращенияРассмотрения()
    Форма.Вид = Вид
    возврат Форма.ОткрытьВМодальномОкне(ОжидатьЗакрытия = Истина)
;

метод ЗаголовокПоля(): Строка
    выбор Вид
    когда ВидыПрекращенияРассмотрения.ОтклонениеКандидата
        возврат "Причина отклонения кандидата" 
    когда ВидыПрекращенияРассмотрения.ОтказКандидата
        возврат "Причина отказа"
    иначе
        возврат "Причина"
    ;
;

метод ДоступностьКнопкиВыбрать(): Булево
    возврат Причина != Неопределено и Компоненты.ПолеВводаКомментария.РезультатПроверкиДанных == РезультатПроверкиДанных.Отсутствует
;

метод ВыбратьПриНажатии(Источник: Кнопка, Событие: СобытиеКомпонента)
    если не ПроверитьКомментарий()
        возврат
    ;
    Закрыть(новый РезультатВыбора(Вид, Причина, Комментарий))
;

метод ДопустимыеТипы(): Множество<Тип>
    выбор Вид
    когда ВидыПрекращенияРассмотрения.ОтклонениеКандидата
        возврат {Тип<ПричиныОтклонения.Ссылка>}
    когда ВидыПрекращенияРассмотрения.ОтказКандидата
        возврат {Тип<ПричиныОтказа.Ссылка>}
    иначе
        возврат {Неопределено.ПолучитьТип()}
    ; 
;

метод ПроверитьКомментарий(): Булево
    если Комментарий.Длина() > 200
        Компоненты.ПолеВводаКомментария.РезультатПроверкиДанных = РезультатПроверкиДанных.Ошибка
        Компоненты.ПолеВводаКомментария.СообщениеОшибка = "Максимальная длинна комментария - 200 символов"
        возврат Ложь
    иначе
        Компоненты.ПолеВводаКомментария.РезультатПроверкиДанных = РезультатПроверкиДанных.Отсутствует
        Компоненты.ПолеВводаКомментария.СообщениеОшибка = ""
        возврат Истина
    ;
;

метод ПолеВводаКомментарияПриИзменении(Источник: ПолеВвода<Строка>, Событие: СобытиеПриИзменении<неизвестно>)
    ПроверитьКомментарий()
;