импорт ИнтерфейсныеКомпонентыСтандартные

конст БАЗОВЫЙ_РАЗМЕР_КЛЮЧЕВОГО_НАВЫКА = 50
конст РАЗМЕР_СИМВОЛА = 7

@ВПроекте
@Реализация
метод Инициализировать()
    
    если КлючевыеНавыки.Пусто()
        возврат
    ;    
    
    пер ГруппыКлючевыхНавыков: Массив<Группа>
    пер НомерТекущегоНавыка: Число
    пока НомерТекущегоНавыка < КлючевыеНавыки.Размер()
        
        пер ГруппаКлючевыхНавыков: ФиксированнаяГруппа
        ГруппаКлючевыхНавыков.Ориентация = ОриентацияСодержимого.Горизонтальная
        ГруппаКлючевыхНавыков.РастягиватьПоГоризонтали = Истина
        ГруппаКлючевыхНавыков.ИнтервалМеждуЭлементамиПоГоризонтали = РазмерОтступа.Половинный
        
        пер ТекущийРазмерГруппы: Число
        пока ТекущийРазмерГруппы < ШиринаКомпонента и НомерТекущегоНавыка < КлючевыеНавыки.Размер()      
            знч КлючевойНавык = КлючевыеНавыки[НомерТекущегоНавыка].Представление()
            знч РазмерТекущегоНавыка = БАЗОВЫЙ_РАЗМЕР_КЛЮЧЕВОГО_НАВЫКА + РАЗМЕР_СИМВОЛА * КлючевойНавык.Длина()
            если ((ТекущийРазмерГруппы + РазмерТекущегоНавыка) > ШиринаКомпонента) и ТекущийРазмерГруппы != 0
                прервать
            ;
            
            ТекущийРазмерГруппы = ТекущийРазмерГруппы + РазмерТекущегоНавыка
            пер ЭлементКлючевогоНавыка: КомпонентВыбора
            ЭлементКлючевогоНавыка.Заголовок = КлючевойНавык
            ЭлементКлючевогоНавыка.РастягиватьПоГоризонтали = РастягиватьЭлементыПоГоризонтали
            ГруппаКлючевыхНавыков.Содержимое.Добавить(ЭлементКлючевогоНавыка)
            НомерТекущегоНавыка = НомерТекущегоНавыка + 1    
        ;
        
        ГруппыКлючевыхНавыков.Добавить(ГруппаКлючевыхНавыков)    
    ;
    
    Содержимое.ДобавитьВсе(ГруппыКлючевыхНавыков)    
;
