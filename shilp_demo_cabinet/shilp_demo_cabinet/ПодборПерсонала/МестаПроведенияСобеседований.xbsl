импорт ВебApi
импорт ОбменДанными
импорт ОбщиеСтандартные

//КонтрольДоступа

@Обработчик
метод ВычислитьРазрешенияДоступа(): ЧитаемаяКоллекция<РазрешениеДоступа>
    возврат ПодборПерсоналаКонтрольДоступаПоставщик.ПолучитьСервис().ВычислитьРазрешенияДоступаМестПроведенияСобеседований()
;

//Конец КонтрольДоступа

@ВПроекте
@НаСервере @ДоступноСКлиента
метод СоздатьПоНаименованиюСПроверкойДубликатов(Наименование: Строка): МестаПроведенияСобеседований.Ссылка?
    исп ПривилегированныйРежим: КонтекстДоступа = КонтекстДоступа.Привилегированный() 
    знч Запрос = Запрос{
        ВЫБРАТЬ ПЕРВЫЕ 1
            Ссылка
        ИЗ
            МестаПроведенияСобеседований
        ГДЕ
            Наименование == %Наименование
    }
    исп РезультатЗапроса = Запрос.Выполнить()
    если РезультатЗапроса.ЕдинственныйИлиУмолчание() != Неопределено
        возврат Неопределено
    ;
    
    знч НовыйОбъект = новый МестаПроведенияСобеседований.Объект(
        Наименование = Наименование
    )
    НовыйОбъект.Записать()
    ПривилегированныйРежим.Закрыть()
    возврат НовыйОбъект.Ссылка
;

// JSON

@ВПроекте
метод ТипОбъектаJson(): Строка
    возврат "interviewLocation"
;

@ВПроекте
структура МестоПроведенияСобеседований_2_0
    @JsonСвойство(Имя = "ID")
    обз знч Идентификатор: Строка
    @JsonСвойство(Имя = "name")
    обз знч Наименование: Строка
;

@ВПодсистеме
метод ТипДанныхJson(ВерсияФорматаОбмена: Версия?): Тип
    
    если ВерсияФорматаОбмена == Неопределено или ВерсияФорматаОбмена < Версия{2.0}
        выбросить новый ИсключениеВыполнения("Неверный формат")
    ;
    
    если ВерсияФорматаОбмена >= Версия{2.0}
        возврат Тип<МестоПроведенияСобеседований_2_0>
    ;
;

@ВПодсистеме
метод ЗагрузитьОбъектИзJson(МестоПроведенияСобеседования: МестоПроведенияСобеседований_2_0, Сервисы: ВебApiСервер.СервисыЗагрузкиJson)
    исп Транзакции.Начать()
    знч ЗагружаемыйОбъект = Сервисы.ПрочитатьОбъект(МестоПроведенияСобеседования.Идентификатор,
        &МестаПроведенияСобеседований.Объект.новый(Ууид), Тип<МестаПроведенияСобеседований.Ссылка>) как МестаПроведенияСобеседований.Объект
    ЗагружаемыйОбъект.Наименование = МестоПроведенияСобеседования.Наименование
    
    Сервисы.ЗаписатьОбъект(новый ВебApiСервер.ПараметрыЗаписиВходящихДанных(
        ЗагружаемыйОбъект, МестоПроведенияСобеседования.Идентификатор), Сервисы.ИнструментыМенеджераОбъекта)           
;

@ВПодсистеме
метод ВнешняяСсылка(Идентификатор: Строка): ВебApiСервер.ВнешняяСсылка
    возврат новый ВебApiСервер.ВнешняяСсылка(Идентификатор, (Ууид) -> МестаПроведенияСобеседований.ПолучитьСсылку(Ууид))
;

@ВПодсистеме
метод ГруппаИзмененийВJson(Ссылки: ЧитаемаяКоллекция<ИсходящиеДанные.Ссылка>, Сервисы: ВебApiСервер.СервисыУпаковкиJson): ЧитаемоеСоответствие<Строка, Массив<Объект?>>
    возврат {"interviewLocations": ИсходящиеДанныеВJson(Ссылки, Сервисы)}
;

метод ИсходящиеДанныеВJson(Ссылки: ЧитаемаяКоллекция<ИсходящиеДанные.Ссылка>, Сервисы: ВебApiСервер.СервисыУпаковкиJson): Массив<Объект?>
    знч Данные: Массив<Объект?>
    для КлючИЗначение из ДанныеОбъектов(Ссылки)
        Данные.Добавить(ДанныеОбъектаВJson_V2_0(КлючИЗначение.Значение, Сервисы))
    ;
    возврат Данные
;

метод ДанныеОбъектов(Ссылки: ЧитаемаяКоллекция<ИсходящиеДанные.Ссылка>): ЧитаемоеСоответствие<МестаПроведенияСобеседований.Ссылка, ЧитаемоеСоответствие<Строка, Объект?>>
    знч Данные: Соответствие<МестаПроведенияСобеседований.Ссылка, ЧитаемоеСоответствие<Строка, Объект?>> 
    знч Запрос = Запрос{
        Выбрать
            Ссылка,
            Наименование
        Из
            МестаПроведенияСобеседований
        Где
            Ссылка В (%Ссылки)        
    }
    исп Результат = Запрос.Выполнить() 
    для СтрокаРезультата из Результат
        знч ДанныеОбъекта = новый Соответствие<Строка, Объект?>()
        ДанныеОбъекта.ВставитьВсе(МенеджерЗапросов.СтрокаРезультатаЗапросаВСоответствие(СтрокаРезультата, Результат))
        Данные.Вставить(
            СтрокаРезультата.Ссылка,
            ДанныеОбъекта
        )
    ;
    
    возврат Данные    
;

метод ДанныеОбъектаВJson_V2_0(ДанныеОбъекта: ЧитаемоеСоответствие<Строка, Объект?>, Сервисы: ВебApiСервер.СервисыУпаковкиJson): МестоПроведенияСобеседований_2_0
    пер МестоПроведенияСобеседований = новый МестоПроведенияСобеседований_2_0(
        Сервисы.ПолучитьИдентификатор(ДанныеОбъекта["Ссылка"] как МестаПроведенияСобеседований.Ссылка),
        ДанныеОбъекта["Наименование"] как Строка)  
    возврат МестоПроведенияСобеседований        
;

// Конец JSON

@ВПодсистеме
метод ПослеЗаписиДополнение(Ссылка: МестаПроведенияСобеседований.Ссылка, ТребуетсяРегистрацияИзменений: Булево)
    если ТребуетсяРегистрацияИзменений
        ОбменДанными.ЗарегистрироватьОтправляемыйДокумент(Ссылка, Истина)
        РегистрацияИзменений.ЗарегистрироватьИзменение(Ссылка, Неопределено, ХарактерыИзменений.Изменение)
    ;
;

@ВПодсистеме
метод ПослеУдаленияДополнение(Ссылка: МестаПроведенияСобеседований.Ссылка, ТребуетсяРегистрацияИзменений: Булево)
    если ТребуетсяРегистрацияИзменений
        РегистрацияИзменений.ЗарегистрироватьУдаление(Ссылка)
    ;
;