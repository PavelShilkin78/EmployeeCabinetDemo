импорт ВебApi

@ВПроекте
@НаСервере @ДоступноСКлиента
метод Получить(Кандидат: РассматриваемыеКандидаты.Ссылка): СтатусыВоронки.Ссылка?
    знч ЗапросСтатус = Запрос{
        ВЫБРАТЬ
            СтатусыРассматриваемыхКандидатов.Статус
        ИЗ 
            СтатусыРассматриваемыхКандидатов.СрезПоследних КАК СтатусыРассматриваемыхКандидатов
        ГДЕ
            СтатусыРассматриваемыхКандидатов.Кандидат == %Кандидат
    }
    
    для Элемент из ЗапросСтатус.Выполнить()
        возврат Элемент.Статус
    ;
;

@ВПроекте
@НаСервере @ДоступноСКлиента
метод Установить(Кандидат: РассматриваемыеКандидаты.Ссылка, Статус: СтатусыВоронки.Ссылка)
    
    знч Запись = новый СтатусыРассматриваемыхКандидатов.Запись(
        Период = Момент.Сейчас(),
        Кандидат = Кандидат, 
        Статус = Статус)
    Записать(Запись)
;

@ВПроекте
@НаСервере @ДоступноСКлиента
метод Установить(Кандидаты: Массив<РассматриваемыеКандидаты.Ссылка>, Статус: СтатусыВоронки.Ссылка)
    для Кандидат из Кандидаты
        Установить(Кандидат, Статус)
    ;
;

@ВПроекте
@НаСервере @ДоступноСКлиента
метод УстановитьПоТриггеру(Кандидат: РассматриваемыеКандидаты.Ссылка, Триггер: ТриггерыПодбораПерсонала)
    знч Статус = НастройкиПодбораПерсонала.СтатусРассматриваемогоКандидатаПоТриггеру(Триггер)
    если Статус != Неопределено
        СтатусыРассматриваемыхКандидатов.Установить(Кандидат, Статус) 
    ;
;
// JSON

@ВПодсистеме
структура СтатусРассматриваемогоКандидата_2_0
    @JsonСвойство(Имя = "status")
    обз знч Статус: Строка?
    @JsonСвойство(Имя = "period")
    обз знч Период: Момент    
;

@ВПодсистеме
метод СтатусыВJson_V2_0(Кандидат: РассматриваемыеКандидаты.Ссылка?, Сервисы: ВебApiСервер.СервисыУпаковкиJson): Массив<СтатусРассматриваемогоКандидата_2_0>
    знч Запрос = Запрос{
        выбрать
            СтатусыРассматриваемыхКандидатов.Период как Период,
            СтатусыРассматриваемыхКандидатов.Статус как Статус
        из
            СтатусыРассматриваемыхКандидатов как СтатусыРассматриваемыхКандидатов
        где
            СтатусыРассматриваемыхКандидатов.Кандидат == %Кандидат
        упорядочить по
            СтатусыРассматриваемыхКандидатов.Период  
    }
    знч Статусы = новый Массив<СтатусРассматриваемогоКандидата_2_0>()
     исп РезультатЗапроса = Запрос.Выполнить()
    для Элемент из РезультатЗапроса
        Статусы.Добавить(новый СтатусРассматриваемогоКандидата_2_0(
           Статус = Сервисы.ПолучитьИдентификатор(Элемент.Статус),
           Период = Элемент.Период
        ))
    ;
    возврат Статусы
;

@ВПодсистеме
метод ЗаписатьСтатусыJson_V2_0(Кандидат: РассматриваемыеКандидаты.Ссылка, Данные: Массив<СтатусРассматриваемогоКандидата_2_0>, Сервисы: ВебApiСервер.СервисыЗагрузкиJson)
    знч НаборЗаписей = новый СтатусыРассматриваемыхКандидатов.НаборЗаписей()
    НаборЗаписей.Фильтр.Кандидат.Установить(Кандидат)
    для Элемент из Данные
        НаборЗаписей.Добавить(
            новый СтатусыРассматриваемыхКандидатов.Запись(
                Кандидат = Кандидат,
                Статус = Сервисы.ПолучитьСсылку(СтатусыВоронки.ВнешняяСсылка(Элемент.Статус)) как СтатусыВоронки.Ссылка,
                Период = Элемент.Период))
    ;
    НаборЗаписей.Записать(Истина)
;
// JSON