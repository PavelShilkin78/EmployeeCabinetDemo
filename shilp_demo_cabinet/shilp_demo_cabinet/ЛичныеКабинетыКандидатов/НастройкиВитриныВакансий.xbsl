импорт КонтрольДоступа

//Область РазрешенияДоступа

@Обработчик
метод ВычислитьРазрешенияДоступа(): ЧитаемаяКоллекция<РазрешениеДоступа>
    возврат [КонтрольДоступа.ЧтениеАутентифицированным(этот),
            КонтрольДоступа.ПолныйДоступАдминистраторуАбонента(этот)]
;

//КонецОбласти

@ВПроекте
метод Получить(): НастройкиВитриныВакансий.Ссылка
    исп Транзакции.Начать()
    Блокировки.Заблокировать(новый НастройкиВитриныВакансий.Блокировки.Ссылка())
    возврат Найти() ?? СоздатьПредопределенный()
;

метод Найти(): НастройкиВитриныВакансий.Ссылка?
    знч Запрос = Запрос{
        Выбрать Первые 2
            Ссылка
        Из
            НастройкиВитриныВакансий    
    }    
    возврат Запрос.Выполнить().ЕдинственныйИлиУмолчание()?.Ссылка    
;

метод СоздатьПредопределенный(): НастройкиВитриныВакансий.Ссылка
    знч ПредопределенныйИд = Ууид{97f29669-d6e5-4bf3-8aba-51ff06d3c79a}
    пер НоваяНастройка = новый НастройкиВитриныВакансий.Объект(Ид = ПредопределенныйИд)
    НоваяНастройка.Наименование = "Основная"
    НоваяНастройка.Записать()
    возврат НоваяНастройка.Ссылка      
;