импорт СтандартноеПриложение
импорт ИнтерфейсныеКомпонентыСтандартные

@ВПодсистеме
@НаКлиенте @НаСервере
структура ДанныеДляОформления
    пер ФоновоеИзображение: ДвоичныйОбъект.Ссылка?
    пер Логотип: ДвоичныйОбъект.Ссылка?
    пер Заголовок: Строка?
    пер Описание: Строка?
    пер Локация: Строка?
    пер СайтКомпании: Строка?
;

@НаСервере @ДоступноСКлиента
статический метод ПользовательАвторизован(): Булево
    возврат Пользователи.ТекущийПользователь != Неопределено
;


@Обработчик
метод ПослеСоздания()
    если ПользовательАвторизован()
        ПерейтиПоСсылке(АдресПриложения(), ОткрыватьВНовомОкне = Ложь)
    ;
    ДанныеДляОформления = ДанныеДляОформления()
;

метод ОсновнаяПанельВидимость(): Булево
    если ИнтерфейсныеКомпоненты.ПользовательИспользуетТелефон()
        возврат Компоненты.ОсновнаяГруппа.Содержимое.Последний() == Компоненты.СтартоваяСтраница
    иначе
        возврат Истина
    ;   
;

метод ОсновнаяПанельОтображатьКнопкуНавигации(): Булево
    если ИнтерфейсныеКомпоненты.ПользовательИспользуетТелефон()
        возврат Ложь
    иначе
        возврат Компоненты.ОсновнаяГруппа.Содержимое.Последний() != Компоненты.СтартоваяСтраница
    ;    
;

метод ОсновнаяПанельНазадКСпискуНажатии(Источник: ОсновнаяПанель, Событие: СобытиеПриНажатии)
    пер СодержимоеСтека = Компоненты.ОсновнаяГруппа.Содержимое
    СодержимоеСтека.УдалитьКроме([СодержимоеСтека.Первый()])
;

метод ОтступОсновногоОкна(): РазмерОтступа | Авто
    возврат ИнтерфейсныеКомпоненты.ПользовательИспользуетТелефон() ? РазмерОтступа.Отсутствует : РазмерОтступа.Двойной
;

метод ОтступГруппОсновногоОкна(): РазмерОтступа | Авто
    возврат ИнтерфейсныеКомпоненты.ПользовательИспользуетТелефон() ? РазмерОтступа.Одинарный : РазмерОтступа.Полуторный
;

метод ВитринаВакансийТемаОформления(): ТемаОформления
    знч ДанныеОформления = ДанныеДляОформления()
    знч Тема  = новый ТемаОформления(
        ФоновоеИзображение = ДанныеОформления.ФоновоеИзображение,
        Логотип = ДанныеОформления.Логотип,
        ФоновоеИзображениеРазмытие = Ложь
    )   
    возврат Тема
;

@ВПодсистеме
@НаСервере @ДоступноСКлиента
статический метод ДанныеДляОформления(): ДанныеДляОформления
    возврат ЛичныеКабинетыКандидатов.ДанныеДляОформления()
;

@НаСервере @ДоступноСКлиента
статический метод АдресПриложения(): Строка
    возврат СтандартноеПриложение.НастройкиПриложения().АдресПриложения
;