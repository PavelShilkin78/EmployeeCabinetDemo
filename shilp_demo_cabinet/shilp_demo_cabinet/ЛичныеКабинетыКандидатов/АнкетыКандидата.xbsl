импорт Анкетирование
импорт Тестирование
импорт ПодборПерсонала
импорт ОбщиеСтандартные
импорт ИнтерфейсныеКомпонентыСтандартные
импорт СтандартноеПриложение

@ВПроекте
@Реализация
метод Инициализировать()
    АнкетыНаправленыКандидату = АнкетыНаправленыКандидату(ЛичныеКабинетыКандидатов.ПараметрыСеанса().Кандидат)
;

метод ГруппаАнкетаЗаголовок(ДанныеСтроки: СтрокаДинамическогоСписка): Строка
    возврат ОсновнойИнтерфейс.РаботаНаУзкомУстройстве() ? ДанныеСтроки.Данные["Ссылка"].Представление() : ""
;

метод ГруппаОписаниеЗаголовок(ДанныеСтроки: СтрокаДинамическогоСписка): Строка
    возврат ОсновнойИнтерфейс.РаботаНаУзкомУстройстве() ? "" : ДанныеСтроки.Данные["Ссылка"].Представление() 
;

метод ГруппаАнкетаОтступПоГоризонтали(): РазмерОтступа | Авто
    возврат ОсновнойИнтерфейс.РаботаНаУзкомУстройстве() ? РазмерОтступа.Отсутствует : РазмерОтступа.Одинарный
;

метод СтатусыАнкетЦвет(ДанныеСтроки: СтрокаДинамическогоСписка): Цвет | Авто
    возврат БиблиотекаЦветов.Цвет(Анкетирование.ВидЦветаПоСостояниюАнкеты(ДанныеСтроки.Данные["Статус"] как СтатусыАнкет))
;

метод ТаблицаАнкетыПриНажатииСтроки(Источник: Список<ДинамическийСписок>, Событие: СобытиеСДанными<СтрокаСписка<СтрокаДинамическогоСписка>>)
    АнкетаФорма.ОткрытьВМодальномОкне(КлючОбъекта = Событие.Данные.ДанныеСтроки.Данные["Ссылка"] как Анкеты.Ссылка?)
;

метод КоличествоВопросовЗаголовок(ДанныеСтроки: СтрокаДинамическогоСписка): Строка
    пер СклонениеВопроса = МенеджерСтрок.ТекстПоКоличеству("вопрос", ДанныеСтроки.Данные["КоличествоВопросов"] как Число)
    возврат "%{ДанныеСтроки.Данные["КоличествоВопросов"]} %СклонениеВопроса"     
;

метод ВыравниваниеВГруппеПоВертикали(): ВыравниваниеПоВертикали | Авто
    возврат ОсновнойИнтерфейс.РаботаНаУзкомУстройстве() ? ВыравниваниеПоВертикали.Низ : ВыравниваниеПоВертикали.Верх
;

метод ДатаСозданияПредставление(ДанныеЭлемента: СтрокаДинамическогоСписка): Строка
    знч ФорматнаяСтрока = "дд.ММ.гг"
    возврат "Создана: " + МенеджерДат.ФорматМомента(ДанныеЭлемента.Данные["ДатаСоздания"] как Момент, ФорматнаяСтрока)
;

@НаСервере @ДоступноСКлиента
статический метод АнкетыНаправленыКандидату(Кандидат: Кандидаты.Ссылка): Булево
    знч Запрос = Запрос{
        Выбрать Первые 1
            Истина как АнкетыНаправлены
        Из
            Анкеты Как Анкеты
            Левое Соединение РезультатыТестирования Как РезультатыТестирования
                По Анкеты.Ссылка == РезультатыТестирования.Анкета
        Где
            Респондент == %Кандидат
            и РезультатыТестирования.Ссылка Есть Null
    }
    знч РезультатЗапроса = Запрос.Выполнить()
    возврат РезультатЗапроса.ЕдинственныйИлиУмолчание()?.АнкетыНаправлены ?? Ложь
;