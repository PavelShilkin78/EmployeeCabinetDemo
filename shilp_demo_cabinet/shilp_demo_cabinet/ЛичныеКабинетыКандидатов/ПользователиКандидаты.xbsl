импорт ПодборПерсонала
импорт КонтрольДоступа

//Область РазрешенияДоступа

@Обработчик
метод ВычислитьРазрешенияДоступа(): Массив<РазрешениеДоступа>
    возврат КонтрольДоступа.РегистрСведенийЧтениеВсемРедактированиеАдминистратору()
;

//КонецОбласти

@ВПодсистеме
метод ДобавитьПользователяКандидата(Пользователь: Пользователи.Ссылка, Вакансия: Вакансии.Ссылка? = Неопределено)
    пер НаборЗаписей: ПользователиКандидаты.НаборЗаписей
    НаборЗаписей.Фильтр.Пользователь.Установить(Пользователь)
    пер Запись: ПользователиКандидаты.Запись
    Запись.Пользователь = Пользователь
    Запись.ПерваяВакансия = Вакансия
    НаборЗаписей.Добавить(Запись)
    НаборЗаписей.Записать() 
;

@ВПодсистеме
метод ПользовательПоКандидату(Кандидат: Кандидаты.Ссылка): Пользователи.Ссылка?
    знч Запрос = Запрос{
        Выбрать
            Пользователь
        Из
            ПользователиКандидаты
        Где
            Кандидат == %Кандидат
    }
     
    возврат Запрос.Выполнить().ЕдинственныйИлиУмолчание()?.Пользователь
;

@ВПодсистеме
метод ПривязатьКандидатаКПользователю(Пользователь: Пользователи.Ссылка, Кандидат: Кандидаты.Ссылка)
    пер НаборЗаписей: ПользователиКандидаты.НаборЗаписей
    НаборЗаписей.Фильтр.Пользователь.Установить(Пользователь)
    НаборЗаписей.Прочитать()
    пер Запись: ПользователиКандидаты.Запись
    если НаборЗаписей.Пусто() 
        Запись.Пользователь = Пользователь
        Запись.Кандидат = Кандидат
        НаборЗаписей.Добавить(Запись)
    иначе
        Запись = НаборЗаписей[0]
        Запись.Пользователь = Пользователь
        Запись.Кандидат = Кандидат
    ;
    НаборЗаписей.Записать(Истина)    
;

@ВПодсистеме
метод ОтразитьПервыйВход(Пользователь: Пользователи.Ссылка)
    пер НаборЗаписей: ПользователиКандидаты.НаборЗаписей
    НаборЗаписей.Фильтр.Пользователь.Установить(Пользователь)
    НаборЗаписей.Прочитать()
    если не НаборЗаписей.Пусто()
        пер Запись = НаборЗаписей[0]
        Запись.Пользователь = Пользователь
        Запись.БылВход = Истина 
        НаборЗаписей.Записать(Истина)   
    ;      
;

@ВПодсистеме
метод ЗаписьПоПользователю(Пользователь: Пользователи.Ссылка): ПользователиКандидаты.Запись?
    знч Запрос = Запрос{
        Выбрать
            Пользователь,
            Кандидат,
            ПерваяВакансия,
            БылВход
        Из
            ПользователиКандидаты
        Где
            Пользователь == %Пользователь
    }    
    исп Результат = Запрос.Выполнить()
    для СтрокаРезультата из Результат 
        пер ЗаписьРегистра: ПользователиКандидаты.Запись
        ЗаписьРегистра.Пользователь = СтрокаРезультата.Пользователь
        ЗаписьРегистра.Кандидат = СтрокаРезультата.Кандидат
        ЗаписьРегистра.ПерваяВакансия = СтрокаРезультата.ПерваяВакансия
        ЗаписьРегистра.БылВход = СтрокаРезультата.БылВход
        возврат ЗаписьРегистра
    ;
    возврат Неопределено
;

@ВПодсистеме
метод КандидатыБезПривязанногоПользователя(Кандидаты: ЧитаемыйМассив<Кандидаты.Ссылка>): ЧитаемыйМассив<Кандидаты.Ссылка>
    исп КонтекстДоступа.Привилегированный()
    пер КандидатыБезПривязанногоПользователя = новый Массив(Кандидаты)
    
    знч Запрос = Запрос{
        Выбрать
            Кандидат
        Из
            ПользователиКандидаты
        Где
            Кандидат В (%Кандидаты)
    }
    исп РезультатЗапроса = Запрос.Выполнить()
    для СтрокаРезультата из РезультатЗапроса
        КандидатыБезПривязанногоПользователя.Удалить(СтрокаРезультата.Кандидат)    
    ;
    
    возврат КандидатыБезПривязанногоПользователя   
;

@ВПодсистеме
метод КандидатПользователя(): Кандидаты.Ссылка?
    знч Запрос = Запрос{
        ВЫБРАТЬ ПЕРВЫЕ 1
            Кандидат
        ИЗ
            ПользователиКандидаты
        ГДЕ
            Пользователь == %{Пользователи.ТекущийПользователь}
    }
    возврат Запрос.Выполнить().ЕдинственныйИлиУмолчание()?.Кандидат
;

@ВПодсистеме
метод КандидатыПользователей(Пользователи: ЧитаемаяКоллекция<Пользователи.Ссылка>):
    ЧитаемоеСоответствие<Пользователи.Ссылка, Кандидаты.Ссылка>
    
    знч КандидатыПользователей: Соответствие<Пользователи.Ссылка, Кандидаты.Ссылка>
    
    знч Запрос = Запрос{
        Выбрать
            Пользователь,
            Кандидат
        Из
            ПользователиКандидаты
        Где
            Пользователь В (%Пользователи)
            и Кандидат != Неопределено
    }
    
    исп РезультатЗапроса = Запрос.Выполнить()
    для СтрокаРезультата из РезультатЗапроса
        КандидатыПользователей.Вставить(СтрокаРезультата.Пользователь, СтрокаРезультата.Кандидат)
    ;
    
    возврат КандидатыПользователей    
;