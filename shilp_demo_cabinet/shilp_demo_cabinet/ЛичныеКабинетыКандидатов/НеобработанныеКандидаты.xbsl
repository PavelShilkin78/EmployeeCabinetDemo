импорт ПодборПерсонала

@ВПодсистеме
@НаСервере
метод Добавить(Кандидат: Кандидаты.Ссылка)
    если СсылкаПоКандидату(Кандидат) == Неопределено
        пер НеобработанныйКандидат: НеобработанныеКандидаты.Объект
        НеобработанныйКандидат.Наименование = Кандидат.Представление()
        НеобработанныйКандидат.Кандидат = Кандидат
        НеобработанныйКандидат.Записать()    
    ;
;

@ВПодсистеме
@НаСервере
метод ВсеНеобработанныеКандидаты(): ЧитаемыйМассив<Кандидаты.Ссылка>
    пер ВсеКандидаты: Массив<Кандидаты.Ссылка>
    знч Запрос = Запрос{
        Выбрать
            Кандидат
        Из 
            НеобработанныеКандидаты    
    } 
    исп Привилегированный = КонтекстДоступа.Привилегированный()   
    исп РезультатЗапроса = Запрос.Выполнить()
    Привилегированный.Закрыть()
    для СтрокаЗапроса из РезультатЗапроса
        ВсеКандидаты.Добавить(СтрокаЗапроса.Кандидат)    
    ;
    
    возврат ВсеКандидаты
;

@ВПодсистеме
@НаСервере
метод Удалить(Кандидат: Кандидаты.Ссылка)
    знч НеобработанныйКандидатСсылка = СсылкаПоКандидату(Кандидат) 
    если НеобработанныйКандидатСсылка != Неопределено
        пер НеобработанныйКандидат = НеобработанныйКандидатСсылка.ЗагрузитьОбъект()
        НеобработанныйКандидат.Удалить()    
    ;     
;

@НаСервере
метод СсылкаПоКандидату(Кандидат: Кандидаты.Ссылка): НеобработанныеКандидаты.Ссылка?
    знч Запрос = Запрос{
        Выбрать
            Ссылка
        Из
            НеобработанныеКандидаты
        Где
            Кандидат == %Кандидат
    } 
    исп РезультатЗапроса = Запрос.Выполнить()
    для СтрокаРезультата из РезультатЗапроса
        возврат СтрокаРезультата.Ссылка    
    ;    
    возврат Неопределено
;