импорт Анкетирование
импорт КонтрольДоступа
импорт ПодборПерсонала
импорт ИнтерфейсныеКомпонентыСтандартные
импорт СтандартноеПриложение

@НаСервере @НаКлиенте
структура ДанныеДляЗаполнения
    пер АнкетаКандидата: Анкеты.Ссылка?
    пер ПервыйВход: Булево
    пер ДанныеАнкеты: Массив<ДанныеЭлементаКонструктора>
    пер ДанныеШаблона: Анкетирование.ДанныеШаблонаАнкет
    пер Статус: СтатусыАнкет?
;

@Обработчик
метод ПослеСоздания()
    
    знч Данные = ДанныеДляЗаполнения()
    если Данные.АнкетаКандидата == Неопределено
        возврат
    ;
    
    ПервыйВход = Данные.ПервыйВход
    ДанныеАнкеты = Данные.ДанныеАнкеты
    ДанныеШаблона = Данные.ДанныеШаблона
    АнкетаКандидата = Данные.АнкетаКандидата
    ТолькоПросмотр = Данные.Статус == СтатусыАнкет.Заполнена
    ИнтерфейсныеКомпоненты.ИнициализироватьПодчиненныеКомпоненты(Компоненты)    
;

@НаСервере @ДоступноСКлиента
статический метод ДанныеДляЗаполнения(): ДанныеДляЗаполнения
    исп КонтекстДоступа.Привилегированный()
    пер ДанныеДляЗаполнения: ДанныеДляЗаполнения
    
    знч Пользователь = Пользователи.ТекущийПользователь
    знч РезюмеКандидата = РезюмеКандидатов.РезюмеКандидата(Пользователь)
    знч ШаблонАнкеты = НастройкиПодбораПерсонала.ШаблонАнкетыКандидата()
    если ШаблонАнкеты == Неопределено
        возврат ДанныеДляЗаполнения
    ;
    если РезюмеКандидата == Неопределено
        ДанныеДляЗаполнения.АнкетаКандидата = НоваяАнкетаКандидата(ШаблонАнкеты)
    иначе
        ДанныеДляЗаполнения.АнкетаКандидата = РезюмеКандидата.Анкета
    ;
    
    знч ЗаписьПользователиКандидаты = ПользователиКандидаты.ЗаписьПоПользователю(Пользователь)
    ДанныеДляЗаполнения.ПервыйВход = не ЗаписьПользователиКандидаты.БылВход
    если ДанныеДляЗаполнения.ПервыйВход
        исп КонтекстДоступа.Привилегированный()
        ПользователиКандидаты.ОтразитьПервыйВход(Пользователь)
    ;
    
    знч АнкетаКандидата = ДанныеДляЗаполнения.АнкетаКандидата.ЗагрузитьОбъект()
    ДанныеДляЗаполнения.Статус = АнкетаКандидата.Статус
    ДанныеДляЗаполнения.ДанныеАнкеты = Анкетирование.ДанныеАнкеты(
        АнкетаКандидата.Ссылка,
        АнкетаКандидата.Статус,
        ШаблонАнкеты
    )
    
    ЗаполнитьКонтактыПоУмолчанию(ДанныеДляЗаполнения.ДанныеАнкеты) 
    
    ДанныеДляЗаполнения.ДанныеШаблона = Анкетирование.ДанныеШаблона(
        ШаблонАнкеты,
        ДанныеДляЗаполнения.ДанныеШаблона
    )
    ДанныеДляЗаполнения.ДанныеШаблона.Разделы = Анкетирование.РазделыШаблона(ДанныеДляЗаполнения.ДанныеШаблона.Ссылка)
    если ДанныеДляЗаполнения.ДанныеШаблона.Разделы.Размер() > 0
        ДанныеДляЗаполнения.ДанныеШаблона.ТекущийРаздел = ДанныеДляЗаполнения.ДанныеШаблона.Разделы[0]
    ;
  
    возврат ДанныеДляЗаполнения
;

@НаСервере @ДоступноСКлиента
статический метод ЗаполнитьКонтактыПоУмолчанию(ДанныеАнкеты: Массив<ДанныеЭлементаКонструктора>)  
   знч КонтактнаяИнформацияПользователя = ПользователиСистемы.КонтактнаяИнформацияПользователя(Пользователи.ТекущийПользователь)
   для Элемент из ДанныеАнкеты
        если Элемент.ДанныеВопроса?.ИдПредопределенногоВопроса == ПредопределенныеВопросыАнкеты.ИД_ВОПРОСА_ЭЛЕКТРОННАЯ_ПОЧТА
            если Элемент.ДанныеОтвета.ЗначениеСтрока.Пусто()
                Элемент.ДанныеОтвета.ЗначениеСтрока = КонтактнаяИнформацияПользователя.ЭлектроннаяПочта
            ;
        ;
        если Элемент.ДанныеВопроса?.ИдПредопределенногоВопроса == ПредопределенныеВопросыАнкеты.ИД_ВОПРОСА_ТЕЛЕФОН
            если Элемент.ДанныеОтвета.ЗначениеСтрока.Пусто()
                Элемент.ДанныеОтвета.ЗначениеСтрока = КонтактнаяИнформацияПользователя.Телефон
            ;
        ;
   ;  
;

@НаСервере @ДоступноСКлиента
статический метод НоваяАнкетаКандидата(ШаблонАнкеты: ШаблоныАнкет.Ссылка?): Анкеты.Ссылка
    исп Транзакции.Начать()
    знч НоваяАнкета = Анкетирование.СоздатьАнкету(Неопределено, ШаблонАнкеты, СтатусыАнкет.Отправлена, ПроисхождениеАнкет.Отдельная)
    РезюмеКандидатов.ЗаписатьРезюмеКандидата(Пользователи.ТекущийПользователь, НоваяАнкета)
    возврат НоваяАнкета 
;

// Конец НачальноеЗаполнение

метод АнкетаКандидатаСохранениеАнкеты(Источник: АнкетаКомпонент, Событие: СобытиеСДанными<СтатусыАнкет>)
    СохранитьАнкету(АнкетаКандидата, ДанныеАнкеты, Событие.Данные)
    ПерейтиПоСсылке(СтандартноеПриложение.ПараметрыСеанса().АдресПриложения, Ложь)
;

@НаСервере @ДоступноСКлиента
статический метод СохранитьАнкету(АнкетаКандидата: Анкеты.Ссылка, ДанныеАнкеты: Массив<ДанныеЭлементаКонструктора>, Статус: СтатусыАнкет)
    
    исп КонтекстДоступа.Привилегированный()

    Анкетирование.УдалитьПредыдущиеОтветы(АнкетаКандидата)
    Анкетирование.СохранитьОтветы(АнкетаКандидата, ДанныеАнкеты)
    
    пер Анкета = АнкетаКандидата.ЗагрузитьОбъект()
    Анкета.Статус = Статус
    если Статус == СтатусыАнкет.Заполнена
        Анкета.УровеньДоступности = УровниДоступностиАнкет.ВсеПриложения
        знч ЗаписьПользователя = ПользователиКандидаты.ЗаписьПоПользователю(Пользователи.ТекущийПользователь)
        Анкета.Респондент = ЗаписьПользователя.Кандидат
        если ЗаписьПользователя != Неопределено и Кандидаты.ИсточникКандидата(ЗаписьПользователя.Кандидат) == ИсточникиКандидатов.ВитринаВакансий
            знч Отклик = ЛичныеКабинетыКандидатов.ЗаписатьОтклик(ПользователиКандидаты.ЗаписьПоПользователю(Пользователи.ТекущийПользователь).ПерваяВакансия)
            если НастройкиПерсональныхДанных.ВедетсяСборСогласий()
                СогласияНаОбработкуПдн.СвязатьОткликССогласиемНаОбработкуПерсональныхДанных(Отклик)
            ;
        ;
    ;
    Анкета.Записать()
;