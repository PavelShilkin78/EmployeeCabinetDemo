импорт Анкетирование
импорт КонтрольДоступа

// КонтрольДоступа

@Обработчик
метод ВычислитьРазрешенияДоступа(): ЧитаемаяКоллекция<РазрешениеДоступа>
    возврат КонтрольДоступа.РазрешенияДоступаТаблицыРегистраСведений()
;

@Обработчик
метод ВычислитьРазрешенияДоступаДляОбъектов(Объекты: ЧитаемыйМассив<РезюмеКандидатов.ДанныеРасчетаРазрешений>): ЧитаемоеСоответствие<РезюмеКандидатов.ДанныеРасчетаРазрешений, ЧитаемаяКоллекция<РазрешениеДоступа>>
    
    знч Разрешения: Соответствие<РезюмеКандидатов.ДанныеРасчетаРазрешений, Множество<РазрешениеДоступа>>
    знч Пользователи: Массив<Пользователи.Ссылка>
    для Объект из Объекты
        Пользователи.Добавить(Объект.Кандидат)
    ;
    знч ПользователиКандидаты = ПользователиКандидаты.КандидатыПользователей(Пользователи)
    для Объект из Объекты
        знч РазрешенияОбъекта: Множество<РазрешениеДоступа>
        РазрешенияОбъекта.ДобавитьВсе(КонтрольДоступа.РазрешенияДоступаРегистраКонтентДанныхАбонентаБезЧтения())
        
        знч КандидатПользователя = ПользователиКандидаты.ПолучитьИлиУмолчание(Объект.Кандидат)
        если КандидатПользователя != Неопределено
            РазрешенияОбъекта.ДобавитьВсе(КонтрольДоступа.ЧтениеРедактированиеДанныхРегистровПерсонально([КандидатПользователя]))
        ;
        
        Разрешения.Вставить(Объект, РазрешенияОбъекта)
    ;
    
    возврат Разрешения
;

// КонтрольДоступа

@ВПодсистеме
@НаСервере @НаКлиенте
структура РезюмеКандидата
    обз пер Анкета: Анкеты.Ссылка?
    обз пер Статус: СтатусыАнкет?
;

@ВПодсистеме
метод РезюмеКандидата(Кандидат: Пользователи.Ссылка): РезюмеКандидата?
    
    знч Запрос = Запрос{
        Выбрать
            Анкета как Анкета,
            Анкета.Статус как Статус
        Из
            РезюмеКандидатов
        Где
            Кандидат == %Кандидат
    }
    исп РезультатЗапроса = Запрос.Выполнить()
    для РезультатЗапросаСтрока из РезультатЗапроса
        пер Статус: СтатусыАнкет?
        если РезультатЗапросаСтрока.Статус != Null
            Статус = РезультатЗапросаСтрока.Статус как СтатусыАнкет
        ;
        возврат новый РезюмеКандидата(
            Анкета = РезультатЗапросаСтрока.Анкета,
            Статус = Статус
        )
    ; 
    возврат Неопределено
;

@ВПодсистеме
метод ЗаписатьРезюмеКандидата(Кандидат: Пользователи.Ссылка, Анкета: Анкеты.Ссылка)
    пер НаборЗаписей: РезюмеКандидатов.НаборЗаписей
    НаборЗаписей.Фильтр.Кандидат.Установить(Кандидат)
    пер Запись = НаборЗаписей.ДобавитьЗапись()
    Запись.Кандидат = Кандидат
    Запись.Анкета = Анкета
    НаборЗаписей.Записать()
;

@ВПодсистеме
метод УдалитьРезюмеКандидата(Кандидат: Пользователи.Ссылка)
    пер НаборЗаписей: РезюмеКандидатов.НаборЗаписей
    НаборЗаписей.Фильтр.Кандидат.Установить(Кандидат)
    НаборЗаписей.Записать(Истина)
;