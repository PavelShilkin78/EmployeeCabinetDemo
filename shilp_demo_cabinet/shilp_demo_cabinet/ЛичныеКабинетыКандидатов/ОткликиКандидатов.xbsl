импорт ПодборПерсонала
импорт КонтрольДоступа

// КонтрольДоступа

@Обработчик
метод ВычислитьРазрешенияДоступа(): ЧитаемаяКоллекция<РазрешениеДоступа>
    возврат КонтрольДоступа.РазрешенияДоступаТаблицыРегистраСведений()
;

@Обработчик
метод ВычислитьРазрешенияДоступаДляОбъектов(Объекты: ЧитаемыйМассив<ОткликиКандидатов.ДанныеРасчетаРазрешений>): ЧитаемоеСоответствие<ОткликиКандидатов.ДанныеРасчетаРазрешений, ЧитаемаяКоллекция<РазрешениеДоступа>>
    
    знч Разрешения: Соответствие<ОткликиКандидатов.ДанныеРасчетаРазрешений, Множество<РазрешениеДоступа>>
    для Объект из Объекты
        знч РазрешенияОбъекта: Множество<РазрешениеДоступа>
        РазрешенияОбъекта.ДобавитьВсе(КонтрольДоступа.РазрешенияДоступаРегистраКонтентДанныхАбонентаБезЧтения())
        РазрешенияОбъекта.ДобавитьВсе(КонтрольДоступа.ЧтениеРедактированиеДанныхРегистровПерсонально([Объект.Отклик]))
        Разрешения.Вставить(Объект, РазрешенияОбъекта)
    ;
    
    возврат Разрешения
;

// КонтрольДоступа

@ВПодсистеме
@НаСервере
метод ЗаписатьОткликКандидата(Кандидат: Пользователи.Ссылка, Вакансия: Вакансии.Ссылка, Отклик: Отклики.Ссылка)
    пер НаборЗаписей: ОткликиКандидатов.НаборЗаписей
    НаборЗаписей.Фильтр.Кандидат.Установить(Кандидат)
    НаборЗаписей.Фильтр.Вакансия.Установить(Вакансия)
    пер Запись: ОткликиКандидатов.Запись
    Запись.Кандидат = Кандидат
    Запись.Вакансия = Вакансия
    Запись.Отклик = Отклик
    НаборЗаписей.Добавить(Запись)
    НаборЗаписей.Записать()
;

@ВПодсистеме
@НаСервере @ДоступноСКлиента
метод КандидатОткликалсяПоВакансии(Кандидат: Пользователи.Ссылка, Вакансия: Вакансии.Ссылка): Булево
    знч Запрос = Запрос{
        Выбрать Первые 1
            Истина Как Откликался
        Из
            ОткликиКандидатов
        Где
            Кандидат == %Кандидат
            и Вакансия == %Вакансия
    }    
    исп Результат = Запрос.Выполнить()
    возврат Результат.ЕдинственныйИлиУмолчание()?.Откликался ?? Ложь
;

@ВПодсистеме
@НаСервере @ДоступноСКлиента
метод ВакансииСОткликомКандидата(Кандидат: Пользователи.Ссылка): Множество<Вакансии.Ссылка>
    
    пер Вакансии: Множество<Вакансии.Ссылка>
    
    знч Запрос = Запрос{
        Выбрать
            Вакансия
        Из
            ОткликиКандидатов
        Где
            Кандидат == %Кандидат
    }
    исп РезультатЗапроса = Запрос.Выполнить()
    для СтрокаРезультата из РезультатЗапроса
        Вакансии.Добавить(СтрокаРезультата.Вакансия)
    ;
    
    возврат Вакансии   
;

@ВПодсистеме
@НаСервере
метод ОткликиПользователей(Пользователи: ЧитаемаяКоллекция<Пользователи.Ссылка>): ЧитаемоеСоответствие<Пользователи.Ссылка, ЧитаемыйМассив<Отклики.Ссылка>>
    
    пер ОткликиПользователей: Соответствие<Пользователи.Ссылка, Массив<Отклики.Ссылка>>
    
    знч Запрос = Запрос{
        Выбрать
            Кандидат,
            Отклик
        Из
            ОткликиКандидатов
        Где
            Кандидат В (%Пользователи)
            и Отклик != Неопределено
    }
    
    исп РезультатЗапроса = Запрос.Выполнить()
    для СтрокаРезультата из РезультатЗапроса
        знч ОткликиПользователя = ОткликиПользователей.ВставитьЕслиОтсутствует(СтрокаРезультата.Кандидат, [])
        ОткликиПользователя.Добавить(СтрокаРезультата.Отклик) 
    ;
    
    возврат ОткликиПользователей            
;
