импорт Тестирование
импорт ОбщиеСтандартные
импорт СтандартноеПриложение
импорт ИнтерфейсныеКомпонентыСтандартные
импорт УниверсальныеМеханизмы

@ВПроекте
@Реализация
метод Инициализировать()
    ТестыНаправленыКандидату = ТестыНаправленыРеспонденту(УниверсальныеМеханизмы.ПараметрыСеанса().РеспондентыТекущегоПользователя)
;

метод ГруппаТестЗаголовок(ДанныеСтроки: СтрокаДинамическогоСписка): Строка
    возврат ОсновнойИнтерфейс.РаботаНаУзкомУстройстве() ? ДанныеСтроки.Данные["Тест"].Представление() : ""
;

метод ГруппаОписаниеЗаголовок(ДанныеСтроки: СтрокаДинамическогоСписка): Строка
    возврат ОсновнойИнтерфейс.РаботаНаУзкомУстройстве() ? "" : ДанныеСтроки.Данные["Тест"].Представление() 
;

метод ГруппаТестОтступПоГоризонтали(): РазмерОтступа | Авто
    возврат ОсновнойИнтерфейс.РаботаНаУзкомУстройстве() ? РазмерОтступа.Отсутствует : РазмерОтступа.Одинарный
;

метод ДатаСозданияПредставление(ДанныеЭлемента: СтрокаДинамическогоСписка): Строка
    знч ФорматнаяСтрока = "дд.ММ.гг"
    возврат "Создан: " + МенеджерДат.ФорматМомента(ДанныеЭлемента.Данные["ДатаСоздания"] как Момент, ФорматнаяСтрока)
;

метод СтатусТестаЦвет(ДанныеСтроки: СтрокаДинамическогоСписка): Цвет | Авто
    возврат БиблиотекаЦветов.Цвет(Тестирование.ВидЦветаПоСостояниюТеста(ДанныеСтроки.Данные["Статус"] как СостоянияТестирования))
;

метод ТаблицаТестыПриНажатииСтроки(Источник: Список<ДинамическийСписок>, Событие: СобытиеСДанными<СтрокаСписка<СтрокаДинамическогоСписка>>)
    РезультатыТестированияФормаОбъекта.ОткрытьВМодальномОкне(КлючОбъекта = Событие.Данные.ДанныеСтроки.Данные["РезультатТестирования"] как РезультатыТестирования.Ссылка?)   
;

метод КоличествоВопросовЗаголовок(ДанныеСтроки: СтрокаДинамическогоСписка): Строка
    пер СклонениеВопроса = МенеджерСтрок.ТекстПоКоличеству("вопрос", ДанныеСтроки.Данные["КоличествоВопросов"] как Число)
    возврат "%{ДанныеСтроки.Данные["КоличествоВопросов"]} %СклонениеВопроса"     
;

@НаСервере @ДоступноСКлиента
статический метод ТестыНаправленыРеспонденту(РеспондентыТекущегоПользователя: Массив<РеспондентАнкетирования.Ссылка>): Булево
    знч Запрос = Запрос{
        Выбрать Первые 1
            Истина как ЕстьТесты
        Из
            РезультатыТестирования
        Где
            Анкета.Респондент В (%РеспондентыТекущегоПользователя)
    }
    знч РезультатЗапроса = Запрос.Выполнить()
    возврат РезультатЗапроса.ЕдинственныйИлиУмолчание()?.ЕстьТесты ?? Ложь
;