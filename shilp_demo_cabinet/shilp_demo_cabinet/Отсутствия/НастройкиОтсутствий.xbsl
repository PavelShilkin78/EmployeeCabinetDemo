        импорт КонтрольДоступа

//Область РазрешенияДоступа

@Обработчик
метод ВычислитьРазрешенияДоступа(): Массив<РазрешениеДоступа>
    возврат КонтрольДоступа.РегистрСведенийРедактированиеАдминистраторуЗагружаемый()
;

//КонецОбласти

@ВПроекте
метод НастройкиПоВидуОтсутствия(ВидОтсутствия: ВидыОтсутствий): НастройкиОтсутствий.Запись
   
    знч НастройкиПоВидуОтсутствия: НастройкиОтсутствий.Запись
    
    знч Запрос = Запрос{
        Выбрать
            ОформлятьЗаднимЧислом,
            ДнейДоСобытияДляИзмененияЗаявки
        Из 
            НастройкиОтсутствий
        Где
            ВидОтсутствия == %ВидОтсутствия
    } 
    исп РезультатЗапроса = Запрос.Выполнить()
    для СтрокаРезультата из РезультатЗапроса
        НастройкиПоВидуОтсутствия.ОформлятьЗаднимЧислом = СтрокаРезультата.ОформлятьЗаднимЧислом
        НастройкиПоВидуОтсутствия.ДнейДоСобытияДляИзмененияЗаявки = СтрокаРезультата.ДнейДоСобытияДляИзмененияЗаявки
    ;
    
    возврат НастройкиПоВидуОтсутствия     
;

// JSON

@ВПодсистеме
структура ДанныеJson_V2_0
    обз знч absenceType: Отсутствие.reasons
    пер requestBackdating: Булево? = Ложь    
;

@ВПодсистеме
структура ДанныеJson_3_0
    @JsonСвойство(Имя = "absenceType")
    обз знч Отсутствие: Отсутствие.reasons
    @JsonСвойство(Имя = "requestBackdating")
    пер ОформлятьЗаднимЧислом: Булево? = Ложь
    @JsonСвойство(Имя = "daysBeforeChangeRequests")
    пер ДнейДоСобытияДляИзмененияЗаявки: Число = 7
;

@ВПодсистеме
метод ЗаписатьНастройкиОтсутствийИзJson(КоллекцияДанныхJson: ЧитаемыйМассив<ДанныеJson_V2_0>)
    
    для ДанныеJson из КоллекцияДанныхJson
        
        знч ВидОтсутствия = Отсутствие.ПричиныОтсутствийИзJson(ДанныеJson.absenceType)
        
        знч НаборЗаписей: НастройкиОтсутствий.НаборЗаписей
        НаборЗаписей.Фильтр.ВидОтсутствия.Установить(ВидОтсутствия)
        знч Запись: НастройкиОтсутствий.Запись
        Запись.ВидОтсутствия = ВидОтсутствия
        Запись.ОформлятьЗаднимЧислом = ДанныеJson.requestBackdating
        НаборЗаписей.Добавить(Запись)
        
        НаборЗаписей.Записать(Истина)     
    ;         
;

@ВПодсистеме
метод ЗаписатьНастройкиОтсутствийИзJson_3_0(КоллекцияДанныхJson: ЧитаемыйМассив<ДанныеJson_3_0>)
    
    для ДанныеJson из КоллекцияДанныхJson
        
        знч ВидОтсутствия = Отсутствие.ПричиныОтсутствийИзJson(ДанныеJson.Отсутствие)
        
        знч НаборЗаписей: НастройкиОтсутствий.НаборЗаписей
        НаборЗаписей.Фильтр.ВидОтсутствия.Установить(ВидОтсутствия)
        знч Запись: НастройкиОтсутствий.Запись
        Запись.ВидОтсутствия = ВидОтсутствия
        Запись.ОформлятьЗаднимЧислом = ДанныеJson.ОформлятьЗаднимЧислом
        Запись.ДнейДоСобытияДляИзмененияЗаявки = ДанныеJson.ДнейДоСобытияДляИзмененияЗаявки
        
        НаборЗаписей.Добавить(Запись)
        
        НаборЗаписей.Записать(Истина)     
    ;         
;
// Конец JSON