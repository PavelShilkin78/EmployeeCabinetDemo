импорт ВебApi
импорт УниверсальныеМеханизмы
импорт УниверсальныеМеханизмы::ШаблоныДокументов

//Область ОбработчикиHTTPМетодов
@ВПодсистеме
метод ОбновитьСостояниеОтсутствия(ЗапросHttp: HttpСервисЗапрос, ФорматОбмена: Версия? = Неопределено)
    ВебApiСервер.ЗагрузитьОбъект(ЗапросHttp, Тип<СостояниеДокументов.Ссылка>,
        [новый СервисыHttp.ОжидаемыйПараметр("requestID", Истина, СервисыHttp.ТипыПараметраUrl.Строка),
        новый СервисыHttp.ОжидаемыйПараметр("status", Истина, СервисыHttp.ТипыПараметраUrl.Строка)], 
    новый ВебApiСервер.ИнструментыМенеджераОбъекта(
        (Объект: Сущность.Объект) -> (Объект как СостояниеДокументов.Объект).Записать()
    ), ФорматОбмена)
;

@ВПодсистеме
метод ПолучитьОтсутствие(ЗапросHttp: HttpСервисЗапрос, ФорматОбмена: Версия? = Неопределено)
    ВебApiСервер.ОтправитьОбъектПоСсылке(ЗапросHttp, Тип<Отсутствие.Ссылка>, 
        новый СервисыHttp.ОжидаемыйПараметр("ID", Истина, СервисыHttp.ТипыПараметраUrl.Строка), 
        (Ууид) -> Отсутствие.ПолучитьСсылку(Ууид), ФорматОбмена)
;

@ВПодсистеме
метод ОбновитьОстаткиДнейДополнительногоОтдыха(ЗапросHttp: HttpСервисЗапрос, ФорматОбмена: Версия? = Неопределено)
    ВебApiСервер.ЗагрузитьОбъекты(ЗапросHttp, Тип<ДниДополнительногоОтдыха.Ссылка>, 
    новый ВебApiСервер.ИнструментыМенеджераОбъекта(
        (Объект: Сущность.Объект) -> (Объект как ДниДополнительногоОтдыха.Объект).Записать()
    ), ФорматОбмена)
;

@ВПодсистеме
метод ОбновитьОстатокДнейДополнительногоОтдыха(ЗапросHttp: HttpСервисЗапрос, ФорматОбмена: Версия? = Неопределено)
    ВебApiСервер.ЗагрузитьОбъект(ЗапросHttp, Тип<ДниДополнительногоОтдыха.Ссылка>, 
    новый ВебApiСервер.ИнструментыМенеджераОбъекта(
        (Объект: Сущность.Объект) -> (Объект как ДниДополнительногоОтдыха.Объект).Записать()
    ), ФорматОбмена)
;

@ВПодсистеме
метод УдалитьОстатокДнейДополнительногоОтдыха(ЗапросHttp: HttpСервисЗапрос)
    ВебApiСервер.УдалитьОбъекты(ЗапросHttp, Тип<ДниДополнительногоОтдыха.Ссылка>, 
        [новый СервисыHttp.ОжидаемыйПараметр("personID", Истина, СервисыHttp.ТипыПараметраUrl.Строка)])
;

@ВПодсистеме
метод ЗаписатьШаблонЗаявления(ЗапросHttp: HttpСервисЗапрос)
    ШаблоныДокументовМенеджер.ЗаписатьШаблонДокумента(
        Отсутствия.ПОДСИСТЕМА_ОТСУТСТВИЯ,
        Отсутствия.ШАБЛОН_ЗАЯВЛЕНИЯ_НА_ОТСУТСТВИЕ,
        ЗапросHttp)
;

@ВПодсистеме
метод ОбновитьНастройкиОтсутствий(ЗапросHttp: HttpСервисЗапрос, ФорматОбмена: Версия? = Неопределено)
    если ФорматОбмена == Неопределено или ФорматОбмена < Версия{2.0}
        выбросить новый ИсключениеВыполнения("Неверный формат")    
    ;
    
    если ФорматОбмена >= Версия{3.0} 
        знч ДанныеJson = СервисыHttp.ИзвлечьДанныеJson(ЗапросHttp, Тип<ЧитаемыйМассив<НастройкиОтсутствий.ДанныеJson_3_0>>)
        НастройкиОтсутствий.ЗаписатьНастройкиОтсутствийИзJson_3_0(ДанныеJson)
    иначе
        знч ДанныеJson = СервисыHttp.ИзвлечьДанныеJson(ЗапросHttp, Тип<ЧитаемыйМассив<НастройкиОтсутствий.ДанныеJson_V2_0>>)
        НастройкиОтсутствий.ЗаписатьНастройкиОтсутствийИзJson(ДанныеJson)
    ;   
;