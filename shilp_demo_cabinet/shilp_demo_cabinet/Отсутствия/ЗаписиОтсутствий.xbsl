импорт ФизическиеЛица
импорт КонтрольДоступа

@ВПроекте
структура ЗаписьОтсутствия
    пер ФизическоеЛицо: ФизическиеЛица.Ссылка?
    пер Причина: ВидыОтсутствий?
    пер Начало: Момент?
    пер Окончание: Момент?
    пер ДатаНачала: Дата?
    пер ДатаОкончания: Дата?
    пер Внутрисменное: Булево
    пер СпособСвязи: СпособыСвязи?
    пер Документ: ОснованиеОтсутствия.Ссылка?
    пер Комментарий: Строка
;

//Область РазрешенияДоступа

@Обработчик
метод ВычислитьРазрешенияДоступа(): Массив<РазрешениеДоступа>
    возврат [КонтрольДоступа.РедактированиеИУдалениеАутентифицированным(этот)]
;


@Обработчик
метод ВычислитьРазрешенияДоступаДляОбъектов(Элементы: ЧитаемыйМассив<ЗаписиОтсутствий.ДанныеРасчетаРазрешений>): ЧитаемоеСоответствие<ЗаписиОтсутствий.ДанныеРасчетаРазрешений, ЧитаемаяКоллекция<РазрешениеДоступа>>
    знч Разрешения: Соответствие<ЗаписиОтсутствий.ДанныеРасчетаРазрешений, ЧитаемаяКоллекция<РазрешениеДоступа>>
    для Элемент из Элементы
        знч РазрешенияЭлемента: Множество<РазрешениеДоступа>
        РазрешенияЭлемента.ДобавитьВсе(КонтрольДоступа.РедактированиеУдалениеДанныхСправочниковПерсонально([Элемент.ФизическоеЛицо]))
        Разрешения.Вставить(Элемент, РазрешенияЭлемента)
    ;
    возврат Разрешения
;

//КонецОбласти

//Область СлужебныПрограммныйИнтерфейс

@ВПроекте
метод ОбновитьЗаписиОтсутствияДокумента(ЗаписиОтсутствия: Массив<ЗаписьОтсутствия>, Документ: ОснованиеОтсутствия.Ссылка)
    УдалитьЗаписиОтсутствияДокумента(Документ)
    СоздатьЗаписиОтсутствия(ЗаписиОтсутствия)
;


метод СоздатьЗаписиОтсутствия(ЗаписиОтсутствия: Массив<ЗаписьОтсутствия>)
    для ДанныеОтсутствия из ЗаписиОтсутствия
        пер ЗаписьОтсутствияОбъект = новый ЗаписиОтсутствий.Объект()
        ЗаполнитьЗаписьОтсутствия(ЗаписьОтсутствияОбъект, ДанныеОтсутствия)
        ЗаписьОтсутствияОбъект.Записать()
    ;
;


метод УдалитьЗаписиОтсутствияДокумента(Документ: ОснованиеОтсутствия.Ссылка)
    знч Запрос = Запрос{
        Выбрать 
            ЗаписиОтсутствий.Ссылка Как Ссылка 
         Из 
            ЗаписиОтсутствий Как ЗаписиОтсутствий
         Где 
            ЗаписиОтсутствий.Документ == %Документ}
    исп РезультатЗапроса = Запрос.Выполнить() 
    для РезультатЗапросаСтрока из РезультатЗапроса
        РезультатЗапросаСтрока.Ссылка.ЗагрузитьОбъект().Удалить()
    ;
;

метод ЗаполнитьЗаписьОтсутствия(ЗаписьОтсутствияОбъект: ЗаписиОтсутствий.Объект, Данные: ЗаписьОтсутствия)

    ЗаписьОтсутствияОбъект.ФизическоеЛицо = Данные.ФизическоеЛицо
    ЗаписьОтсутствияОбъект.Причина = Данные.Причина
    ЗаписьОтсутствияОбъект.Начало = Данные.Начало
    ЗаписьОтсутствияОбъект.Окончание = Данные.Окончание
    ЗаписьОтсутствияОбъект.ДатаНачала = Данные.ДатаНачала
    ЗаписьОтсутствияОбъект.ДатаОкончания = Данные.ДатаОкончания
    ЗаписьОтсутствияОбъект.Внутрисменное = Данные.Внутрисменное
    ЗаписьОтсутствияОбъект.СпособСвязи = Данные.СпособСвязи
    ЗаписьОтсутствияОбъект.Документ = Данные.Документ
    ЗаписьОтсутствияОбъект.Комментарий = Данные.Комментарий

;