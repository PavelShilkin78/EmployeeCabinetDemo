импорт Компания
импорт Компания::Согласования
импорт ОбщиеСтандартные
импорт ФизическиеЛица
импорт ЛичныеКабинетыПользователей

@Обработчик
метод ПослеСоздания()
    Инициализировать()
    МенеджерГлобальныхКлиентскихСобытий.ПодключитьОбработчик(метод (Источник, Параметры) ->
        выбор Источник
        когда ФизЛица.ТРЕБУЕТСЯ_ОБНОВЛЕНИЕ_ДАННЫХ_ФИЗИЧЕСКОГО_ЛИЦА
            ПрочитатьИнформациюОбОтсутствиях()
        когда Согласования.ДОКУМЕНТ_СОГЛАСОВАН, Согласования.ДОКУМЕНТ_ОТКЛОНЕН
            ПослеИзмененияКоличестваНесогласованныхДокументов(Параметры)
        ;
        возврат
    ;)
;

метод Инициализировать()
    пер УстановленноеФизическоеЛицо: ФизическиеЛица.Ссылка? = ЛичныеКабинетыПользователей.ПараметрыСеанса().ТекущееФизическоеЛицо
    если УстановленноеФизическоеЛицо == Неопределено
        возврат
    ;
    ПрочитатьИнформациюОбОтсутствиях()
;

метод ПослеИзмененияКоличестваНесогласованныхДокументов(Документ: Сущность.Ключ)
    если Документ это ОснованиеОтсутствия.Ссылка
        ПрочитатьИнформациюОбОтсутствиях()
    ;
;

метод ПрочитатьИнформациюОбОтсутствиях()
    КоличествоНесогласованныхОтсутствий = ПрочитатьИнформациюОбОтсутствиях(
        ЛичныеКабинетыПользователей.ПараметрыСеанса().ТекущееФизическоеЛицо, 
        Компания.ПараметрыСеанса().ПодразделенияРуководителя
    )
;

метод ОтсутствияПояснение(): Строка
    если КоличествоНесогласованныхОтсутствий == 0
        возврат "Нет заявлений, требующих вашего согласования."
    ;
    если КоличествоНесогласованныхОтсутствий == 1
        возврат "Одно заявление требует вашего согласования."
    ;
    возврат "Есть заявления, которые требуют вашего согласования."
;

метод ОтсутствияНетВидимость(): Булево
    возврат КоличествоНесогласованныхОтсутствий == 0
;

метод ОтсутствияЕстьВидимость(): Булево
    возврат не ОтсутствияНетВидимость()
;

@НаСервере @ДоступноСКлиента
статический метод ПрочитатьИнформациюОбОтсутствиях(ТекущееФизическоеЛицо: ФизическиеЛица.Ссылка?, Подразделения: ЧитаемыйМассив<СтруктураПредприятия.Ссылка>): Число
    возврат Согласования.КоличествоДокументовНаСогласовании(ТекущееФизическоеЛицо)
;