импорт ИнтерфейсныеКомпонентыСтандартные
импорт СтандартноеПриложение
импорт УниверсальныеМеханизмы
импорт ФизическиеЛица

@Обработчик
метод ПослеСоздания()
    Компоненты.СпискиОтсутствий.ТекущаяСтраница = Компоненты.СтраницаМои
    КомандыСоздания = Отсутствия.СформироватьКомандыСоздания()
    ИнтерфейсныеКомпоненты.ИнициализироватьПодчиненныеКомпоненты(Компоненты)
;

метод СпискиОтсутствийПриСменеСтраницы(Источник: Страницы, Событие: СобытиеСДанными<Страница>)
    СтатистикаИспользованияПриложения.ЗарегистрироватьЦель("ВсеОтсутствияМоиОтсутствияПереключение")
;

метод ПанельМинимальнаяШирина(): Число
    возврат ОсновнойИнтерфейс.МинимальнаяШиринаПанелейПодразделов()
;

метод ПанельМаксимальнаяШирина(): Число
    возврат ОсновнойИнтерфейс.МаксимальнаяШиринаПанелейПодразделов()
;

метод МоиОтсутствияГруппаЗаголовок(): Строка
    возврат (КлиентскоеУстройство.ВидИнтерфейса == ВидИнтерфейса.Телефон ? "Мои" : "Мои отсутствия")
;

метод ОтсутствияКоллегГруппаЗаголовок(): Строка
    возврат (КлиентскоеУстройство.ВидИнтерфейса == ВидИнтерфейса.Телефон ? "Коллеги" : "Отсутствия коллег")
;

метод ПросмотрЗаявленияВидимость(): Булево
    возврат Компоненты.СпискиОтсутствий.ТекущаяСтраница == Компоненты.СтраницаМои
;

метод ПросмотрОтсутствияВидимость(): Булево
    возврат не ПросмотрЗаявленияВидимость()
;

метод ТекущееОтутствиеВидимость(): Булево
    возврат не ОсновнойИнтерфейс.РаботаНаУзкомУстройстве()
;

метод ОтсутствияПриАктивацииСтроки(Источник: ЗаявленияОбОтсутствииКомпонент, Событие: СобытиеСДанными<ОснованиеОтсутствия.Ссылка?>)
    пер ЗаявлениеИзменилось: Булево = (ТекущееМоеЗаявление != Событие.Данные)
    ТекущееМоеЗаявление = Событие.Данные
    если ОсновнойИнтерфейс.РаботаНаУзкомУстройстве()
        ОткрытьФормуЗаявления()
    иначе если ЗаявлениеИзменилось
        УстановитьТекущееМоеЗаявление()
    ;
;

метод УстановитьТекущееМоеЗаявление()
    знч Сервисы = ДокументОтсутствия.ПолучитьСервисы()
    для Сервис из Сервисы
        знч Компонент = Сервис.Компонент(ТекущееМоеЗаявление)
        если Компонент != Неопределено
            ВставитьЗаявление(Компонент)
        ;
    ;
;

метод ВставитьЗаявление(Компонент: Компонент)
    знч Контейнер = Компоненты.ЗаявлениеКнопка.Содержимое
    если Контейнер.Размер() > 1
        Контейнер.Удалить(Контейнер[0])
    ;
    Контейнер.Вставить(0, Компонент)
;

метод ОткрытьФормуЗаявления(): Булево
    пер Действие: СтандартныеФормы.ДействиеВФорме?
    
    знч Сервисы = ДокументОтсутствия.ПолучитьСервисы()
    для Сервис из Сервисы
        знч Форма = Сервис.Форма(ТекущееМоеЗаявление)
        если Форма != Неопределено
            Действие = Форма.ОткрытьВМодальномОкне()
        ;
    ;
    если Действие == СтандартныеФормы.ДействиеВФорме.Удален
        ТекущееМоеЗаявление = Неопределено
    ;
    возврат (Действие == СтандартныеФормы.ДействиеВФорме.Изменен)
;

метод ОбщийСписокОтсутствийПриАктивацииСтроки(Источник: ОбщийСписокОтсутствийКомпонент, Событие: СобытиеСДанными<ОтсутствиеСотрудникаКомпонентДанных?>)
    знч Отсутствующий: ФизическиеЛица.Ссылка = Событие.Данные.ФизическоеЛицо
    ТекущийОтсутствующий = Отсутствующий
    пер ЗаявлениеИзменилось = Ложь
    знч ДокументСобытия = Событие.Данные.Документ
    ЗаявлениеИзменилось = (ТекущееЗаявление != ДокументСобытия)
    ТекущееЗаявление = ДокументСобытия
    
    Компоненты.ОтсутствияСотрудника.Инициализировать()
;

метод ИзменитьПриНажатии(Источник: Кнопка, Событие: СобытиеПриНажатии)
    если ОткрытьФормуЗаявления()
        УстановитьТекущееМоеЗаявление()
    ;
;

метод ЗаявлениеЗаглушкаВидимость(): Булево
    возврат ТекущееМоеЗаявление == Неопределено
;

метод ЗаявлениеКнопкаВидимость(): Булево
    возврат не ЗаявлениеЗаглушкаВидимость()
;

метод ПросмотрЗаявленияВертикальноеПоложениеДочерних(): ВыравниваниеПоВертикали | Авто
    возврат ЗаявлениеЗаглушкаВидимость() ? ВыравниваниеПоВертикали.Центр : ВыравниваниеПоВертикали.Верх
;

метод ПросмотрОтсутствияВертикальноеПоложениеДочерних(): ВыравниваниеПоВертикали | Авто
    возврат ОтутствиеЗаглушкаВидимость() ? ВыравниваниеПоВертикали.Центр : ВыравниваниеПоВертикали.Верх
;

метод ОтутствиеЗаглушкаВидимость(): Булево
    возврат ТекущийОтсутствующий == Неопределено
;

метод ОтсутствияСотрудникаВидимость(): Булево
    возврат не ОтутствиеЗаглушкаВидимость()
;

метод ЗаявлениеКнопкаЗаголовок(): Строка
    если ТекущееМоеЗаявление == Неопределено
        возврат ""
    иначе 
        возврат СостояниеДокумента(ТекущееМоеЗаявление как Справочник.Ссылка) == СостоянияДокумента.Черновик ? "Изменить" : "Открыть"
    ;

    
;

@НаСервере
@ДоступноСКлиента
статический метод СостояниеДокумента(Ссылка: Справочник.Ссылка): СостоянияДокумента?
    возврат СостДокументов.ДанныеСостоянияДокумента(Ссылка).Состояние
;