импорт ОбщиеСтандартные

//Область РазрешенияДоступа

@Обработчик
метод ВычислитьРазрешенияДоступа(): Массив<РазрешениеДоступа>
    возврат ДанныеФизическихЛицПоставщикОграничений.ПолучитьСервис().ЛичныеДанныеРазрешения()
;

@Обработчик
метод ВычислитьРазрешенияДоступаДляОбъектов(Элементы: ЧитаемыйМассив<ЛичныеДанныеФизическихЛиц.ДанныеРасчетаРазрешений>): ЧитаемоеСоответствие<ЛичныеДанныеФизическихЛиц.ДанныеРасчетаРазрешений, ЧитаемаяКоллекция<РазрешениеДоступа>>
    возврат ДанныеФизическихЛицПоставщикОграничений.ПолучитьСервис().ЛичныеДанныеРазрешенияДляОбъектов(Элементы)
;

//КонецОбласти

//Область СлужебныйПрограммныйИнтерфейс

@ВПроекте
метод Заполнить(ЛичныеДанные: ЛичныеДанныеФизическихЛиц.Объект, ДанныеФизлица: ФизЛица.ДанныеФизлица)
    ЛичныеДанные.ДатаРождения = ДанныеФизлица.ПерсональныеДанные.ДатаРождения
    ЛичныеДанные.МестоРождения = ДанныеФизлица.ПерсональныеДанные.МестоРождения
    
    ЛичныеДанные.ИНН = ДанныеФизлица.ПерсональныеДанные.ИНН
    ЛичныеДанные.СНИЛС = ДанныеФизлица.ПерсональныеДанные.СНИЛС
    
    ЛичныеДанные.ВидУдостоверенияЛичности = ДанныеФизлица.УдостоверениеЛичности.Вид
    ЛичныеДанные.ДокументСерия = ДанныеФизлица.УдостоверениеЛичности.Серия
    ЛичныеДанные.ДокументНомер = ДанныеФизлица.УдостоверениеЛичности.Номер
    ЛичныеДанные.ДокументВыдавшийОрган = ДанныеФизлица.УдостоверениеЛичности.ВыдавшийОрган
    ЛичныеДанные.ДокументКодВыдавшегоОргана = ДанныеФизлица.УдостоверениеЛичности.КодВыдавшегоОргана
    ЛичныеДанные.ДокументДатаВыдачи = ДанныеФизлица.УдостоверениеЛичности.ДатаВыдачи
    
    ЛичныеДанные.АдресРегистрации = ДанныеФизлица.ПерсональныеДанные.АдресРегистрации
    ЛичныеДанные.АдресМестаПроживания = ДанныеФизлица.ПерсональныеДанные.АдресМестаПроживания
    ЛичныеДанные.ДокументСтрана = ДанныеФизлица.УдостоверениеЛичности.Страна
;

@ВПодсистеме
метод ЛичныеДанныеФизическогоЛица(ФизическоеЛицо: ФизическиеЛица.Ссылка): ЛичныеДанныеФизическихЛиц.Ссылка?
    пер СсылкаПоФизлицу: ЛичныеДанныеФизическихЛиц.Ссылка?
    знч Запрос = Запрос{
        Выбрать Ссылка
        Из ЛичныеДанныеФизическихЛиц
        Где Владелец == %ФизическоеЛицо}
    исп Результат = Запрос.Выполнить()
    для СтрокаРезультата из Результат
        СсылкаПоФизлицу = СтрокаРезультата.Ссылка
    ;
    возврат СсылкаПоФизлицу
;

//КонецОбласти

//Область ОбработчикиОбновления

@ВПроекте
метод ЗаполнитьДанныеФизическихЛиц(
        ЗапрашиваемыеДанные: ФизЛица.ЗапрашиваемыеДанныеФизическихЛиц, 
        ДанныеФизическихЛиц: Соответствие<ФизическиеЛица.Ссылка, ФизЛица.ДанныеФизлица>)
        
    пер ЗапрашиваемыеПоляТаблицы: Строка = ЗапрашиваемыеПоляТаблицы(ЗапрашиваемыеДанные)
    если не ЗапрашиваемыеПоляТаблицы.Пусто()
        пер Запрос = новый ПроизвольныйЗапрос() // текст запроса динамический
        ЗапрашиваемыеПоляТаблицы += ",Владелец"
        Запрос.Текст = 
            "Выбрать %ЗапрашиваемыеПоляТаблицы
            Из ЛичныеДанныеФизическихЛиц
            Где Владелец В (&ФизическиеЛица)"
        Запрос.УстановитьПараметр("ФизическиеЛица", ДанныеФизическихЛиц.Ключи())
        исп Результат = Запрос.Выполнить()
        для РезультатСтрока из Результат
            ЗаполнитьПоРезультатуЗапроса(ЗапрашиваемыеДанные, ДанныеФизическихЛиц, РезультатСтрока)
        ;
    ;
;

метод ЗапрашиваемыеПоляТаблицы(ЗапрашиваемыеДанные: ФизЛица.ЗапрашиваемыеДанныеФизическихЛиц): Строка
    пер ЗапрашиваемыеПоляТаблицы: Строка
    если ЗапрашиваемыеДанные.ПерсональныеДанные
        ЗапрашиваемыеПоляТаблицы = МенеджерСтрок.ДобавитьСтрокуСРазделителем(ЗапрашиваемыеПоляТаблицы, "ДатаРождения,ИНН,СНИЛС,МестоРождения,АдресРегистрации,АдресМестаПроживания")
    ;
    если ЗапрашиваемыеДанные.УдостоверениеЛичности
        ЗапрашиваемыеПоляТаблицы = МенеджерСтрок.ДобавитьСтрокуСРазделителем(ЗапрашиваемыеПоляТаблицы, "ВидУдостоверенияЛичности,ДокументСерия,ДокументНомер,ДокументДатаВыдачи,ДокументВыдавшийОрган,ДокументКодВыдавшегоОргана,ДокументСтрана")
    ;
    если ЗапрашиваемыеДанные.ЛичнаяКонтактнаяИнформация
        ЗапрашиваемыеПоляТаблицы = МенеджерСтрок.ДобавитьСтрокуСРазделителем(ЗапрашиваемыеПоляТаблицы, "СкайпУдалить") 
    ;
    возврат ЗапрашиваемыеПоляТаблицы
;

метод ЗаполнитьПоРезультатуЗапроса(
        ЗапрашиваемыеДанные: ФизЛица.ЗапрашиваемыеДанныеФизическихЛиц, 
        ДанныеФизическихЛиц: Соответствие<ФизическиеЛица.Ссылка, ФизЛица.ДанныеФизлица>, 
        СтрокаРезультата: СтрокаРезультатаПроизвольногоЗапроса)
    
    пер ДанныеФизическогоЛица: ФизЛица.ДанныеФизлица = ДанныеФизическихЛиц[СтрокаРезультата.Владелец]
    если ЗапрашиваемыеДанные.ПерсональныеДанные
        ДанныеФизическогоЛица.ПерсональныеДанные.ДатаРождения = СтрокаРезультата.ДатаРождения
        ДанныеФизическогоЛица.ПерсональныеДанные.ИНН = СтрокаРезультата.ИНН
        ДанныеФизическогоЛица.ПерсональныеДанные.СНИЛС = СтрокаРезультата.СНИЛС
        ДанныеФизическогоЛица.ПерсональныеДанные.МестоРождения = СтрокаРезультата.МестоРождения
        ДанныеФизическогоЛица.ПерсональныеДанные.АдресРегистрации = СтрокаРезультата.АдресРегистрации
        ДанныеФизическогоЛица.ПерсональныеДанные.АдресМестаПроживания = СтрокаРезультата.АдресМестаПроживания
    ;
    если ЗапрашиваемыеДанные.УдостоверениеЛичности
        ДанныеФизическогоЛица.УдостоверениеЛичности.Вид  = СтрокаРезультата.ВидУдостоверенияЛичности
        ДанныеФизическогоЛица.УдостоверениеЛичности.Серия = СтрокаРезультата.ДокументСерия
        ДанныеФизическогоЛица.УдостоверениеЛичности.Номер = СтрокаРезультата.ДокументНомер
        ДанныеФизическогоЛица.УдостоверениеЛичности.ДатаВыдачи = СтрокаРезультата.ДокументДатаВыдачи
        ДанныеФизическогоЛица.УдостоверениеЛичности.ВыдавшийОрган = СтрокаРезультата.ДокументВыдавшийОрган
        ДанныеФизическогоЛица.УдостоверениеЛичности.КодВыдавшегоОргана = СтрокаРезультата.ДокументКодВыдавшегоОргана
        ДанныеФизическогоЛица.УдостоверениеЛичности.Страна = СтрокаРезультата.ДокументСтрана
    ;
    если ЗапрашиваемыеДанные.ЛичнаяКонтактнаяИнформация
        ДанныеФизическогоЛица.ЛичнаяКонтактнаяИнформация.Скайп = СтрокаРезультата.СкайпУдалить
    ;
;

//КонецОбласти