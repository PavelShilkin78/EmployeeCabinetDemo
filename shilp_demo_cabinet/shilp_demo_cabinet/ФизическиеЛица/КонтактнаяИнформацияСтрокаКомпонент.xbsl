импорт ИнтерфейсныеКомпонентыСтандартные

метод КонтактнаяИнформацияСтрокаКомпонентВидимость(): Булево
    возврат не Представление.Пусто() или Редактируется
;

метод НадписьГиперссылка(): Булево
    возврат не Ссылка.Пусто()
;

метод КартинкаАдресКартинки(): ДвоичныйОбъект.Ссылка?
    возврат Картинки[СпособСвязи]
;

метод КартинкаЦвет(): Цвет
    возврат БиблиотекаЦветов.Цвет(ВидЦветаПоСпособуСвязи())
;

метод ИконкаСФономВидЦвета(): БиблиотекаЦветов.ВидыЦвета
    возврат ВидЦветаПоСпособуСвязи()
;

метод ВидЦветаПоСпособуСвязи(): БиблиотекаЦветов.ВидыЦвета
    пер ВидЦвета: БиблиотекаЦветов.ВидыЦвета
    выбор СпособСвязи 
    когда Email
        ВидЦвета = БиблиотекаЦветов.ВидыЦвета.Оранжевый
    когда Skype
        ВидЦвета = БиблиотекаЦветов.ВидыЦвета.Синий
    когда Sms
        ВидЦвета = БиблиотекаЦветов.ВидыЦвета.Болотный
    когда Звонок
        ВидЦвета = БиблиотекаЦветов.ВидыЦвета.Болотный
    ;
    возврат ВидЦвета
;

метод НадписьЗаголовок(): Строка
    если ЗаголовокСтроки != ""
        возврат ЗаголовокСтроки
    ;
    
    пер Надпись: Строка
    выбор СпособСвязи 
    когда Email
        Надпись = "Написать"
    когда Skype
        Надпись = "Скайп"
    когда Sms
        Надпись = "Отправить sms"
    когда Звонок
        Надпись = "Позвонить"
    ;
    возврат Надпись
;

метод СсылкаПриНажатии(Источник: Надпись, Событие: СобытиеПриНажатии): Булево
    СтатистикаИспользованияПриложения.ЗарегистрироватьЦель("КонтактнаяИнформация%{СпособСвязи.ВСтроку()}")
    возврат Ложь
;

метод НеРедактируется(): Булево
    возврат не Редактируется
;

метод ПредставлениеПриИзменении(Источник: ПолеВвода<Строка>, Событие: СобытиеПриИзменении<Строка>)
    ПриИзменении(новый СобытиеПриИзменении<Строка>(Событие.НовоеЗначение, Событие.СтароеЗначение))
;