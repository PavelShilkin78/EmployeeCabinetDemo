// Контракт ИнициализируемыйКомпонент

@ВПроекте
@Реализация
метод Инициализировать()
    если ФизическоеЛицо != Неопределено
        Данные = ДанныеКонтактнойИнформации(ФизическоеЛицо)
    ;
    если этот.ЗаголовокИнформации != Неопределено
        этот.Заголовок = этот.ЗаголовокИнформации
    ;
    если ПросмотрВладельцем
        этот.Заголовок = "Мои контакты"
    ;
;

// Конец ИнициализируемыйКомпонент

@ВПроекте
метод ЕстьЛичнаяКонтактнаяИнформация(): Булево
    возврат Данные.ЛичныйМобильныйТелефон != ""
            или Данные.ДомашнийТелефон != ""
            или Данные.ЛичнаяЭлектроннаяПочта != ""
            или Данные.Скайп != ""
;

метод КомандаРедактировать(Команда: ОбычнаяКоманда)
    Редактируется = Истина    
;

метод КомандаЗавершитьРедактирование(Команда: ОбычнаяКоманда)
    ЗаписатьЛичнуюКонтактнуюИнформацию(ФизическоеЛицо, Данные)
    Редактируется = Ложь
;

метод КомандаРедактироватьВидимость(): Булево
    возврат не Редактируется и ПросмотрВладельцем
;

метод НеПросмотрВладельцем(): Булево
    возврат не ПросмотрВладельцем
;

метод ЛичныйМобильныйТелефонПозвонитьСсылка(): Строка
    если Данные.ЛичныйМобильныйТелефон == ""
        возврат ""
    иначе
        возврат "tel:%{Данные.ЛичныйМобильныйТелефон}"
    ;
;

метод ЛичныйМобильныйТелефонСмсСсылка(): Строка
    если Данные.ЛичныйМобильныйТелефон == ""
        возврат ""
    иначе
        возврат "sms:%{Данные.ЛичныйМобильныйТелефон}"
    ;        
;

метод ДомашнийТелефонПозвонитьСсылка(): Строка
    если Данные.ДомашнийТелефон == ""
        возврат ""
    иначе
        возврат "tel:%{Данные.ДомашнийТелефон}"
    ;
;

метод ЛичнаяЭлектроннаяПочтаНаписатьСсылка(): Строка
    если Данные.ЛичнаяЭлектроннаяПочта == ""
        возврат ""
    иначе
        возврат "mailto:%{Данные.ЛичнаяЭлектроннаяПочта}"
    ;
;

метод ЗаглушкаВидимость(): Булево
    возврат ПросмотрВладельцем и не ЕстьЛичнаяКонтактнаяИнформация() и не Редактируется    
;

@НаСервере @ДоступноСКлиента
статический метод ДанныеКонтактнойИнформации(ФизическоеЛицо: ФизическиеЛица.Ссылка?): ДанныеКонтактнойИнформации
    пер Данные: ДанныеКонтактнойИнформации
    если ФизическоеЛицо != Неопределено
        знч ЗапрашиваемыеДанные: ФизЛица.ЗапрашиваемыеДанныеФизическихЛиц
        ЗапрашиваемыеДанные.ЛичнаяКонтактнаяИнформация = Истина
        знч ДанныеФизлица = ФизЛица.ДанныеФизическогоЛица(ФизическоеЛицо, ЗапрашиваемыеДанные)
        Данные.Скайп = ДанныеФизлица.ЛичнаяКонтактнаяИнформация.Скайп
        Данные.ЛичныйМобильныйТелефон = ДанныеФизлица.ЛичнаяКонтактнаяИнформация.МобильныйТелефон
        Данные.ДомашнийТелефон = ДанныеФизлица.ЛичнаяКонтактнаяИнформация.ДомашнийТелефон
        Данные.ЛичнаяЭлектроннаяПочта = ДанныеФизлица.ЛичнаяКонтактнаяИнформация.ЭлектроннаяПочта
    ;
    возврат Данные 
;

@НаСервере @ДоступноСКлиента
статический метод ЗаписатьЛичнуюКонтактнуюИнформацию(ФизическоеЛицо: ФизическиеЛица.Ссылка, ДанныеКонтактнойИнформации: ДанныеКонтактнойИнформации)
    пер ЛичныеДанные = новый ЛичныеКонтакты.КонтактнаяИнформация(
        ДомашнийТелефон = ДанныеКонтактнойИнформации.ДомашнийТелефон,
        Скайп = ДанныеКонтактнойИнформации.Скайп,
        ЭлектроннаяПочта = [новый ЛичныеКонтактыСтрока(ДанныеКонтактнойИнформации.ЛичнаяЭлектроннаяПочта, Истина)],
        МобильныйТелефон = [новый ЛичныеКонтактыСтрока(ДанныеКонтактнойИнформации.ЛичныйМобильныйТелефон, Истина)]
    )
    ЛичныеКонтакты.ЗаписатьКонтактыВладельца(ФизическоеЛицо, ЛичныеДанные)
;