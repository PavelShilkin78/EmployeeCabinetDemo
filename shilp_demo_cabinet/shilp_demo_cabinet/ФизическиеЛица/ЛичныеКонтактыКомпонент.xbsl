

@ВПроекте
@НаКлиенте @НаСервере
перечисление ВидКонтактнойИнформации
    МобильныйТелефон,
    ДомашнийТелефон,
    ЭлектроннаяПочта,
    Скайп
;

@ВПроекте
@НаКлиенте @НаСервере
структура ДанныеСтроки
    обз пер Вид: ВидКонтактнойИнформации
    обз пер Значение: Строка
;


@ВПроекте
статический метод КонтактнаяИнформацияВДанныеСтроки(КонтактнаяИнформация: ЛичныеКонтакты.КонтактнаяИнформация): Массив<ДанныеСтроки>
    знч Данные = новый Массив<ДанныеСтроки>()
    для МобильныйТелефон из КонтактнаяИнформация.МобильныйТелефон
        Данные.Добавить(новый ЛичныеКонтактыКомпонент.ДанныеСтроки(ЛичныеКонтактыКомпонент.ВидКонтактнойИнформации.МобильныйТелефон, МобильныйТелефон.Данные))
    ;
    если не КонтактнаяИнформация.ДомашнийТелефон.Пусто()
        Данные.Добавить(новый ЛичныеКонтактыКомпонент.ДанныеСтроки(ЛичныеКонтактыКомпонент.ВидКонтактнойИнформации.ДомашнийТелефон, КонтактнаяИнформация.ДомашнийТелефон))
    ;
    для ЭлектроннаяПочта из КонтактнаяИнформация.ЭлектроннаяПочта
        Данные.Добавить(новый ЛичныеКонтактыКомпонент.ДанныеСтроки(ЛичныеКонтактыКомпонент.ВидКонтактнойИнформации.ЭлектроннаяПочта, ЭлектроннаяПочта.Данные))
    ;
    если не КонтактнаяИнформация.Скайп.Пусто()
        Данные.Добавить(новый ЛичныеКонтактыКомпонент.ДанныеСтроки(ЛичныеКонтактыКомпонент.ВидКонтактнойИнформации.Скайп, КонтактнаяИнформация.Скайп))
    ;
    возврат Данные
;

@ВПроекте
метод УстановитьКонтакты(КонтактнаяИнформация: ЛичныеКонтакты.КонтактнаяИнформация, РежимПросмотра: Булево = Ложь)
    этот.РежимПросмотра = РежимПросмотра
    Контакты.Очистить()
    Контакты.ДобавитьВсе(КонтактнаяИнформацияВДанныеСтроки(КонтактнаяИнформация))
;

@ВПроекте
метод Обновить()
    Компоненты.Список.Обновить()
;

метод Заголовок(): Строка
    возврат РежимПросмотра ? "" : "Контакты"
;

@ВПроекте
метод ПолучитьКонтакты(): ЛичныеКонтакты.КонтактнаяИнформация
    знч КонтактнаяИнформация = новый ЛичныеКонтакты.КонтактнаяИнформация()
    для ДанныеСтроки из Контакты
        выбор ДанныеСтроки.Вид
        когда ЛичныеКонтактыКомпонент.ВидКонтактнойИнформации.МобильныйТелефон
            КонтактнаяИнформация.МобильныйТелефон.Добавить(новый ЛичныеКонтактыСтрока(Данные = ДанныеСтроки.Значение))
        когда ЛичныеКонтактыКомпонент.ВидКонтактнойИнформации.ДомашнийТелефон
            КонтактнаяИнформация.ДомашнийТелефон = ДанныеСтроки.Значение
        когда ЛичныеКонтактыКомпонент.ВидКонтактнойИнформации.ЭлектроннаяПочта
            КонтактнаяИнформация.ЭлектроннаяПочта.Добавить(новый ЛичныеКонтактыСтрока(Данные = ДанныеСтроки.Значение))
        когда ЛичныеКонтактыКомпонент.ВидКонтактнойИнформации.Скайп
            КонтактнаяИнформация.Скайп = ДанныеСтроки.Значение
        ;
    ;    
    возврат КонтактнаяИнформация
;

метод Данные(): Массив<ДанныеСтроки>
    возврат Контакты
;

метод КомандыДобавления():  Массив<Команда|ФрагментКомандногоИнтерфейса<Команда>|ГруппаКомандногоИнтерфейса<Команда>>
    знч КомандыДобавления = новый Массив<Команда|ФрагментКомандногоИнтерфейса<Команда>|ГруппаКомандногоИнтерфейса<Команда>>()
    пер ЕстьДомашнийТелефон = Ложь
    пер ЕстьСкайп = Ложь
    для Элемент из Контакты
        выбор Элемент.Вид
        когда ВидКонтактнойИнформации.ДомашнийТелефон
            ЕстьДомашнийТелефон = Истина
        когда ВидКонтактнойИнформации.Скайп
            ЕстьСкайп = Истина
        ;
    ;
    если не ЕстьДомашнийТелефон
        КомандыДобавления.Добавить(
        новый ОбычнаяКоманда(
            Представление = "Домашний телефон",
            Обработчик = &ДобавитьДомашнийТелефон,
            Изображение = Ресурс{СпособыСвязи/Звонок.svg}.Ссылка
            )
        ) 
    ;
    КомандыДобавления.Добавить(
        новый ОбычнаяКоманда(
            Представление = "Мобильный телефон",
            Обработчик = &ДобавитьМобильныйТелефон,
            Изображение = Ресурс{СпособыСвязи/Звонок.svg}.Ссылка
        )
    )
    КомандыДобавления.Добавить(
        новый ОбычнаяКоманда(
            Представление = "Электронная почта",
            Обработчик = &ДобавитьЭлектроннуюПочту,
            Изображение = Ресурс{СпособыСвязи/EMail.svg}.Ссылка
        )
    )
    если не ЕстьСкайп
        КомандыДобавления.Добавить(
        новый ОбычнаяКоманда(
            Представление = "Skype",
            Обработчик = &ДобавитьСкайп,
            Изображение = Ресурс{СпособыСвязи/Skype.svg}.Ссылка
            )
        ) 
    ;
    возврат КомандыДобавления 
;

метод КонтактнаяИнформацияПриИзменении()
    ПриИзменении(новый СобытиеКомпонента())
;

метод ДобавитьДомашнийТелефон(Источник: ОбычнаяКоманда)
    КонтактнаяИнформацияПриИзменении()
    Контакты.Добавить(новый ДанныеСтроки(ВидКонтактнойИнформации.ДомашнийТелефон, ""))
;

метод ДобавитьМобильныйТелефон(Источник: ОбычнаяКоманда)
    КонтактнаяИнформацияПриИзменении()
    Контакты.Добавить(новый ДанныеСтроки(ВидКонтактнойИнформации.МобильныйТелефон, ""))
;

метод ДобавитьЭлектроннуюПочту(Источник: ОбычнаяКоманда)
    КонтактнаяИнформацияПриИзменении()
    Контакты.Добавить(новый ДанныеСтроки(ВидКонтактнойИнформации.ЭлектроннаяПочта, ""))
;

метод ДобавитьСкайп(Источник: ОбычнаяКоманда)
    КонтактнаяИнформацияПриИзменении()
    Контакты.Добавить(новый ДанныеСтроки(ВидКонтактнойИнформации.Скайп, ""))
;

метод РастягиваниеСписка(): Булево | Авто
    возврат РежимПросмотра ? Истина : Авто
;

метод ПослеСозданияСтроки(Источник: Список<ИсточникДанныхМассив<ДанныеСтроки>>, Событие: СобытиеСДанными<СтрокаСписка<ДанныеСтроки>>)
    (Событие.Данные как ЛичныеКонтактыСтрокаСписка).Редактируется = не РежимПросмотра
    (Событие.Данные как ЛичныеКонтактыСтрокаСписка).ПриУдалении = &ПриНажатииУдаления
    (Событие.Данные как ЛичныеКонтактыСтрокаСписка).ПриИзменении = &ПриИзменении
;

метод ПриНажатииУдаления(Источник: ЛичныеКонтактыСтрокаСписка, Данные: ЛичныеКонтактыКомпонент.ДанныеСтроки)
    КонтактнаяИнформацияПриИзменении()
    Контакты.Удалить(Данные)
;

метод ПриИзменении(Источник: ЛичныеКонтактыСтрокаСписка, Событие: СобытиеКомпонента)
    КонтактнаяИнформацияПриИзменении()
;

метод ЗаглушкаВидимость(): Булево
    возврат не РежимПросмотра и Контакты.Пусто()
;