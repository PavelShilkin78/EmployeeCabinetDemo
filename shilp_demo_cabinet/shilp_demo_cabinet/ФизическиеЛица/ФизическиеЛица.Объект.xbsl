импорт ОбщиеСтандартные


//Область ОбработчикиСобытий

@Обработчик
@ВыполнятьПриЗагрузкеДанных
метод ПередЗаписью(ПредыдущееЗначение: ФизическиеЛица.Данные, ПараметрыЗаписи: ФизическиеЛица.ПараметрыЗаписи)
    ЗаполнитьНаименование()
    
    если не ЭтоНовый()
        если ПредыдущееЗначение.Фотография != Фотография или Миниатюра == Неопределено
            ОбновитьМиниатюру()
        ; 
    ;

;

@Обработчик
@ВыполнятьПриЗагрузкеДанных
метод ПередУдалением(ПараметрыУдаления: ФизическиеЛица.ПараметрыУдаления)
    знч Сервисы = ФизическиеЛицаОбработчикСобытий.ПолучитьСервисы()
    для Сервис из Сервисы
        Сервис.ПередУдалениемФизлица(Ссылка)
    ;
;

//КонецОбласти

//Область реализация контракта УчастникВзаимодействия

@ВПроекте
@Реализация
метод Аватар(): ДвоичныйОбъект.Ссылка?
    если Миниатюра != Неопределено
        возврат МенеджерСущности.ЗначениеРеквизитаОбъекта(Миниатюра, "Хранилище") как ДвоичныйОбъект.Ссылка
    ;
    возврат Неопределено
;
//КонецОбласти

//Область СлужебныеПроцедурыИФункции

метод ЗаполнитьНаименование()
    Наименование = Фамилия.Сократить()
    если не Имя.Сократить().Пусто()
        Наименование += " " + Имя.Сократить()
    ;
    если не Отчество.Сократить().Пусто()
        Наименование += " " + Отчество.Сократить()
    ;
;

метод ОбновитьМиниатюру()
    если Фотография == Неопределено
        если Миниатюра != Неопределено
            знч НаименованиеФайлаМиниатюры = МенеджерСущности.ЗначениеРеквизитаОбъекта(Миниатюра, "Наименование")
            если НаименованиеФайлаМиниатюры != Неопределено
                и не (НаименованиеФайлаМиниатюры как Строка).НачинаетсяС("Миниатюра по умолчанию - ")
                
                ФизическиеЛица.УдалитьфайлМиниатюры(Миниатюра)
                Миниатюра = Неопределено
            ;
        ;
    иначе
        знч НоваяМиниатюра = ФизическиеЛица.МиниатюраФотографии(Фотография)
        если НоваяМиниатюра != Неопределено
            Миниатюра = НоваяМиниатюра
        ;
    ;
    если Миниатюра == Неопределено
        Миниатюра = ФизическиеЛица.МиниатюраПоУмолчанию(Ссылка, Фамилия, Имя)
    ;
;

//КонецОбласти