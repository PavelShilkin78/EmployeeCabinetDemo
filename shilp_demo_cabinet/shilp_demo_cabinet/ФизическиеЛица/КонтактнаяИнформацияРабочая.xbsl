// Контракт ИнициализируемыйКомпонент

@ВПроекте
@Реализация
метод Инициализировать()
    если ФизическоеЛицо != Неопределено
        Данные = ДанныеКонтактнойИнформации(ФизическоеЛицо)
    ;
    если этот.ЗаголовокИнформации != Неопределено
        этот.Заголовок = этот.ЗаголовокИнформации
    ;
    если ПросмотрВладельцем
        этот.Заголовок = "Мои рабочие контакты"
    ;
;

// Конец ИнициализируемыйКомпонент

@ВПроекте
метод ЕстьРабочаяКонтактнаяИнформация(): Булево
    возврат Данные.МобильныйТелефон != ""
            или Данные.РабочийТелефон != ""
            или Данные.ЭлектроннаяПочта != ""
;

метод НеПросмотрВладельцем(): Булево
    возврат не ПросмотрВладельцем
;

метод ЭлектроннаяПочтаНаписатьСсылка(): Строка
    возврат "mailto:%{Данные.ЭлектроннаяПочта}"
;

метод РабочийТелефонПозвонитьСсылка(): Строка
    возврат "tel:%{Данные.РабочийТелефон}"
;

метод МобильныйТелефонПозвонитьСсылка(): Строка
    возврат "tel:%{Данные.МобильныйТелефон}"
;

метод МобильныйТелефонСмсСсылка(): Строка
    возврат "sms:%{Данные.МобильныйТелефон}"
;

@НаСервере @ДоступноСКлиента
статический метод ДанныеКонтактнойИнформации(ФизическоеЛицо: ФизическиеЛица.Ссылка?): ДанныеКонтактнойИнформации
    пер Данные: ДанныеКонтактнойИнформации
    если ФизическоеЛицо != Неопределено
        знч ЗапрашиваемыеДанные: ФизЛица.ЗапрашиваемыеДанныеФизическихЛиц
        ЗапрашиваемыеДанные.РабочаяКонтактнаяИнформация = Истина
        знч ДанныеФизлица = ФизЛица.ДанныеФизическогоЛица(ФизическоеЛицо, ЗапрашиваемыеДанные)
        Данные.МобильныйТелефон = ДанныеФизлица.РабочаяКонтактнаяИнформация.МобильныйТелефон
        Данные.РабочийТелефон = ДанныеФизлица.РабочаяКонтактнаяИнформация.Телефон
        Данные.ЭлектроннаяПочта = ДанныеФизлица.РабочаяКонтактнаяИнформация.ЭлектроннаяПочта
    ;
    возврат Данные 
;