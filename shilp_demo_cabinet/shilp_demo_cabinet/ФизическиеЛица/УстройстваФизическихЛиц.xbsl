импорт СтандартноеПриложение

конст СТРОКА_ПО_ВИД_ПОДПИСЧИКА: ЧитаемоеСоответствие<ВидПодписчикаДоставляемыхУведомлений, Строка> = { 
        ВидПодписчикаДоставляемыхУведомлений.Fcm : "FCM", 
        ВидПодписчикаДоставляемыхУведомлений.Apns : "APNS",
        ВидПодписчикаДоставляемыхУведомлений.Hms : "HMS"}

//Область РазрешенияДоступа

@Обработчик
метод ВычислитьРазрешенияДоступа(): Массив<РазрешениеДоступа>
    возврат ДанныеФизическихЛицПоставщикОграничений.ПолучитьСервис().УстройстваФизлицРазрешения()
;

@Обработчик
метод ВычислитьРазрешенияДоступаДляОбъектов(Элементы: ЧитаемыйМассив<УстройстваФизическихЛиц.ДанныеРасчетаРазрешений>): ЧитаемоеСоответствие<УстройстваФизическихЛиц.ДанныеРасчетаРазрешений, ЧитаемаяКоллекция<РазрешениеДоступа>>
    возврат ДанныеФизическихЛицПоставщикОграничений.ПолучитьСервис().УстройстваФизлицРазрешенияДляОбъектов(Элементы)
;

//КонецОбласти

метод ВидПодписчикаВСтроку(ВидПодписчика: ВидПодписчикаДоставляемыхУведомлений): Строка
    возврат СТРОКА_ПО_ВИД_ПОДПИСЧИКА[ВидПодписчика]
;


метод СтрокуВВидПодписчика(ВидПодписчика: Строка): ВидПодписчикаДоставляемыхУведомлений
    пер ВидПодписчикаПоСтроке: Соответствие<Строка, ВидПодписчикаДоставляемыхУведомлений>
    для КлючИЗначение из СТРОКА_ПО_ВИД_ПОДПИСЧИКА
         ВидПодписчикаПоСтроке.Вставить(КлючИЗначение.Значение, КлючИЗначение.Ключ)
    ;
    возврат ВидПодписчикаПоСтроке[ВидПодписчика]
;

@ВПроекте
метод ЗарегистрироватьУстройство(ФизическоеЛицо: ФизическиеЛица.Ссылка, ИдентификаторПодписчика: ИдПодписчикаДоставляемыхУведомлений)
    пер Устройство: УстройстваФизическихЛиц.Объект? = УстройствоФизическогоЛицаПоИдентификатору(ИдентификаторПодписчика.ИдУстройства)
    если Устройство == Неопределено
        пер НовоеУстройство: УстройстваФизическихЛиц.Объект = новый УстройстваФизическихЛиц.Объект()
        НовоеУстройство.Владелец = ФизическоеЛицо
        НовоеУстройство.ВидПодписчика = ВидПодписчикаВСтроку(ИдентификаторПодписчика.ВидПодписчика)
        НовоеУстройство.ИдУстройства = ИдентификаторПодписчика.ИдУстройства
        НовоеУстройство.ИдПриложения = ИдентификаторПодписчика.ИдПриложения
        НовоеУстройство.ИспользоватьПесочницу = ИдентификаторПодписчика.ИспользоватьПесочницу
        НовоеУстройство.Использование = Истина
        НовоеУстройство.Записать()
        Журналирование.ЗарегистрироватьСобытие("ЗарегистрированоУстройство", 
            "Вид подписчика: %{НовоеУстройство.ВидПодписчика}
            ИдУстройства: %{НовоеУстройство.ИдУстройства}
            ИдПриложения: %{НовоеУстройство.ИдПриложения}")
    иначе если Устройство.Владелец != ФизическоеЛицо
        выбросить новый ИсключениеВыполнения("Изменился владелец устройства")
    иначе если не Устройство.Использование 
        Устройство.Использование = Истина
        Устройство.Записать()
    ;
;

метод УстройствоФизическогоЛицаПоИдентификатору(ИдентификаторУстройства: Строка): УстройстваФизическихЛиц.Объект?
    пер УстройствоФизическогоЛицаПоИдентификатору: УстройстваФизическихЛиц.Объект?
    знч Запрос = Запрос{
        Выбрать 
            Ссылка Как Ссылка
        Из УстройстваФизическихЛиц Как УстройстваФизическихЛиц
        Где УстройстваФизическихЛиц.ИдУстройства == %ИдентификаторУстройства}
    исп Результат = Запрос.Выполнить()
    знч РезультатСтроки = новый ЧитаемыйМассив(Результат)
    если (не РезультатСтроки.Пусто())
        УстройствоФизическогоЛицаПоИдентификатору = РезультатСтроки[0].Ссылка.ЗагрузитьОбъект()
    ;
    возврат УстройствоФизическогоЛицаПоИдентификатору
;

@ВПроекте 
метод ИдентификаторыПодписчика(ФизическоеЛицо: ФизическиеЛица.Ссылка): Массив<ИдПодписчикаДоставляемыхУведомлений>
    пер ИдентификаторыПодписчика: Массив<ИдПодписчикаДоставляемыхУведомлений>
    знч Запрос = Запрос{
        Выбрать 
            ВидПодписчика,
            ИдУстройства,
            ИспользоватьПесочницу,
            ИдПриложения
        Из 
            УстройстваФизическихЛиц 
        Где 
            Владелец == %ФизическоеЛицо}
    исп Результат = Запрос.Выполнить()
    для РезультатСтрока из Результат
        пер ИдентификаторПодписчика: ИдПодписчикаДоставляемыхУведомлений = новый ИдПодписчикаДоставляемыхУведомлений()
        ИдентификаторПодписчика.ИдПриложения = РезультатСтрока.ИдПриложения
        ИдентификаторПодписчика.ИдУстройства = РезультатСтрока.ИдУстройства
        ИдентификаторПодписчика.ВидПодписчика = СтрокуВВидПодписчика(РезультатСтрока.ВидПодписчика)
        ИдентификаторПодписчика.ИспользоватьПесочницу = РезультатСтрока.ИспользоватьПесочницу
        ИдентификаторыПодписчика.Добавить(ИдентификаторПодписчика)
    ;
    возврат ИдентификаторыПодписчика
;

@ВПроекте
метод УстановитьИспользованиеУстройств(ИдентификаторыУстройств: ЧитаемыйМассив<Строка>, Использование: Булево)
    если ИдентификаторыУстройств.Пусто()
        возврат
    ;
    знч Запрос = Запрос{
        Выбрать 
            Ссылка Как Ссылка
        Из УстройстваФизическихЛиц Как УстройстваФизическихЛиц
        Где 
            УстройстваФизическихЛиц.ИдУстройства В (%ИдентификаторыУстройств) И 
            УстройстваФизическихЛиц.Использование != %Использование}
    исп Результат = Запрос.Выполнить()
    для РезультатСтрока из Результат
        пер Устройство: УстройстваФизическихЛиц.Объект = РезультатСтрока.Ссылка.ЗагрузитьОбъект()
        Устройство.Использование = Использование
        Устройство.Записать()
    ;        
;