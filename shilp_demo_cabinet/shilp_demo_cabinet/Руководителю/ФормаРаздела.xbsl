импорт Документы
импорт ГрафикОтпусков
импорт ГрафикОтпусковСотрудников
импорт СтандартноеПриложение
импорт Компания
импорт ИнтерфейсныеКомпонентыСтандартные
импорт Отпуск

@Обработчик
метод ПослеСоздания()
    ИнтерфейсныеКомпоненты.ИнициализироватьПодчиненныеКомпоненты(Компоненты)
    ДоступныеВидыДокументов = ДоступныеВидыДокументов()
;

метод ГрафикОтпусковГруппаВидимость(): Булево
    возврат Компоненты.ГрафикОтпусков.ЕстьДанные    
;

метод ФормаРазделаМаксимальнаяШирина(): Число
    возврат Содержимое.МаксимальнаяШирина
;

метод ФормаРазделаМинимальнаяШирина(): Число
    возврат Содержимое.МинимальнаяШирина
;

метод ПустойРазделВидимость(): Булево
    пер ГрафикОтпусков = Ложь
    пер РабочиеГруппы = Ложь
    для Элемент из ДоступныеВидыДокументов
        выбор Элемент.Значение
        когда ВидыДокументов.ГрафикОтпусков
            ГрафикОтпусков = Истина   
        когда ВидыДокументов.РабочиеГруппы
            РабочиеГруппы = Истина
        ;             
    ;
    если не (Компоненты.ДокументыНаСогласование.Пусто
        и Компоненты.ДокументыПодчиненных.Пусто)
        возврат Ложь
    иначе если ГрафикОтпусков 
        возврат Ложь
    иначе если РабочиеГруппы
        возврат Ложь
    иначе
        возврат Истина
    ;
;

метод ДокументыВидимость(): Булево
    возврат ВидДокумента == ВидыДокументов.Документы
;

метод ДокументыПодчиненныхГруппаВидимость(): Булево
    возврат ДокументыВидимость() и ПолученныеДокументы.ПараметрыСеанса().НапоминатьОПолученныхДокументахПодчиненных
;

метод ВкладкаГрафикОтпусковВидимость(): Булево
    возврат ВидДокумента == ВидыДокументов.ГрафикОтпусков
;

метод СоставлениеГрафикаОтпусковГруппаВидимость(): Булево
    возврат ГрафикОтпусков::ГрафикОтпусков.ПараметрыСеанса().ФункциональныеОпции.ИспользованиеПодсистемы
            и ГрафикОтпусковСотрудников.ПараметрыСеанса().КампанияСоставленияГрафикаОтпусковАктивна
;

метод ВладкаРабочиеГруппыВидимость(): Булево
    возврат ВидДокумента == ВидыДокументов.РабочиеГруппы
;

метод ПустойРазделПояснение(): Строка
    пер Пояснение: Строка
    выбор
    когда Отпуск.ПараметрыСеанса().ФункциональныеОпции.ИнформацияОГрафикеОтпусков 
        и ПолученныеДокументы.ПараметрыСеанса().НапоминатьОПолученныхДокументахПодчиненных
        
        Пояснение = "Здесь будут согласования отпусков и отсутствий подчиненных, список документов, с которыми необходимо ознакомить подчиненных, график отпусков подразделения."
    когда Отпуск.ПараметрыСеанса().ФункциональныеОпции.ИнформацияОГрафикеОтпусков
        Пояснение = "Здесь будут согласования отпусков и отсутствий подчиненных, список документов, с которыми необходимо ознакомить подчиненных."
    иначе
        Пояснение = "Здесь будут согласования отпусков и отсутствий подчиненных."
    ;
    возврат Пояснение
;

метод ПанельМаксимальнаяШирина(): Число
    возврат ОсновнойИнтерфейс.МаксимальнаяШиринаПанелейПодразделов()
;

метод ПанельМинимальнаяШирина(): Число
    возврат ОсновнойИнтерфейс.МинимальнаяШиринаПанелейПодразделов()
;

@ВПодсистеме
перечисление ВидыДокументов
    Документы умолчание,
    ГрафикОтпусков,
    РабочиеГруппы 
;

@ВПодсистеме
метод ДоступныеВидыДокументов(): Массив<ЭлементСпискаЗначений<ФормаРаздела.ВидыДокументов>> 
    пер ЭлементыВидовДокументов: Массив<ЭлементСпискаЗначений<ФормаРаздела.ВидыДокументов>>
    пер Элемент = новый ЭлементСпискаЗначений(
        Представление = ПредставлениеВидаДокумента(ВидыДокументов.Документы),
        Значение = ВидыДокументов.Документы
    )
    ЭлементыВидовДокументов.Добавить(Элемент)
    
    если  Отпуск.ПараметрыСеанса().ФункциональныеОпции.ИнформацияОГрафикеОтпусков 
        или ГрафикОтпусковСотрудников.ПараметрыСеанса().КампанияСоставленияГрафикаОтпусковАктивна
        Элемент = новый ЭлементСпискаЗначений(
            Представление = ПредставлениеВидаДокумента(ВидыДокументов.ГрафикОтпусков),
            Значение = ВидыДокументов.ГрафикОтпусков
        )
        ЭлементыВидовДокументов.Добавить(Элемент)
    ;
    
    если Компания.ПараметрыСеанса().ФункциональныеОпции.ИспользоватьРабочиеГруппы
        Элемент = новый ЭлементСпискаЗначений(
            Представление = ПредставлениеВидаДокумента(ВидыДокументов.РабочиеГруппы),
            Значение = ВидыДокументов.РабочиеГруппы
        )
        ЭлементыВидовДокументов.Добавить(Элемент)
    ;
      
    возврат ЭлементыВидовДокументов 
;

метод ВкладкаДокументыОриентация(): ОриентацияСодержимого
    возврат ОриентацияВкладок()
;

метод ОриентацияВкладок(): ОриентацияСодержимого
    если ОсновнойИнтерфейс.РаботаНаУзкомУстройстве()
        возврат ОриентацияСодержимого.Вертикальная
    иначе
        возврат ОриентацияСодержимого.Горизонтальная
    ;
;

метод ПредставлениеВидаДокумента(ВидДокумента: ВидыДокументов): Строка
    пер Представление: Строка
    если КлиентскоеУстройство.ШиринаОкнаПриложения > 400
        выбор ВидДокумента
        когда ГрафикОтпусков
            Представление = "График отпусков"
        когда Документы
            Представление = "Документы"
        когда РабочиеГруппы
            Представление = "Рабочие группы"
        ;
    иначе
        выбор ВидДокумента
        когда ГрафикОтпусков
            Представление = "Отпуска"
        когда Документы
            Представление = "Документы"
        когда РабочиеГруппы
            Представление = "Группы"
        ;
    ;
    возврат Представление
;