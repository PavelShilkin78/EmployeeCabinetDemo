импорт ИнтерфейсныеКомпонентыСтандартные
импорт ОбщиеСтандартные
импорт СтандартноеПриложение
импорт УниверсальныеМеханизмы

@ВПроекте
@Реализация
метод Инициализировать()
    
    если Объект == Неопределено
        ЭтоНовый = Истина
        возврат
    ;
    МасштабПредставления = (((ШиринаКонтейнераЗаявления() - 2 * Метрики.РАЗМЕР_ПОЛУИНТЕРВАЛА) / ПредставленияДокументов.МИНИМАЛЬНАЯ_ШИРИНА_ЗАЯВЛЕНИЯ) * 100).ЦелаяЧасть() / 100
    МасштабПредставления = МасштабПредставления > 1 ? 1 : МасштабПредставления
    
    знч Данные = ДанныеПредставленийОбъекта(Объект)
    ЭтоНовый = Данные.ЭтоНовый
    Представления = Данные.Представления
    ПредставленияИдентифкаторов = Данные.ПредставленияИдентификаторов
    УстановитьПредставления()
;

метод УстановитьПредставления()
    пер ПервыйИдентификатор: Строка? = Неопределено
    Компоненты.ТекущийВариант.СписокВыбора.Очистить()
    для ПредставлениеИдентифкатора из ПредставленияИдентифкаторов
        если Представления.СодержитКлюч(ПредставлениеИдентифкатора.Ключ)
            если ПервыйИдентификатор == Неопределено
                ПервыйИдентификатор = ПредставлениеИдентифкатора.Ключ
            ;
            пер СтрокаСписка = новый ЭлементСпискаЗначений<Строка>()
            СтрокаСписка.Значение = ПредставлениеИдентифкатора.Ключ
            СтрокаСписка.Представление = ПредставлениеИдентифкатора.Значение
            Компоненты.ТекущийВариант.СписокВыбора.Добавить(СтрокаСписка)
        ;
    ;
    
    если ПервыйИдентификатор != Неопределено
        УстановитьПредставление(ПервыйИдентификатор)
        Компоненты.ТекущийВариант.Значение = ПервыйИдентификатор
    ;
;

метод PdfДокументВидимость(): Булево
    возврат ДанныеPdf != Неопределено
;

метод ПереключательСтраницВидимость(): Булево
    возврат не ЭтоНовый и не Представления.Пусто()
;

метод КомпонентыДокументаВидимость(): Булево
    возврат ЭтоНовый или СтраницаФормы == 0
;

метод БланкВидимость(): Булево
    возврат не ЭтоНовый и СтраницаФормы == 1
;

метод ТекущийВариантВидимость(): Булево
    возврат Компоненты.ТекущийВариант.СписокВыбора.Размер() > 1
;

метод СохранитьЗаголовок(): Строка
    если Компоненты.ТекущийВариант.СписокВыбора.Размер() < 2
        возврат "Сохранить документ"
;
    возврат ""
;

метод ПроверитьПодписиЗаголовок(): Строка
    если Компоненты.ТекущийВариант.СписокВыбора.Размер() < 2
        возврат "Проверить подписи"
    ;
    возврат ""
;

метод ТекущийВариантПриИзменении(Источник: ПолеВвода<Строка>, СобытиеПриИзменении: СобытиеПриИзменении<Строка>)
    УстановитьПредставление(СобытиеПриИзменении.НовоеЗначение)
;

метод УстановитьПредставление(ИдентифкаторЭлектронногоДокумента: Строка)
    знч ФайлПредставления = ПрисоединеныйФайлПредставления(
            ЭлектронныйДокументИдентификатора(ИдентифкаторЭлектронногоДокумента))
    если ФайлПредставления != Неопределено
        если ФайлПредставления.Расширение == "pdf"
            ДанныеPdf = ФайлПредставления.Хранилище
        иначе
            СсылкаНаПредставление = ФайлПредставления!.СсылкаНаСкачивание()
        ;
    иначе
        СсылкаНаПредставление = ""
        ДанныеPdf = Неопределено
    ;
;

метод СодержимоеДокумента(): Строка
    пер ТекущееТелоДокумента: Строка
    если СсылкаНаПредставление != ""
        знч Масштаб: Строка = МасштабПредставления.ВСтроку()
        знч РазмерВПроцентах: Строка = (100/МасштабПредставления).ЦелаяЧасть().ВСтроку()
        ТекущееТелоДокумента = 
            "<div style=\"flex: 1 0 auto; align-self: stretch; height: 100\%; overflow: hidden;\">
                <iframe style=\"transform-origin: top left; transform: scale(%Масштаб); 
                    width:%РазмерВПроцентах\%; height:%РазмерВПроцентах\%\" 
                    src=\"%СсылкаНаПредставление\" frameborder=0>
                </iframe>
            </div>"
    иначе
        ТекущееТелоДокумента = "Заявление еще не сформировано"
    ;
    возврат ТекущееТелоДокумента
;

метод ПереключательСтраницПриИзменении(Источник: Переключатель<Число>, Событие: СобытиеПриИзменении<Число>)
    если Источник.Значение == 1 
        если Модифицированность()
            СсылкаНаПредставление = ""
            пер ДанныеОбъекта = новый Соответствие<Строка, Объект?>()
            знч ОбъектПредставления = (ОсновнойКомпонент как неизвестно).Объект как Справочник.Объект
            знч ТипОбъекта = ОбъектПредставления.ПолучитьТип()
            знч Свойства = ТипОбъекта.ПолучитьСвойства()
            для Свойство из Свойства
                ДанныеОбъекта.Вставить(Свойство.Имя.Русский, Свойство.Получить(ОбъектПредставления))
            ;
            пер ДополнительныеПараметрыПредставления: Соответствие<Строка, Объект>? = Неопределено
            если ОбъектСДополнительнымиПараметрамиПредставления()
                ДополнительныеПараметрыПредставления = (ОсновнойКомпонент как неизвестно).ДополнительныеПараметрыФормированияПредставлений()
            ;   
            Представления = ПредставленияДокументаНаСервере(
                ОбъектПредставления.Ссылка,
                ДанныеОбъекта, ДополнительныеПараметрыПредставления)
            УстановитьПредставления()
        ;
    ;
;

метод ЭлектронныйДокументИдентификатора(ИдентифкаторЭлектронногоДокумента: Строка): ПрисоединенныеФайлыКлиентСервер.ЭлектронныйДокумент?
    если Представления.СодержитКлюч(ИдентифкаторЭлектронногоДокумента)
        возврат Представления.Получить(ИдентифкаторЭлектронногоДокумента)
    ;
    возврат Неопределено
;

метод ПрисоединеныйФайлПредставления(ЭлектронныйДокумент: ПрисоединенныеФайлыКлиентСервер.ЭлектронныйДокумент?): ПрисоединенныйФайл?
    если ЭлектронныйДокумент != Неопределено
        если ЭлектронныйДокумент.Представления.Пусто()
            и ЭлектронныйДокумент.Оригинал != Неопределено
            
            возврат ЭлектронныйДокумент.Оригинал
        иначе если не ЭлектронныйДокумент.Представления.Пусто()
            возврат ЭлектронныйДокумент.Представления[0]
        ;
    ;
    возврат Неопределено
;

метод СохранитьТекущееПредставление()
    знч ФайлПредставления = ПрисоединеныйФайлПредставления(
            ЭлектронныйДокументИдентификатора(Компоненты.ТекущийВариант.Значение))
    если ФайлПредставления != Неопределено
        УниверсальныеМеханизмы.НачатьВыгрузкуФайла(
            ФайлПредставления.Хранилище, ФайлПредставления.Наименование, ФайлПредставления.Расширение)
    ;
;

метод Модифицированность(): Булево
    возврат (ОсновнойКомпонент как неизвестно)["РассчитаннаяМодифицированность"] 
;

конст КоэффициентМасштабирования: Число = 1.1
конст МаксимальныйМасштаб: Число = 1
конст МинимальныйМасштаб: Число = 0.3

метод УвеличитьПриНажатии(Источник: Картинка, Событие: СобытиеПриНажатии)
    если МасштабПредставления < МаксимальныйМасштаб
        МасштабПредставления = (МасштабПредставления * КоэффициентМасштабирования).Округлить(3)
        МасштабПредставления = МасштабПредставления > МаксимальныйМасштаб ? МаксимальныйМасштаб : МасштабПредставления
    ;
;

метод УменьшитьПриНажатии(Источник: Картинка, Событие: СобытиеПриНажатии)
    если МасштабПредставления > МинимальныйМасштаб
        МасштабПредставления = (МасштабПредставления / КоэффициентМасштабирования).Округлить(3)
        МасштабПредставления = МасштабПредставления < МинимальныйМасштаб ? МинимальныйМасштаб : МасштабПредставления
    ;
;

метод УвеличитьДоступность(): Булево
    возврат МасштабПредставления < МаксимальныйМасштаб
;

метод УменьшитьДоступность(): Булево
    возврат МасштабПредставления > МинимальныйМасштаб
;

метод ЗаявлениеШирина(): Число
    возврат ШиринаКонтейнераЗаявления()
;

метод ШиринаКонтейнераЗаявления(): Число
    если КлиентскоеУстройство.ВидИнтерфейса == ВидИнтерфейса.Телефон
        возврат КлиентскоеУстройство.ШиринаОкнаПриложения - 4 * Метрики.РАЗМЕР_ПОЛУИНТЕРВАЛА
    иначе
        возврат ОсновнойИнтерфейс.МаксимальнаяШиринаФормыДокумента() - 8 * Метрики.РАЗМЕР_ПОЛУИНТЕРВАЛА
    ;
;

@НаСервере @НаКлиенте
структура ДанныеПредставлений
    пер ЭтоНовый: Булево
    пер Представления: Соответствие<Строка, ПрисоединенныеФайлыКлиентСервер.ЭлектронныйДокумент>
    пер ПредставленияИдентификаторов: Соответствие<Строка, Строка>
    пер СодержимоеПредставлений: Соответствие<Строка, Строка>
;

@НаСервере @ДоступноСКлиента
статический метод ДанныеПредставленийОбъекта(Объект: Справочник.Ссылка): ДанныеПредставлений
    пер Данные = новый ДанныеПредставлений()
    знч Менеджер = МенеджерСущности.МенеджерСущностиПоСсылке(Объект)
    Данные.ЭтоНовый = не МенеджерСущности.СсылкаСуществует(Объект)
    Данные.Представления = Файлы.ЭлектронныеДокументыОбъекта(Объект как ВладелецФайлы.Ссылка, Истина)
    Данные.ПредставленияИдентификаторов = (Менеджер как неизвестно).ПредставленияИдентификаторовЭлектронныхДокументов(Объект)
    для ОписаниеПредставления из Данные.Представления
        если ОписаниеПредставления.Значение.Представления.Пусто()
            Данные.СодержимоеПредставлений.Вставить(ОписаниеПредставления.Ключ,
                ОписаниеПредставления.Значение.Оригинал!.ТекстовоеСодержимое())
        иначе
            Данные.СодержимоеПредставлений.Вставить(ОписаниеПредставления.Ключ,
                ОписаниеПредставления.Значение.Представления[0].ТекстовоеСодержимое())
        ;
    ;
    возврат Данные
;


@НаСервере @НаКлиенте
структура РезультатПроверкиПодписей
    обз пер ВсеПодписиВерны: Булево?
    обз пер Комментарий: Строка
;

@НаСервере @ДоступноСКлиента
статический метод ПредставленияДокументаНаСервере(
        СсылкаНаОбъект: Справочник.Ссылка, 
        ДанныеОбъекта: Соответствие<Строка, Объект?>,
        ДополнительныеПараметры: Соответствие<Строка, Объект>?): Соответствие<Строка, ПрисоединенныеФайлыКлиентСервер.ЭлектронныйДокумент>
    
    возврат ПредставленияДокументов.ПредставленияДокумента(
        СсылкаНаОбъект,
        ПредставленияДокументов.ТекущиеОбъект(ДанныеОбъекта),
        ДополнительныеПараметры)
;

метод ОбъектСДополнительнымиПараметрамиПредставления(): Булево
    для Сервис из ПараметрыФормированияПредставленийПоставщик.ПолучитьСервисы()
        если Сервис.ЭтоОбъектСДополнительнымиПараметрамиПредставления((ОсновнойКомпонент как неизвестно).Объект.Ссылка как Справочник.Ссылка)
            возврат Истина
        ;
    ;
    возврат Ложь
;