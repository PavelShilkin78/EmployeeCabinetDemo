импорт СтандартноеПриложение
//Область РазрешенияДоступа

@Обработчик
метод ВычислитьРазрешенияДоступа(): Массив<РазрешениеДоступа>
    возврат СтроковыеРесурсыПоставщикОграничений.ПолучитьСервис().СтроковыеРесурсыРазрешенияДоступа()
;
//КонецОбласти

@ВПодсистеме
метод ЗаполнитьГруппыРесурсов()
    знч Запрос = Запрос{
        ВЫБРАТЬ
            Ссылка
        ИЗ
            СтроковыеРесурсы
        ГДЕ
            ГруппаРесурсов == ""
    }

    для Выборка из Запрос.Выполнить()
        знч Ресурс = Выборка.Ссылка.ЗагрузитьОбъект()
        знч ПозицияРазделителя = Ресурс.Ид.Найти(УниверсальныеМеханизмы.СЕПАРАТОР_ИДЕНТИФИКАТОРА_РЕСУРСА)
        Ресурс.ГруппаРесурсов = ПозицияРазделителя > 0 ? Ресурс.Ид.ПодстрокаСНачала(ПозицияРазделителя) : Ресурс.Ид
        Ресурс.Записать()
    ;
;

@ВПодсистеме
метод УдалитьУстаревшие()
    знч СписокИдРесурсов = СписокИдРесурсов(УчитыватьИспользуемуюФункциональность = Ложь)

    исп РезультатЗапроса = 
        Запрос{
        ВЫБРАТЬ
            Ссылка,
            Ид
        ИЗ
            СтроковыеРесурсы
        ГДЕ
            НЕ Ид В (%СписокИдРесурсов)
    }.Выполнить()
    
    для СтрокаЗапроса из РезультатЗапроса
        знч Ресурс = СтрокаЗапроса.Ссылка.ЗагрузитьОбъект()
        если Ресурс != Неопределено
            Ресурс.Удалить()
            Журналирование.ЗарегистрироватьСобытие("СтроковыеРесурсыУдалитьУстаревшие", "Удален ресурс: %{Ресурс.Ид}",
                ВажностьСобытияЖурналаСобытий.Максимальная)
        ;
    ;
;

@ВПодсистеме
@ИменованныеПараметры
метод СписокИдРесурсов(УчитыватьИспользуемуюФункциональность: Булево): ЧитаемыйМассив<Строка>
    возврат СтроковыеРесурсыПоставщик.ПолучитьСервисы()
        .ПреобразоватьЛинейно(Сервис -> Сервис.ИменаРесурсовПоГруппеРесурсов(УчитыватьИспользуемуюФункциональность = Ложь)
            .ПреобразоватьЛинейно(КлючИзначение -> новый Массив(
                КлючИзначение.Значение.Преобразовать(Значение -> УниверсальныеМеханизмы.Ид(КлючИзначение.Ключ, Значение)))))
;

@ВПодсистеме
метод ЗаписатьЗначениеРесурса(ГруппаРесурсов: Строка, ИмяРесурса: Строка, Комментарий: Строка,
    СтандартноеЗначение: Строка,
    МаксимальныйРазмер: Число = 0)
    пер Ид = УниверсальныеМеханизмы.Ид(ГруппаРесурсов, ИмяРесурса)

    знч Запрос = Запрос{
        ВЫБРАТЬ
            Ссылка
        ИЗ
            СтроковыеРесурсы
        ГДЕ
            Ид == %Ид
    }
    исп РезультатЗапроса = Запрос.Выполнить()
    пер Ресурс: СтроковыеРесурсы.Объект?
    знч РезультатЗапросаСтроки = новый ЧитаемыйМассив(РезультатЗапроса)
    если (не РезультатЗапросаСтроки.Пусто())
        Ресурс = РезультатЗапросаСтроки[0].Ссылка.ЗагрузитьОбъект()
    иначе
        Ресурс = новый СтроковыеРесурсы.Объект()
        Ресурс.Ид = Ид
    ;
    если Ресурс.Комментарий != Комментарий или Ресурс.МаксимальныйРазмер != МаксимальныйРазмер или Ресурс
            .СтандартноеЗначение != СтандартноеЗначение
        если Ресурс.Значение.Пусто() или Ресурс.Значение == Ресурс.СтандартноеЗначение
            Ресурс.Значение = СтандартноеЗначение
        ;
        Ресурс.Комментарий = Комментарий
        Ресурс.МаксимальныйРазмер = МаксимальныйРазмер
        Ресурс.СтандартноеЗначение = СтандартноеЗначение
        Ресурс.ГруппаРесурсов = ГруппаРесурсов
        Ресурс.Записать()
    ;
;


@ВПодсистеме
метод ЗначенияРесурсовПодсистемы(ГруппаРесурсов: Строка, ИменаРесурсов: Массив<Строка>): Соответствие<Строка, Строка>
    пер ЗначенияРесурсов: Соответствие<Строка, Строка>

    пер Идентификаторы: Массив<Строка>
    для ИмяРесурса из ИменаРесурсов
        Идентификаторы.Добавить(УниверсальныеМеханизмы.Ид(ГруппаРесурсов, ИмяРесурса))
    ;

    знч Запрос = Запрос{
        ВЫБРАТЬ
            Ид КАК Ид,
            ВЫБОР
                КОГДА Стандартный
                    ТОГДА СтандартноеЗначение
                ИНАЧЕ Значение
            КОНЕЦ КАК Значение
        ИЗ
            СтроковыеРесурсы
        ГДЕ
            Ид В (%Идентификаторы)
    }
    исп РезультатЗапроса = Запрос.Выполнить()
    для РезультатЗапросаСтрока из РезультатЗапроса
        пер ИмяРесурса = РезультатЗапросаСтрока.Ид.Удалить(УниверсальныеМеханизмы.Ид(ГруппаРесурсов), Истина)
        ЗначенияРесурсов.Вставить(ИмяРесурса, РезультатЗапросаСтрока.Значение)
    ;

    возврат ЗначенияРесурсов
;