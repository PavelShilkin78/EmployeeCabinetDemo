импорт Компания::Согласования

@Обработчик
@ВыполнятьПриЗагрузкеДанных
метод ПередЗаписью(До: ШаблоныДокументов.Данные, ПараметрыЗаписи: ШаблоныДокументов.ПараметрыЗаписи)
    если ВидОбращения == Неопределено
        ВидОбращения = ШаблоныДокументов.НовыйВидОбращения(
            Наименование, НеобходимоСогласование и не Архивирован)
        Согласования.ЗаписатьПравилоСогласования(
            ШаблоныДокументов.ПравилоСогласованияПоУмолчанию(ВидОбращения))
    ;
;

@Обработчик
@ВыполнятьПриЗагрузкеДанных
метод ПослеЗаписи(До: ШаблоныДокументов.Данные, ПараметрыЗаписи: ШаблоныДокументов.ПараметрыЗаписи) 
    исп КонтекстДоступа.Привилегированный()
    если До.Наименование != Наименование и До.ВидОбращения != Неопределено
        ВидыСогласуемыхЗаявлений.ОбновитьПредставление(ВидОбращения, Наименование)
    ;
    пер ОбновлятьПравилаСогласования = До.ВидОбращения == Неопределено
    если (НеобходимоСогласование и не Архивирован) != (До.НеобходимоСогласование и не До.Архивирован)
        ВидыСогласуемыхЗаявлений.УстановитьИспользуемостьВидаСогласования(
            ВидОбращения, НеобходимоСогласование и не Архивирован)
        ОбновлятьПравилаСогласования = Истина
    ;
    если ОбновлятьПравилаСогласования
        Согласования.ОбновитьПравилаСогласования([ВидОбращения])
    ;
;

@Обработчик
@ВыполнятьПриЗагрузкеДанных
метод ПередУдалением(ПараметрыУдаления: ШаблоныДокументов.ПараметрыУдаления)
    если ВидОбращения != Неопределено
        знч ВидОбращенияОбъект = ВидОбращения.ЗагрузитьОбъект()
        ВидОбращенияОбъект.Удалить()
    ;
;
