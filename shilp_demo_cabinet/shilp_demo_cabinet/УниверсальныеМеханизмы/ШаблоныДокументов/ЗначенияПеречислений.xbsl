//Область РазрешенияДоступа

@Обработчик
метод ВычислитьРазрешенияДоступа(): Массив<РазрешениеДоступа>
    возврат ШаблоныДокументовПоставщикОграничений.ПолучитьСервис().ЗначенияПеречисленийРазрешенияДоступа()
;

//КонецОбласти

@ВПроекте
структура ДоступноеЗначение_3_0
    @JsonСвойство(Имя = "presentation")
    обз знч Представление: Строка
    @JsonСвойство(Имя = "value")
    обз знч Значение: Число|Строка
;

@ВПодсистеме
метод ЗагрузитьДоступныеЗначенияИзJson(Ид: Строка, Имя: Строка, ДоступныеЗначения: Массив<ДоступноеЗначение_3_0>?)
    знч НаборЗаписей = новый ЗначенияПеречислений.НаборЗаписей()
	НаборЗаписей.Фильтр.Ид.Установить(Ид)
	НаборЗаписей.Фильтр.Имя.Установить(Имя)
    если ДоступныеЗначения != Неопределено
        пер Порядок = 1
        для ДоступноеЗначение из ДоступныеЗначения
            НаборЗаписей.ДобавитьЗапись(
                Порядок        = Порядок,
                Представление  = ДоступноеЗначение.Представление,
                Значение       = ДоступноеЗначение.Значение)
            Порядок += 1
        ;
    ;
    НаборЗаписей.Записать()
;

@ВПодсистеме
метод ЗаполнитьДоступныеЗначения(Ид: Строка, Реквизит: РеквизитШаблона)
    знч Запрос = Запрос{
        Выбрать
            Представление,
            Значение
        Из
            ЗначенияПеречислений
        Где
            Ид == %Ид
            И Имя == %{Реквизит.Имя}
        Упорядочить По            
            Порядок}
    исп Результат = Запрос.Выполнить()
    для СтрокаРезультата из Результат
        Реквизит.ДоступныеЗначения.Добавить(
            новый ЭлементСпискаЗначений<Строка|Число?>(
                Значение = СтрокаРезультата.Значение,
                Представление = СтрокаРезультата.Представление
            )
        )
    ;
;