
//Область РазрешенияДоступа

@Обработчик
метод ВычислитьРазрешенияДоступа(): Массив<РазрешениеДоступа>
    возврат ШаблоныДокументовПоставщикОграничений.ПолучитьСервис().ЗначенияРеквизитовМакетовДокументовПоставщикОграничений()
;

//КонецОбласти

метод РеквизитыОбъекта(Объект: ОбъектСПечатнойФормой.Ссылка): Массив<РеквизитШаблона>
    возврат РеквизитыОбъекта(Объект, "")
;

@ВПодсистеме
метод РеквизитыОбъекта(Объект: ОбъектСПечатнойФормой.Ссылка, Ид: Строка): Массив<РеквизитШаблона>
    пер Реквизиты = новый Массив<РеквизитШаблона>()
    знч Запрос = Запрос{
        Выбрать
            ЗначенияРеквизитовМакетовДокументов.Имя Как Имя,
            ЗначенияРеквизитовМакетовДокументов.Заголовок Как Заголовок,
            РеквизитыШаблонов.ТипЗначения.ЗаменитьNull(ЗначенияРеквизитовМакетовДокументов.ТипЗначения) Как ТипЗначения,
            ЗначенияРеквизитовМакетовДокументов.Значение Как Значение,
            ЗначенияРеквизитовМакетовДокументов.ЗначениеВторое Как ЗначениеВторое
        Из
            ЗначенияРеквизитовМакетовДокументов Как ЗначенияРеквизитовМакетовДокументов
            Левое Соединение РеквизитыШаблонов Как РеквизитыШаблонов
            По ЗначенияРеквизитовМакетовДокументов.Имя == РеквизитыШаблонов.Имя
                И РеквизитыШаблонов.Ид == %Ид
        Где
            ЗначенияРеквизитовМакетовДокументов.Объект == %Объект
        Упорядочить По
            ЗначенияРеквизитовМакетовДокументов.Порядок}
    исп Результат = Запрос.Выполнить()
    для СтрокаРезультата из Результат
        пер РеквизитОбъекта = новый РеквизитШаблона()
        РеквизитОбъекта.Имя             = СтрокаРезультата.Имя
        РеквизитОбъекта.Заголовок       = СтрокаРезультата.Заголовок
        РеквизитОбъекта.ТипЗначения     = СтрокаРезультата.ТипЗначения
        РеквизитОбъекта.Значение        = СтрокаРезультата.Значение
        РеквизитОбъекта.ЗначениеВторое  = СтрокаРезультата.ЗначениеВторое
        Реквизиты.Добавить(РеквизитОбъекта)
        если РеквизитОбъекта.ТипЗначения == ТипыПользовательскихПолей.Перечисление
            ЗначенияПеречислений.ЗаполнитьДоступныеЗначения(Ид, РеквизитОбъекта)
        ;
    ;
    возврат Реквизиты
;

@ВПодсистеме
метод СохранитьРеквизитыОбъекта(Объект: ОбъектСПечатнойФормой.Ссылка, РеквизитыОбъекта: Массив<РеквизитШаблона>)
    знч НаборЗаписей = новый ЗначенияРеквизитовМакетовДокументов.НаборЗаписей()
	НаборЗаписей.Фильтр.Объект.Установить(Объект)
    пер Порядок = 1
    для ПараметрДанных из РеквизитыОбъекта
        НаборЗаписей.ДобавитьЗапись(
            Порядок        = Порядок,
            Имя            = ПараметрДанных.Имя,
            Заголовок      = ПараметрДанных.Заголовок,
            ТипЗначения    = ПараметрДанных.ТипЗначения,
            Значение       = ПараметрДанных.Значение,
            ЗначениеВторое = ПараметрДанных.ЗначениеВторое)
        Порядок += 1
    ;
    НаборЗаписей.Записать()
;

@ВПодсистеме
метод УдалитьЗначенияРеквизитовОбъекта(Объект: ОбъектСПечатнойФормой.Ссылка)
    знч НаборЗаписей = новый ЗначенияРеквизитовМакетовДокументов.НаборЗаписей()
	НаборЗаписей.Фильтр.Объект.Установить(Объект)
    НаборЗаписей.Записать()
;

@ВПроекте
структура ДополнительныйРеквизит_3_0
    @JsonСвойство(Имя = "name")
    обз знч Имя: Строка
    @JsonСвойство(Имя = "presentations")
    обз знч Заголовок: Строка 
    @JsonСвойство(Имя = "value")
    обз знч Значение: Булево|Число|Строка|ДатаВремя|?
    @JsonСвойство(Имя = "secondValue")
    знч ЗначениеВторое: ДатаВремя|?
;

@ВПроекте
метод ДополнительныеРеквизиты_3_0(Объект: ОбъектСПечатнойФормой.Ссылка): Массив<ДополнительныйРеквизит_3_0>
    знч ДополнительныеРеквизиты = новый Массив<ДополнительныйРеквизит_3_0>()
    
    знч Реквизиты = РеквизитыОбъекта(Объект)
    для Реквизит из Реквизиты
        пер Значение: Булево|Число|Строка|ДатаВремя|?
        если Реквизит.Значение это Дата
            Значение = (Реквизит.Значение как Дата).НачалоДня()
        иначе
            Значение = Реквизит.Значение как Булево|Число|Строка|?
        ;
        пер ЗначениеВторое: ДатаВремя?
        если Реквизит.ЗначениеВторое это Дата
            ЗначениеВторое = (Реквизит.ЗначениеВторое).НачалоДня()
        ;
        знч ДополнительныйРеквизит = новый ДополнительныйРеквизит_3_0(
            Имя = Реквизит.Имя,
            Заголовок = Реквизит.Заголовок,
            Значение = Значение,
            ЗначениеВторое = ЗначениеВторое)
        ДополнительныеРеквизиты.Добавить(ДополнительныйРеквизит)
    ;
    возврат ДополнительныеРеквизиты
;