импорт ОбщиеСтандартные

структура ДополнительнаяИнформация
    знч Описание: Строка
    знч ОценкаИнформации: ОценкаИнформации    
;

метод ДополнительнаяИнформация(): ДополнительнаяИнформация
    если ФайлОбъекта.ОшибкаЗагрузки
        возврат новый ДополнительнаяИнформация(
            Описание = "Ошибка загрузки",
            ОценкаИнформации = ОценкаИнформации.Отрицательная
        )
    иначе если ФайлОбъекта.ЗагрузкаФайла != Неопределено
        пер ЗагруженоПроцентов = 0
        если ФайлОбъекта.ЗагрузкаФайла.Файл.Размер > 0
            ЗагруженоПроцентов = ФайлОбъекта.ЗагрузкаФайла.Прогресс * 100 / ФайлОбъекта.ЗагрузкаФайла.Файл.Размер
        ;
        возврат новый ДополнительнаяИнформация(
            Описание = "%{ФайлОбъекта.ПредставлениеРазмера()} (%{ЗагруженоПроцентов.Округлить()}%)",
            ОценкаИнформации = ОценкаИнформации.Предупреждающая
        )          
    иначе
        знч ЧастиОписания: Массив<Строка>
        если ОтображатьДатуДобавленияФайла и ФайлОбъекта.ДатаСоздания != Неопределено
            ЧастиОписания.Добавить(МенеджерДат.ФорматМомента(ФайлОбъекта.ДатаСоздания!, "дд.ММ.гг"))
        ;
        ЧастиОписания.Добавить(ФайлОбъекта.ПредставлениеРазмера())
        возврат новый ДополнительнаяИнформация(
            Описание = ЧастиОписания.Соединить(", "),
            ОценкаИнформации = ОценкаИнформации.Обычная
        )                   
    ;    
;

метод НаименованиеПриНажатии(Источник: Надпись, Событие: СобытиеПриНажатии): Булево
    возврат ОткрытьФайл()
;

метод ИконкаПриНажатии(Источник: Картинка, Событие: СобытиеПриНажатии): Булево
    возврат ОткрытьФайл()
;

метод ОткрыватьПриНажатии(): Булево
    возврат ФайлОбъекта.ЗагрузкаФайла == Неопределено
;

метод ОткрытьФайл(): Булево
    пер Хранилище: ДвоичныйОбъект.Ссылка?
    если ФайлОбъекта.Хранилище == Неопределено
        Хранилище =  ПрисоединенныеФайлыКлиентСервер.ХранилищеФайла(ФайлОбъекта.Ссылка)
    иначе
        Хранилище = ФайлОбъекта.Хранилище
    ;
    если Хранилище != Неопределено
        пер ИмяФайлаСРасширением =  ПрисоединенныеФайлыКлиентСервер.НаименованиеФайлаПоИмениИРасширению(
                ФайлОбъекта.Наименование, ФайлОбъекта.Расширение)
        если МенеджерФайловКлиентСервер.ЭтоРасширениеФайлаКартинки(ФайлОбъекта.Расширение)
            ПросмотрКартинки.ОткрытьВоВсплывающемОкне(Заголовок = ИмяФайлаСРасширением, Изображение = Хранилище)
        иначе
            ВыгрузкаФайлов.НачатьВыгрузку(Хранилище, ИмяФайлаСРасширением)
        ;
    ;
    возврат Истина
;