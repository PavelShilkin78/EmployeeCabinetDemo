импорт УниверсальныеМеханизмы
импорт ВебApi

@ВПодсистеме
метод СкачатьВложение(ЗапросHttp: HttpСервисЗапрос)
    попытка
        знч ОжидаемыйПараметр = новый СервисыHttp.ОжидаемыйПараметр("ID", Истина, СервисыHttp.ТипыПараметраUrl.Строка)
        знч ПараметрыЗапроса = СервисыHttp.ПараметрыЗапроса(ЗапросHttp, [ОжидаемыйПараметр])
        знч ФайлСРасширением = Файлы.ФайлСРасширением(Файлы.ПолучитьСсылку(новый Ууид(ПараметрыЗапроса.Получить(ОжидаемыйПараметр.Имя) как Строка)))
        СервисыHttp.ЗаполнитьОтвет(ЗапросHttp, СервисыHttp.КодыСостояний.ОК_200)
        ЗапросHttp.Ответ.Заголовки.Установить("Content-Type", MimeТипПоРасширению(ФайлСРасширением.Расширение))
        ЗапросHttp.Ответ.Заголовки.Установить("Content-Length", ФайлСРасширением.Файл.ВСтроку())
        ЗапросHttp.Ответ.УстановитьТело(ФайлСРасширением.Файл.Ссылка)
    поймать Ошибка: Исключение
        СервисыHttp.ЗаполнитьОтвет(ЗапросHttp, Ошибка)
    ;
;

метод MimeТипПоРасширению(Расширение: Строка): Строка
    пер Тип: Строка
    выбор Расширение.ВВерхнийРегистр()
    когда "HTM", "HTML"
        Тип = "text/html"
    когда "TXT"
        Тип = "text/plain"
    когда "PDF"
        Тип = "application/pdf"
    когда "PNG"
        Тип = "image/png"
    когда "SVG"
        Тип = "image/svg+xml"
    когда "BMP"
        Тип = "image/bmp"
    иначе
        Тип = "text/plain"
    ;
    возврат Тип
;