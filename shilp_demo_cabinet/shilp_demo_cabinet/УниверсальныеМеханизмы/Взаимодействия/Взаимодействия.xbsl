
// Возвращает аватары участников сообщений
@ВПроекте
@НаСервере @ДоступноСКлиента
статический метод АватарыУчастников(Участники: ЧитаемыйМассив<УчастникВзаимодействия.Ссылка>): Соответствие<УчастникВзаимодействия.Ссылка, ДвоичныйОбъект.Ссылка>
    знч Аватары = новый Соответствие<УчастникВзаимодействия.Ссылка, ДвоичныйОбъект.Ссылка>()
    для Ссылка из Участники
        знч Объект = Ссылка.ЗагрузитьОбъект()
        Аватары.Вставить(Ссылка, Объект.Аватар())
    ;
    возврат Аватары
;

@ВПроекте
@НаСервере @ДоступноСКлиента
статический метод АватарыУчастниковВзаимодействия(ВладелецВзаимодействия: ВладелецВзаимодействия.Ссылка): Соответствие<УчастникВзаимодействия.Ссылка, ДвоичныйОбъект.Ссылка>
    знч Запрос = Запрос{
        ВЫБРАТЬ
            Взаимодействие.Участник КАК Участник
        ИЗ 
            Взаимодействие КАК Взаимодействие
        ГДЕ 
            Взаимодействие.ВладелецВзаимодействия == %ВладелецВзаимодействия
        Сгруппировать По 
            Взаимодействие.Участник 
    }
    
    знч Аватары = новый Соответствие<УчастникВзаимодействия.Ссылка, ДвоичныйОбъект.Ссылка>()
    для Элемент из Запрос.Выполнить()
        знч Объект = Элемент.Участник?.ЗагрузитьОбъект()
        знч Аватар = Объект?.Аватар()
        если Аватар != Неопределено
            Аватары.Вставить(Элемент.Участник, Аватар)
        ;
    ;
    возврат Аватары
;
