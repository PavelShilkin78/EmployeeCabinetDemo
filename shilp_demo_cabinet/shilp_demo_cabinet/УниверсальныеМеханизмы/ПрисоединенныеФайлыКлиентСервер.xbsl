импорт ОбщиеСтандартные

@ВПроекте
структура ЭлектронныйДокумент
    пер Оригинал: ПрисоединенныйФайл?
    пер ТабличныйДокументОригинала: ПрисоединенныйФайл?
    пер Представления: Массив<ПрисоединенныйФайл>?
;

@ВПроекте
метод ПредставлениеФайла(ДанныеЭлемента: ПрисоединенныйФайл): Строка

    пер Наименование: Строка = ДанныеЭлемента.Наименование
    если Наименование.Сократить().Пусто()
        Наименование = "<Наименование не задано>"
    ;
    пер Расширение = ДанныеЭлемента.Расширение
    если не Расширение.Пусто()
        Расширение = "." + Расширение
    ;
    пер Размер = ДанныеЭлемента.Размер
    пер Представление = "%Наименование%Расширение  (%{МенеджерФайловКлиентСервер.ПредставлениеРазмера(Размер)})"

    если ДанныеЭлемента.ЗагрузкаФайла != Неопределено

        пер УжеЗагружено = 0
        если ДанныеЭлемента.ЗагрузкаФайла.Файл.Размер > 0
            УжеЗагружено = ДанныеЭлемента.ЗагрузкаФайла.Прогресс * 100 / ДанныеЭлемента.ЗагрузкаФайла.Файл.Размер
        ;
        Представление = "%{УжеЗагружено.Округлить()}\% %Представление"

     ;

    возврат Представление

;

@ВПроекте
метод НаименованиеФайлаПоИмениИРасширению(Имя: Строка, Расширение: Строка): Строка
    Имя = Имя.Сократить()
    Расширение = Расширение.Сократить()
    пер ИмяБезРасширения = Имя
    знч ПозицияТочки = ИмяБезРасширения.НайтиСКонца(".")
    если ПозицияТочки > 0
        ИмяБезРасширения = ИмяБезРасширения.ПодстрокаСНачала(ПозицияТочки)
    ;
    если Расширение.Пусто()
        возврат ИмяБезРасширения 
    ;
    возврат "%ИмяБезРасширения.%Расширение"
;

@ВПроекте
@НаСервере @ДоступноСКлиента
метод ХранилищеФайла(ПрисоединенныйФайл: Файлы.Ссылка?): ДвоичныйОбъект.Ссылка?
    пер СвойстваФайла = Файлы.СведенияОФайле(ПрисоединенныйФайл)
    возврат СвойстваФайла.Хранилище
;

@ВПроекте
@НаСервере @ДоступноСКлиента
метод МаксимальноДопустимыйРазмерФайлаВМегабайтах(): Число
    возврат УниверсальныеМеханизмы.НастройкиПрисоединенныхФайлов().МаксимальноДопустимыйРазмерФайлаВМегабайтах 
;