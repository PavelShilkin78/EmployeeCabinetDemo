импорт ОбщиеСтандартные

@НаКлиенте
@ВПроекте
метод КнопкаВыгрузкиВCsv(
        Таблица: Таблица<ДинамическийСписок>, 
        ВыгружаемыеПоляИИхЗаголовки: ЧитаемоеСоответствие<Строка, Строка|Авто>?,
        ИмяФайла: Строка = "Выгрузка"): Кнопка
        
    возврат новый Кнопка(
            Вид = ВидКнопки.Дополнительная,
            ВыравниваниеВГруппеПоГоризонтали = ВыравниваниеПоГоризонтали.Конец,
            Команды = КомандаВыгрузкиВCsv(
                    Таблица = Таблица,
                    ВыгружаемыеПоляИИхЗаголовки = ВыгружаемыеПоляИИхЗаголовки,
                    ИмяФайла = ИмяФайла))
;

@НаКлиенте
@ВПроекте
метод КомандаВыгрузкиВCsv(
        Таблица: Таблица<ДинамическийСписок>, 
        ВыгружаемыеПоляИИхЗаголовки: ЧитаемоеСоответствие<Строка, Строка|Авто>?,
        ИмяФайла: Строка = "Выгрузка"): ОбычнаяКоманда
        
    возврат новый ОбычнаяКоманда(
            Изображение = Ресурс{Стд::Скачать.svg}.Ссылка,
            Обработчик = (Команда) -> ВCsv(
                    Таблица = Таблица, 
                    ВыгружаемыеПоляИИхЗаголовки = ВыгружаемыеПоляИИхЗаголовки,
                    ИмяФайла = ИмяФайла), 
            Представление = "")
;

@НаКлиенте
@ВПроекте
метод ВCsv(
        Таблица: Таблица<ДинамическийСписок>, 
        ВыгружаемыеПоляИИхЗаголовки: ЧитаемоеСоответствие<Строка, Строка|Авто>?,
        ИмяФайла: Строка = "Выгрузка")
    
    если ИмяФайла.Пусто()
        ИмяФайла = "Выгрузка"
    ;
        
    ВыгрузкаФайлов.НачатьВыгрузку(
            ДанныеCsv(
                    Источник = ДинамическийСписокНаСервере.ИзДинамическогоСписка(Таблица.Источник),
                    ВыгружаемыеПоляИИхЗаголовки = ВыгружаемыеПоляИИхЗаголовки), 
            "%ИмяФайла.csv")
;

@НаСервере @ДоступноСКлиента
метод ДанныеCsv(
        Источник: ДинамическийСписокНаСервере,
        ВыгружаемыеПоляИИхЗаголовки: ЧитаемоеСоответствие<Строка, Строка|Авто>?): ДвоичныйОбъект.Ссылка
        
    знч ПрототипЗапроса = Источник.ПрототипЗапроса()    
    пер Запрос = новый ПроизвольныйЗапрос(ПрототипЗапроса.Текст)
    для ПараметрЗапроса из ПрототипЗапроса.Параметры
        Запрос.УстановитьПараметр(
                ПараметрЗапроса.Имя, 
                ПараметрЗапроса.Значение)
    ;
    возврат МенеджерЗапросов.РезультатЗапросаВCsv(
            Запрос = Запрос,
            ВыгружаемыеПоляИИхЗаголовки = ВыгружаемыеПоляИИхЗаголовки)
;
