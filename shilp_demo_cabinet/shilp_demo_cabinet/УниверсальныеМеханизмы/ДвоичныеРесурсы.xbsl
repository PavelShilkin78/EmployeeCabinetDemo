импорт ОбщиеСтандартные

//Область РазрешенияДоступа

@Обработчик
метод ВычислитьРазрешенияДоступа(): Массив<РазрешениеДоступа>
    возврат ДвоичныеРесурсыПоставщикОграничений.ПолучитьСервис().ДвоичныеРесурсыРазрешенияДоступа()
;
//КонецОбласти

@ВПодсистеме
метод ЗаписатьДвоичныеДанные(Подсистема: Строка, ИмяРесурса: Строка, Хранилище: ДвоичныйОбъект.Ссылка)
    знч Ид = УниверсальныеМеханизмы.Ид(Подсистема, ИмяРесурса)
    знч Ресурс = СсылкаНаРесурс(Ид)
    пер РесурсОбъект: ДвоичныеРесурсы.Объект?
    если Ресурс != Неопределено
        РесурсОбъект = Ресурс.ЗагрузитьОбъект()
    иначе
        РесурсОбъект = новый ДвоичныеРесурсы.Объект()
        РесурсОбъект.Ид = Ид
    ;
    РесурсОбъект.Хранилище = Хранилище
    РесурсОбъект.Записать()
;

@ВПодсистеме
метод ДвоичныеДанные(Ид: Строка): ДвоичныйОбъект.Ссылка?
    знч Ресурс = СсылкаНаРесурс(Ид)
    если Ресурс != Неопределено
        возврат МенеджерСущности.ЗначениеРеквизитаОбъекта(Ресурс, "Хранилище")
    ;
    возврат Неопределено
 ;
 
@ВПодсистеме
метод ДвоичныеДанные(Подсистема: Строка, ИмяРесурса: Строка): ДвоичныйОбъект.Ссылка?
    возврат ДвоичныеДанные(УниверсальныеМеханизмы.Ид(Подсистема, ИмяРесурса))
 ;
 
метод СсылкаНаРесурс(Ид: Строка): ДвоичныеРесурсы.Ссылка?
    знч Запрос = Запрос{
        Выбрать 
            Ссылка 
        Из 
            ДвоичныеРесурсы 
        Где 
            Ид == %Ид}
    исп РезультатЗапроса = Запрос.Выполнить()
    знч РезультатЗапросаСтроки = новый ЧитаемыйМассив(РезультатЗапроса)
    если (не РезультатЗапросаСтроки.Пусто())
        возврат РезультатЗапросаСтроки[0].Ссылка
    ;
    возврат Неопределено
;