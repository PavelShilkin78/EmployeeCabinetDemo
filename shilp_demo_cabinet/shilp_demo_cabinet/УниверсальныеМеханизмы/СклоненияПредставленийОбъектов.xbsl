импорт ОбщиеСтандартные

@ВПроекте
структура СклоненияПредставленияОбъектаJson
    пер nominative: Строка
    пер genitive: Строка
    пер dative: Строка
    пер accusative: Строка
    пер instrumental: Строка
    пер prepositional: Строка
;

@ВПроекте
@НаСервере @ДоступноСКлиента
метод ПредставлениеВПадеже(Объект: Справочник.Ссылка, Падеж: Падежи): Строка
    пер Представление = ""
    
    пер Запрос = новый ПроизвольныйЗапрос(
        "Выбрать
            ФормаСлова
        Из
            СклоненияПредставленийОбъектов
        Где
            Объект == &Объект
            И Падеж == &Падеж")
    Запрос.УстановитьПараметр("Объект", Объект)
    Запрос.УстановитьПараметр("Падеж", Падеж)
    исп Результат = Запрос.Выполнить()
    знч РезультатСтроки = новый ЧитаемыйМассив(Результат)
    если (не РезультатСтроки.Пусто())
        Представление = РезультатСтроки[0].ФормаСлова
    иначе
        Представление = МенеджерСущности.ПредставлениеСсылки(Объект)
    ;
    возврат Представление
;

@ВПроекте
метод ПредставленияОбъектаВПадежах(Объект: ВладелецСклоняемогоПредставления.Ссылка): ЧитаемоеСоответствие<Падежи, Строка>
    пер ПредставленияОбъекта = новый Соответствие<Падежи, Строка>()
    пер Запрос = новый ПроизвольныйЗапрос(
        "Выбрать
            Падеж,
            ФормаСлова
        Из
            СклоненияПредставленийОбъектов
        Где
            Объект == &Объект")
    Запрос.УстановитьПараметр("Объект", Объект)
    исп Результат = Запрос.Выполнить()
    для РезультатСтрока из Результат
        ПредставленияОбъекта.Вставить(РезультатСтрока.Падеж, РезультатСтрока.ФормаСлова)
    ;
    возврат ПредставленияОбъекта
;

@ВПроекте
метод ЗаписатьСклоненияПредставления(Объект: Справочник.Ссылка, СклоненияПредставления: УниверсальныеМеханизмы.СклоненияПредставленияОбъекта)
    если не СклоненияПредставления.Именительный.Пусто()
        пер НаборЗаписей = новый СклоненияПредставленийОбъектов.НаборЗаписей()
        НаборЗаписей.Фильтр.Объект.Установить(Объект как неизвестно)
        НаборЗаписей.ДобавитьЗапись(Объект = Объект как неизвестно, Падеж = Падежи.Именительный, ФормаСлова = СклоненияПредставления.Именительный)
        НаборЗаписей.ДобавитьЗапись(Объект = Объект как неизвестно, Падеж = Падежи.Родительный, ФормаСлова = СклоненияПредставления.Родительный)
        НаборЗаписей.ДобавитьЗапись(Объект = Объект как неизвестно, Падеж = Падежи.Дательный, ФормаСлова = СклоненияПредставления.Дательный)
        НаборЗаписей.ДобавитьЗапись(Объект = Объект как неизвестно, Падеж = Падежи.Винительный, ФормаСлова = СклоненияПредставления.Винительный)
        НаборЗаписей.ДобавитьЗапись(Объект = Объект как неизвестно, Падеж = Падежи.Творительный, ФормаСлова = СклоненияПредставления.Творительный)
        НаборЗаписей.ДобавитьЗапись(Объект = Объект как неизвестно, Падеж = Падежи.Предложный, ФормаСлова = СклоненияПредставления.Предложный)
        НаборЗаписей.Записать()
    ;
;

@ВПроекте
метод СклоненияПредставленияОбъектаВJson(Ссылка: Справочник.Ссылка): СклоненияПредставленияОбъектаJson?
    пер СклоненияПредставленя: СклоненияПредставленияОбъектаJson?
    знч ВсеСклоненияПредставленя = ПредставленияОбъектаВПадежах(Ссылка как ВладелецСклоняемогоПредставления.Ссылка)
    если не ВсеСклоненияПредставленя.Пусто()
        СклоненияПредставленя = новый СклоненияПредставленияОбъектаJson()
        для СклонениеПредставленя из ВсеСклоненияПредставленя
            если СклонениеПредставленя.Ключ == Падежи.Именительный
                СклоненияПредставленя.nominative = СклонениеПредставленя.Значение
            иначе если СклонениеПредставленя.Ключ == Падежи.Родительный
                СклоненияПредставленя.genitive = СклонениеПредставленя.Значение
            иначе если СклонениеПредставленя.Ключ == Падежи.Дательный
                СклоненияПредставленя.dative = СклонениеПредставленя.Значение
            иначе если СклонениеПредставленя.Ключ == Падежи.Винительный
                СклоненияПредставленя.accusative = СклонениеПредставленя.Значение
            иначе если СклонениеПредставленя.Ключ == Падежи.Творительный
                СклоненияПредставленя.instrumental = СклонениеПредставленя.Значение
            иначе если СклонениеПредставленя.Ключ == Падежи.Предложный
                СклоненияПредставленя.prepositional = СклонениеПредставленя.Значение
            ;
        ;
    ;
    возврат СклоненияПредставленя
;

@ВПроекте
метод СклоненияПредставленияОбъектаИзJson(СклоненияПредставленийJson: СклоненияПредставленияОбъектаJson): УниверсальныеМеханизмы.СклоненияПредставленияОбъекта
    пер СклоненияПредставления = новый УниверсальныеМеханизмы.СклоненияПредставленияОбъекта()
    СклоненияПредставления.Именительный = СклоненияПредставленийJson.nominative
    СклоненияПредставления.Родительный =  СклоненияПредставленийJson.genitive
    СклоненияПредставления.Дательный =    СклоненияПредставленийJson.dative
    СклоненияПредставления.Винительный =  СклоненияПредставленийJson.accusative
    СклоненияПредставления.Творительный = СклоненияПредставленийJson.instrumental
    СклоненияПредставления.Предложный =   СклоненияПредставленийJson.prepositional
    возврат СклоненияПредставления
;
