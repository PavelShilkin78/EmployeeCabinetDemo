импорт ОбщиеСтандартные


// Контракт ИнициализируемыйКомпонент
@ВПроекте
@Реализация
метод Инициализировать()
    УстановитьФильтр()
;
// Конец ИнициализируемыйКомпонент

метод СтроковыеРесурсыПриНажатииЭлемента(Источник: Список<ДинамическийСписок>, Событие: СобытиеСДанными<СтрокаСписка<СтрокаДинамическогоСписка>>)
    РедакторСтроковыхРесурсовКарточка.ОткрытьВМодальномОкне(КлючОбъекта = Событие.Данные.ДанныеСтроки.Ключ как СтроковыеРесурсы.Ссылка)
;

метод ЗначениеЗаголовок(ДанныеЭлемента: СтрокаДинамическогоСписка): Строка
    возврат (ДанныеЭлемента.Данные["Стандартный"] как Булево) ? 
        (ДанныеЭлемента.Данные["СтандартноеЗначение"] как Строка) : 
        (ДанныеЭлемента.Данные["Значение"] как Строка)
;

метод УстановитьФильтр()
    если не ТолькоДоступные
        возврат
    ;
    
    знч ГруппыРесурсов = ГруппыРесурсов()
    если не ГруппыРесурсов.Пусто()
        Компоненты.СтроковыеРесурсы.Источник.Фильтр.Элементы.Добавить(
            новый ЭлементФильтра(
                Поле = "ГруппаРесурсов",
                ВидСравнения = ВидСравнения.ВСписке,
                Значение = ГруппыРесурсов))
    ;
;

@НаСервере @ДоступноСКлиента
статический метод ГруппыРесурсов(): Массив<Строка>
    возврат СтроковыеРесурсыПоставщик.ПолучитьСервисы()
        .ПреобразоватьЛинейно(Сервис -> 
            новый Массив<Строка>(Сервис.ИменаРесурсовПоГруппеРесурсов(УчитыватьИспользуемуюФункциональность = Истина).Ключи().ВМассив()))
;

@НаСервере @ДоступноСКлиента
статический метод СписокВыбораГруппыРесурсов(): Массив<ЭлементСпискаЗначений<Строка>>
    возврат ГруппыРесурсов()
        .Сортировать()
        .Преобразовать(ГруппаРесурсов -> новый ЭлементСпискаЗначений<Строка>(ГруппаРесурсов, ПредставлениеГруппы(ГруппаРесурсов)))
;

@НаСервере
статический метод ПредставлениеГруппы(Строка: Строка): Строка
   возврат МенеджерСтрок.ИзВерблюжьегоРегистра(Строка)
;

метод ПолеГруппаРесурсовПриИзменении(Источник: ПолеВвода<Массив<Строка>>, Событие: СобытиеПриИзменении<Массив<Строка>>)
    знч Элементы = этот.Компоненты.СтроковыеРесурсы.Источник.Фильтр.Элементы
    Элементы.Очистить()
    если не Источник.Значение.Пусто()
        Элементы.Добавить(новый ЭлементФильтра("ГруппаРесурсов", ВидСравнения.ВСписке, Источник.Значение, Истина))
    ;
;

метод ПолеГруппаРесурсовИзображение(): ДвоичныйОбъект.Ссылка?
    если этот.Компоненты.СтроковыеРесурсы.Источник.Фильтр.Элементы.Пусто()
        возврат Ресурс{Стд::ФильтрКонтур.svg}.Ссылка
    иначе
        возврат Ресурс{Стд::Фильтр.svg}.Ссылка
    ;
;