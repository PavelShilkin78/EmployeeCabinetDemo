импорт ОбщиеСтандартные

метод ВыборИзображенияПриИзменении(Источник: ВыборФайлов, Событие: СобытиеПриИзменении<ЧитаемыйМассив<ВыбранныйФайл>>)
    знч ВыбранноеИзображение = Событие.НовоеЗначение.Единственный()
    если МожноЗагрузитьИзображение(ВыбранноеИзображение)
        Изображение = ЗагрузитьИзображение(ВыбранноеИзображение)
        ПриИзменении(новый СобытиеКомпонента())
    ;
;

метод УдалитьИзображениеПриНажатии(Источник: Кнопка, Событие: СобытиеПриНажатии)
    Изображение = новый ПрисоединенныйФайл()
    ПриИзменении(новый СобытиеКомпонента())
;

метод МожноЗагрузитьИзображение(ВыбранноеИзображение: ВыбранныйФайл): Булево
    знч МаксимальныйРазмерФайла = ПрисоединенныеФайлыКлиентСервер.МаксимальноДопустимыйРазмерФайлаВМегабайтах() * МенеджерФайловКлиентСервер.МЕГАБАЙТ
    если ВыбранноеИзображение.Размер > МаксимальныйРазмерФайла
        Сообщить("Слишком большой файл %{МенеджерФайловКлиентСервер.ПредставлениеРазмера(ВыбранноеИзображение.Размер)}")
        возврат Ложь
    ;
    возврат Истина
;

метод ЗагрузитьИзображение(ВыбранноеИзображение: ВыбранныйФайл): ПрисоединенныйФайл
    знч НовоеИзображение: ПрисоединенныйФайл
    НовоеИзображение.Хранилище = ЗагрузкаФайлов.НачатьЗагрузку(ВыбранноеИзображение).ПолучитьРезультат()
    НовоеИзображение.Наименование = ВыбранноеИзображение.Имя
    НовоеИзображение.Расширение = ВыбранноеИзображение.Расширение
    если не НовоеИзображение.Расширение.Пусто()
        НовоеИзображение.Наименование = НовоеИзображение.Наименование.Заменить(
            "." + НовоеИзображение.Расширение, "")
    ;
    НовоеИзображение.Размер = ВыбранноеИзображение.Размер
    НовоеИзображение.АдресКартинкиРасширения = АдресКартинкиРасширения(ВыбранноеИзображение.Расширение)
    НовоеИзображение.ДатаСоздания = МенеджерДат.ТекущаяДатаВремя()
    возврат НовоеИзображение
;

@НаСервере @ДоступноСКлиента
статический метод АдресКартинкиРасширения(Расширение: Строка): ДвоичныйОбъект.Ссылка?
    возврат ПрисоединенныеФайлыМенеджер.АдресКартинкиРасширения(Расширение)
;