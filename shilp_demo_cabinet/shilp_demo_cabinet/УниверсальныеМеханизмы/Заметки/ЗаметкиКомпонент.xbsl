импорт ФизическиеЛица
импорт ИнтерфейсныеКомпонентыСтандартные

@ВПодсистеме
@НаКлиенте @НаСервере
структура ДанныеЗаметки
    
    пер Ссылка: ЗаметкиКОбъектам.Ссылка?
    пер ПредставлениеИмени: Строка
    пер ПредставлениеДаты: Строка
    пер ТекстЗаметки: Строка
    пер КомментарийТекущегоПользователя: Булево
    пер ПрисоединенныеФайлы: ПрисоединенныеФайлы
    пер Редактирование: Булево
;

@ВПодсистеме
@НаКлиенте
структура ДанныеФормыРедактирования
    обз знч Действие: СтандартныеФормы.ДействиеВФорме
    обз знч ДанныеЗаметки: ДанныеЗаметки
;

@ВПодсистеме
@НаКлиенте @НаСервере
структура ДанныеЗаписи
    пер Ссылка: ЗаметкиКОбъектам.Ссылка?
    пер ОбъектСохраненияЗаметки: ОбъектСохраненияЗаметки.Ссылка?
    пер Автор: ФизическиеЛица.Ссылка?
    пер ТекстЗаметки: Строка
    пер ПрисоединенныеФайлы: ПрисоединенныеФайлы
;


метод ДобавитьПриНажатии(Команда: ОбычнаяКоманда)
    пер ДанныеЗаметки = новый ДанныеЗаметки()
    ДанныеЗаметки.Редактирование = Ложь
    ОбработатьРезультатСохранения(ДанныеЗаметки)
;

метод РедактированиеКомментария(Источник: ЗаметкаСтрока, Событие: СобытиеСДанными<СтрокаДинамическогоСписка>)
    пер ДанныеЗаметки = новый ДанныеЗаметки()
    ДанныеЗаметки.ТекстЗаметки = Событие.Данные.Данные["ТекстЗаметки"] как Строка
    ДанныеЗаметки.Ссылка = Событие.Данные.Данные["Ссылка"] как ЗаметкиКОбъектам.Ссылка
    ДанныеЗаметки.ПрисоединенныеФайлы = Заметки.ПрисоединенныеФайлыВладельца(Событие.Данные.Данные["Ссылка"] как ЗаметкиКОбъектам.Ссылка)
    ДанныеЗаметки.Редактирование = Истина
    ОбработатьРезультатСохранения(ДанныеЗаметки)
;

метод ОбработатьРезультатСохранения(ДанныеЗаметки: ДанныеЗаметки)
    пер РезультатРедактирования = РедактированиеЗаметки.ОткрытьВМодальномОкне(ДанныеЗаметки = ДанныеЗаметки, ОжидатьЗакрытия = Истина)
    если РезультатРедактирования == Неопределено
        возврат
    ;
    
    выбор РезультатРедактирования.Действие
    когда Удален
        УдалитьКомментарийПослеВопроса(РезультатРедактирования.ДанныеЗаметки.Ссылка)
    когда Изменен
        СохранитьКомментарий(ДанныеЗаписи(РезультатРедактирования.ДанныеЗаметки), РезультатРедактирования.ДанныеЗаметки.Редактирование)
    ;
    
    Компоненты.СписокЗаметок.Обновить()
    Инициализировать()
;

метод ДанныеЗаписи(ДанныеЗаметки: ДанныеЗаметки): ДанныеЗаписи
    возврат новый ДанныеЗаписи(
            ДанныеЗаметки.Ссылка,
            ОбъектСохраненияЗаметки,
            ТекущееФизлицо,
            ДанныеЗаметки.ТекстЗаметки,
            ДанныеЗаметки.ПрисоединенныеФайлы)
;

@ВПроекте
@Реализация
метод Инициализировать()
    КоличествоКомментариев = КоличествоКомментариев(ОбъектСохраненияЗаметки)
;

@НаСервере @ДоступноСКлиента
статический метод КоличествоКомментариев(ОбъектСохраненияЗаметки: ОбъектСохраненияЗаметки.Ссылка?): Число
    если ОбъектСохраненияЗаметки == Неопределено
        возврат 0
    ;
    знч Запрос = Запрос{
        Выбрать
            Количество(Ссылка) как Количество
        Из
            ЗаметкиКОбъектам
        Где 
            ОбъектСохраненияЗаметки == %ОбъектСохраненияЗаметки}
    исп РезультатЗапроса = Запрос.Выполнить()
    возврат РезультатЗапроса.ПервыйИлиУмолчание()?.Количество ?? 0
;

метод КомпонентСтрокаПослеСозданияСтроки(Источник: Список<ДинамическийСписок>, Событие: СобытиеСДанными<СтрокаСписка<СтрокаДинамическогоСписка>>)
    пер ДанныеСтроки = Событие.Данные как ЗаметкаСтрока
    ДанныеСтроки.ТекущееФизлицо = ТекущееФизлицо
    ДанныеСтроки.Удаление = &УдалениеКомментария
    ДанныеСтроки.Редактирование = &РедактированиеКомментария
;

метод УдалениеКомментария(Источник: ЗаметкаСтрока, Событие: СобытиеСДанными<ЗаметкиКОбъектам.Ссылка>)
    УдалитьКомментарийПослеВопроса(Событие.Данные)
;

метод УдалитьКомментарийПослеВопроса(Комментарий: ЗаметкиКОбъектам.Ссылка)
    знч Ответ = Диалог.Вопрос(
                    "Комментарий будет удалён. Продолжить?", 
                    [КнопкаДиалога.Да, КнопкаДиалога.Нет], 
                    "Удаление")     
    если Ответ == КнопкаДиалога.Да
        УдалитьКомментарий(Комментарий)
    ;
    
    Компоненты.СписокЗаметок.Обновить()
    Инициализировать()
;

@НаСервере @ДоступноСКлиента
статический метод УдалитьКомментарий(Заметка: ЗаметкиКОбъектам.Ссылка)
    пер ЗаметкаОбъект = Заметка.ЗагрузитьОбъект()
    ЗаметкаОбъект.Удалить()
;

@НаСервере @ДоступноСКлиента
статический метод СохранитьКомментарий(Заметка: ДанныеЗаписи, Редактируется: Булево)
    пер ЗаметкаОбъект: ЗаметкиКОбъектам.Объект
    
    если Редактируется
        ЗаметкаОбъект = Заметка.Ссылка.ЗагрузитьОбъект()
    иначе
        ЗаметкаОбъект = новый ЗаметкиКОбъектам.Объект()
        ЗаметкаОбъект.МоментСоздания = Момент.Сейчас()
        ЗаметкаОбъект.ОбъектСохраненияЗаметки = Заметка.ОбъектСохраненияЗаметки
        ЗаметкаОбъект.Автор = Заметка.Автор
    ;
        
    ЗаметкаОбъект.ТекстЗаметки = Заметка.ТекстЗаметки
    ЗаметкаОбъект.МоментРедактирования = Момент.Сейчас()
    
    ЗаметкаОбъект.Записать()
    
    если не Заметка.ПрисоединенныеФайлы.Файлы.Пусто()
        Заметка.ПрисоединенныеФайлы.Владелец = ЗаметкаОбъект.Ссылка
        ПрисоединенныеФайлыМенеджер.Записать(Заметка.ПрисоединенныеФайлы)
    ;

;