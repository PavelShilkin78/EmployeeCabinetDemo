импорт ВебApi

конст МАКСИМАЛЬНО_ДОПУСТИМЫЙ_РАЗМЕР_ФАЙЛА_В_МЕГАБАЙТАХ = 30

//Область РазрешенияДоступа

@Обработчик
метод ВычислитьРазрешенияДоступа(): Массив<РазрешениеДоступа>
    возврат ФайлыПоставщикОграничений.ПолучитьСервис().НастройкиФайловРазрешенияДоступа()
    //возврат КонтрольДоступа.РазрешенияДоступаТаблицыКонтентДанныхАбонентаЧтениеВсем()
;

//КонецОбласти

//Область СлужебныйПрограммныйИнтерфейс

@ВПроекте
структура ДанныеJson
    обз знч AttachmentsMaximumSize: Число?
;

@ВПроекте
метод ТипДанныхJson(ВерсияФорматаОбмена: Версия?): Тип
    если ВерсияФорматаОбмена == Неопределено
        возврат Тип<ДанныеJson>
    иначе если ВерсияФорматаОбмена >= Версия{1.0}
        возврат Тип<ДанныеJson>
    ;
;

@ВПроекте
метод ТипКоллекцииДанныхJson(ВерсияФорматаОбмена: Версия?): Тип
    если ВерсияФорматаОбмена == Неопределено
        возврат Тип<Массив<ДанныеJson>>
    иначе если ВерсияФорматаОбмена >= Версия{1.0}
        возврат Тип<Массив<ДанныеJson>>
    ;
;

@ВПроекте
метод ТипОбъектаJson(): Строка
    возврат "attachmentsSettings"
;

@ВПроекте
метод ЗагрузитьОбъектИзJson(ДанныеJson: ДанныеJson, Сервисы: ВебApiСервер.СервисыЗагрузкиJson)
    исп Транзакции.Начать()
    знч Запрос = Запрос{
        Выбрать 
            Ссылка
        Из 
            НастройкиПрисоединенныхФайлов}

    исп РезультатЗапроса = Запрос.Выполнить() 
    пер ОбъектНастройки: НастройкиПрисоединенныхФайлов.Объект?
    знч РезультатЗапросаСтроки = новый ЧитаемыйМассив(РезультатЗапроса)
    если (не РезультатЗапросаСтроки.Пусто())
        ОбъектНастройки = РезультатЗапросаСтроки[0].Ссылка.ЗагрузитьОбъект(Истина)
    иначе
        ОбъектНастройки = новый НастройкиПрисоединенныхФайлов.Объект()
    ;
    ОбъектНастройки.МаксимальноДопустимыйРазмерФайлаВМегабайтах =
        ДанныеJson.AttachmentsMaximumSize == Неопределено ? МАКСИМАЛЬНО_ДОПУСТИМЫЙ_РАЗМЕР_ФАЙЛА_В_МЕГАБАЙТАХ : ДанныеJson.AttachmentsMaximumSize
    ОбъектНастройки.Записать()
;
//КонецОбласти

@ВПроекте
метод Настройки(): УниверсальныеМеханизмы.НастройкиПрисоединенныхФайлов
    пер Настройки: УниверсальныеМеханизмы.НастройкиПрисоединенныхФайлов
    знч Запрос = Запрос{
        Выбрать Первые 1
            МаксимальноДопустимыйРазмерФайлаВМегабайтах
        Из
            НастройкиПрисоединенныхФайлов}
    исп Результат = Запрос.Выполнить()
    знч РезультатСтроки = новый ЧитаемыйМассив(Результат)
    если (не РезультатСтроки.Пусто())
        Настройки.МаксимальноДопустимыйРазмерФайлаВМегабайтах = РезультатСтроки[0].МаксимальноДопустимыйРазмерФайлаВМегабайтах
    иначе
        исп ПривилегированныйРежим: КонтекстДоступа = КонтекстДоступа.Привилегированный()
        знч ОбъектНастройки = новый НастройкиПрисоединенныхФайлов.Объект()
        ОбъектНастройки.МаксимальноДопустимыйРазмерФайлаВМегабайтах = МАКСИМАЛЬНО_ДОПУСТИМЫЙ_РАЗМЕР_ФАЙЛА_В_МЕГАБАЙТАХ
        ОбъектНастройки.Записать()
        Настройки.МаксимальноДопустимыйРазмерФайлаВМегабайтах = ОбъектНастройки.МаксимальноДопустимыйРазмерФайлаВМегабайтах
        ПривилегированныйРежим.Закрыть()
    ;
    возврат Настройки
;