импорт Обсуждения

@ВПодсистеме
структура СостояниеПодключения
    обз знч НадписьЗаголовок: Строка
    обз знч НадписьШрифт: СтилевойШрифт
    знч НадписьКоннотация: ОценкаИнформации?
    знч НастройкаВидимость: Булево
    знч НастройкаЗаголовок: Строка
;

@Обработчик
метод ПослеСоздания()
    ОбновленыНастройкиСистемыВзаимодействия.ПодключитьОбработчик((метод () -> 
        Инициализировать()
    ;))
    
    Инициализировать()
    
;

метод Инициализировать()
    СостояниеПодключения = СостояниеПодключения()
;

метод НастройкаСистемыВзаимодействияПриНажатии(Источник: Кнопка, Событие: СобытиеПриНажатии)
    Обсуждения.ОткрытьФормуНастроек(этот)
;

метод СостояниеПодключения(): СостояниеПодключения
        
    знч НастройкиСистемыВзаимодействия = НастройкиСистемыВзаимодействия()
    
    если НастройкиСистемыВзаимодействия == Неопределено
        возврат новый СостояниеПодключения("Система взаимодействия еще не подключена. Подключение к системе взаимодействия выполняется в кадровой информационной системе.",
                                            СтилевыеШрифты.Мелкий)
    иначе если НастройкиСистемыВзаимодействия.ИдПриложения == Неопределено
        возврат новый СостояниеПодключения("Кадровая информационная система готова для совместного использования системы взаимодействия. Для завершения настройки зарегистрируйте это приложение в системе взаимодействия.",
                                            СтилевыеШрифты.Мелкий,
                                            НастройкаВидимость = Истина,
                                            НастройкаЗаголовок = "Зарегистрировать")    
    иначе если не НастройкиСистемыВзаимодействия.ИспользованиеОбсуждений
        возврат новый СостояниеПодключения("Приложение зарегистрировано в системе взаимодействия. Необходимо завершить настройку в кадровой информационной системе.",
                                            СтилевыеШрифты.Мелкий,
                                            НастройкаВидимость = Истина,
                                            НастройкаЗаголовок = "Параметры подключения")  
    иначе если НастройкиСистемыВзаимодействия.ИспользованиеОбсуждений 
        возврат новый СостояниеПодключения("Система взаимодействия подключена. Вы можете изменить регистрацию приложения в Системе взаимодействия",
                                            СтилевыеШрифты.Обычный, 
                                            НадписьКоннотация = ОценкаИнформации.Положительная,
                                            НастройкаВидимость = Истина,
                                            НастройкаЗаголовок = "Изменить регистрацию")
    иначе
        возврат новый СостояниеПодключения("Неизвестное состояние подключения к системе взаимодействия",
                                            СтилевыеШрифты.Обычный, 
                                            НадписьКоннотация = ОценкаИнформации.Отрицательная,
                                            НастройкаВидимость = Истина,
                                            НастройкаЗаголовок = "Параметры подключения")     
    ;
;

@НаСервере @НаКлиенте
структура НастройкиСистемыВзаимодействия
    обз пер ИдПриложения: Ууид?
    обз пер ИспользованиеОбсуждений: Булево
;

@НаСервере @ДоступноСКлиента
статический метод НастройкиСистемыВзаимодействия(): НастройкиСистемыВзаимодействия?
    исп ПривилегированныйРежим = КонтекстДоступа.Привилегированный() 
    знч Запрос = Запрос{
        Выбрать Первые 1
            Ссылка как Настройка,
            ИдПриложения как ИдПриложения
        Из 
            НастройкиСистемыВзаимодействия как НастройкиСистемыВзаимодействия}
        
    исп РезультатЗапроса = Запрос.Выполнить()  
    ПривилегированныйРежим.Закрыть()
    для СтрокаРезультата из РезультатЗапроса
        возврат новый НастройкиСистемыВзаимодействия(СтрокаРезультата.ИдПриложения, Обсуждения.ИспользуютсяОбсуждения())
    ;
    возврат Неопределено
;
    