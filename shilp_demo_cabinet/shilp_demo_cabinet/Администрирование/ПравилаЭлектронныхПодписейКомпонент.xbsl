импорт Криптография
импорт ФизическиеЛица
импорт УниверсальныеМеханизмы

метод ЗаглушкаВидимость(): Булево
    возврат не ПравилаСуществуют(СчетчикИзменений)
;

метод ДобавитьПравилоПриНажатии(Команда: ОбычнаяКоманда)
    знч НовоеПравилоЭлектронныхПодписей = новый ПравилоЭлектронныхПодписей()
    РедактироватьПравило(НовоеПравилоЭлектронныхПодписей)
    СчетчикИзменений += 1
;

метод ПравилаЭлектронныхПодписейПриНажатииЭлемента(Источник: Список<ДинамическийСписок>, Событие: СобытиеСДанными<СтрокаСписка<СтрокаДинамическогоСписка>>) 
    пер ПравилоЭлектронныхПодписей = новый ПравилоЭлектронныхПодписей(
        Событие.Данные.ДанныеСтроки.Данные["Владелец"] как ФизическиеЛица.Ссылка|ВладелецПравилаЭлектронныхПодписей.Ссылка,
        Событие.Данные.ДанныеСтроки.Данные["ВидЭлектроннойПодписи"] как ВидыЭлектронныхПодписей,
        Событие.Данные.ДанныеСтроки.Данные["ВариантИспользованияЭлектроннойПодписи"] как ВариантыИспользованияЭлектроннойПодписи
    )
    РедактироватьПравило(ПравилоЭлектронныхПодписей)
    СчетчикИзменений += 1
;

метод РедактироватьПравило(ПравилоЭлектронныхПодписей: ПравилоЭлектронныхПодписей)
    знч ДоступноУдаление = ПравилоЭлектронныхПодписей.Владелец != Неопределено
    ФормаРедактированияПравилаЭлектронныхПодписей.ОткрытьМодально(ПравилоЭлектронныхПодписей, ДоступноУдаление)
    Компоненты.ПравилоЭлектронныхПодписей.Обновить()
;

метод ВладелецЗаголовок(ДанныеСтроки: СтрокаДинамическогоСписка): Строка
    возврат (ДанныеСтроки.Данные["Владелец"] как неизвестно).Представление()
;

метод ВариантаИспользованияЭлектроннойПодписиЗаголовок(ДанныеСтроки: СтрокаДинамическогоСписка): Строка
    возврат (ДанныеСтроки.Данные["ВариантИспользованияЭлектроннойПодписи"] как ВариантыИспользованияЭлектроннойПодписи?)?.Представление() ?? ""
;

метод ВидЭлектроннойПодписиЗаголовок(ДанныеСтроки: СтрокаДинамическогоСписка): Строка
    возврат (ДанныеСтроки.Данные["ВидЭлектроннойПодписи"] как ВидыЭлектронныхПодписей?)?.Представление() ?? ""
;

@НаСервере @ДоступноСКлиента
статический метод ПравилаСуществуют(СчетчикИзменений: Число): Булево
    знч Запрос = Запрос{
        Выбрать Первые 1
        Истина как ПравилаСуществуют
        Из ПравилаЭлектронныхПодписей}
    исп Результат = Запрос.Выполнить()
    возврат Результат.ЕдинственныйИлиУмолчание()?.ПравилаСуществуют ?? Ложь
;