импорт КонтрольДоступа

@Обработчик
метод ВычислитьРазрешенияДоступа(): Массив<РазрешениеДоступа>
    возврат КонтрольДоступа.РазрешенияДоступаТаблицыСервисныхДанныхАбонента()
;

@ВПроекте
@НаКлиенте @НаСервере
структура ДанныеДляКастомизации
    пер НаименованиеАбонента: Строка
    пер ФиоОтветственного: Строка
    пер ЭлектроннаяПочтаОтветственного: Строка
    пер ТелефонОтветственного: Строка
;

@ВПроекте
@НаСервере
метод ДанныеДляКастомизации(): ДанныеДляКастомизации
    пер ДанныеДляКастомизации: ДанныеДляКастомизации
    знч Запрос = Запрос{
        Выбрать Первые 1
            НаименованиеАбонента как НаименованиеАбонента,
            ФиоОтветственного как ФиоОтветственного,
            ЭлектроннаяПочтаОтветственного как ЭлектроннаяПочтаОтветственного,
            ТелефонОтветственного как ТелефонОтветственного
        Из ДанныеДляКастомизации}
        
    исп Результат = Запрос.Выполнить()
    знч Параметры = Результат.ЕдинственныйИлиУмолчание()
    если Параметры != Неопределено
        ДанныеДляКастомизации = новый ДанныеДляКастомизации(
            НаименованиеАбонента = Параметры.НаименованиеАбонента, 
            ФиоОтветственного = Параметры.ФиоОтветственного,
            ЭлектроннаяПочтаОтветственного = Параметры.ЭлектроннаяПочтаОтветственного,
            ТелефонОтветственного = Параметры.ТелефонОтветственного)
    ;
    возврат ДанныеДляКастомизации
;

@ВПроекте
@НаСервере
метод СохранитьДанныеДляКастомизации(Настройки: ДанныеДляКастомизации)
    исп Транзакции.Начать()
    знч Запрос = Запрос{
        Выбрать Первые 1 
            Ссылка 
        Из ДанныеДляКастомизации}
    исп Результат = Запрос.Выполнить()
    пер ДанныеОбъект: ДанныеДляКастомизации.Объект?
    ДанныеОбъект = Результат.ЕдинственныйИлиУмолчание()?.Ссылка.ЗагрузитьОбъект(Истина) ?? новый ДанныеДляКастомизации.Объект()
    
    ДанныеОбъект.НаименованиеАбонента = Настройки.НаименованиеАбонента
    ДанныеОбъект.ФиоОтветственного = Настройки.ФиоОтветственного
    ДанныеОбъект.ЭлектроннаяПочтаОтветственного = Настройки.ЭлектроннаяПочтаОтветственного
    ДанныеОбъект.ТелефонОтветственного = Настройки.ТелефонОтветственного
    
    ДанныеОбъект.Записать()
;