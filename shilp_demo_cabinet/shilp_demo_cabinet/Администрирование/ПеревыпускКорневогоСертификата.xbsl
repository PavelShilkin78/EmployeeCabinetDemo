импорт ИнтерфейсныеКомпонентыСтандартные
импорт Криптография
импорт ЛичныеКабинетыПользователей
импорт ОбщиеСтандартные
импорт СтандартноеПриложение

@Обработчик
метод ПослеСоздания()
    СрокДействияКорневогоСертификата = СрокДействияКорневогоСертификата()
;

метод ПериодДействияЗаголовок(): Строка
    пер Сейчас = Момент.Сейчас()
    пер Окончание = МенеджерДат.ФорматМомента(МенеджерДат.ДобавитьГоды(Сейчас, СрокДействияКорневогоСертификата), "д ММММ гггг")
    возврат "Сертификат будет действителен до %Окончание"
;

метод СрокДействияКорневогоСертификатаПриИзменении(Источник: ПолеВвода<Число>, Событие: СобытиеПриИзменении<Число>)
    если СрокДействияКорневогоСертификата < 5
        СрокДействияКорневогоСертификата = 5
    ;
    если СрокДействияКорневогоСертификата > 10
        СрокДействияКорневогоСертификата = 10
    ;
    СохранитьСрокиДействияСертификатовСервер(СрокДействияКорневогоСертификата)
;

метод ФормаДокументаВысота(): Число
    возврат ОсновнойИнтерфейс.ВысотаФормыДокумента(50)
;

метод ПанельОсновнаяЗаписатьИЗакрыть(Источник: КоманднаяПанельФорм, Событие: СобытиеКомпонента)
    ОбновитьКорневойСертификатСервер()
    МенеджерГлобальныхКлиентскихСобытий.Оповестить(Криптография.ОБНОВЛЕНА_ЭЛЕКТРОННАЯ_ПОДПИСЬ)
    Закрыть()
;

метод ПанельОсновнаяСохранитьЧерновик(Источник: КоманднаяПанельФорм, Событие: СобытиеКомпонента)
    Закрыть()
;

@НаСервере @ДоступноСКлиента
статический метод СрокДействияКорневогоСертификата(): Число
    возврат Криптография.СрокиДействияСертификатов().КорневойСертификат
;

@НаСервере @ДоступноСКлиента
статический метод СохранитьСрокиДействияСертификатовСервер(КорневойСертификат: Число)
    исп ПривилегированныйРежим: КонтекстДоступа = КонтекстДоступа.Привилегированный()
    пер СрокиДействия = Криптография.СрокиДействияСертификатов()
    СрокиДействия.КорневойСертификат = КорневойСертификат
    Криптография.СохранитьСрокиДействияСертификатов(СрокиДействия)
    ПривилегированныйРежим.Закрыть()
;

@НаСервере @ДоступноСКлиента
статический метод ОбновитьКорневойСертификатСервер()
    исп ПривилегированныйРежим: КонтекстДоступа = КонтекстДоступа.Привилегированный()
    знч НастройкиКабинетов: ЛичныеКабинетыПользователей.НастройкиЛичныхКабинетов = ЛичныеКабинетыПользователей.ТекущиеНастройкиСозданияУчетныхЗаписей()
    РаботаСПодписями.СоздатьЦифровойСертификатУдостоверяющегоЦентра(НастройкиКабинетов.АдресСервиса)
    ПривилегированныйРежим.Закрыть()
;