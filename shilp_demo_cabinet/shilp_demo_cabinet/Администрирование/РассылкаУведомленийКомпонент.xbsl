импорт ОбщегоНазначения
импорт ИнтерфейсныеКомпонентыСтандартные
импорт Уведомления
импорт Приложение
импорт ПриложениеНастройки

@Обработчик
метод ПослеСоздания()
    
    знч Настройки = НастройкиРассылкиУведомлений()
    ВремяПолученияУведомлений = Настройки.ВремяПолученияУведомлений
    ОтложеннаяОтправкаУведомлений = Настройки.ИспользоватьОтложеннуюОтправку
    КоличествоСекунд = Настройки.Задержка
    если НастройкаОтравкиSmsВидимость()
        ИспользоватьSms = ИспользоватьSmsДляУведомлений()
    ;
;

метод ВремяПолученияУведомленийПриИзменении(Источник: ПолеВвода<ЗакрытыйДиапазон<Время>?>, Событие: СобытиеПриИзменении<ЗакрытыйДиапазон<Время>?>)
    если ВремяПолученияУведомлений == Неопределено
        возврат
    ;
    
    Уведомления.ПрименитьВремяПолученияУведомлений(ВремяПолученияУведомлений)
;

метод ОтложеннаяОтправкаУведомленийПриИзмененииЗначения(Источник: ФлажокРасширенный, Событие: СобытиеПриИзменении<Булево?>)
    УстановитьНастройкиОтложеннойОтправкиУведомлений()
;

метод КоличествоСекундПриИзменении(Источник: ПолеВвода<Число>, Событие: СобытиеПриИзменении<Число>)
    УстановитьНастройкиОтложеннойОтправкиУведомлений()
;

метод УстановитьНастройкиОтложеннойОтправкиУведомлений()
    УстановитьНастройкиОтложеннойОтправкиУведомлений(ОтложеннаяОтправкаУведомлений, КоличествоСекунд)
;

метод ПереключениеЗапретаSms(Источник: Флажок, Событие: СобытиеПриИзменении<Булево?>)
    если не ИспользоватьSms
        знч Ответ = Диалог.Вопрос(
                        "Пользователи не смогут получать SMS уведомления. Настройки пользователей принудительно будут заменены с SMS на PUSH уведомления. Продолжить?", 
                        [КнопкаДиалога.Да, КнопкаДиалога.Отмена], 
                        "Вы вбрали отказ от SMS", Ресурс{ПредупреждениеКонтур.svg}.Ссылка)     
        если Ответ == КнопкаДиалога.Отмена
            возврат
        ;
    ;
    ИдетПроцессОбновления = Истина
    НастройкаЗапретаSmsУведомлений(ИспользоватьSms)
    ИдетПроцессОбновления = Ложь
;

метод НастройкаОтравкиSmsВидимость(): Булево
    возврат Приложение.ПараметрыСеанса().РежимРаботы == РежимыРаботы.Локальный
;

метод ВремяПолученияУведомленийМобильныйВидимость(): Булево
    возврат КлиентскоеУстройство.ВидИнтерфейса == ВидИнтерфейса.Телефон
;

метод ВремяПолученияУведомленийСЗаголовкомВидимость(): Булево
    возврат КлиентскоеУстройство.ВидИнтерфейса != ВидИнтерфейса.Телефон
;

метод КоличествоСекундТолькоЧтение(): Булево
    возврат не ОтложеннаяОтправкаУведомлений 
;

метод ОтложеннаяОтправкаУведомленийВертикальныйИнтервал(): РазмерОтступа | Авто
    возврат КлиентскоеУстройство.ВидИнтерфейса == ВидИнтерфейса.Телефон ? РазмерОтступа.Отсутствует : Авто
;

@НаСервере @ДоступноСКлиента
статический метод НастройкиРассылкиУведомлений(): Администрирование.НастройкиРассылкиУведомлений
    знч ПериодПолученияУведомленийПоУмолчанию = Уведомления.НастройкиУведомлений(СпособыУведомления.Push).ВремяПолученияУведомлений
    знч НастройкиОтложеннойОтправки = ОбщегоНазначения.НастройкиОтложеннойОтправкиУведомлений()
    
    знч Настройки = новый Администрирование.НастройкиРассылкиУведомлений(ПериодПолученияУведомленийПоУмолчанию)

    Настройки.ИспользоватьОтложеннуюОтправку = НастройкиОтложеннойОтправки.ИспользоватьОтложеннуюОтправку
    Настройки.Задержка = НастройкиОтложеннойОтправки.Задержка
    возврат Настройки
;

@НаСервере @ДоступноСКлиента
статический метод УстановитьНастройкиОтложеннойОтправкиУведомлений(ИспользоватьОтложеннуюОтправку: Булево, Задержка: Число)
    ОбщегоНазначения.УстановитьНастройкиОтложеннойОтправкиУведомлений(ИспользоватьОтложеннуюОтправку, Задержка)
;

@ВПроекте
@НаСервере @ДоступноСКлиента
статический метод ИспользоватьSmsДляУведомлений(): Булево
    возврат Уведомления.НастройкиУведомлений(СпособыУведомления.Sms) != Неопределено
;

@ВПроекте
@НаСервере @ДоступноСКлиента
статический метод НастройкаЗапретаSmsУведомлений(ИспользоватьSms: Булево)
    если ИспользоватьSms
        Уведомления.ВключитьОтправкуSms()
    иначе
        Уведомления.ОтключитьОтправкуSms()
    ;
;

