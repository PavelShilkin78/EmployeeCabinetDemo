импорт ФизическиеЛица
импорт Компания
импорт КонтрольДоступа

//КонтрольДоступа

@Обработчик
метод ВычислитьРазрешенияДоступа(): ЧитаемаяКоллекция<РазрешениеДоступа>
   
    возврат КонтрольДоступа.РегистрСведенийЧтениеВсемРедактированиеАдминистратору()
;

//КонтрольДоступа

@ВПодсистеме
метод ОбновитьСтатусПодключенияФизическогоЛицаВСервисе(
    Организация: Организации.Ссылка, 
    ФизическоеЛицо: ФизическиеЛица.Ссылка, 
    СтатусПодключения: СтатусыПодключенияСотрудника) 
    
    исп ПривилегированныйРежим = КонтекстДоступа.Привилегированный()
    пер НаборЗаписей: ФизическиеЛицаЗарплатаСразу.НаборЗаписей
    НаборЗаписей.Фильтр.Организация.Установить(Организация)
    НаборЗаписей.Фильтр.ФизическоеЛицо.Установить(ФизическоеЛицо)
    пер Запись: ФизическиеЛицаЗарплатаСразу.Запись
    Запись.ФизическоеЛицо = ФизическоеЛицо
    Запись.Организация = Организация
    Запись.СтатусПодключения = СтатусПодключения
    НаборЗаписей.Добавить(Запись)
    НаборЗаписей.Записать()
    ПривилегированныйРежим.Закрыть() 
;    

@ВПодсистеме
@НаСервере @ДоступноСКлиента
метод СтатусПодключенияФизическогоЛица(
    Организация: Организации.Ссылка,
    ФизическоеЛицо: ФизическиеЛица.Ссылка): СтатусыПодключенияСотрудника?
    знч Запрос = Запрос{
        Выбрать
            СтатусПодключения
        Из
            ФизическиеЛицаЗарплатаСразу
        Где
            Организация == %Организация
            И ФизическоеЛицо == %ФизическоеЛицо
    }
    
    пер Результат = Запрос.Выполнить().ЕдинственныйИлиУмолчание()
    если Результат == Неопределено
        возврат Неопределено
    ;
    
    возврат Результат.СтатусПодключения
    
;

@ВПодсистеме
@НаСервере @ДоступноСКлиента
метод СтатусПодключенияТекущегоФизическогоЛица(): СтатусыПодключенияСотрудника?
    знч ФизическоеЛицо = ПользователиСистемы.ТекущееФизическоеЛицо()
    
    если ФизическоеЛицо == Неопределено
        возврат Неопределено
    ;
    
    знч Организация = Сотрудники.ОрганизацияФизическогоЛицаПоОсновномуМестуРаботы(ФизическоеЛицо)
    
    если Организация == Неопределено 
        возврат Неопределено
    ;
    
    возврат СтатусПодключенияФизическогоЛица(Организация, ФизическоеЛицо)
;