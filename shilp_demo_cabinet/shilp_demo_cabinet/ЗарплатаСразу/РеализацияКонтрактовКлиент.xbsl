импорт ИнтерфейсныеКомпонентыСтандартные
импорт СтандартноеПриложение
импорт УниверсальныеМеханизмы
импорт ЛичныеКабинетыПользователей
импорт Зарплата
импорт Документы

// Контракт ОсновнойИнтерфейсПоставщик
@ВПроекте
@Реализация
метод РазделыПриложения(): ЧитаемаяКоллекция<ОсновнойИнтерфейс.ОписаниеРаздела>
    возврат []
;

@ВПроекте
метод ИспользованиеРазделаЗарплатаСразу(): Булево

    возврат ЗарплатаСразу.ЗарплатаСразуДоступна()    

;

@ВПроекте
@Реализация
метод Виджеты(): ЧитаемаяКоллекция<ОсновнойИнтерфейс.ОписаниеВиджета>
    знч Виджеты: Массив<ОсновнойИнтерфейс.ОписаниеВиджета>
    если ИспользованиеРазделаЗарплатаСразу()
        Виджеты.Добавить(ОписаниеВиджета())
    ;
    возврат Виджеты
;

@ВПроекте
@Реализация
метод КомандыВсеФункции(): ЧитаемаяКоллекция<ОсновнойИнтерфейс.ОписательКомандыВсеФункции>
    знч Группа = новый ОсновнойИнтерфейс.ОписательГруппыКомандВсеФункции(ЗарплатаСразу.ИМЯ_РАЗДЕЛА_ЗАРПЛАТА_СРАЗУ, 2)
    возврат [
        новый ОсновнойИнтерфейс.ОписательКомандыВсеФункции(
            Группа = Группа, 
            Представление = "Заявление на выплату",
            СоздательФормыСписка = метод()-> возврат новый ЗаявлениеНаВыплату.АвтоматическаяФормаСписка();,
            СоздательФормыОбъекта = метод()-> возврат новый ЗаявлениеНаВыплату.АвтоматическаяФормаОбъекта();
            ),
        новый ОсновнойИнтерфейс.ОписательКомандыВсеФункции(
            Группа = Группа, 
            Представление = "Физические лица (Зарплата сразу)",
            СоздательФормыСписка = метод()-> возврат новый ФизическиеЛицаЗарплатаСразу.АвтоматическаяФормаСписка();,
            СоздательФормыОбъекта = метод()-> возврат новый ФизическиеЛицаЗарплатаСразу.АвтоматическаяФормаОбъекта();
            ),
        новый ОсновнойИнтерфейс.ОписательКомандыВсеФункции(
            Группа = Группа, 
            Представление = "Настройка взаимодействия (Зарплата сразу)",
            СоздательФормыСписка = метод()-> возврат новый НастройкаВзаимодействияСЗарплатаСразу.АвтоматическаяФормаСписка();,
            СоздательФормыОбъекта = метод()-> возврат новый НастройкаВзаимодействияСЗарплатаСразу.АвтоматическаяФормаОбъекта();
            ),
        новый ОсновнойИнтерфейс.ОписательКомандыВсеФункции(
            Группа = Группа, 
            Представление = "Используемая функциональность (Зарплата сразу)",
            СоздательФормыСписка = метод()-> возврат новый ИспользуемаяФункциональность.АвтоматическаяФормаСписка();,
            СоздательФормыОбъекта = метод()-> возврат новый ИспользуемаяФункциональность.АвтоматическаяФормаОбъекта();
            )
    ]
;

@ВПроекте
@Реализация
метод ДополнительныеКоманды(): ЧитаемыйМассив<Команда>
    возврат []
;

@ВПроекте
@Реализация
метод КомандыПанелиДействий(): ЧитаемыйМассив<Команда>
    возврат []
;

@ВПроекте
@Реализация
метод ДополнительныеКомандыПанелиГлавное(): ЧитаемыйМассив<Команда>
    возврат []
;

// Конец ОсновнойИнтерфейсПоставщик

// Контракт ДокументыСотрудниковПоставщик

@ВПроекте
@Реализация
метод КомандыСоздатьЗаявление(): ЧитаемыйМассив<ЭлементСпискаЗначений<Объект?>>
    возврат []
;

@ВПроекте
@Реализация
метод КомпонентДокумента(Заявление: ДокументСотрудника.Ссылка): ДокументСотрудникаКомпонент?
    выбор Заявление
    когда это ЗаявлениеНаВыплату.Ссылка
        возврат новый ЗапросНаВыплатуКомпонент()
    иначе
        возврат Неопределено
    ;
;

@ВПроекте
@Реализация
метод ОткрытьЗаявлениеМодально(Заявление: ДокументСотрудника.Ссылка): СтандартныеФормы.ДействиеВФорме?
    если Заявление это ЗаявлениеНаВыплату.Ссылка
        возврат ЗапросНаВыплатуФорма.ОткрытьВМодальномОкне(КлючОбъекта = Заявление как ЗаявлениеНаВыплату.Ссылка)
    иначе
        возврат Неопределено
    ;
;

@ВПроекте
@Реализация
метод ВыполнитьКомандуСоздать(Действие: ЭлементСпискаЗначений<Объект?>)
;

@ВПроекте
@Реализация
метод ПредставленияДокументов(): ЧитаемаяКоллекция<СостояниеДокументаКомпонентДанных>
    возврат ЗарплатаСразу.ПредставленияЗаявленийФизлица(ЛичныеКабинетыПользователей.ПараметрыСеанса().ТекущееФизическоеЛицо)  
;

// Конец ДокументыСотрудниковПоставщик

// Контракт НаполнениеРазделаЗарплата
@ВПроекте
@Реализация
метод СтраницыРазделаЗарплата(): ЧитаемаяКоллекция<СтандартноеПриложение.ОписаниеДополнительнойСтраницы>
    знч СтраницыРазделаЗарплата: Массив<СтандартноеПриложение.ОписаниеДополнительнойСтраницы>
    если ИспользованиеРазделаЗарплатаСразу()
        СтраницыРазделаЗарплата.Добавить(ОписаниеСтраницы())
    ;
    возврат СтраницыРазделаЗарплата
;

метод ОписаниеСтраницы(): СтандартноеПриложение.ОписаниеДополнительнойСтраницы
        
    возврат новый СтандартноеПриложение.ОписаниеДополнительнойСтраницы(
        ЗарплатаСразу.ИД_РАЗДЕЛА_ЗАРПЛАТА_СРАЗУ,
        ЗарплатаСразу.ИД_СТРАНИЦА_РАЗДЕЛА,
        ЗарплатаСразу.ИМЯ_РАЗДЕЛА_ЗАРПЛАТА_СРАЗУ,
        новый ЗарплатаСразуСтраница())
;

// Конец НаполнениеРазделаЗарплата

// Контракт НастраиваемаяПодсистема

@ВПроекте
@Реализация
метод ПанелиНастроек(): Массив<СтандартноеПриложение.ОписаниеДополнительнойСтраницы>
    знч Настройки: Массив<СтандартноеПриложение.ОписаниеДополнительнойСтраницы>
    если НастройкаЗарплатаСразуДоступна()
        Настройки.Добавить(новый СтандартноеПриложение.ОписаниеДополнительнойСтраницы(
                ЗарплатаСразу.ИД_ПОДСИСТЕМЫ,
                    ЗарплатаСразу.ИД_НАСТРОЙКА_ЗАРПЛАТА_СРАЗУ,
                    "Зарплата Сразу",
                    новый НастройкиЗарплатаСразуФорма()))
    ;
    возврат Настройки
;

@ВПроекте
@Реализация
метод ЕстьДоступныеПанелиНастроек(): Булево
    возврат НастройкаЗарплатаСразуДоступна()
;

// Конец НастраиваемаяПодсистема

метод НастройкаЗарплатаСразуДоступна(): Булево
    возврат СтандартноеПриложение.ПараметрыСеанса().ТекущийПользовательИмеетПолныеПрава
;

метод ОписаниеВиджета(): ОсновнойИнтерфейс.ОписаниеВиджета
     возврат новый ОсновнойИнтерфейс.ОписаниеВиджета(
        Ид = ЗарплатаСразу.ИД_РАЗДЕЛА_ЗАРПЛАТА_СРАЗУ,
        ИдРаздела = ЗарплатаСразу.ИД_РАЗДЕЛА_ЗАРПЛАТА_СРАЗУ,
        Имя = ЗарплатаСразу.ИМЯ_РАЗДЕЛА_ЗАРПЛАТА_СРАЗУ,
        Компонент = новый ВиджетЗарплатаСразуСодержимое(),
        Команда = Неопределено,
        Пиктограмма = НавигацияЗарплатаСразу.Изображение,
        ЦветПиктограммы = БиблиотекаЦветов.ВидыЦвета.Оранжевый,
        НавигационнаяКоманда = НавигацияЗарплатаСразу)
;