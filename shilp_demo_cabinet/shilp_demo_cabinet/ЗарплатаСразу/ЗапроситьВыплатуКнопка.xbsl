импорт ИнтерфейсныеКомпонентыСтандартные
импорт ВладельцыКабинетов
импорт УниверсальныеМеханизмы

метод ЗапроситьВыплатуКнопкаПриНажатии(Источник: Кнопка, Собитие: СобытиеПриНажатии)
    
    пер ИдКомпании = ЗарплатаСразу.ПараметрыСеанса().ИдКомпании
    
    пер АктивностьСервиса = ЗарплатаСразу.СервисАктивен(ИдКомпании)
    
    если АктивностьСервиса == Неопределено
        возврат
    ;
     
    если не АктивностьСервиса 
        СтандартныеФормы.ПоказатьУведомление("Сервис недоступен", 
            "Сервис временно недоступен, попробуйте через 10 минут",
            ОценкаИнформации.Отрицательная)
        возврат
    ;
    
    ИнформацияОСотруднике = ЗарплатаСразу.ОбновитьИнформациюОСотруднике(ИнформацияОСотруднике.Сотрудник.Ид)
    
    СозданоЗаявлениеНаВыплату.ПодключитьОбработчик(&ЗаполнитьСсылкуНаЗаявление)
    ОтправленоЗаявлениеНаВыплату.ПодключитьОбработчик(&ОбновитьИнформациюОЗапросе)
    ПодписаноЗаявлениеНаВыплату.ПодключитьОбработчик(&ЗаявлениеПодписано)
        
    пер РезультатСозданияНовогоЗапроса = НовыйЗапросНаВыплатуФорма.ОткрытьВМодальномОкне(ИнформацияОСотруднике = ИнформацияОСотруднике, 
        ИнформацияОЗапросеНаВыплату = ИнформацияОЗапросеНаВыплату)
    
    ИнформацияОСотруднике = ЗарплатаСразу.ПараметрыСеанса().ИнформацияОСотруднике
    
    //Закрыто окно с запросом
    если РезультатСозданияНовогоЗапроса == Неопределено и СсылкаНаЗаявление == Неопределено
        возврат
    ;
    
    //Нажали Запросить, подписали заявление, запросили деньги
    если РезультатСозданияНовогоЗапроса != Неопределено и РезультатСозданияНовогоЗапроса.Результат == Истина 
        и РезультатСозданияНовогоЗапроса.НовыйЗапросНаВыплату != Неопределено
        ИнформацияОЗапросеНаВыплату = РезультатСозданияНовогоЗапроса.НовыйЗапросНаВыплату
        ЗарплатаСразу.ЖдатьИзменениеСтатусаЗапроса(
            РезультатСозданияНовогоЗапроса.НовыйЗапросНаВыплату.ЗапросНаВыплату.Ид, 
            СтатусыЗапросовНаВыплату.Исполнен,
            ВладельцыКабинетов.ПараметрыСеанса().ФизическоеЛицо)
    //Запрос вернулся с отказом
    иначе если РезультатСозданияНовогоЗапроса != Неопределено и РезультатСозданияНовогоЗапроса.Результат == Истина
        и РезультатСозданияНовогоЗапроса.НовыйЗапросНаВыплату == Неопределено
        УстановитьОтказ(СсылкаНаЗаявление)
        ЗарплатаСразу.ОбновитьИнформациюОСотруднике(ИнформацияОСотруднике.Сотрудник.Ид)
    //Нажали Запросить, не подписали заявление
    иначе если РезультатСозданияНовогоЗапроса == Неопределено и СсылкаНаЗаявление != Неопределено и не ЗаявлениеПодписано
        ЗаявлениеНаВыплату.УдалитьЗаявление(СсылкаНаЗаявление) 
    иначе если РезультатСозданияНовогоЗапроса == Неопределено и СсылкаНаЗаявление != Неопределено и ЗаявлениеПодписано
        ЗарплатаСразу.ОбновитьИнформациюОСотруднике(ИнформацияОСотруднике.Сотрудник.Ид)
        ЗаявлениеПодписано = Ложь
    ;
    
    СсылкаНаЗаявление = Неопределено

;

метод ЗаполнитьСсылкуНаЗаявление(Ссылка: ЗаявлениеНаВыплату.Ссылка)

    СсылкаНаЗаявление = Ссылка
    
;

метод ЗаявлениеПодписано()
    
    ЗаявлениеПодписано = Истина
;

метод ОбновитьИнформациюОЗапросе(ИдЗапроса: Строка)
 
    ИнформацияОЗапросеНаВыплату = ЗарплатаСразу.ИнформацияОЗапросеНаВыплату(ИдЗапроса)
    ЗаявлениеНаВыплату.Перезаписать(СсылкаНаЗаявление, ИнформацияОЗапросеНаВыплату)

;

@НаСервере @ДоступноСКлиента
статический метод УстановитьОтказ(СсылкаНаЗаявление: ЗаявлениеНаВыплату.Ссылка)

    СостДокументов.УстановитьСостояниеДокумента(СсылкаНаЗаявление, СостоянияДокумента.ПолученОтказ)
    
;