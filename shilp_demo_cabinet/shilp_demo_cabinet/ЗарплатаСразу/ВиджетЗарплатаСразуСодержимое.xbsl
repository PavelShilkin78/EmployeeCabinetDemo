импорт ВладельцыКабинетов
импорт ОбщиеСтандартные


@Обработчик
метод ПослеСоздания()
    
    ПолучитьИнформациюОСотруднике()
       
;

метод ЗаголовокНадписьАванс(): Строка
    
    выбор ИнформацияОСотруднике.Сотрудник.СтатусПодключения
    когда СтатусыПодключенияСотрудника.Подключен
        если ЕстьЗаработанныеДеньги()
            возврат "Вы можете получить заработанные Вами деньги"
        иначе 
            возврат "В настоящее время нет доступной для выплаты суммы"
        ;
    когда СтатусыПодключенияСотрудника.ДоступноПодключение
        возврат "Ваша компания использует сервис досрочной выплаты зарплаты. Подключитесь, чтобы получать заработанные деньги по запросу."
    ;
;

метод ЗаголовокДоступныйАванс(): Строка
    
    пер Сумма = МенеджерСтрок.ФорматЧисла(ИнформацияОСотруднике.Лимит.Баланс.ЦелаяЧасть(), ДлинаДробнойЧасти = 0)
    возврат  "%Сумма ₽"
    
;

метод КнопкаПодключенияВидимость(): Булево

    возврат ИнформацияОСотруднике!= Неопределено 
        и ИнформацияОСотруднике.Сотрудник.СтатусПодключения == СтатусыПодключенияСотрудника.ДоступноПодключение
    
;

@ВПодсистеме
метод ОбновитьИнформациюОСотруднике(ИзмененнаяИнформацияОСотруднике: ЗарплатаСразу.ИнформацияОСотруднике?)
    
    ИнформацияОСотруднике = ИзмененнаяИнформацияОСотруднике
   
;

метод ПолучитьИнформациюОСотруднике()

    ИнформацияОСотруднике = ЗарплатаСразу.ПараметрыСеанса().ИнформацияОСотруднике
    ИзменениеИнформацииОСотруднике.ПодключитьОбработчик(&ОбновитьИнформациюОСотруднике)

;

метод КоличествоОткрытыхСимволовДоступныйАванс(): Число

    возврат (Компоненты.ДоступныйАванс.НачальныйЗаголовок.Длина() / 2).Округлить()

;

метод МаскировкаВключенаДоступныйАванс(): Булево
    
    возврат ВладельцыКабинетов.ПараметрыСеанса().РежимПриватностиПриложения 
        и ИнформацияОСотруднике.Сотрудник.СтатусПодключения == СтатусыПодключенияСотрудника.Подключен
        и ИнформацияОСотруднике.Лимит.Баланс.ЦелаяЧасть() > 0
    
;

метод ВидимостьАванса(): Булево
    
    возврат ИнформацияОСотруднике != Неопределено 
        и ИнформацияОСотруднике.Сотрудник.СтатусПодключения != СтатусыПодключенияСотрудника.Заблокирован    

;

метод КнопкаПодключенияПриНажатии(Источник: Кнопка, Событие: СобытиеПриНажатии)
    Зарплата::ФормаРаздела.Открыть(ТекущаяСтраница = "Зарплата сразу")
;

метод ЕстьЗаработанныеДеньги(): Булево

    возврат ИнформацияОСотруднике != Неопределено 
        и ИнформацияОСотруднике.Лимит.Баланс.ЦелаяЧасть() > 0    

;

метод МожноСоздатьЗапросНаВыплату(): Булево

    возврат ИнформацияОСотруднике!= Неопределено 
        и ИнформацияОСотруднике.Лимит.МожноСоздатьЗапросНаВыплату

;