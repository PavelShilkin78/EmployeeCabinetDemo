импорт КонтрольДоступа

@Обработчик
метод ВычислитьРазрешенияДоступа(): Массив<РазрешениеДоступа>
    возврат КонтрольДоступа.РазрешенияДоступаТаблицыСервисныхДанныхПриложения()
;

@ВПроекте
метод УстановитьНастройкиВзаимодействияСЗарплатаСразу(ДанныеНастроекВзаимодействияСЗарплатаСразу: ЗарплатаСразу.ПараметрыДоступаКЗарплатаСразу)

    знч Запрос = Запрос{
        Выбрать Первые 1
            Ссылка
        Из 
            НастройкаВзаимодействияСЗарплатаСразу}
    исп РезультатЗапроса = Запрос.Выполнить()
    
    пер НастройкаВзаимодействияСЗарплатаСразуОбъект: НастройкаВзаимодействияСЗарплатаСразу.Объект?
    знч РезультатЗапросаСтроки = новый ЧитаемыйМассив(РезультатЗапроса)
    если (не РезультатЗапросаСтроки.Пусто())
        НастройкаВзаимодействияСЗарплатаСразуОбъект = РезультатЗапросаСтроки[0].Ссылка.ЗагрузитьОбъект()
    ;
    если НастройкаВзаимодействияСЗарплатаСразуОбъект == Неопределено
        НастройкаВзаимодействияСЗарплатаСразуОбъект = новый НастройкаВзаимодействияСЗарплатаСразу.Объект()
    ;
    
    НастройкаВзаимодействияСЗарплатаСразуОбъект.АдресСервиса = ДанныеНастроекВзаимодействияСЗарплатаСразу.АдресСервиса
    НастройкаВзаимодействияСЗарплатаСразуОбъект.ApiКлюч = ДанныеНастроекВзаимодействияСЗарплатаСразу.ApiКлюч
    
    НастройкаВзаимодействияСЗарплатаСразуОбъект.Записать()
;

@ВПроекте
метод ДанныеНастроекВзаимодействияСЗарплатаСразу(): ЗарплатаСразу.ПараметрыДоступаКЗарплатаСразу?
    
    пер ДанныеНастроекВзаимодействияСЗарплатаСразу = новый ЗарплатаСразу.ПараметрыДоступаКЗарплатаСразу()
    
    знч Запрос = Запрос{
        Выбрать Первые 1
            АдресСервиса,
            ApiКлюч
        Из 
            НастройкаВзаимодействияСЗарплатаСразу}
    исп РезультатЗапроса = Запрос.Выполнить()
    знч РезультатЗапросаСтроки = новый ЧитаемыйМассив(РезультатЗапроса)
    если (не РезультатЗапросаСтроки.Пусто()) и не РезультатЗапросаСтроки[0].АдресСервиса.Пусто()
        ДанныеНастроекВзаимодействияСЗарплатаСразу.АдресСервиса = РезультатЗапросаСтроки[0].АдресСервиса
        ДанныеНастроекВзаимодействияСЗарплатаСразу.ApiКлюч = РезультатЗапросаСтроки[0].ApiКлюч
    иначе
        возврат Неопределено
    ;

    возврат ДанныеНастроекВзаимодействияСЗарплатаСразу
;