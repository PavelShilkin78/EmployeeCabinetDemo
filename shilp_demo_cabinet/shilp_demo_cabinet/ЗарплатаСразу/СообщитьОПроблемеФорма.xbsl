импорт ИнтерфейсныеКомпонентыСтандартные
импорт ОбщиеСтандартные

@Обработчик
метод ПослеСоздания()
    
    ИнформацияОСотруднике = ЗарплатаСразу.ИнформацияОТекущемСотруднике()
    ЗаполнитьСписокЗапросов()    
    Компоненты.СписокЗапросовПолеВвода.Значение = ИдТекущегоЗапроса
    
;

метод ЗаполнитьСписокЗапросов()
  
    для ЗапросНаВыплату из ИнформацияОСотруднике.ЗапросыНаВыплату
        знч Дата = МенеджерДат.ФорматМомента(ЗапросНаВыплату.ДатаВремяСоздания, "дд.ММ.гггг")
        знч Время = МенеджерДат.ФорматМомента(ЗапросНаВыплату.ДатаВремяСоздания, "ЧЧ:мм")
        знч Сумма = ЗапросНаВыплату.Сумма.ЦелаяЧасть()
        
        знч ЗначениеСписок = новый ЭлементСпискаЗначений<Строка>(
            Значение = ЗапросНаВыплату.Ид,
            Представление = "%Дата - %Время - %Сумма руб."
        )
        
        СписокЗапросов.Добавить(ЗначениеСписок)
    ;
    
;

метод СообщитьОПроблемеКнопкаПриНажатии(Источник: Кнопка, Событие: СобытиеПриНажатии)
    
    знч ТекстПроблемы = Компоненты.ПроблемаПолеВвода.Значение
    
    если ТекстПроблемы == ""
        возврат
    ;
    
    если ЗарплатаСразу.СообщитьОПроблеме(ИнформацияОСотруднике.Сотрудник.Ид, 
        Компоненты.СписокЗапросовПолеВвода.Значение, 
        ТекстПроблемы)    
        
        СтандартныеФормы.ПоказатьУведомление("Запрос отправлен", "Запрос направлен в службу поддержки. Ответ поступит на Ваш email.", ОценкаИнформации.Положительная)
        Закрыть()
    
    иначе
        
        СтандартныеФормы.ПоказатьУведомление("Ошибка при отправке запроса", "Ошибка при отправке запроса. Попробуйте позже.", ОценкаИнформации.Отрицательная)
     
    ;
    
;