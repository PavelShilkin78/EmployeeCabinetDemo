импорт ИнтерфейсныеКомпонентыСтандартные
импорт ЛичныеКабинетыПользователей
импорт ОбщиеСтандартные

@ВПроекте
@Реализация
метод Инициализировать()
    
    Компоненты.СуммаЗапросаПолеВвода.Значение = ИнформацияОСотруднике.Лимит.МинимальныйРазмерДоступнойВыплаты
    СуммаВыплаты = ИнформацияОСотруднике.Лимит.МинимальныйРазмерДоступнойВыплаты
    ЗаполнитьМассивСумм(ИнформацияОСотруднике.Лимит.МинимальныйРазмерДоступнойВыплаты, ИнформацияОСотруднике.Лимит.МаксимальныйРазмерДоступнойВыплаты)
    УстановитьЗаголовкиКомпонентов(МассивСумм[0])
    
;

структура УсловияКомиссии
    пер СуммаКомиссии: Число = 0
    пер СуммаКомиссииСоСкидкой: Число = 0
    пер ДнейДляОплаты: Число = 0
;

метод ЗаполнитьМассивСумм(МинимальнаяСуммаЗапроса: Число, МаксимальнаяСуммаЗапроса: Число)
    
    МассивСумм.Добавить(МинимальнаяСуммаЗапроса.ЦелаяЧасть())
    
    пер ЧетвертьМаксимальнойСуммы = 
        (МаксимальнаяСуммаЗапроса/4000).Округлить(1, РежимОкругления.ПоловинаВверх) * 1000
    если ЧетвертьМаксимальнойСуммы > МинимальнаяСуммаЗапроса 
        и ЧетвертьМаксимальнойСуммы < МаксимальнаяСуммаЗапроса
        и МассивСумм.Найти(ЧетвертьМаксимальнойСуммы) == Неопределено
        МассивСумм.Добавить(ЧетвертьМаксимальнойСуммы)
    ;
    пер ПоловинаМаксимальнойСуммы = 
        (МаксимальнаяСуммаЗапроса/2000).Округлить(1, РежимОкругления.ПоловинаВверх) * 1000
    если ПоловинаМаксимальнойСуммы > МинимальнаяСуммаЗапроса
        и ПоловинаМаксимальнойСуммы < МаксимальнаяСуммаЗапроса
        и МассивСумм.Найти(ЧетвертьМаксимальнойСуммы) == Неопределено
        МассивСумм.Добавить(ПоловинаМаксимальнойСуммы)
    ;
    пер ТриЧетвертиМаксимальнойСуммы = 
        (МаксимальнаяСуммаЗапроса*3/4000).Округлить(1, РежимОкругления.ПоловинаВверх) * 1000
    если ТриЧетвертиМаксимальнойСуммы > МинимальнаяСуммаЗапроса
        и ТриЧетвертиМаксимальнойСуммы < МаксимальнаяСуммаЗапроса
        и МассивСумм.Найти(ТриЧетвертиМаксимальнойСуммы) == Неопределено
        МассивСумм.Добавить(ТриЧетвертиМаксимальнойСуммы)
    ;
    
    пер МаксимальнаяСуммаЗапросаБезКопеек = МаксимальнаяСуммаЗапроса.ЦелаяЧасть()
    если МассивСумм.Найти(МаксимальнаяСуммаЗапросаБезКопеек) == Неопределено
        МассивСумм.Добавить(МаксимальнаяСуммаЗапросаБезКопеек)
    ;
        
  ;
  
метод ЗаголовокКнопки(ИндексКнопки: Число): Строка

    пер КоличествоЭлементов = МассивСумм.Граница()
    
    если ИндексКнопки > КоличествоЭлементов
        возврат ""
    иначе если ИндексКнопки == КоличествоЭлементов
        пер Сумма = МенеджерСтрок.ФорматЧисла(МассивСумм[ИндексКнопки], ДлинаДробнойЧасти = 0)
        возврат "%Сумма  ₽"
    иначе 
        возврат МенеджерСтрок.ФорматЧисла(МассивСумм[ИндексКнопки], ДлинаДробнойЧасти = 0) + " ₽"
    ;

;

метод ВидимостьКнопки(ИндексКнопки: Число): Булево

    возврат ИндексКнопки <= МассивСумм.Граница()

;

метод ПриНажатииКнопки(Источник: ЗапросСуммыКнопка, Событие: СобытиеПриНажатии)
    
    Компоненты.СуммаЗапросаПолеВвода.Значение = МассивСумм[Источник.ИндексКнопки]
    СуммаВыплаты = МассивСумм[Источник.ИндексКнопки]
    УстановитьЗаголовкиКомпонентов(МассивСумм[Источник.ИндексКнопки])
    ПроверитьЗначение(Компоненты.СуммаЗапросаПолеВвода, МассивСумм[Источник.ИндексКнопки])
   
;

метод СуммаЗапросаПолеВводаПриИзменении(Источник: ПолеВвода<Число>, Событие: СобытиеКомпонента)
    
    Источник.Значение = Источник.Значение.Округлить(0, РежимОкругления.Вверх)
    ПроверитьЗначение(Источник, Источник.Значение)
    УстановитьЗаголовкиКомпонентов(Источник.Значение)
    СуммаВыплаты = Источник.Значение
    
;

метод ПроверитьЗначение(КомпонентВвода: ПолеВвода<Число>, Значение: Число)
     
    если Значение < ИнформацияОСотруднике.Лимит.МинимальныйРазмерДоступнойВыплаты
        или Значение > ИнформацияОСотруднике.Лимит.МаксимальныйРазмерДоступнойВыплаты
        КомпонентВвода.РезультатПроверкиДанных = РезультатПроверкиДанных.Ошибка
        Компоненты.КнопкаЗапросить.Доступность = Ложь
    иначе 
        КомпонентВвода.РезультатПроверкиДанных = РезультатПроверкиДанных.Успех
        Компоненты.КнопкаЗапросить.Доступность = Истина
    ;
;

метод ЗаголовокБалансНадпись(): Строка
    
    пер Сумма = МенеджерСтрок.ФорматЧисла(ИнформацияОСотруднике.Лимит.Баланс.ЦелаяЧасть(), ДлинаДробнойЧасти = 0)
    
    возврат "Текущий лимит %Сумма ₽"
    
;

метод ЗаголовокМинимальнаяСумма(): Строка
    
    пер Сумма = МенеджерСтрок.ФорматЧисла(ИнформацияОСотруднике.Лимит.МинимальныйРазмерДоступнойВыплаты.ЦелаяЧасть(), ДлинаДробнойЧасти = 0)
    
    возврат "Минимальная сумма %Сумма ₽"
    
;

метод УстановитьЗаголовкиКомпонентов(Сумма: Число)
    
    пер ПредставлениеСуммы = МенеджерСтрок.ФорматЧисла(Сумма, ДлинаДробнойЧасти = 0) + " ₽"
    Компоненты.КнопкаЗапросить.Заголовок = "Запросить " + ПредставлениеСуммы
  
;

метод ЗаголовокЛицензия(): Строка
    
    пер УсловияКомиссии = УсловияКомиссии(СуммаВыплаты)
    
    пер ТекстКадендарныхДней = МенеджерСтрок.ТекстПоКоличеству("календарный день", УсловияКомиссии.ДнейДляОплаты)
    
    если ТекстКадендарныхДней == "календарный день"
        ТекстКадендарныхДней = "календарного дня"
    ;
    
    пер ЛицензияСервиса = МенеджерСтрок.ФорматЧисла(УсловияКомиссии.СуммаКомиссииСоСкидкой, ДлинаДробнойЧасти = 0)
    пер ДнейДляОплатыЛицензии = УсловияКомиссии.ДнейДляОплаты
    пер Сумма = МенеджерСтрок.ФорматЧисла(УсловияКомиссии.СуммаКомиссии, ДлинаДробнойЧасти = 0)
    
    возврат "Оплата лицензии сервиса: %ЛицензияСервиса ₽ при оплате в течение %ДнейДляОплатыЛицензии %ТекстКадендарныхДней с момента зачисления денег или %Сумма ₽ при оплате позже."
    
;

метод ПриНажатииЗапросить(Источник: Кнопка, Событие: СобытиеПриНажатии)
    
    пер УсловияКомиссии = УсловияКомиссии(СуммаВыплаты)
    
    СуммаКомиссииСоСкидкой = УсловияКомиссии.СуммаКомиссииСоСкидкой
    СуммаКомиссии = УсловияКомиссии.СуммаКомиссии
    ДнейДляОплаты = УсловияКомиссии.ДнейДляОплаты
    
    знч ФизическоеЛицо = ЛичныеКабинетыПользователей.ПараметрыСеанса().ТекущееФизическоеЛицо
    
    ЗаявлениеНаВыплату.Записать(
        ФизическоеЛицо,
        СуммаВыплаты,
        ИнформацияОСотруднике, 
        Ссылка)   
        
    СозданоЗаявлениеНаВыплату.Оповестить(Ссылка)
        
    ФайлЗаявления = ЗарплатаСразу.ФайлЗаявления(Ссылка)
            
    ЗапросВыполнен = Истина
;

метод УсловияКомиссии(СуммаВыплаты: Число): УсловияКомиссии

    пер Комиссии = ИнформацияОСотруднике.Лимит.СписокВозможныхКомиссий
    пер УсловияКомиссии = новый УсловияКомиссии()
  
    если Комиссии == Неопределено
      возврат УсловияКомиссии
    ;
    
    для Комиссия из Комиссии
        если СуммаВыплаты >= Комиссия.ВыплатаОт и СуммаВыплаты <= Комиссия.ВыплатаПо
            УсловияКомиссии.СуммаКомиссииСоСкидкой = Комиссия.СуммаКомиссииСоСкидкой
            УсловияКомиссии.ДнейДляОплаты = Комиссия.ДнейДляОплаты
            УсловияКомиссии.СуммаКомиссии = Комиссия.СуммаКомиссии
            прервать
        ;
    ;
        
    возврат УсловияКомиссии

;