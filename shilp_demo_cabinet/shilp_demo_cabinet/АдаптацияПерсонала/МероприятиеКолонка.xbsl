импорт ФизическиеЛица

@ВПодсистеме
структура Значение
    обз знч Мероприятие: Мероприятия.Ссылка
    обз знч Наименование: Строка
    обз знч АдаптируемыйСотрудник: ФизЛица.ФИО?
    обз знч Ответственный: ФизЛица.ФИО
    обз знч ПровестиНеРанее: Дата
    обз знч ПровестиНеПозднее: Дата
    обз знч КоличествоУчастников: Число
    обз знч Статус: СтатусыМероприятия
;

@Обработчик
метод ПослеСоздания()
    КонтекстДоступаКСтатусамМероприятий = АдаптацияПерсонала.КонтекстДоступаКСтатусамМероприятий()
;

метод СтатусПриИзменении(Источник: ВыборСтатусМероприятия, Событие: СобытиеПриИзменении<СтатусыМероприятия>)
    СтатусыМероприятий.Установить(Значение!.Мероприятие, Событие.НовоеЗначение)
;

метод АдаптируемыйСотрудникУказан(): Булево
    возврат Значение!.АдаптируемыйСотрудник != Неопределено
;

метод АдаптируемыйСотрудникЗаголовок(): Строка
    возврат АдаптируемыйСотрудникУказан()
                ? Значение!.АдаптируемыйСотрудник!.ПредставлениеИмени(ФизЛица.ВариантПредставленияИмени.ФамилияИмяОтчество)
                : ""
;

метод ГруппаОтветственныйВесПриРастягивании(): Число
    возврат АдаптируемыйСотрудникУказан() ? 3 : 5
;

метод ОтветственныйЗаголовок(): Строка
    возврат Значение!.Ответственный.ПредставлениеИмени(
                    АдаптируемыйСотрудникУказан()
                    ? ФизЛица.ВариантПредставленияИмени.ФамилияИОРаздельно
                    : ФизЛица.ВариантПредставленияИмени.ФамилияИмяОтчество)
;

метод ОтветственныйШрифт(): СтилевойШрифт
    возврат АдаптируемыйСотрудникУказан() ? СтилевыеШрифты.Мелкий : СтилевыеШрифты.Обычный
;

метод КоличествоУчастниковЗначениеЗначка(): Строка|Число
    возврат Значение!.КоличествоУчастников > 1 ? "+${Значение!.КоличествоУчастников-1}" : 0
;

метод СрокиПроведенияЗаголовок(): Строка
    возврат Мероприятия.ПредставлениеСроковПроведения(Значение!.ПровестиНеРанее, Значение!.ПровестиНеПозднее)
;

метод СтатусТолькоЧтение(): Булево
    если КонтекстДоступаКСтатусамМероприятий == Неопределено или ПризнакиДоступаКСтатусамМероприятий == Неопределено
        возврат СтатусТолькоЧтение
    ;
    возврат СтатусТолькоЧтение
            или не АдаптацияПерсонала.ЕстьВозможностьРедактироватьСтатусМероприятия(КонтекстДоступаКСтатусамМероприятий!, ПризнакиДоступаКСтатусамМероприятий!)
;