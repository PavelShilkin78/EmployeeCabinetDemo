@ВПроекте
метод Изображение(): ДвоичныйОбъект.Ссылка
    выбор этот
    когда Черновик
        возврат Ресурс{Карандаш.svg}.Ссылка
    когда Запланировано
        возврат Ресурс{Планирование.svg}.Ссылка
    когда Проведено
        возврат Ресурс{ГалочкаВКруге.svg}.Ссылка
    когда Отменено
        возврат Ресурс{КрестикВКруге.svg}.Ссылка
    ;
;

@ВПроекте
метод Коннотация(): ОценкаИнформации
    выбор этот
    когда Черновик
        возврат ОценкаИнформации.Обычная
    когда Запланировано
        возврат ОценкаИнформации.Предупреждающая
    когда Проведено
        возврат ОценкаИнформации.Положительная
    когда Отменено
        возврат ОценкаИнформации.Отрицательная
    ;
;

@ВПроекте
метод ДоступныПереходыВ(): Множество<СтатусыМероприятия>
    выбор этот
    когда Черновик
        возврат {СтатусыМероприятия.Запланировано}
    когда Запланировано
        возврат {СтатусыМероприятия.Проведено, СтатусыМероприятия.Отменено}
    когда Проведено
        возврат {СтатусыМероприятия.Запланировано}
    когда Отменено
        возврат {СтатусыМероприятия.Запланировано}
    ;
    возврат {}
;

@ВПроекте
метод ДоступенПереход(Из: СтатусыМероприятия, В: СтатусыМероприятия): Булево
    возврат Из.ДоступныПереходыВ().Содержит(В)
;

@ВПроекте
метод Конечный(): Булево
    выбор этот
    когда Проведено, Отменено
        возврат Истина
    иначе
        возврат Ложь
    ;
;