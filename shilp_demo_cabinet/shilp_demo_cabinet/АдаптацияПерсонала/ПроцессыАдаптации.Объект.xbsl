импорт ОбщиеСтандартные

@Обработчик
@ВыполнятьПриЗагрузкеДанных
метод ПередЗаписью(До: ПроцессыАдаптации.Данные, ПараметрыЗаписи: ПроцессыАдаптации.ПараметрыЗаписи)
    СформироватьПредставление()
    ПривестиПлановуюДатуЗавершения()
    УстановитьСтатусПоУмолчаниюДляНового(ПараметрыЗаписи)
;

@Обработчик
@ВыполнятьПриЗагрузкеДанных
метод ПослеЗаписи(До: ПроцессыАдаптации.Данные, ПараметрыЗаписи: ПроцессыАдаптации.ПараметрыЗаписи)
    исп КонтекстДоступа.Привилегированный()
    ЗаписатьСтатус(ПараметрыЗаписи)
;

@ВПодсистеме
@НаСервере @НаКлиенте
метод ДопустимаПлановаяДатаЗавершения(): Булево
    если этот.ПлановаяДатаЗавершения == Неопределено
        возврат Истина
    ;
    возврат этот.ПлановаяДатаЗавершения! >= этот.ДатаСобытия
;

метод СформироватьПредставление()
    ПредставлениеПроцесса = МенеджерСтрок.ПодстрокаСНачала("$Сотрудник", 150)
;

метод УстановитьСтатусПоУмолчаниюДляНового(ПараметрыЗаписи: ПроцессыАдаптации.ПараметрыЗаписи)
    если не ПараметрыЗаписи.РежимЗагрузкиДанных и ЭтоНовый() и ПараметрыЗаписи.Статус == Неопределено
        знч СтатусПоУмолчанию: СтатусыПроцессаАдаптации
        ПараметрыЗаписи.Статус = СтатусПоУмолчанию
    ;
;

метод ЗаписатьСтатус(ПараметрыЗаписи: ПроцессыАдаптации.ПараметрыЗаписи)
    если ПараметрыЗаписи.Статус != Неопределено
        СтатусыПроцессовАдаптации.Записать(Ссылка, ПараметрыЗаписи.Статус!)
    ;
;

метод ПривестиПлановуюДатуЗавершения()
    если ПлановаяДатаЗавершения == Дата{}
        ПлановаяДатаЗавершения = Неопределено
    ;
;