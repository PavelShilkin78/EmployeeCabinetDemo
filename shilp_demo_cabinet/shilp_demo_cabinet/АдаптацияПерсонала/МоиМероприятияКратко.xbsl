импорт ИнтерфейсныеКомпонентыСтандартные
импорт КонтрольДоступа
импорт СтандартноеПриложение
импорт ЛичныеКабинетыПользователей

@ВПодсистеме
статический метод МожноИспользовать(): Булево
    возврат ЛичныеКабинетыПользователей.ПараметрыСеанса().ТекущееФизическоеЛицо != Неопределено
; 

@ВПроекте
@Реализация
метод Обновить()
    ОбновитьДанные()
;

@Обработчик
метод ПослеСоздания()
    ОбновитьДанные()
;

метод ПерейтиКСпискуОбработчик(Команда: ОбычнаяКоманда)
    ОткрытьМоиМероприятия()
;

метод МероприятияОтветственногоПриНажатии()
    ОткрытьМоиМероприятия(РолиУчастниковМероприятия.Ответственный)    
;

метод МероприятияУчастникаПриНажатии()
    ОткрытьМоиМероприятия(РолиУчастниковМероприятия.Участник)    
;

метод ОткрытьМоиМероприятия(РольУчастника: РолиУчастниковМероприятия? = Неопределено)
    если ФормаВладелец != Неопределено
        ИнтерфейсныеКомпоненты.ОткрытьЗамещающуюФорму(ФормаВладелец, новый МоиМероприятияФорма(РольУчастника = РольУчастника))
    иначе
        (новый МоиМероприятияФорма(РольУчастника = РольУчастника)).ОткрытьВМодальномОкне()
    ;
;

метод МероприятияОтветственногоЗаглушкаВидимость(): Булево
    возврат не МероприятияОтветственногоВидимость()    
;

метод МероприятияОтветственногоЗаглушкаПояснение(): Строка
    возврат ЗагрузкаЗавершена ? "Нет мероприятий за которые вы отвечаете" : ""    
;

метод МероприятияОтветственногоВидимость(): Булево
    возврат не МероприятияОтветственного.Пусто()   
;

метод МероприятияУчастникаЗаглушкаВидимость(): Булево
    возврат не МероприятияУчастникаВидимость()   
;

метод МероприятияУчастникаЗаглушкаПояснение(): Строка
    возврат ЗагрузкаЗавершена ? "Нет мероприятий в которых вы участвуете" : ""    
;

метод МероприятияУчастникаВидимость(): Булево
    возврат не МероприятияУчастника.Пусто()    
;

метод ОбновитьДанные()
    знч Данные = ПолучитьДанные()
    МероприятияОтветственного = Данные.МероприятияОтветственного
    МероприятияУчастника = Данные.МероприятияУчастника
    ЗагрузкаЗавершена = Истина
;

@ВПодсистеме
@НаСервере @НаКлиенте
перечисление СостоянияМероприятия
    Проведено,
    Отменено,
    Предстоит,
    Просрочено,
    Активно    
;

@НаСервере @НаКлиенте
структура Данные
    обз знч МероприятияОтветственного: Множество<КольцеваяДиаграмма.ЗначениеДиаграммы>
    обз знч МероприятияУчастника: Множество<КольцеваяДиаграмма.ЗначениеДиаграммы>     
;

@НаСервере @ДоступноСКлиента
статический метод ПолучитьДанные(): Данные
    возврат новый Данные(
        МероприятияОтветственного = ПолучитьДанные(РолиУчастниковМероприятия.Ответственный),
        МероприятияУчастника = ПолучитьДанные(РолиУчастниковМероприятия.Участник)
    )
;

@НаСервере
статический метод ПолучитьДанные(РольУчастника: РолиУчастниковМероприятия): Множество<КольцеваяДиаграмма.ЗначениеДиаграммы>
    исп КонтекстДоступа.Привилегированный()
    знч ТекущаяДата = Дата.Сейчас()
    знч Запрос = Запрос{
        Выбрать
            Мероприятия.Состояние Как Состояние,
            Сумма(1) Как Количество
        Из
        (Выбрать
            Выбор
                Когда СтатусыМероприятий.Статус == %{СтатусыМероприятия.Проведено} Тогда %{СостоянияМероприятия.Проведено}
                Когда СтатусыМероприятий.Статус == %{СтатусыМероприятия.Отменено} Тогда %{СостоянияМероприятия.Проведено}
                Когда Мероприятия.ПровестиНеРанее > %{ТекущаяДата} Тогда %{СостоянияМероприятия.Предстоит}
                Когда Мероприятия.ПровестиНеПозднее < %{ТекущаяДата} Тогда %{СостоянияМероприятия.Просрочено}
                Иначе %{СостоянияМероприятия.Активно}
            Конец Как Состояние
        Из
        УчастникиМероприятий Как УчастникиМероприятий
        Соединение Мероприятия Как Мероприятия
            По УчастникиМероприятий.Мероприятие == Мероприятия.Ссылка
        Соединение СтатусыМероприятий.СрезПоследних Как СтатусыМероприятий
            По Мероприятия.Ссылка == СтатусыМероприятий.Мероприятие
        Соединение ПроцессыАдаптации Как ПроцессыАдаптации
            По ПроцессыАдаптации.Ссылка == Мероприятия.ПроцессАдаптации
        Соединение СтатусыПроцессовАдаптации Как СтатусыПроцессовАдаптации
            По ПроцессыАдаптации.Ссылка == СтатусыПроцессовАдаптации.ПроцессАдаптации
        Где
            УчастникиМероприятий.Участник == %{ПользователиСистемы.ТекущееФизическоеЛицо()!}
            И УчастникиМероприятий.Роль == %{РольУчастника}
            И СтатусыПроцессовАдаптации.Статус == %{СтатусыПроцессаАдаптации.Выполняется}
            И СтатусыМероприятий.Статус != %{СтатусыМероприятия.Черновик}) Как Мероприятия
        Сгруппировать По
            Мероприятия.Состояние
    }
    исп РезультатЗапроса = Запрос.Выполнить()
    знч Данные: Множество<КольцеваяДиаграмма.ЗначениеДиаграммы>
    для СтрокаРезультата из РезультатЗапроса
        Данные.Добавить(новый КольцеваяДиаграмма.ЗначениеДиаграммы(
            СтрокаРезультата.Количество,
            "${СтрокаРезультата.Состояние}"
        ))
    ;
    возврат Данные
;