импорт ФизическиеЛица

@ВПодсистеме
метод Загрузить(Участники: Массив<УчастникМероприятия>)
    ТаблицаУчастники.Загрузить(Участники, ПроцессАдаптации!)
;

@ВПодсистеме
метод Выгрузить(): Массив<УчастникМероприятия>
    возврат ТаблицаУчастники.Выгрузить()
;

метод Данные(): Массив<ТаблицаУчастникиМероприятия.Участник>
    знч Данные: Массив<ТаблицаУчастникиМероприятия.Участник>
    Данные.ДобавитьВсе(ТаблицаУчастники.Строки)
    возврат Данные
;

метод ГруппаКомандДобавления(): ГруппаКомандногоИнтерфейса<Команда>
    знч Группа = новый ГруппаКомандногоИнтерфейса<Команда>("")
    Группа.Элементы.ДобавитьВсе(КомандыДобавления())
    возврат Группа
;

метод КомандыДобавления(): Массив<Команда>
    знч Команды = новый Массив<Команда>()
    для ОтсутствующаяРоль из ТаблицаУчастники.ОтсутствующиеРоли()
        Команды.Добавить(новый ОбычнаяКоманда(
            Обработчик = (Команда) -> ДобавитьУчастника(ОтсутствующаяРоль),
            Представление = "$ОтсутствующаяРоль"
        ))
    ;
    Команды.Добавить(новый ОбычнаяКоманда(
        Обработчик = &ДобавитьКонкретногоЧеловека,
        Представление = "Конкретный человек"
    ))
    возврат Команды
;

метод УдалитьСтроку(Команда: КомандаСПараметром<ТаблицаУчастникиМероприятия.Участник>, Участник: ТаблицаУчастникиМероприятия.Участник)
    ТаблицаУчастники.Удалить(Участник)
    ПриИзменении(новый СобытиеКомпонента())
;

метод ДобавитьКонкретногоЧеловека(Команды: Команда)
    знч Участник = ВыборУчастникаФорма.Выбрать()
    если Участник != Неопределено
        ДобавитьУчастника(Участник)
    ;
;

метод ДобавитьУчастника(Участник: ФизическиеЛица.Ссылка)
    ТаблицаУчастники.ДобавитьУчастника(Участник, ПроцессАдаптации!)
    ПриИзменении(новый СобытиеКомпонента())
;

метод ДобавитьУчастника(Роль: РолиУчастниковАдаптации)
    ТаблицаУчастники.ДобавитьУчастника(Роль, ПроцессАдаптации!)
    ПриИзменении(новый СобытиеКомпонента())
;

метод ЗаглушкаВидимость(): Булево
    возврат ТаблицаУчастники.Пусто()
;

метод ЗаглушкаПояснение(): Строка
    возврат ТолькоЧтение ? "" : "Можете добавить участников мероприятия"
;

метод УчастникСсылка(Участник: ТаблицаУчастникиМероприятия.Участник): ФизическиеЛица.Ссылка
    возврат Участник.Физлицо
;

метод РолиУчастникаВидимость(Участник: ТаблицаУчастникиМероприятия.Участник): Булево
    возврат не Участник.РолиАдаптации.Пусто()
;

метод РолиУчастникаЗаголовок(Участник: ТаблицаУчастникиМероприятия.Участник): Строка
    знч Роли: Массив<Строка>
    для Роль из Участник.РолиАдаптации
        Роли.Добавить("$Роль")
    ;
    возврат Строки.Соединить(Роли, " | ")
;