
@ВПодсистеме
метод ЭлементФильтра(): ЭлементФильтра
    возврат новый ЭлементФильтра(
        Поле = "Роль",
        ВидСравнения = ВидСравнения.Равно,
        Значение = РольУчастника,
        Использовать = РольУчастника != Неопределено
    )
;

метод Команды(): ГруппаКомандногоИнтерфейса<Команда>?
    знч Группа = новый ГруппаКомандногоИнтерфейса<Команда>("")
    знч ЛюбаяРоль: РолиУчастниковМероприятия? = Неопределено
    знч РолиУчастников = [ЛюбаяРоль]
    РолиУчастников.ДобавитьВсе(РолиУчастниковМероприятия.Элементы())
    для ТекущаяРоль из РолиУчастников
        Группа.Элементы.Добавить(новый ОбычнаяКоманда(
            Обработчик = Команда -> УстановитьВариантУчастия(ТекущаяРоль),
            Представление = ПредставлениеВарианта(ТекущаяРоль),
            Изображение = ИзображениеВарианта(ТекущаяРоль)
        ))
    ;
    возврат Группа
;

метод УстановитьВариантУчастия(НоваяРоль: РолиУчастниковМероприятия?)
    РольУчастника = НоваяРоль
    ПриИзменении(новый СобытиеКомпонента())
;

метод ИзображениеВарианта(Роль: РолиУчастниковМероприятия?): ДвоичныйОбъект.Ссылка
    выбор Роль
    когда Ответственный
        возврат Ресурс{АккаунтНастройки.svg}.Ссылка
    когда Участник
        возврат Ресурс{Аккаунт.svg}.Ссылка
    иначе
        возврат Ресурс{Команда.svg}.Ссылка
    ;
;

метод ПредставлениеВарианта(Роль: РолиУчастниковМероприятия?): Строка
    выбор Роль
    когда Неопределено
        возврат "Любое участие"
    иначе
        возврат Роль.Представление()
    ;
;