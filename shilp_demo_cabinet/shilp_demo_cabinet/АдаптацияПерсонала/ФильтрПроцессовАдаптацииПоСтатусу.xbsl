
@ВПроекте
метод ЭлементФильтра(): ЭлементФильтра
    возврат новый ЭлементФильтра(
        Поле = "Статус",
        ВидСравнения = Значения.Размер() > 1 ? ВидСравнения.ВСписке : ВидСравнения.Равно,
        Значение = Значения.Размер() > 1 ? Значения : Значения.ЕдинственныйИлиУмолчание(),
        Использовать = Значения.Размер() != СтатусыПроцессаАдаптации.Элементы().Размер()
    )
;

конст ВсеСтатусыПредставление = "Любой статус"
конст ВсеСтатусыИзображение = Ресурс{Список2.svg}

метод Заголовок(): Строка
    если ВыбраныВсеСтатусы()
        возврат ВсеСтатусыПредставление
    иначе
        возврат Строки.Соединить(Значения, " | ")
    ;
;

метод Изображение(): ДвоичныйОбъект.Ссылка
    если Значения.Размер() == 1
        возврат Значения.ЕдинственныйИлиУмолчание()!.Изображение()
    иначе
        возврат ВсеСтатусыИзображение.Ссылка
    ;
;

метод ГруппаКоманд(): ГруппаКомандногоИнтерфейса<Команда>
    знч Группа = новый ГруппаКомандногоИнтерфейса<Команда>("")
    Группа.Элементы.ДобавитьВсе(Команды())
    возврат Группа        
;

метод Команды(): Массив<Команда>
    знч КомандыСтатусов: Массив<Команда>
    КомандыСтатусов.Добавить(новый ПереключаемаяКоманда(
        Обработчик = (Команда) -> ВыбратьВсеСтатусы(),
        ПредставлениеАктивного = ВсеСтатусыПредставление, 
        ПредставлениеНеактивного = ВсеСтатусыПредставление,
        ИзображениеАктивного = ВсеСтатусыИзображение.Ссылка,
        ИзображениеНеактивного = ВсеСтатусыИзображение.Ссылка,
        Активна = ВыбраныВсеСтатусы()
    ))  
    для Статус из ДопустимыеЗначения
        КомандыСтатусов.Добавить(новый ПереключаемаяКоманда(
            Обработчик = (Команда) -> ВыбратьСтатус(Статус),
            ПредставлениеАктивного = Статус.Представление(),
            ПредставлениеНеактивного = Статус.Представление(),
            ИзображениеАктивного = Статус.Изображение(),
            ИзображениеНеактивного = Статус.Изображение(),
            Активна = Значения.Содержит(Статус)
        )) 
    ;
    возврат КомандыСтатусов
;

метод ВыбраныВсеСтатусы(): Булево
    возврат Значения.Размер() == ДопустимыеЗначения.Размер()
;

метод ВыбратьВсеСтатусы()
    если ВыбраныВсеСтатусы()
        Значения.Очистить()
        Значения.Добавить(ДопустимыеЗначения.ПервыйИлиУмолчание()!)
    иначе    
        Значения.Очистить()
        Значения.ДобавитьВсе(ДопустимыеЗначения)
    ;    
;

метод ВыбратьСтатус(Статус: СтатусыПроцессаАдаптации)
    если Значения.Содержит(Статус)
        если Значения.Размер() > 1
            Значения.Удалить(Статус)
        иначе
            Значения.Удалить(Статус)
            Значения.Добавить(Статус)
        ;
    иначе
        Значения.Добавить(Статус)
    ;
;