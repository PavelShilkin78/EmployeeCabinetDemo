импорт КонтрольДоступа
импорт Компания
импорт ФизическиеЛица
импорт СтандартноеПриложение
импорт УниверсальныеМеханизмы

@ВПодсистеме конст КЛЮЧ_ПАРАМЕТРОВ_СЕАНСА: Строка = "АдаптацияПерсонала"

@ВПроекте конст ИД_ПОДСИСТЕМЫ = "АдаптацияПерсонала"

@ВПодсистеме конст ГРУППА_РЕСУРСОВ_АДАПТАЦИЯ_ПЕРСОНАЛА = "АдаптацияПерсонала"

@ВПроекте конст ИД_РАЗДЕЛА_АДАПТАЦИЯ_ПЕРСОНАЛА = "АдаптацияПерсонала"
@ВПроекте конст ИМЯ_РАЗДЕЛА_АДАПТАЦИЯ_ПЕРСОНАЛА = "Адаптация персонала"

@ВПроекте конст АДАПТАЦИЯ_НОВОГО_СОТРУДНИКА_SMS = "АдаптацияНовогоСотрудникаSMS"
@ВПроекте конст АДАПТАЦИЯ_НОВОГО_СОТРУДНИКА_PUSH = "АдаптацияНовогоСотрудникаPush"
@ВПроекте конст АДАПТАЦИЯ_НОВОГО_СОТРУДНИКА_EMAIL = "АдаптацияНовогоСотрудникаEmail"
@ВПроекте конст АДАПТАЦИЯ_НОВОГО_СОТРУДНИКА_ТЕМА = "АдаптацияНовогоСотрудникаТема"
@ВПроекте конст ИЗМЕНЕН_СТАТУС_ПРОЦЕССА_АДАПТАЦИИ_SMS = "ИзмененСтатусПроцессаАдаптацииSMS"
@ВПроекте конст ИЗМЕНЕН_СТАТУС_ПРОЦЕССА_АДАПТАЦИИ_PUSH = "ИзмененСтатусПроцессаАдаптацииPush"
@ВПроекте конст ИЗМЕНЕН_СТАТУС_ПРОЦЕССА_АДАПТАЦИИ_EMAIL = "ИзмененСтатусПроцессаАдаптацииEmail"
@ВПроекте конст ИЗМЕНЕН_СТАТУС_ПРОЦЕССА_АДАПТАЦИИ_ТЕМА = "ИзмененСтатусПроцессаАдаптацииТема"
@ВПроекте конст МЕРОПРИЯТИЕ_ПРОСРОЧЕНО_РУКОВОДИТЕЛЬ_SMS = "МероприятиеПросроченоРуководительSMS"
@ВПроекте конст МЕРОПРИЯТИЕ_ПРОСРОЧЕНО_РУКОВОДИТЕЛЬ_PUSH = "МероприятиеПросроченоРуководительPush"
@ВПроекте конст МЕРОПРИЯТИЕ_ПРОСРОЧЕНО_РУКОВОДИТЕЛЬ_EMAIL = "МероприятиеПросроченоРуководительEmail"
@ВПроекте конст МЕРОПРИЯТИЕ_ПРОСРОЧЕНО_РУКОВОДИТЕЛЬ_ТЕМА = "МероприятиеПросроченоРуководительТема"
@ВПроекте конст МЕРОПРИЯТИЕ_ПРОСРОЧЕНО_СОТРУДНИК_SMS = "МероприятиеПросроченоСотрудникSMS"
@ВПроекте конст МЕРОПРИЯТИЕ_ПРОСРОЧЕНО_СОТРУДНИК_PUSH = "МероприятиеПросроченоСотрудникPush"
@ВПроекте конст МЕРОПРИЯТИЕ_ПРОСРОЧЕНО_СОТРУДНИК_EMAIL = "МероприятиеПросроченоСотрудникEmail"
@ВПроекте конст МЕРОПРИЯТИЕ_ПРОСРОЧЕНО_СОТРУДНИК_ТЕМА = "МероприятиеПросроченоСотрудникТема"

@ВПодсистеме
структура ПараметрыСеанса
    пер ФункциональныеОпции: ФункциональныеОпции
    пер ПоказыватьРаздел: Булево
    пер ЭтоАдаптируемыйСотрудник: Булево
    пер ЭтоУчастникМероприятий: Булево
    пер ЭтоОтветственныйЗаАдаптацию: Булево
    пер ЭтоКонтролирующийАдаптацию: Булево
    пер ДоступКОтчетам: Булево
;

//КонтрольДоступа

конст ПОЛНОМОЧИЕ_ПРОСМОТР_ИНФОРМАЦИИ_ПО_ПРОЦЕССУ_АДАПТАЦИИ = "ПросмотрИнформацииПоПроцессуАдаптации"
конст ПОЛНОМОЧИЕ_РЕДАКТИРОВАНИЕ_ИНФОРМАЦИИ_ПО_ПРОЦЕССУ_АДАПТАЦИИ = "РедактированиеИнформацииПоПроцессуАдаптации"
конст ПОЛНОМОЧИЕ_ПРОСМОТР_УЧАСТНИКОВ_МЕРОПРИЯТИЯ = "ПросмотрУчастниковМероприятия"

@ВПроекте
@НаСервере
метод ДобавитьПолномочия()
    ДобавитьПолномочиеПросмотрИнформацииПоПроцессуАдаптации()
    ДобавитьПолномочиеРедактированиеИнформацииПоПроцессуАдаптации()
    ДобавитьПолномочиеПросмотрУчастниковМероприятия()
;

@ВПроекте
@НаСервере
метод ПолномочиеПросмотрИнформацииПоПроцессуАдаптации(): Полномочия.Ссылка
    возврат КонтрольДоступа.Полномочие(ПОЛНОМОЧИЕ_ПРОСМОТР_ИНФОРМАЦИИ_ПО_ПРОЦЕССУ_АДАПТАЦИИ)
;

@ВПроекте
@НаСервере
метод ПолномочиеРедактированиеИнформацииПоПроцессуАдаптации(): Полномочия.Ссылка
    возврат КонтрольДоступа.Полномочие(ПОЛНОМОЧИЕ_РЕДАКТИРОВАНИЕ_ИНФОРМАЦИИ_ПО_ПРОЦЕССУ_АДАПТАЦИИ)
;

@ВПроекте
@НаСервере
метод ПолномочиеПросмотрУчастниковМероприятия(): Полномочия.Ссылка
    возврат КонтрольДоступа.Полномочие(ПОЛНОМОЧИЕ_ПРОСМОТР_УЧАСТНИКОВ_МЕРОПРИЯТИЯ)
;

@НаСервере
метод ДобавитьПолномочиеПросмотрИнформацииПоПроцессуАдаптации()
    КонтрольДоступа.ДобавитьПолномочие(
        "Просмотр информации по процессу адаптации",
        ПОЛНОМОЧИЕ_ПРОСМОТР_ИНФОРМАЦИИ_ПО_ПРОЦЕССУ_АДАПТАЦИИ,
        "Позволяет просматривать всю информацию по процессу адаптации, включая все мероприятия.",
        Компания.ТипАтрибутаДоступаПодразделения()
    )
;

@НаСервере
метод ДобавитьПолномочиеРедактированиеИнформацииПоПроцессуАдаптации()
    КонтрольДоступа.ДобавитьПолномочие(
        "Редактирование информации по процессу адаптации",
        ПОЛНОМОЧИЕ_РЕДАКТИРОВАНИЕ_ИНФОРМАЦИИ_ПО_ПРОЦЕССУ_АДАПТАЦИИ,
        "Позволяет управлять процессом адаптации, включая планирование мероприятий.",
        Компания.ТипАтрибутаДоступаПодразделения()
    )
;

@НаСервере
метод ДобавитьПолномочиеПросмотрУчастниковМероприятия()
    КонтрольДоступа.ДобавитьПолномочие(
        "Просмотр участников мероприятия",
        ПОЛНОМОЧИЕ_ПРОСМОТР_УЧАСТНИКОВ_МЕРОПРИЯТИЯ,
        "Позволяет просматривать участников мероприятий.",
        КонтрольДоступа.ТипАтрибутаДоступаФизическиеЛица()
    )
;

//КонтрольДоступа

@ВПроекте
@НаСервере @ДоступноСКлиента
метод ОтветственныеПоАдаптации(): ЧитаемаяКоллекция<ФизическиеЛица.Ссылка>
    возврат КонтрольДоступа.ФизическиеЛицаПоПолномочию(ПолномочиеРедактированиеИнформацииПоПроцессуАдаптации())
;

@ВПроекте
@НаКлиенте
метод НастройкиВводаФизическоеЛицоЯвляетсяОтветственнымПоАдаптации(): НастройкиВводаСсылок
    возврат ФизЛица.НастройкиВводаОграниченныйСписок(ОтветственныеПоАдаптации())
;

@ВПроекте
@НаКлиенте
метод НастройкиВводаФизическоеЛицоЯвляетсяСотрудником(): НастройкиВводаСсылок
    знч Настройка = новый НастройкиВводаСсылки(
        ПсевдонимОсновнойТаблицы = "ФизическиеЛица",
        ПрисоединенныеТаблицы = [
            новый ПрисоединеннаяТаблица(
                Таблица = "%Сотрудники",
                Псевдоним = "Сотрудники",
                Фильтр = новый ГруппаЭлементовФильтра(
                    Элементы = [
                        новый ЭлементФильтраВыражение(
                            Выражение = "ФизическиеЛица.Ссылка == Сотрудники.ФизическоеЛицо"
                        )
                    ]
                )
            )
        ],
        Фильтр = новый ГруппаЭлементовФильтра(
            Элементы = [
                новый ЭлементФильтраВыражение(
                    Выражение = "Сотрудники.Ссылка ЕСТЬ НЕ NULL"
                )
            ]
        )
    )
    возврат новый НастройкиВводаСсылок({Тип<ФизическиеЛица.Ссылка>: Настройка})
;

@ВПодсистеме
@НаСервере
метод ЭтоАдаптируемыйСотрудник(Сотрудник: ФизическиеЛица.Ссылка): Булево
    исп КонтекстДоступа.Привилегированный()
    возврат ПроцессыАдаптации.ВыполняющийсяПроцессАдаптацииПоСотруднику(Сотрудник) != Неопределено
;

@ВПодсистеме
@НаСервере
метод ЭтоУчастникМероприятий(ФизическоеЛицо: ФизическиеЛица.Ссылка): Булево
    возврат УчастникиМероприятий.ЕстьЗапланированныеМероприятия(ФизическоеЛицо)
;

@ВПодсистеме
@НаСервере
метод ДоступныеПодразделения(): ЧитаемоеМножество<СтруктураПредприятия.Ссылка>
    знч Запрос = Запрос{
        ВЫБРАТЬ РАЗЛИЧНЫЕ
            Подразделение
        ИЗ
            ПроцессыАдаптации
    }
    исп РезультатЗапроса = Запрос.Выполнить()
    возврат РезультатЗапроса.Преобразовать(С -> С.Подразделение!).ВоМножество()    
;

@ВПроекте
@НаКлиенте
метод ПараметрыСеанса(): ПараметрыСеанса
    возврат ПараметрыСеансаСервис.ПолучитьСервис().ПараметрПоКлючу(КЛЮЧ_ПАРАМЕТРОВ_СЕАНСА) как ПараметрыСеанса
;

// Область ФункциональныеОпции

@ВПроекте
структура ФункциональныеОпции
    пер ИспользованиеПодсистемы: Булево
;

@ВПроекте
@НаСервере
метод ПолучитьФункциональныеОпции(): ФункциональныеОпции?
    возврат ИспользуемаяФункциональность.ФункциональныеОпции()
;

@ВПроекте
@НаСервере @ДоступноСКлиента
метод ЗаписатьФункциональныеОпции(ФункциональныеОпции: ФункциональныеОпции)
    ИспользуемаяФункциональность.ЗаписатьФункциональныеОпции(ФункциональныеОпции)
;

// Конец ФункциональныеОпции

@ВПроекте
@НаСервере
метод ПодсистемаИспользуется(): Булево
    возврат ИспользуемаяФункциональность.ПодсистемаИспользуется()
;

@ВПроекте
@НаСервере @НаКлиенте
метод МожноСоздатьПроцессАдаптации(): Булево
    возврат РасширениеПерсонал.Доступно()
;

@ВПроекте
@НаСервере @ДоступноСКлиента
метод ЕстьПравоСоздатьПроцессАдаптации(): Булево
    если РасширениеПерсонал.Доступно()
        возврат РасширениеПерсонал.ЕстьПравоСоздатьПроцессАдаптации()
    иначе
        возврат КонтрольДоступа.ЕстьПравоНаСоздание(Тип<ПроцессыАдаптации>)
    ;
;

@ВПроекте
@НаСервере @НаКлиенте
метод МожноЗапуститьПроцессАдаптации(Статус: СтатусыПроцессаАдаптации): Булево
    если РасширениеПерсонал.Доступно()
        возврат Статус == СтатусыПроцессаАдаптации.Черновик
    иначе
        возврат Ложь
    ;  
;

@ВПроекте
@НаСервере @НаКлиенте
метод МожноЗапуститьПроцессАдаптации(Статус: СтатусыПроцессаАдаптации, HrМенеджерУказанВПроцессеАдаптации: Булево): Булево
    если РасширениеПерсонал.Доступно()
        возврат Статус == СтатусыПроцессаАдаптации.Черновик и HrМенеджерУказанВПроцессеАдаптации
    иначе
        возврат Ложь
    ;  
;

@ВПроекте
@НаСервере @ДоступноСКлиента
метод ЕстьПравоЗапуститьПроцессАдаптации(ПроцессАдаптации: ПроцессыАдаптации.Ссылка): Булево
    если РасширениеПерсонал.Доступно()
        возврат КонтрольДоступа.ЕстьПравоНаИзменение(ПроцессАдаптации)
    иначе
        возврат Ложь    
    ;   
;

@ВПроекте
@НаСервере @НаКлиенте
метод МожноЗавершитьПроцессАдаптации(Статус: СтатусыПроцессаАдаптации): Булево
    если РасширениеПерсонал.Доступно()
        возврат Статус == СтатусыПроцессаАдаптации.Выполняется
    иначе
        возврат Ложь    
    ;    
;

@ВПроекте
@НаСервере @ДоступноСКлиента
метод ЕстьПравоЗавершитьПроцессАдаптации(ПроцессАдаптации: ПроцессыАдаптации.Ссылка): Булево
    если РасширениеПерсонал.Доступно()
        возврат КонтрольДоступа.ЕстьПравоНаИзменение(ПроцессАдаптации)
    иначе
        возврат Ложь    
    ;   
;

@ВПроекте
@НаСервере @НаКлиенте
метод МожноОтменитьПроцессАдаптации(Статус: СтатусыПроцессаАдаптации): Булево
    если РасширениеПерсонал.Доступно()
        возврат Статус == СтатусыПроцессаАдаптации.Выполняется
    иначе
        возврат Ложь    
    ;    
;

@ВПроекте
@НаСервере @ДоступноСКлиента
метод ЕстьПравоОтменитьПроцессАдаптации(ПроцессАдаптации: ПроцессыАдаптации.Ссылка): Булево
    если РасширениеПерсонал.Доступно()
        возврат КонтрольДоступа.ЕстьПравоНаИзменение(ПроцессАдаптации)
    иначе
        возврат Ложь    
    ;   
;

@ВПроекте
@НаСервере @НаКлиенте
метод МожноУдалитьПроцессАдаптации(Статус: СтатусыПроцессаАдаптации): Булево
    если РасширениеПерсонал.Доступно()
        возврат Статус == СтатусыПроцессаАдаптации.Черновик
    иначе
        возврат Ложь
    ;
;

@ВПроекте
@НаСервере @ДоступноСКлиента
метод ЕстьПравоУдалитьПроцессАдаптации(ПроцессАдаптации: ПроцессыАдаптации.Ссылка): Булево
    если РасширениеПерсонал.Доступно()
        возврат РасширениеПерсонал.ЕстьПравоУдалитьПроцессАдаптации(ПроцессАдаптации)
    иначе
        возврат КонтрольДоступа.ЕстьПравоНаИзменение(ПроцессАдаптации)
    ;
;

@ВПроекте
@НаСервере @НаКлиенте
метод МожноРедактироватьМероприятие(СтатусМероприятия: СтатусыМероприятия, СтатусПроцессаАдаптации: СтатусыПроцессаАдаптации): Булево
    возврат не СтатусМероприятия.Конечный() и не СтатусПроцессаАдаптации.Конечный()
;

@ВПроекте
@НаСервере @ДоступноСКлиента
метод ЕстьПравоРедактироватьМероприятие(Мероприятие: Мероприятия.Ссылка): Булево
    возврат КонтрольДоступа.ЕстьПравоНаИзменение(Мероприятие)
;

@ВПроекте
@НаСервере @НаКлиенте
метод МожноУдалитьМероприятие(СтатусМероприятия: СтатусыМероприятия): Булево
    возврат СтатусМероприятия == СтатусыМероприятия.Черновик
;

@ВПроекте
@НаСервере @ДоступноСКлиента
метод ЕстьПравоУдалитьМероприятие(Мероприятие: Мероприятия.Ссылка): Булево
    возврат КонтрольДоступа.ЕстьПравоНаИзменение(Мероприятие)
;

@ВПроекте
@НаСервере @НаКлиенте
метод МожноРедактироватьПроцессАдаптации(): Булево
    возврат РасширениеПерсонал.Доступно()
;

@ВПроекте
@НаСервере @НаКлиенте
метод МожноРедактироватьПроцессАдаптации(Статус: СтатусыПроцессаАдаптации): Булево
    возврат МожноРедактироватьПроцессАдаптации() ? не Статус.Конечный() : Ложь
;

@ВПроекте
@НаСервере @НаКлиенте
метод МожноРедактироватьHrМенеджераПроцессаАдаптации(СтатусПроцессаАдаптации: СтатусыПроцессаАдаптации, ЕстьМероприятия: Булево): Булево
    возврат МожноРедактироватьПроцессАдаптации(СтатусПроцессаАдаптации) и СтатусПроцессаАдаптации == СтатусыПроцессаАдаптации.Черновик и не ЕстьМероприятия
;

@ВПроекте
@НаСервере @ДоступноСКлиента
метод ЕстьПравоРедактироватьПроцессАдаптации(ПроцессАдаптации: ПроцессыАдаптации.Ссылка): Булево
    возврат КонтрольДоступа.ЕстьПравоНаИзменение(ПроцессАдаптации)
;

@ВПроекте
@НаСервере @НаКлиенте
метод МожноРедактироватьСтатусПроцессаАдаптации(HrМенеджерУказанВПроцессеАдаптации: Булево): Булево
    возврат МожноРедактироватьПроцессАдаптации() ? HrМенеджерУказанВПроцессеАдаптации : Ложь
;

@ВПроекте
@НаСервере @ДоступноСКлиента
метод ЕстьПравоРедактироватьСтатусПроцессаАдаптации(ПроцессАдаптации: ПроцессыАдаптации.Ссылка): Булево
    возврат ЕстьПравоРедактироватьПроцессАдаптации(ПроцессАдаптации)
;

@ВПроекте
@НаСервере @НаКлиенте
метод МожноРедактироватьСтатусыМероприятий(СтатусПроцессаАдаптации: СтатусыПроцессаАдаптации): Булево
    возврат СтатусПроцессаАдаптации == СтатусыПроцессаАдаптации.Выполняется
;

@ВПроекте
@НаСервере @НаКлиенте
метод МожноРедактироватьСтатусМероприятия(СтатусПроцессаАдаптации: СтатусыПроцессаАдаптации, СтатусМероприятия: СтатусыМероприятия): Булево
    возврат МожноРедактироватьСтатусыМероприятий(СтатусПроцессаАдаптации)
            или СтатусПроцессаАдаптации.Конечный() и не СтатусМероприятия.Конечный()
;

@ВПроекте
@НаСервере @ДоступноСКлиента
метод МожноРедактироватьСтатусМероприятия(Мероприятие: Мероприятия.Ссылка): Булево
    исп ПривилегированныйРежим = КонтекстДоступа.Привилегированный()
    знч ПроцессАдаптации = Мероприятия.Прочитать(Мероприятие).ПроцессАдаптации
    знч СтатусПроцессаАдаптации = СтатусыПроцессовАдаптации.Прочитать(ПроцессАдаптации)!
    знч СтатусМероприятия = СтатусыМероприятий.Прочитать(Мероприятие)
    ПривилегированныйРежим.Закрыть()
    возврат МожноРедактироватьСтатусМероприятия(СтатусПроцессаАдаптации, СтатусМероприятия)
;

@ВПодсистеме
@НаСервере @НаКлиенте
структура КонтекстДоступаКСтатусамМероприятий
    обз знч ТекущееФизическоеЛицо: ФизическиеЛица.Ссылка?
    обз знч ЕстьПравоРедактироватьПроцессыАдаптацииВсехПодразделений: Булево
    обз знч ПодразделенияПоКоторымЕстьПравоРедактироватьПроцессыАдаптации: ЧитаемыйМассив<СтруктураПредприятия.Ссылка>
;

@ВПодсистеме
@НаКлиенте
метод КонтекстДоступаКСтатусамМероприятий(): КонтекстДоступаКСтатусамМероприятий
    возврат новый КонтекстДоступаКСтатусамМероприятий(
        ТекущееФизическоеЛицо = КонтрольДоступа.ПараметрыСеанса().ФизлицоТекущегоПользователя,
        ЕстьПравоРедактироватьПроцессыАдаптацииВсехПодразделений = ЕстьПравоРедактироватьПроцессыАдаптацииВсехПодразделений(),
        ПодразделенияПоКоторымЕстьПравоРедактироватьПроцессыАдаптации = []
    )
;

@ВПодсистеме
@НаСервере @НаКлиенте
структура ПризнакиДоступаКСтатусамМероприятий
    обз знч ОтветственныйЗаМероприятие: ФизическиеЛица.Ссылка?
    обз знч РуководительПроцессаАдаптации: ФизическиеЛица.Ссылка?
    обз знч ПодразделениеПроцессаАдаптации: СтруктураПредприятия.Ссылка?
    обз знч СтатусМероприятия: СтатусыМероприятия
    обз знч СтатусПроцессаАдаптации: СтатусыПроцессаАдаптации
;

@НаСервере @ДоступноСКлиента
метод ЕстьПравоРедактироватьПроцессыАдаптацииВсехПодразделений(): Булево
    возврат КонтрольДоступа.ЕстьПравоНаПолномочие(ПолномочиеРедактированиеИнформацииПоПроцессуАдаптации())
;

@ВПодсистеме
@НаСервере @НаКлиенте
метод ЕстьВозможностьРедактироватьСтатусМероприятия(КонтекстДоступа: КонтекстДоступаКСтатусамМероприятий, ПризнакиДоступа: ПризнакиДоступаКСтатусамМероприятий): Булево
    возврат МожноРедактироватьСтатусМероприятия(ПризнакиДоступа.СтатусПроцессаАдаптации, ПризнакиДоступа.СтатусМероприятия)
            и ЕстьПравоРедактироватьСтатусМероприятия(КонтекстДоступа, ПризнакиДоступа)
;

@НаСервере @НаКлиенте
метод ЕстьПравоРедактироватьСтатусМероприятия(КонтекстДоступа: КонтекстДоступаКСтатусамМероприятий, ПризнакиДоступа: ПризнакиДоступаКСтатусамМероприятий): Булево
    знч ФизлицоОпределено = КонтекстДоступа.ТекущееФизическоеЛицо != Неопределено
    знч ЕстьПравоОтветственного = ФизлицоОпределено и КонтекстДоступа.ТекущееФизическоеЛицо! == ПризнакиДоступа.ОтветственныйЗаМероприятие
    знч ЕстьПравоРуководителя = ФизлицоОпределено и КонтекстДоступа.ТекущееФизическоеЛицо! == ПризнакиДоступа.РуководительПроцессаАдаптации
    пер ЕстьПравоHrМенеджера = КонтекстДоступа.ЕстьПравоРедактироватьПроцессыАдаптацииВсехПодразделений
    если РасширениеПерсонал.Доступно()
        если ПризнакиДоступа.ПодразделениеПроцессаАдаптации != Неопределено
            ЕстьПравоHrМенеджера = ЕстьПравоHrМенеджера или КонтекстДоступа.ПодразделенияПоКоторымЕстьПравоРедактироватьПроцессыАдаптации.Содержит(ПризнакиДоступа.ПодразделениеПроцессаАдаптации!)
        ;
    ;
    возврат ЕстьПравоОтветственного или ЕстьПравоРуководителя или ЕстьПравоHrМенеджера
;

@ВПроекте
@НаСервере @ДоступноСКлиента
метод ЕстьПравоРедактироватьСтатусМероприятия(Мероприятие: Мероприятия.Ссылка): Булево
    исп ПривилегированныйРежим = КонтекстДоступа.Привилегированный()
    знч ДанныеМероприятия = Мероприятия.Прочитать(Мероприятие)
    ПривилегированныйРежим.Закрыть()
    знч ЭтоОтветственный = ДанныеМероприятия.Ответственный == ПользователиСистемы.ТекущееФизическоеЛицо()
    возврат ЭтоОтветственный или ЕстьПравоРедактироватьСтатусЛюбогоМероприятия(ДанныеМероприятия.ПроцессАдаптации)
;

@ВПроекте
@НаСервере @ДоступноСКлиента
метод ЕстьПравоРедактироватьСтатусЛюбогоМероприятия(ПроцессАдаптации: ПроцессыАдаптации.Ссылка): Булево
    знч ЕстьПравоРедактироватьПроцессАдаптации = ЕстьПравоРедактироватьПроцессАдаптации(ПроцессАдаптации)
    если РасширениеПерсонал.Доступно()
        возврат ЕстьПравоРедактироватьПроцессАдаптации
    иначе
        исп КонтекстДоступа.Привилегированный()
        возврат ЕстьПравоРедактироватьПроцессАдаптации или ПроцессыАдаптации.Прочитать(ПроцессАдаптации).Руководитель == ПользователиСистемы.ТекущееФизическоеЛицо()
    ;
;

@ВПроекте
@НаСервере @НаКлиенте
метод МожноДобавлятьВнеплановыеМероприятия(СтатусПроцессаАдаптации: СтатусыПроцессаАдаптации, HrМенеджерУказанВПроцессеАдаптации: Булево): Булево
    возврат не СтатусПроцессаАдаптации.Конечный() и HrМенеджерУказанВПроцессеАдаптации
;

@ВПроекте
@НаСервере @ДоступноСКлиента
метод ЕстьПравоДобавлятьВнеплановыеМероприятия(ПроцессАдаптации: ПроцессыАдаптации.Ссылка): Булево
    знч ЕстьПравоРедактироватьПроцессАдаптации = ЕстьПравоРедактироватьПроцессАдаптации(ПроцессАдаптации)
    если РасширениеПерсонал.Доступно()
        возврат ЕстьПравоРедактироватьПроцессАдаптации
    иначе
        исп КонтекстДоступа.Привилегированный()
        возврат ЕстьПравоРедактироватьПроцессАдаптации или ПроцессыАдаптации.Прочитать(ПроцессАдаптации).Руководитель == ПользователиСистемы.ТекущееФизическоеЛицо()
    ;
;

@ВПроекте
@НаСервере
метод ОбновитьСтроковыеРесурсы()

    пер АдаптацияНовогоСотрудникаSMS = "У вас новый сотрудник. Назначьте ему план адаптации или добавьте мероприятия."
    пер Комментарий = "Адаптация нового сотрудника (sms)"
    УниверсальныеМеханизмы.ЗаписатьЗначениеРесурса(ГРУППА_РЕСУРСОВ_АДАПТАЦИЯ_ПЕРСОНАЛА, АДАПТАЦИЯ_НОВОГО_СОТРУДНИКА_SMS, Комментарий, АдаптацияНовогоСотрудникаSMS, 70)

    пер АдаптацияНовогоСотрудникаPush = "У Вас новый сотрудник \%КМПолноеИмяКандидата\%. Назначьте ему план или добавьте мероприятия для адаптации."
    Комментарий = "Адаптация нового сотрудника (push)"
    УниверсальныеМеханизмы.ЗаписатьЗначениеРесурса(ГРУППА_РЕСУРСОВ_АДАПТАЦИЯ_ПЕРСОНАЛА, АДАПТАЦИЯ_НОВОГО_СОТРУДНИКА_PUSH, Комментарий, АдаптацияНовогоСотрудникаPush)

    пер АдаптацияНовогоСотрудникаEmail =
        "Добрый день, \%КМПолноеИмяПолучателя\%

        Скоро выйдет новый сотрудник \%КМПолноеИмяКандидата\% на должность \%КМНаименованиеДолжности\%.
        Пожалуйста назначьте план для успешного прохождения процесса адаптации.
        Дата выхода сотрудника: \%ДатаВыходаНаРаботу\%

        \%КМПодпись\%"
    Комментарий = "Адаптация нового сотрудника (email)"
    УниверсальныеМеханизмы.ЗаписатьЗначениеРесурса(ГРУППА_РЕСУРСОВ_АДАПТАЦИЯ_ПЕРСОНАЛА, АДАПТАЦИЯ_НОВОГО_СОТРУДНИКА_EMAIL, Комментарий, АдаптацияНовогоСотрудникаEmail)

    пер АдаптацияНовогоСотрудникаТема = "Новый сотрудник \%КМПолноеИмяКандидата\% для адаптации"
    Комментарий = "Адаптация нового сотрудника (тема письма)"
    УниверсальныеМеханизмы.ЗаписатьЗначениеРесурса(ГРУППА_РЕСУРСОВ_АДАПТАЦИЯ_ПЕРСОНАЛА, АДАПТАЦИЯ_НОВОГО_СОТРУДНИКА_ТЕМА, Комментарий, АдаптацияНовогоСотрудникаТема)

    пер ИзмененСтатусПроцессаАдаптацииSMS = "Статус процесса адаптации изменился. Ознакомьтесь с деталями."
    Комментарий = "Изменен статус процесса адаптации (sms)"
    УниверсальныеМеханизмы.ЗаписатьЗначениеРесурса(ГРУППА_РЕСУРСОВ_АДАПТАЦИЯ_ПЕРСОНАЛА, ИЗМЕНЕН_СТАТУС_ПРОЦЕССА_АДАПТАЦИИ_SMS, Комментарий, ИзмененСтатусПроцессаАдаптацииSMS, 70)

    пер ИзмененСтатусПроцессаАдаптацииPush = "Статус процесса адаптации сотрудника \%КМПолноеИмяСотрудника\%. изменен на \%КМСтатусАдаптации\%. Ознакомьтесь с деталями адаптации."
    Комментарий = "Изменен статус процесса адаптации (push)"
    УниверсальныеМеханизмы.ЗаписатьЗначениеРесурса(ГРУППА_РЕСУРСОВ_АДАПТАЦИЯ_ПЕРСОНАЛА, ИЗМЕНЕН_СТАТУС_ПРОЦЕССА_АДАПТАЦИИ_PUSH, Комментарий, ИзмененСтатусПроцессаАдаптацииPush)

    пер ИзмененСтатусПроцессаАдаптацииEmail =
        "Добрый день, \%КМПолноеИмяПолучателя\%

        Статус процесса адаптации по сотруднику \%КМПолноеИмяСотрудника\% изменен на статус \%КМСтатусАдаптации\%.
        Вы можете ознакомиться с деталями по ссылке
        \%КМПроцессАдаптации\%

        \%КМПодпись\%"
    Комментарий = "Изменен статус процесса адаптации (email)"
    УниверсальныеМеханизмы.ЗаписатьЗначениеРесурса(ГРУППА_РЕСУРСОВ_АДАПТАЦИЯ_ПЕРСОНАЛА, ИЗМЕНЕН_СТАТУС_ПРОЦЕССА_АДАПТАЦИИ_EMAIL, Комментарий, ИзмененСтатусПроцессаАдаптацииEmail)

    пер ИзмененСтатусПроцессаАдаптацииТема = "Статус процесса адаптации сотрудника \%КМПолноеИмяСотрудника\% изменен"
    Комментарий = "Изменен статус процесса адаптации (тема письма)"
    УниверсальныеМеханизмы.ЗаписатьЗначениеРесурса(ГРУППА_РЕСУРСОВ_АДАПТАЦИЯ_ПЕРСОНАЛА, ИЗМЕНЕН_СТАТУС_ПРОЦЕССА_АДАПТАЦИИ_ТЕМА, Комментарий, ИзмененСтатусПроцессаАдаптацииТема)

    пер МероприятиеПросроченоРуководительSMS = "Мероприятие просрочено. Скорректируйте его или измените статус."
    Комментарий = "Уведомление о просроченном мероприятии для руководителя (sms)"
    УниверсальныеМеханизмы.ЗаписатьЗначениеРесурса(ГРУППА_РЕСУРСОВ_АДАПТАЦИЯ_ПЕРСОНАЛА, МЕРОПРИЯТИЕ_ПРОСРОЧЕНО_РУКОВОДИТЕЛЬ_SMS, Комментарий, МероприятиеПросроченоРуководительSMS, 70)

    пер МероприятиеПросроченоРуководительPush = "Срок по мероприятию \%КМНаименованиеМероприятия\% был нарушен у \%КМПолноеИмяСотрудника\%. Необходимо выяснить причину и внести изменения."
    Комментарий = "Уведомление о просроченном мероприятии для руководителя (push)"
    УниверсальныеМеханизмы.ЗаписатьЗначениеРесурса(ГРУППА_РЕСУРСОВ_АДАПТАЦИЯ_ПЕРСОНАЛА, МЕРОПРИЯТИЕ_ПРОСРОЧЕНО_РУКОВОДИТЕЛЬ_PUSH, Комментарий, МероприятиеПросроченоРуководительPush)

    пер МероприятиеПросроченоРуководительEmail =
        "Добрый день, \%КМПолноеИмяПолучателя\%

        Срок по мероприятию \%КМНаименованиеМероприятия\% был просрочен.
        Пожалуйста проверьте статус мероприятия, скорректируйте сроки и обсудите с сотрудником.

        \%КМПодпись\%"
    Комментарий = "Уведомление о просроченном мероприятии для руководителя (email)"
    УниверсальныеМеханизмы.ЗаписатьЗначениеРесурса(ГРУППА_РЕСУРСОВ_АДАПТАЦИЯ_ПЕРСОНАЛА, МЕРОПРИЯТИЕ_ПРОСРОЧЕНО_РУКОВОДИТЕЛЬ_EMAIL, Комментарий, МероприятиеПросроченоРуководительEmail)

    пер МероприятиеПросроченоРуководительТема = "Срок по мероприятию \%КМНаименованиеМероприятия\% был нарушен у \%КМПолноеИмяСотрудника\%"
    Комментарий = "Уведомление о просроченном мероприятии для руководителя (тема письма)"
    УниверсальныеМеханизмы.ЗаписатьЗначениеРесурса(ГРУППА_РЕСУРСОВ_АДАПТАЦИЯ_ПЕРСОНАЛА, МЕРОПРИЯТИЕ_ПРОСРОЧЕНО_РУКОВОДИТЕЛЬ_ТЕМА, Комментарий, МероприятиеПросроченоРуководительТема)

    пер МероприятиеПросроченоСотрудникSMS = "Мероприятие просрочено. Скорректируйте его или измените статус."
    Комментарий = "Уведомление о просроченном мероприятии для сотрудника (sms)"
    УниверсальныеМеханизмы.ЗаписатьЗначениеРесурса(ГРУППА_РЕСУРСОВ_АДАПТАЦИЯ_ПЕРСОНАЛА, МЕРОПРИЯТИЕ_ПРОСРОЧЕНО_СОТРУДНИК_SMS, Комментарий, МероприятиеПросроченоСотрудникSMS, 70)

    пер МероприятиеПросроченоСотрудникPush = "Срок по мероприятию \%КМНаименованиеМероприятия\% был нарушен. Внесите изменения или сообщение HR."
    Комментарий = "Уведомление о просроченном мероприятии для сотрудника (push)"
    УниверсальныеМеханизмы.ЗаписатьЗначениеРесурса(ГРУППА_РЕСУРСОВ_АДАПТАЦИЯ_ПЕРСОНАЛА, МЕРОПРИЯТИЕ_ПРОСРОЧЕНО_СОТРУДНИК_PUSH, Комментарий, МероприятиеПросроченоСотрудникPush)

    пер МероприятиеПросроченоСотрудникEmail =
        "Добрый день, \%КМПолноеИмяПолучателя\%

        Срок по мероприятию \%КМНаименованиеМероприятия\% прошел.
        Пожалуйста проверьте статус мероприятия и скорректируйте сроки или сообщите HR.

        \%КМПодпись\%"
    Комментарий = "Уведомление о просроченном мероприятии для сотрудника (email)"
    УниверсальныеМеханизмы.ЗаписатьЗначениеРесурса(ГРУППА_РЕСУРСОВ_АДАПТАЦИЯ_ПЕРСОНАЛА, МЕРОПРИЯТИЕ_ПРОСРОЧЕНО_СОТРУДНИК_EMAIL, Комментарий, МероприятиеПросроченоСотрудникEmail)

    пер МероприятиеПросроченоСотрудникТема = "Срок по мероприятию \%КМНаименованиеМероприятия\% уже прошел"
    Комментарий = "Уведомление о просроченном мероприятии для сотрудника (тема письма)"
    УниверсальныеМеханизмы.ЗаписатьЗначениеРесурса(ГРУППА_РЕСУРСОВ_АДАПТАЦИЯ_ПЕРСОНАЛА, МЕРОПРИЯТИЕ_ПРОСРОЧЕНО_СОТРУДНИК_ТЕМА, Комментарий, МероприятиеПросроченоСотрудникТема)

;