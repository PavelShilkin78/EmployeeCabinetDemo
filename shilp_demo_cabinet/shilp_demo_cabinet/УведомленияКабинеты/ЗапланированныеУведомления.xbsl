импорт АдаптацияПерсонала
импорт ОбщиеСтандартные
импорт Уведомления
импорт УниверсальныеМеханизмы
импорт ФизическиеЛица
импорт Компания

@ВПодсистеме
метод ЗапланироватьУведомленияОПросроченныхМероприятиях()
    пер Расписание = [Расписание.Ежедневно(Время{06:00:00})]
    пер КлючЗадания = "УведомленияОПросроченныхМероприятиях"
    МенеджерЗаданий.ПроверитьАвтозапускЗапланированныхЗаданийВключен()
    если ЗапланированныеЗадания.ПолучитьПоКлючу(КлючЗадания) == Неопределено
        ЗапланированныеЗадания.Создать(&ОтправитьУведомленияОПросроченныхМероприятиях)
            .Настроить(Ключ = КлючЗадания, Расписание = Расписание)
            .Запланировать()
    иначе
        ЗапланированныеЗадания.Изменить(КлючЗадания)
            ?.Настроить(Расписание = Расписание)
            .Запланировать()
    ;
;

@ВПроекте
метод ОтправитьУведомленияОПросроченныхМероприятиях()
    
    знч Вчера: Дата = Дата.Сейчас() - 1д
    знч Запрос = Запрос{
        Выбрать 
            Мероприятия.Ссылка Как Мероприятие,
            Мероприятия.ПроцессАдаптации.Сотрудник Как Сотрудник,
            Мероприятия.ПроцессАдаптации.Руководитель Как Руководитель
        Из
            Мероприятия Как Мероприятия
            Левое Соединение 
            СтатусыМероприятий.СрезПоследних Как СтатусыМероприятий
            По Мероприятия.Ссылка == СтатусыМероприятий.Мероприятие
        Где
            Мероприятия.ПровестиНеПозднее == %Вчера
            И СтатусыМероприятий.Статус == СтатусыМероприятия.Запланировано
    }
    
    исп РезультатЗапроса = Запрос.Выполнить() 
    для РезультатЗапросаСтрока из РезультатЗапроса        
        если РезультатЗапросаСтрока.Руководитель != Неопределено
            знч ИменаРесурсов = [
                АдаптацияПерсонала.МЕРОПРИЯТИЕ_ПРОСРОЧЕНО_РУКОВОДИТЕЛЬ_SMS,
                АдаптацияПерсонала.МЕРОПРИЯТИЕ_ПРОСРОЧЕНО_РУКОВОДИТЕЛЬ_PUSH,
                АдаптацияПерсонала.МЕРОПРИЯТИЕ_ПРОСРОЧЕНО_РУКОВОДИТЕЛЬ_EMAIL,
                АдаптацияПерсонала.МЕРОПРИЯТИЕ_ПРОСРОЧЕНО_РУКОВОДИТЕЛЬ_ТЕМА]
            знч ЗначенияРесурсов = УниверсальныеМеханизмы.ЗначенияРесурсовПодсистемы(АдаптацияПерсонала.ИД_РАЗДЕЛА_АДАПТАЦИЯ_ПЕРСОНАЛА, ИменаРесурсов)
            
            знч Получатель = РезультатЗапросаСтрока.Руководитель как ФизическиеЛица.Ссылка
            пер ТекстыСообщений = ФизЛица.ЗаполнитьПараметрыПолучателяВСообщении(ЗначенияРесурсов, Получатель)       
            ТекстыСообщений = Мероприятия.ЗаполнитьПараметрыМероприятияВСообщении(ТекстыСообщений, РезультатЗапросаСтрока.Мероприятие)
            ТекстыСообщений = ФизЛица.ЗаполнитьПолноеИмяФизическогоЛицаВСообщении(ТекстыСообщений, РезультатЗапросаСтрока.Сотрудник как ФизическиеЛица.Ссылка, "\%КМПолноеИмяСотрудника\%")       
            ТекстыСообщений = ОписаниеРаботодателя.ЗаполнитьОписаниеРаботодателяВСообщении(ТекстыСообщений)
            знч ТекстыУведомлений = новый УведомленияСервер.ТекстыСообщений(
                ТекстыСообщений[АдаптацияПерсонала.МЕРОПРИЯТИЕ_ПРОСРОЧЕНО_РУКОВОДИТЕЛЬ_ТЕМА],
                ТекстыСообщений[АдаптацияПерсонала.МЕРОПРИЯТИЕ_ПРОСРОЧЕНО_РУКОВОДИТЕЛЬ_SMS],
                ТекстыСообщений[АдаптацияПерсонала.МЕРОПРИЯТИЕ_ПРОСРОЧЕНО_РУКОВОДИТЕЛЬ_PUSH],
                ТекстыСообщений[АдаптацияПерсонала.МЕРОПРИЯТИЕ_ПРОСРОЧЕНО_РУКОВОДИТЕЛЬ_EMAIL])
            знч ДанныеУведомления = новый УведомленияСервер.ДанныеУведомления(Уведомления.ВидыУведомлений.ПерсональнаяИнформация, Получатель)
            УведомленияСервер.ОтправитьУведомление(Получатель, ТекстыУведомлений, ДанныеУведомления)
        ;
        
        если РезультатЗапросаСтрока.Сотрудник != Неопределено
            знч ИменаРесурсов = [
                АдаптацияПерсонала.МЕРОПРИЯТИЕ_ПРОСРОЧЕНО_СОТРУДНИК_SMS,
                АдаптацияПерсонала.МЕРОПРИЯТИЕ_ПРОСРОЧЕНО_СОТРУДНИК_PUSH,
                АдаптацияПерсонала.МЕРОПРИЯТИЕ_ПРОСРОЧЕНО_СОТРУДНИК_EMAIL,
                АдаптацияПерсонала.МЕРОПРИЯТИЕ_ПРОСРОЧЕНО_СОТРУДНИК_ТЕМА]
            знч ЗначенияРесурсов = УниверсальныеМеханизмы.ЗначенияРесурсовПодсистемы(АдаптацияПерсонала.ИД_РАЗДЕЛА_АДАПТАЦИЯ_ПЕРСОНАЛА, ИменаРесурсов)
            
            знч Получатель = РезультатЗапросаСтрока.Сотрудник как ФизическиеЛица.Ссылка
            пер ТекстыСообщений = ФизЛица.ЗаполнитьПараметрыПолучателяВСообщении(ЗначенияРесурсов, Получатель)       
            ТекстыСообщений = Мероприятия.ЗаполнитьПараметрыМероприятияВСообщении(ТекстыСообщений, РезультатЗапросаСтрока.Мероприятие)
            ТекстыСообщений = ОписаниеРаботодателя.ЗаполнитьОписаниеРаботодателяВСообщении(ТекстыСообщений)
            знч ТекстыУведомлений = новый УведомленияСервер.ТекстыСообщений(
                ТекстыСообщений[АдаптацияПерсонала.МЕРОПРИЯТИЕ_ПРОСРОЧЕНО_СОТРУДНИК_ТЕМА],
                ТекстыСообщений[АдаптацияПерсонала.МЕРОПРИЯТИЕ_ПРОСРОЧЕНО_СОТРУДНИК_SMS],
                ТекстыСообщений[АдаптацияПерсонала.МЕРОПРИЯТИЕ_ПРОСРОЧЕНО_СОТРУДНИК_PUSH],
                ТекстыСообщений[АдаптацияПерсонала.МЕРОПРИЯТИЕ_ПРОСРОЧЕНО_СОТРУДНИК_EMAIL])
            знч ДанныеУведомления = новый УведомленияСервер.ДанныеУведомления(Уведомления.ВидыУведомлений.ПерсональнаяИнформация, Получатель)
            УведомленияСервер.ОтправитьУведомление(Получатель, ТекстыУведомлений, ДанныеУведомления)
        ;
    ;
;