импорт СтандартноеПриложение

@ВПроекте
структура Приложение
    @JsonСвойство(Псевдонимы = {"version"})
    пер Версия: Строка
    @JsonСвойство(Псевдонимы = {"id"})
    пер Ид: Строка
    @JsonСвойство(Псевдонимы = {"uuid"})
    пер Ууид: Строка
;

@ВПроекте
исключение ИсключениеВыполненияЗапросаКEssm
;

@ВПроекте
исключение ИсключениеНеизвестнаяВерсияПриложения
;

@ВПроекте
метод СервисДоступен(ПараметрыДоступа: ВзаимодействиеСEssm.ПараметрыДоступаКEssm): Булево
    попытка
        исп Ответ = ЗапросыEssm.ВыполнитьОпросДоступности(ПараметрыДоступа)
        возврат Ответ.КодСостояния == 200
    поймать Ошибка: ЗапросыEssm.ИсключениеВыполненияЗапросаКEssm
        Журналирование.ЗарегистрироватьОшибку(
            Тип = "ДоступКВнешнимСервисам", 
            Описание = "Сервис Essm не доступен или отсутствует подключение к Интернет",
            Причина = Ошибка,
            Важность = ВажностьСобытияЖурналаСобытий.Высокая)
    ;
    возврат Ложь
;

@ВПроекте
метод ЗарегистрироватьПриложения(ПараметрыДоступа: ВзаимодействиеСEssm.ПараметрыДоступаКEssm): Приложение
    попытка 
        исп Ответ: ОтветHttp = ЗапросыEssm.ВыполнитьЗапросСоздания(
            ПараметрыДоступа,
            новый РесурсыEssm.Приложения(),
            ФорматEssm.ПриложениеВJson()
            )
        возврат ФорматEssm.ПриложениеИзJson(Ответ)
    поймать Ошибка: ЗапросыEssm.ИсключениеВыполненияЗапросаКEssm
        если ФорматEssm.ЭтоОшибкаНеизвестнаяВерсияПриложения(Ошибка)
            выбросить новый ИсключениеНеизвестнаяВерсияПриложения(
                "Не удалось зарегистрировать приложение. Неизвестная версия приложения",
                Ошибка
            )
        иначе
            выбросить новый ИсключениеВыполненияЗапросаКEssm(
                "Отсутствует соединение интернет или сервис не доступен.",
                Ошибка
            )
        ;   
    поймать Ошибка: ФорматEssm.НеверныйФорматОтветаСервера
        выбросить новый ИсключениеВыполненияЗапросаКEssm(
            "Не удалось получить данные приложения. Неверный формат ответа сервера.",
            Ошибка
        )   
    ;
;