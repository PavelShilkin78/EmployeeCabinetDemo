@ВПроекте
@НаКлиенте @НаСервере
структура ПараметрыДоступаКEssm
    пер АдресСервераEssm: Строка
    пер ИмяПриложенияEssm: Строка
    пер ИдКлиентаEssm: Строка
    пер СекретКлиентаEssm: Строка
;

@ВПроекте
@НаСервере
метод ПараметрыДоступаКEssm(): ПараметрыДоступаКEssm
    пер ПараметрыДоступаКEssm: ПараметрыДоступаКEssm
    знч Запрос = Запрос{
        Выбрать Первые 1
            АдресСервераEssm как АдресСервераEssm,
            ИмяПриложенияEssm как ИмяПриложенияEssm,
            ИдКлиентаEssm как ИдКлиентаEssm,
            СекретКлиентаEssm как СекретКлиентаEssm
        Из НастройкиСоединенияСEssm}
        
    исп Результат = Запрос.Выполнить()
    знч Параметры = Результат.ЕдинственныйИлиУмолчание()
    если Параметры != Неопределено
        ПараметрыДоступаКEssm = новый ПараметрыДоступаКEssm(
            АдресСервераEssm = Параметры.АдресСервераEssm, 
            ИмяПриложенияEssm = Параметры.ИмяПриложенияEssm,
            ИдКлиентаEssm = Параметры.ИдКлиентаEssm,
            СекретКлиентаEssm = Параметры.СекретКлиентаEssm)
    ;
    возврат ПараметрыДоступаКEssm
;

@ВПроекте
@НаСервере
метод СохранитьПараметрыДоступаКEssm(Настройки: ПараметрыДоступаКEssm)
    исп Транзакции.Начать()
    знч Запрос = Запрос{
        Выбрать Первые 1 
            Ссылка 
        Из НастройкиСоединенияСEssm}
    исп Результат = Запрос.Выполнить()
    пер НастройкиОбъект: НастройкиСоединенияСEssm.Объект?
    НастройкиОбъект = Результат.ЕдинственныйИлиУмолчание()?.Ссылка.ЗагрузитьОбъект(Истина) ?? новый НастройкиСоединенияСEssm.Объект()
    
    НастройкиОбъект.АдресСервераEssm = Настройки.АдресСервераEssm
    НастройкиОбъект.ИмяПриложенияEssm = Настройки.ИмяПриложенияEssm
    НастройкиОбъект.ИдКлиентаEssm = Настройки.ИдКлиентаEssm
    НастройкиОбъект.СекретКлиентаEssm = Настройки.СекретКлиентаEssm
    
    НастройкиОбъект.Записать()
;