импорт ВзаимодействиеСBearerСервером
импорт СтандартноеПриложение

@ВПроекте
исключение ИсключениеВыполненияЗапросаКEssm
    обз пер АдресРесурса: Строка
    обз пер КодСостояния: Число
    обз пер ТелоОтвета: Строка   
;

@ВПодсистеме
метод ВыполнитьОпросДоступности(ПараметрыДоступа: ВзаимодействиеСEssm.ПараметрыДоступаКEssm): ОтветHttp
    пер Адрес = "%{ПараметрыДоступа.АдресСервераEssm}/applications/%{ПараметрыДоступа.ИмяПриложенияEssm}"
    пер Запрос = СоединениеHttp.Клиент().ЗапросGet(Адрес)
    возврат ВыполнитьЗапрос(Запрос, ПараметрыДоступа)
;

@ВПодсистеме
метод ВыполнитьЗапросСоздания(ПараметрыДоступа: ВзаимодействиеСEssm.ПараметрыДоступаКEssm, Ресурс: РесурсыEssm.Приложения, ТелоЗапроса: Строка): ОтветHttp
    пер Запрос: ЗапросHttp = СоединениеHttp.Клиент().ЗапросPost(
        АдресЗапроса(ПараметрыДоступа.АдресСервераEssm, ПараметрыДоступа.ИмяПриложенияEssm, Ресурс))
        .УстановитьЗаголовок("Content-Type", "application/json")
        .УстановитьТело(ТелоЗапроса)
    
    возврат ВыполнитьЗапрос(Запрос, ПараметрыДоступа)
;

метод ВыполнитьЗапрос(Запрос: ЗапросHttp, ПараметрыДоступа: ВзаимодействиеСEssm.ПараметрыДоступаКEssm): ОтветHttp
    попытка
        пер ПараметрыДоступаКСерверу: КлиентBearerСервера.ПараметрыДоступаКСерверу =
            новый КлиентBearerСервера.ПараметрыДоступаКСерверу()

        ПараметрыДоступаКСерверу.АдресСервера = ПараметрыДоступа.АдресСервераEssm
        ПараметрыДоступаКСерверу.ИдКлиента = ПараметрыДоступа.ИдКлиентаEssm
        ПараметрыДоступаКСерверу.СекретКлиента = ПараметрыДоступа.СекретКлиентаEssm

        знч Ответ = КлиентBearerСервера.ВыполнитьЗапрос(Запрос, ПараметрыДоступаКСерверу)
        знч КодСостояния = Ответ.КодСостояния
        если КодСостояния != 200 и КодСостояния != 201 и КодСостояния != 204
            знч ТелоОтвета = Ответ.Тело.ПрочитатьКакСтроку()
            пер ТекстОшибки: Строка =
                "Ошибка обращения к менеджеру сервиса по адресу \"%{Запрос
                .АбсолютныйUrl}\" получен код состояния %{КодСостояния}"
            Журналирование.ЗарегистрироватьОшибку("ЗапросыEssm.ИсключениеВыполненияЗапросаКEssm", Описание = ТекстОшибки,
                Важность = ВажностьСобытияЖурналаСобытий.Высокая)
            Журналирование.ЗарегистрироватьHttpЗапрос("ЗапросыEssm", Запрос = Запрос, Ответ = Ответ, ТелоОтвета = ТелоОтвета)
            Ответ.Закрыть()
            выбросить новый ИсключениеВыполненияЗапросаКEssm(ТекстОшибки, Запрос.АбсолютныйUrl, КодСостояния,
                ТелоОтвета)
        ;
        возврат Ответ
    поймать Ошибка: Аутентификация.ИсключениеАутентификации
        выбросить новый ИсключениеВыполненияЗапросаКEssm(
            "Ошибка аутентификации при выполнении запроса к менеджеру сервиса по адресу \"%{Запрос.АбсолютныйUrl}\"",
            Запрос.АбсолютныйUrl, Ошибка.КодСостояния, Ошибка.ТелоОтвета, Ошибка)
    ;
;

метод АдресЗапроса(АдресСервера: Строка, ИмяПриложения: Строка, Ресурс: РесурсыEssm.Приложения): Строка
    возврат "%АдресСервера/applications/%ИмяПриложения/api/%{РесурсыEssm.АдресРесурса(Ресурс)}/"
;