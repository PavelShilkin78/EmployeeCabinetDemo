
@ВПроекте
метод ПредставлениеПериодаРаботы(): Строка
    если ПоНастоящееВремя 
        возврат "%{НачалоРаботы.Представление()} по настоящее время"
    иначе
        возврат "%{НачалоРаботы.Представление()} - %{ОкончаниеРаботы.Представление()}"
    ;
;

// JSON

@ВПроекте
структура ДанныеJson_V2_0
    обз знч nameOrg: Строка
    обз знч speciality: Строка
    обз знч startDate: Дата
    пер endDate: Дата?
    пер responsibilities: Строка?
;

@ВПроекте
метод ЗаполнитьПоJson(ДанныеJson: ДанныеJson_V2_0)
    НаименованиеОрганизации = ДанныеJson.nameOrg
    Специальность = ДанныеJson.speciality
    НачалоРаботы = ДанныеJson.startDate
    если ДанныеJson.endDate != Неопределено
        ОкончаниеРаботы = ДанныеJson.endDate
    иначе
        ПоНастоящееВремя = Истина
    ;
    Обязанности = ДанныеJson.responsibilities ?? ""     
;

@ВПроекте
метод ПолучитьJson_V2_0(): ДанныеJson_V2_0
    
    пер ДанныеJson = новый ДанныеJson_V2_0(
        НаименованиеОрганизации,
        Специальность,
        НачалоРаботы
    )    
    если ПоНастоящееВремя
        ДанныеJson.endDate = Неопределено
    иначе
        ДанныеJson.endDate = ОкончаниеРаботы
    ;
    ДанныеJson.responsibilities = Обязанности
    
    возврат ДанныеJson
;

// Конец JSON