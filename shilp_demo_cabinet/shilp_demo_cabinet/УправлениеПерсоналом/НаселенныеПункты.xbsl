импорт ОбменДанными

//КонтрольДоступа

@Обработчик
метод ВычислитьРазрешенияДоступа(): ЧитаемаяКоллекция<РазрешениеДоступа>
    возврат УправлениеПерсоналомКонтрольДоступаПоставщик.ПолучитьСервис().ВычислитьРазрешенияДоступаНаселенныхПунктов()
;

//Конец КонтрольДоступа
// НачальноеЗаполнение

@ВПроекте
структура КлассификаторНаселенныеПункты
    знч НаселенныеПункты: Массив<НаселенныйПункт>
    знч Идентификаторы: Массив<Идентификаторы>
    знч Регионы: Соответствие<Строка, Строка>
;

@ВПроекте
структура НаселенныйПункт
    знч Ид: Строка
    знч Регион: Строка?
    знч Имя: Строка
;

@ВПроекте
структура Идентификаторы
    знч Сервис: Строка
    знч Ид: Строка
    знч НаселенныйПункт: Строка
;

@ВПроекте
метод КлассификаторНаселенныхПунктов(): КлассификаторНаселенныеПункты
    знч ДвоичныеДанные: ДвоичныйОбъект = Ресурс{НаселенныеПункты.json}.Ссылка.Загрузить()
    знч ПотокЗаписи = новый СтроковыйПотокЗаписи()
    ДвоичныеДанные.Записать(ПотокЗаписи)
    знч НаселенныеПунктыКласификаторы = СериализацияJson.ПрочитатьОбъект(ПотокЗаписи.ВСтроку(), Тип<КлассификаторНаселенныеПункты>) 
    ПотокЗаписи.Закрыть()
    возврат НаселенныеПунктыКласификаторы
;

@ВПроекте
метод ЗаполнитьСубъектыРФ(Регионы: Соответствие<Строка, Строка>)
    для Элемент из Регионы
        знч Ууид = новый Ууид(Элемент.Ключ)
        знч Ссылка = СубъектыРФ.ПолучитьСсылку(Ууид)
        пер Объект: СубъектыРФ.Объект
        знч НайденныйОбъект = Ссылка.ЗагрузитьОбъект(Ложь)
        если НайденныйОбъект это Неопределено
            Объект = новый СубъектыРФ.Объект(Ууид) 
        иначе
            Объект = НайденныйОбъект!
        ;
        Объект.Наименование = Элемент.Значение.ВСтроку()
        Объект.Записать()  
    ;
;

@ВПроекте
метод ЗаполнитьНаселенныеПункты(Список: Массив<НаселенныйПункт>)
    для Элемент из Список
        знч Ид = Элемент.Ид
        знч Регион = Элемент.Регион
        знч Имя = Элемент.Имя
        
        знч Ууид = новый Ууид(Ид)
        знч Ссылка = НаселенныеПункты.ПолучитьСсылку(Ууид)
        пер Объект: НаселенныеПункты.Объект
        знч НайденныйОбъект = Ссылка.ЗагрузитьОбъект(Ложь)
        если НайденныйОбъект это Неопределено
            Объект = новый НаселенныеПункты.Объект(Ууид) 
        иначе
            Объект = НайденныйОбъект!
        ;
    
        Объект.Наименование = Имя
        если Регион != Неопределено
            Объект.Субъект = СубъектыРФ.ПолучитьСсылку(новый Ууид(Регион))
        ;
        Объект.Записать() 
    ;
;

@ВПроекте
метод НачальноеЗаполнениеНаселенныхПунктов()
    знч НаселенныеПунктыКласификаторы = КлассификаторНаселенныхПунктов()
    ЗаполнитьСубъектыРФ(НаселенныеПунктыКласификаторы.Регионы) 
    ЗаполнитьНаселенныеПункты(НаселенныеПунктыКласификаторы.НаселенныеПункты)
;

// Конец НачальноеЗаполнение

@ВПодсистеме
метод ПослеУдаленияДополнение(Ссылка: НаселенныеПункты.Ссылка, ТребуетсяРегистрацияИзменений: Булево)
    РегистрацияИзменений.ЗарегистрироватьУдаление(Ссылка)
;