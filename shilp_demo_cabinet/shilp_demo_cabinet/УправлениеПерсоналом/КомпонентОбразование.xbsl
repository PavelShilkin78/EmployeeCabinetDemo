импорт УправлениеПерсоналом


метод ТаблицаДанных(): Массив<ОписаниеОбразования>
    знч Данные = новый Массив<ОписаниеОбразования>()
    Данные.ДобавитьВсе(Значение)
    возврат Данные
;

метод ДобавитьПриНажатии(Команда: ОбычнаяКоманда)
    Свернут = Ложь
    Значение.Добавить(новый ОписаниеОбразования())
;

метод УдалитьСтроку(Команда: КомандаСПараметром<Массив<ОписаниеОбразования>>, Параметр: Массив<ОписаниеОбразования>)
    для ТекущаяСтрока из Параметр
        Значение.Удалить(ТекущаяСтрока)            
    ;
;

метод ПредставлениеГода(ДанныеСтроки: ОписаниеОбразования): Строка
    возврат "%{ДанныеСтроки.ГодОкончания.Представление("4")} г."
;

метод ВидимостьЗаставки(): Булево
    возврат Компоненты.Список.Источник.Данные.Размер() == 0
;

@ВПроекте
метод УровеньОбразования(): УровниОбразования?
    знч УровниОбразованияПоУбыванию = Значение
        .СортироватьПо(ОписаниеОбразования -> ОписаниеОбразования.Уровень?.Индекс ?? 0, НаправлениеСортировки.ПоУбыванию)
    возврат УровниОбразованияПоУбыванию.ПервыйИлиУмолчание()?.Уровень
;

метод ТекстПодзаголовка(): Строка  
    возврат УровеньОбразования()?.Представление() ?? "Информация об образовании не указана" 
;