@ВПроекте
@НаСервере @НаКлиенте
перечисление ОшибкиПроверкиЗаполнения
    СуммаНеЗаполнена,
    СуммыНеСогласованы,
    ВалютаНеЗаполнена
;

@ВПроекте
@НаСервере @НаКлиенте
метод ПроверитьЗаполнение(Обязательное: Булево = Ложь, ДиапазонСумм: Булево = Истина): Множество<ОшибкиПроверкиЗаполнения>
    знч Ошибки: Множество<ОшибкиПроверкиЗаполнения>
    если Обязательное
        если ДиапазонСумм и СуммаОт == 0 и СуммаДо == 0
            Ошибки.Добавить(ОшибкиПроверкиЗаполнения.СуммаНеЗаполнена) 
        иначе если СуммаОт == 0
            Ошибки.Добавить(ОшибкиПроверкиЗаполнения.СуммаНеЗаполнена) 
        ;
    ;
    если ДиапазонСумм
        если (СуммаОт != 0 и СуммаДо != 0) и СуммаОт > СуммаДо
            Ошибки.Добавить(ОшибкиПроверкиЗаполнения.СуммыНеСогласованы)
        ;  
    ;
    
    если Валюта == Неопределено и (Обязательное или (СуммаОт != 0 или СуммаДо != 0))
        Ошибки.Добавить(ОшибкиПроверкиЗаполнения.ВалютаНеЗаполнена) 
    ;
    
    возврат Ошибки
;

@ВПроекте
@ИменованныеПараметры
метод ПредставлениеОплаты(УказыватьНеттоГросс = Истина, ПредставлениеНетто = "на руки", 
    ПредставлениеГросс = "до вычета налогов", ПредставлениеНеУказана = "не указана"): Строка
    знч Постфикс: Строка = УказыватьНеттоГросс ? (СуммаНетто ? " ($ПредставлениеНетто)" : " ($ПредставлениеГросс)") : ""
    знч СимволВалюты = Валюты.СимволВалюты(Валюта)
    если СуммаОт == 0 и СуммаДо == 0
        возврат ПредставлениеНеУказана
    иначе если СуммаОт == СуммаДо
        возврат "${СуммаДо} ${СимволВалюты}${Постфикс}"
    иначе если СуммаОт == 0
        возврат "до ${СуммаДо} ${СимволВалюты}${Постфикс}"
    иначе если СуммаДо == 0
        возврат "от ${СуммаОт} ${СимволВалюты}${Постфикс}"
    иначе
        возврат "${СуммаОт} ${СимволВалюты} - ${СуммаДо} ${СимволВалюты}${Постфикс}"
    ;  
;

// JSON

@ВПроекте
структура ДанныеJson_V2_0
    пер from: Число
    пер to: Число
    пер currencyID: Строка?
    пер net: Булево
;

@ВПроекте
@НаСервере
метод ЗаполнитьПоJson(ДанныеJson: ДанныеJson_V2_0)
    СуммаОт = ДанныеJson.from
    СуммаДо = ДанныеJson.to
    если ДанныеJson.currencyID != Неопределено
        Валюта = Валюты.ПолучитьСсылку(новый Ууид(ДанныеJson.currencyID))
    ;
    СуммаНетто = ДанныеJson.net        
;

@ВПроекте
@НаСервере
метод ПолучитьJson_V2_0(): ДанныеJson_V2_0
    
    пер ДанныеJson: ДанныеJson_V2_0
    ДанныеJson.from = СуммаОт
    ДанныеJson.to = СуммаДо
    если Валюта != Неопределено
        ДанныеJson.currencyID = Валюта.Ид.ВСтроку()
    ;
    ДанныеJson.net = СуммаНетто
    
    возврат ДанныеJson    
;

// Конец JSON