импорт УправлениеПерсоналом
импорт УниверсальныеМеханизмы
импорт ОбщиеСтандартные

конст ФИЛЬТР_ДОКУМЕНТОВ = [".doc", ".docx", "xls", "xlsx", ".pdf", "image/*"]

метод ТаблицаДанных(): Массив<ПортфолиоРезюме.ДанныеПортфолиоРезюме>
    возврат Значение
;

метод НадписьВыбораФайлов(): Строка
    возврат "Выберите файл портфолио"  
;

метод ПриИзменении()
    ПриИзменении(новый СобытиеСДанными<Массив<ПортфолиоРезюме.ДанныеПортфолиоРезюме>>(Значение))
;

метод ДобавитьПриНажатии(Команда: ОбычнаяКоманда)
    Свернут = Ложь
    Значение.Добавить(новый ПортфолиоРезюме.ДанныеПортфолиоРезюме())
    ПриИзменении()
;

метод ВидимостьЗаставки(): Булево
    возврат Компоненты.Список.Источник.Данные.Размер() == 0
;

метод УдалитьСтроку(Команда: КомандаСПараметром<Массив<ПортфолиоРезюме.ДанныеПортфолиоРезюме>>, Параметр: Массив<ПортфолиоРезюме.ДанныеПортфолиоРезюме>)
    для ТекущаяСтрока из Параметр
        Значение.Удалить(ТекущаяСтрока)            
    ;
    ПриИзменении()
;

метод ПрисоединенныеФайлыДоступныеРасширения(): Строка
    возврат Строки.Соединить(ФИЛЬТР_ДОКУМЕНТОВ, ",")
;

метод ПрисоединенныеФайлыСтроки(ДанныеСтроки: ПортфолиоРезюме.ДанныеПортфолиоРезюме): ПрисоединенныеФайлы
    знч ПрисоединенныеФайлы = новый ПрисоединенныеФайлы()
    если ДанныеСтроки.Файл != Неопределено
        ПрисоединенныеФайлы.Файлы.Добавить(ДанныеСтроки.Файл!)
    ;
    возврат ПрисоединенныеФайлы 
;

метод ПрисоединенныеФайлыЗавершенаЗагрузкаФайлов(Источник: ПрисоединенныеФайлыКомпонент, Событие: неизвестно, ДанныеСтроки: Объект)
    (ДанныеСтроки как ПортфолиоРезюме.ДанныеПортфолиоРезюме).Файл = Источник.ТаблицаФайлы.Файлы.ПервыйИлиУмолчание()
    ПриИзменении()
;

метод ПрисоединенныеФайлыФайлПомеченНаУдаление(Источник: ПрисоединенныеФайлыКомпонент, Событие: СобытиеСДанными<ПрисоединенныйФайл>, ДанныеСтроки: Объект?)
    (ДанныеСтроки как ПортфолиоРезюме.ДанныеПортфолиоРезюме).Файл = Неопределено
    ПриИзменении()
;

метод ПолеВводаПриИзменении(Источник: ПолеВвода<Строка>, Событие: СобытиеПриИзменении<Строка>, ДанныеСтроки: ПортфолиоРезюме.ДанныеПортфолиоРезюме)
    ПриИзменении()
;

метод ТекстПодзаголовка(): Строка
    знч Записей = Значение.Размер()
    если Записей == 0
        возврат "Портфолио не заполнено"
    иначе 
        возврат "%Записей %{МенеджерСтрок.ТекстПоКоличеству("запись", Записей)} в портфолио"
    ; 
;