импорт КонтрольДоступа


//КонтрольДоступа
@Обработчик
метод ВычислитьРазрешенияДоступа(): ЧитаемаяКоллекция<РазрешениеДоступа>
    знч Разрешения = новый Массив<РазрешениеДоступа>()
    Разрешения.ДобавитьВсе(КонтрольДоступа.РазрешенияДоступаТаблицыКонтентДанныхАбонентаЧтениеВсем())
    возврат Разрешения
;
//Конец КонтрольДоступа

структура ДанныеJson
    обз знч ID: Строка
    обз знч code: Строка
    обз знч abbr: Строка
    обз знч name: Строка
;

конст ИДЕНТИФИКАТОР_ВАЛЮТЫ_РУБЛИ = Ууид{4ab89775-7802-4457-9983-bf3f7be45a26}

@ВПроекте
метод ЗаполнитьВалютыПоУмолчанию()
    исп Транзакции.Начать()
    знч ДвоичныеДанные = Ресурс{currency.json}.Ссылка.Загрузить()
    знч ПотокЗаписи = новый СтроковыйПотокЗаписи()
    ДвоичныеДанные.Записать(ПотокЗаписи)
    знч ВалютыПоУмолчанию = СериализацияJson.ПрочитатьОбъект(ПотокЗаписи.ВСтроку(), Тип<Массив<ДанныеJson>>)
    ПотокЗаписи.Закрыть()
    
    для Элемент из ВалютыПоУмолчанию
        знч Ид = новый Ууид(Элемент.ID)
        пер ВалютаОбъект = Валюты.ПолучитьСсылку(Ид).ЗагрузитьОбъект()
        если ВалютаОбъект == Неопределено
            ВалютаОбъект = новый Валюты.Объект(Ид)
        ;
        ВалютаОбъект.Код = Элемент.code
        ВалютаОбъект.КраткоеНаименование = Элемент.abbr
        ВалютаОбъект.Наименование = Элемент.name
        ВалютаОбъект.Записать()
    ;
;

@ВПроекте
@НаСервере @ДоступноСКлиента
метод ВалютаПоУмолчанию(): Валюты.Ссылка
    возврат Валюты.ПолучитьСсылку(ИДЕНТИФИКАТОР_ВАЛЮТЫ_РУБЛИ)
;

@ВПроекте
@НаСервере @ДоступноСКлиента
метод СимволВалюты(Валюта: Валюты.Ссылка?): Строка
    если Валюта == Неопределено
        возврат ""
    ;
    исп Результат = Запрос{выбрать КраткоеНаименование из Валюты где Ссылка == %Валюта}.Выполнить()
    возврат Результат.ПервыйИлиУмолчание()?.КраткоеНаименование ?? ""
;  