импорт ВебApi
импорт УниверсальныеМеханизмы
импорт ОбщиеСтандартные
импорт КонтрольДоступа

//КонтрольДоступа
@Обработчик
метод ВычислитьРазрешенияДоступа(): ЧитаемаяКоллекция<РазрешениеДоступа>
    возврат КонтрольДоступа.РазрешенияДоступаТаблицыБезПраваУдаления()
;

@Обработчик
метод ВычислитьРазрешенияДоступаДляОбъектов(Объекты: ЧитаемыйМассив<СертификатыРезюме.ДанныеРасчетаРазрешений>): ЧитаемоеСоответствие<СертификатыРезюме.ДанныеРасчетаРазрешений, ЧитаемаяКоллекция<РазрешениеДоступа>>
    возврат УправлениеПерсоналомКонтрольДоступаПоставщик.ПолучитьСервис().ВычислитьРазрешенияСертификатыРезюме(Объекты)
;

//Конец КонтрольДоступа

@ВПроекте
@НаСервере @НаКлиенте
структура ДанныеСертификатаРезюме
    пер ДатаПолучения: Дата
    пер Наименование: Строка
    пер Url: Строка
    пер Файл: ПрисоединенныйФайл?
;

метод УдалитьСертификатыВладельца(Владелец: Резюме.Ссылка)
    исп РезультатСертификатыВладельца = Запрос{
        Выбрать Ссылка 
        Из 
            СертификатыРезюме 
        Где 
            Владелец == %Владелец
    }.Выполнить()

    для Элемент из РезультатСертификатыВладельца
        знч СертификатРезюмеОбъект = Элемент.Ссылка.ЗагрузитьОбъект()
        СертификатРезюмеОбъект.Удалить()
    ;
;

@ВПроекте
метод ЗаписатьСертификатыВладельца(Владелец: Резюме.Ссылка, Данные: ЧитаемыйМассив<ДанныеСертификатаРезюме>)
    исп Транзакции.Начать()
    УдалитьСертификатыВладельца(Владелец)
    для Элемент из Данные
        знч СертификатРезюмеОбъект = новый СертификатыРезюме.Объект(
            Владелец = Владелец,
            ДатаПолучения = Элемент.ДатаПолучения,
            Наименование = Элемент.Наименование,
            Url = Элемент.Url
        )
        СертификатРезюмеОбъект.Записать(Файл = Элемент.Файл)
    ;
;

@ВПроекте
метод ПолучитьСертификатыВладельца(Владелец: Резюме.Ссылка): Массив<ДанныеСертификатаРезюме>
    исп РезультатСертификатыВладельца = Запрос{
        Выбрать
            Ссылка,
            ДатаПолучения,
            Наименование,
            Url
        Из 
            СертификатыРезюме 
        Где 
            Владелец == %Владелец
    }.Выполнить()

    знч СертификатыВладельца = новый Массив<ДанныеСертификатаРезюме>()
    для Элемент из РезультатСертификатыВладельца
        знч Файл = ПрисоединенныеФайлыМенеджер.ФайлыОбъекта(Элемент.Ссылка).ПервыйИлиУмолчание()
        СертификатыВладельца.Добавить(
            новый ДанныеСертификатаРезюме(
                ДатаПолучения = Элемент.ДатаПолучения,
                Наименование = Элемент.Наименование,
                Url = Элемент.Url, 
                Файл = Файл
            )
        )
    ;
    возврат СертификатыВладельца
;

@ВПроекте
структура ДанныеJson_V2_0
    обз знч date: Дата
    обз знч name: Строка
    обз знч url: Строка
    обз знч attachedFile: МенеджерФайлов.ДанныеФайлаJson?
;

@ВПроекте
метод СертификатыВладельцаВДанныеJson(Владелец: Резюме.Ссылка, Сервисы: ВебApiСервер.СервисыУпаковкиJson): ЧитаемыйМассив<ДанныеJson_V2_0>
    знч Сертификаты = ПолучитьСертификатыВладельца(Владелец) 
    знч СертификатыДанныеJson = новый Массив<ДанныеJson_V2_0>()
    для Элемент из Сертификаты
        пер ДанныеФайла: МенеджерФайлов.ДанныеФайлаJson?
        если Элемент.Файл?.Ссылка != Неопределено
            ДанныеФайла = Файлы.ДанныеФайлаJson(Элемент.Файл?.Ссылка, Сервисы)
        ;
        СертификатыДанныеJson.Добавить(
            новый ДанныеJson_V2_0(
                date = Элемент.ДатаПолучения,
                name = Элемент.Наименование,
                url = Элемент.Url,
                attachedFile = ДанныеФайла
            )
        )
    ;
    возврат СертификатыДанныеJson
;

@ВПроекте
метод ЗаписатьСертификатыИзДанныеJson(ДанныеJson: Массив<ДанныеJson_V2_0>, Владелец: Резюме.Ссылка, Сервисы: ВебApiСервер.СервисыЗагрузкиJson)
    исп Транзакции.Начать() 
    УдалитьСертификатыВладельца(Владелец)
    для Элемент из ДанныеJson
        знч Объект = новый СертификатыРезюме.Объект(
            Владелец = Владелец,
            ДатаПолучения = Элемент.date,
            Наименование = Элемент.name,
            Url = Элемент.url)
        Объект.Записать()
        если Элемент.attachedFile != Неопределено
            знч ВложенныеФайлы = Файлы.ВложенияИзJson([Элемент.attachedFile], Сервисы, Объект.Ссылка)
            Файлы.ЗаписатьФайлыПоВладельцу(ВложенныеФайлы, Объект.Ссылка)
        ;
    ;
;