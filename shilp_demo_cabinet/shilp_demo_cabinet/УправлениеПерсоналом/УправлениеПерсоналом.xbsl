импорт ОбщиеСтандартные

@ВПроекте
конст ИД_ПОДСИСТЕМЫ = "УправлениеПерсоналом"

@ВПроекте
конст МИНИМАЛЬНЫЙ_ВОЗРАСТ_ТРУДОУСТРОЙСТВА = 14

@ВПроекте
@НаСервере @НаКлиенте
метод ДлительностьРаботы(ОпытРаботы: ЧитаемыйМассив<ОписаниеМестаРаботы>): Длительность
    знч ПериодыРаботы = новый Массив<ЗакрытыйДиапазон<Дата>>()
    для Опыт из ОпытРаботы
        если Опыт.НачалоРаботы == Дата{}
            продолжить
        ;
        знч ОкончаниеРаботы = Опыт.ПоНастоящееВремя или Опыт.ОкончаниеРаботы == Дата{} ? Дата.Сейчас() : Опыт.ОкончаниеРаботы
        если Опыт.НачалоРаботы > ОкончаниеРаботы
            продолжить
        ;
        ПериодыРаботы.Добавить(новый ЗакрытыйДиапазон<Дата>(Опыт.НачалоРаботы, ОкончаниеРаботы))   
    ;
    пер ДлительностьРаботы = 0д
    для ПериодРаботы из ОбъединитьПериоды(ПериодыРаботы)
        ДлительностьРаботы += (ПериодРаботы.До - ПериодРаботы.От) + 1д
    ;
    возврат ДлительностьРаботы
;

@ВПроекте
@НаСервере @НаКлиенте
метод ПредставлениеДлительностиРаботы(ОпытРаботы: ЧитаемыйМассив<ОписаниеМестаРаботы>, ПредставлениеБезОпыта = "Без опыта"): Строка
    знч Длительность = ДлительностьРаботы(ОпытРаботы)
    знч ДлительностьВМесяцах = (Длительность.Дни / (365/12)).Округлить(РежимОкругления.Вверх)
    знч Годов = (ДлительностьВМесяцах / 12).ЦелаяЧасть()
    знч Месяцев = ДлительностьВМесяцах - Годов * 12
    знч ПредставлениеМесяцев = МенеджерСтрок.ТекстПоКоличеству("месяц", Месяцев)
    знч ПредставлениеГодов = МенеджерСтрок.ТекстПоКоличеству("год", Годов)
    если Годов == 0 и Месяцев == 0
        возврат ПредставлениеБезОпыта        
    иначе если Годов < 1
        возврат "%Месяцев %ПредставлениеМесяцев" 
    иначе если Месяцев < 1
        возврат "%Годов %ПредставлениеГодов"
    иначе
        возврат "%Годов %ПредставлениеГодов %Месяцев %ПредставлениеМесяцев"
    ;
;

@ВПроекте
@НаСервере
метод ЗаполнитьРезюме(Резюме: Резюме.Объект, ДанныеРезюме: Резюме.Объект.ДанныеРезюме)
    Резюме.Наименование = ДанныеРезюме.Наименование
    Резюме.Фамилия = ДанныеРезюме.Фамилия
    Резюме.Имя = ДанныеРезюме.Имя
    Резюме.Отчество = ДанныеРезюме.Отчество
    Резюме.ДатаРождения = ДанныеРезюме.ДатаРождения
    Резюме.Пол = ДанныеРезюме.Пол
    Резюме.Гражданство = ДанныеРезюме.Гражданство
    Резюме.Должность = ДанныеРезюме.Должность
    Резюме.ОСебе = ДанныеРезюме.ОСебе
    
    Резюме.ИностранныеЯзыки.Очистить()
    Резюме.ИностранныеЯзыки.ДобавитьВсе(ДанныеРезюме.ИностранныеЯзыки)
    
    Резюме.Образование.Очистить()
    Резюме.Образование.ДобавитьВсе(ДанныеРезюме.Образование)
    
    Резюме.Курсы.Очистить()
    Резюме.Курсы.ДобавитьВсе(ДанныеРезюме.Курсы)
    
    Резюме.ОпытРаботы.Очистить()
    Резюме.ОпытРаботы.ДобавитьВсе(ДанныеРезюме.ОпытРаботы)
    Резюме.ПредставлениеОпытаРаботы = ПредставлениеДлительностиРаботы(Резюме.ОпытРаботы)
    
    Резюме.ВодительскиеКатегории.Очистить()
    Резюме.ВодительскиеКатегории.ДобавитьВсе(ДанныеРезюме.ВодительскиеКатегории)

    Резюме.ВодительскийСтаж = ДанныеРезюме.ВодительскийСтаж

    Резюме.НаселенныйПункт = ДанныеРезюме.НаселенныйПункт
    Резюме.Должность = ДанныеРезюме.Должность
    Резюме.ОСебе = ДанныеРезюме.ОСебе
;

@ВПроекте
@НаСервере
метод ДанныеРезюме(Резюме: Резюме.Объект): Резюме.Объект.ДанныеРезюме
    знч ДанныеРезюме = новый Резюме.Объект.ДанныеРезюме()
    ДанныеРезюме.Наименование = Резюме.Наименование
    ДанныеРезюме.Фамилия = Резюме.Фамилия
    ДанныеРезюме.Имя = Резюме.Имя
    ДанныеРезюме.Отчество = Резюме.Отчество
    ДанныеРезюме.ДатаРождения = Резюме.ДатаРождения
    ДанныеРезюме.Пол = Резюме.Пол
    ДанныеРезюме.Гражданство = Резюме.Гражданство
    ДанныеРезюме.Должность = Резюме.Должность
    ДанныеРезюме.ОСебе = Резюме.ОСебе    
    ДанныеРезюме.ИностранныеЯзыки.ДобавитьВсе(Резюме.ИностранныеЯзыки)
    ДанныеРезюме.Образование.ДобавитьВсе(Резюме.Образование)
    ДанныеРезюме.Курсы.ДобавитьВсе(Резюме.Курсы)
    ДанныеРезюме.ОпытРаботы.ДобавитьВсе(Резюме.ОпытРаботы)
    ДанныеРезюме.ПредставлениеОпытаРаботы = ПредставлениеДлительностиРаботы(ДанныеРезюме.ОпытРаботы)
    ДанныеРезюме.ВодительскиеКатегории.ДобавитьВсе(Резюме.ВодительскиеКатегории)
    ДанныеРезюме.ВодительскийСтаж = Резюме.ВодительскийСтаж
    ДанныеРезюме.НаселенныйПункт = Резюме.НаселенныйПункт
    
    ДанныеРезюме.Сертификаты = СертификатыРезюме.ПолучитьСертификатыВладельца(Резюме.Ссылка)
    ДанныеРезюме.Портфолио = ПортфолиоРезюме.ПолучитьПортфолиоВладельца(Резюме.Ссылка)
    
    возврат ДанныеРезюме
;

@НаСервере @НаКлиенте
метод ОбъединитьПериоды(Периоды: ЧитаемыйМассив<ЗакрытыйДиапазон<Дата>>): ЧитаемыйМассив<ЗакрытыйДиапазон<Дата>>
    если Периоды.Пусто()
        возврат []
    ;
    Периоды = Периоды.СортироватьПо(Диапазон -> Диапазон.От, НаправлениеСортировки.ПоВозрастанию)
    знч ОбъединенныеПериоды: Массив<ЗакрытыйДиапазон<Дата>>
    ОбъединенныеПериоды.Добавить(Периоды.Первый())
    для Индекс = 1 по Периоды.Граница()
        знч Последний = ОбъединенныеПериоды.Граница()
        если ОбъединенныеПериоды[Последний].До + 1д >= Периоды[Индекс].От
            ОбъединенныеПериоды[Последний] = новый ЗакрытыйДиапазон(
                От = ОбъединенныеПериоды[Последний].От,
                До = Макс(ОбъединенныеПериоды[Последний].До, Периоды[Индекс].До))
        иначе
            ОбъединенныеПериоды.Добавить(Периоды[Индекс])    
        ;
    ;
    возврат ОбъединенныеПериоды
;