импорт УправлениеПерсоналом
импорт УниверсальныеМеханизмы

конст ФИЛЬТР_ДОКУМЕНТОВ = [".doc", ".docx", "xls", "xlsx", ".pdf", "image/*"]

метод ТаблицаДанных(): Массив<СертификатыРезюме.ДанныеСертификатаРезюме>
    возврат Значение
;

метод Подзаголовок(): Строка
    если Компоненты.Список.Источник.Данные.Размер() == 0
        возврат "Информация о сертификатах не указана"
    иначе
        возврат "Указана информация о %{Компоненты.Список.Источник.Данные.Размер()} сертифкатах"
    ;
;

метод НадписьВыбораФайлов(): Строка
    возврат "Выберите файл сертификата"  
;

метод ПриИзменении()
    ПриИзменении(новый СобытиеСДанными<Массив<СертификатыРезюме.ДанныеСертификатаРезюме>>(Значение))
;

метод ДобавитьПриНажатии(Команда: ОбычнаяКоманда)
    Свернут = Ложь
    Значение.Добавить(новый СертификатыРезюме.ДанныеСертификатаРезюме())
    ПриИзменении()
;

метод ВидимостьЗаставки(): Булево
    возврат Компоненты.Список.Источник.Данные.Размер() == 0
;

метод УдалитьСтроку(Команда: КомандаСПараметром<Массив<СертификатыРезюме.ДанныеСертификатаРезюме>>, Параметр: Массив<СертификатыРезюме.ДанныеСертификатаРезюме>)
    для ТекущаяСтрока из Параметр
        Значение.Удалить(ТекущаяСтрока)            
    ;
    ПриИзменении()
;

метод ПрисоединенныеФайлыДоступныеРасширения(): Строка
    возврат Строки.Соединить(ФИЛЬТР_ДОКУМЕНТОВ, ",")
;

метод ПрисоединенныеФайлыСтроки(ДанныеСтроки: СертификатыРезюме.ДанныеСертификатаРезюме): ПрисоединенныеФайлы
    знч ПрисоединенныеФайлы = новый ПрисоединенныеФайлы()
    если ДанныеСтроки.Файл != Неопределено
        ПрисоединенныеФайлы.Файлы.Добавить(ДанныеСтроки.Файл!)
    ;
    возврат ПрисоединенныеФайлы 
;

метод ПрисоединенныеФайлыЗавершенаЗагрузкаФайлов(Источник: ПрисоединенныеФайлыКомпонент, Событие: неизвестно, ДанныеСтроки: Объект)
    (ДанныеСтроки как СертификатыРезюме.ДанныеСертификатаРезюме).Файл = Источник.ТаблицаФайлы.Файлы.ПервыйИлиУмолчание()
    ПриИзменении()
;

метод ПрисоединенныеФайлыФайлПомеченНаУдаление(Источник: ПрисоединенныеФайлыКомпонент, Событие: СобытиеСДанными<ПрисоединенныйФайл>, ДанныеСтроки: Объект?)
    (ДанныеСтроки как СертификатыРезюме.ДанныеСертификатаРезюме).Файл = Неопределено
    ПриИзменении()
;

метод ПолеВводаПриИзменении(Источник: неизвестно, Событие: неизвестно, ДанныеСтроки: СертификатыРезюме.ДанныеСертификатаРезюме)
    ПриИзменении()
;