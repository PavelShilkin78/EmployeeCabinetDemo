импорт ОбщиеСтандартные
импорт ИнтерфейсныеКомпонентыСтандартные

// ИнициализируемыйКомпонент
@ВПроекте
@Реализация
метод Инициализировать()
    УстановитьВалютуПоУмолчанию()
;
//Конец ИнициализируемыйКомпонент

@ВПроекте
метод ПроверитьЗаполнение()
    если ОбязательноеЗаполнение и СтандартныеФормы.ПроверкаЗаполенияПолей({Компоненты.СуммаОт, Компоненты.СуммаДо})
        ПроверитьСогласованностьСумм()
    ;
    если ОбязательноеЗаполнение
        ПроверитьЗаполненностьВалюты()
    ;
;

метод ПроверитьСогласованностьСумм()
    если ДиапазонСумм и Значение.СуммаОт > Значение.СуммаДо
        Компоненты.СуммаОт.РезультатПроверкиДанных = РезультатПроверкиДанных.Ошибка
        Компоненты.СуммаОт.СообщениеОшибка = "'От' > 'До'"
    иначе
        Компоненты.СуммаОт.РезультатПроверкиДанных = РезультатПроверкиДанных.Отсутствует            
        Компоненты.СуммаОт.СообщениеОшибка = ""
    ;        
;

метод ПроверитьЗаполненностьВалюты()
    Компоненты.Валюта.РезультатПроверкиДанных =
        Значение.Валюта == Неопределено ? РезультатПроверкиДанных.Ошибка : РезультатПроверкиДанных.Отсутствует
;

метод УстановитьВалютуПоУмолчанию()
    если Значение.Валюта == Неопределено
        Значение.Валюта = Валюты.ВалютаПоУмолчанию()
    ;
;

метод ЗаголовокСуммаОт(): Строка 
    пер ПредставлениеВалюты = ""
    если Значение.Валюта != Неопределено
        ПредставлениеВалюты = Валюты.СимволВалюты(Значение.Валюта)
    ;
    
    возврат ДиапазонСумм ? "${Заголовок} от, ${ПредставлениеВалюты}" : "${Заголовок}, ${ПредставлениеВалюты}"    
;

метод СуммаОтПриИзменении(Источник: ПолеВвода<Число>, Событие: СобытиеПриИзменении<Число>)
    УстановитьВалютуПоУмолчанию()
    ПривестиСуммуОтКНеотрицательномуЧислу()
    если не ДиапазонСумм
        Значение.СуммаДо = Значение.СуммаОт
    ;
    ПроверитьЗаполнение()
    ПриИзменении(новый СобытиеКомпонента())
;

метод СуммаДоПриИзменении(Источник: ПолеВвода<Число>, Событие: СобытиеПриИзменении<Число>)
    УстановитьВалютуПоУмолчанию()
    ПривестиСуммуДоКНеотрицательномуЧислу()
    ПроверитьЗаполнение()
    ПриИзменении(новый СобытиеКомпонента())
;

метод ВалютаПриИзменении(Источник: ПолеВвода<Валюты.Ссылка?>, Событие: СобытиеПриИзменении<неизвестно>)
    ПроверитьЗаполнение()
    ПриИзменении(новый СобытиеКомпонента())
;

метод СуммаНеттоПриИзменении(Источник: Флажок, Событие: СобытиеПриИзменении<Булево?>)
    ПриИзменении(новый СобытиеКомпонента())
;

метод ПривестиСуммуОтКНеотрицательномуЧислу()
    Значение.СуммаОт = МенеджерЧисел.Модуль(Значение.СуммаОт)    
;

метод ПривестиСуммуДоКНеотрицательномуЧислу()
    Значение.СуммаДо = МенеджерЧисел.Модуль(Значение.СуммаДо)    
;