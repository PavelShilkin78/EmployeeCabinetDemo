@ВПроекте
@НаКлиенте
структура ОписаниеРубрики
    обз пер Компонент: Компонент
    пер Заголовок: Строка
    пер ПроцентЗаполнения: Число = 0
    пер Предупреждение: Булево
;

@НаКлиенте
метод СодержимоеГруппы(): Массив<Компонент>
    знч СодержимоеГруппы: Массив<Компонент>
    для Рубрика из Рубрики
        пер Изображение: ДвоичныйОбъект.Ссылка?
        если Рубрика.Предупреждение
            Изображение = Ресурс{Пиктограммы/Предупреждение.svg}.Ссылка
        иначе если Рубрика.ПроцентЗаполнения >= 100
            Изображение = Ресурс{ГалочкаВКругеУспех.svg}.Ссылка
        иначе если Рубрика.ПроцентЗаполнения > 0
            Изображение = ПрогрессБарКруговой.ИзображениеSVG(Рубрика.ПроцентЗаполнения, 100, 20)
        иначе
            Изображение = Ресурс{Пустышка.svg}.Ссылка
        ;
        
        пер ЗаголовокРубрики: Строка
        если не Рубрика.Заголовок.Пусто()
            ЗаголовокРубрики = Рубрика.Заголовок
        иначе
            выбор Рубрика.Компонент
            когда это ФиксированнаяГруппа
                ЗаголовокРубрики = (Рубрика.Компонент как ФиксированнаяГруппа).Заголовок
            когда это АвтоматическаяГруппа
                ЗаголовокРубрики = (Рубрика.Компонент как АвтоматическаяГруппа).Заголовок
            ;
        ;
        
        
        СодержимоеГруппы.Добавить(
            новый Надпись(
                Изображение = Изображение,
                Значение = ЗаголовокРубрики,
                ОбрабатыватьНажатие = Истина,
                Цвет = Цвета.Стилевые.Текст,
                ПриНажатии = метод(Источник, Событие)-> Рубрика.Компонент.Активировать();))
    ;
    возврат СодержимоеГруппы
;

@ВПроекте
@НаКлиенте
статический метод ПроцентЗаполнения(РезультатыПроверкиЗаполнения: Массив<Булево>): Число
    если РезультатыПроверкиЗаполнения.Размер() == 0
        возврат 0
    ;
    пер КоличествоЗаполненныхПолей: Число
    для Элемент из РезультатыПроверкиЗаполнения
        если Элемент 
            КоличествоЗаполненныхПолей += 1
        ;
    ;
    возврат (КоличествоЗаполненныхПолей / РезультатыПроверкиЗаполнения.Размер() * 100).Округлить(РежимОкругления.Вверх)
;