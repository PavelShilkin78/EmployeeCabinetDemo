
@ВПроекте
перечисление ШиринаОбластиДополнительногоРаздела
    Одинарная умолчание,
    Двойная
;


метод ШиринаОсновногоРаздела(): ШиринаВКолонках
    выбор ШиринаОбластиДополнительногоРаздела
    когда Одинарная
        возврат ШиринаВКолонках.Тройная
    иначе
        возврат ШиринаВКолонках.Двойная
    ;
;

метод ШиринаДополнительногоРаздела(): ШиринаВКолонках
    выбор ШиринаОбластиДополнительногоРаздела
    когда Одинарная
        возврат ШиринаВКолонках.Одинарная
    иначе
        возврат ШиринаВКолонках.Двойная
    ;
;

метод РежимМобильногоУстройства(): Булево
    возврат КлиентскоеУстройство.ВидИнтерфейса == ВидИнтерфейса.Телефон
;

метод КомандыВРежимеДесктоп(): Массив<Компонент>
    знч КомандыВРежимеДесктоп: Массив<Компонент> 
    если не РежимМобильногоУстройства()
        для Команда из Команды
            знч Кнопка = новый Кнопка(Команды = Команда)    
            КомандыВРежимеДесктоп.Добавить(Кнопка)   
        ;
    ;
    возврат КомандыВРежимеДесктоп
;

@ВПроекте
@НаКлиенте
метод ДополнительныеКомандыФормы(): ФрагментКомандногоИнтерфейса<Команда>?
    знч КомандыВРежимеДесктоп: ФрагментКомандногоИнтерфейса<Команда>?
    если РежимМобильногоУстройства()
       возврат новый ФрагментКомандногоИнтерфейса<Команда>(Элементы = Команды)
    иначе
        возврат Неопределено
    ;
    возврат КомандыВРежимеДесктоп
;

метод СодержимоеОсновногоРаздела(): Массив<Компонент>
    знч СодержимоеРаздела: Массив<Компонент>
    если ОсновнойРаздел.Размер() == 1 
        СодержимоеРаздела.Добавить(ОсновнойРаздел.Первый())
    иначе    
        СодержимоеРаздела.Добавить(новый Страницы(Страницы = ОсновнойРаздел))
    ;
    возврат СодержимоеРаздела
;

метод СодержимоеДополнительногоРаздела(): Массив<Компонент>
    знч СодержимоеРаздела: Массив<Компонент>
    выбор ШиринаОбластиДополнительногоРаздела
    когда Одинарная
        СодержимоеРаздела.ДобавитьВсе(ДополнительныйРаздел)
    иначе
        знч Страницы = новый Страницы(ШиринаВКолонках = ШиринаВКолонках.Двойная)
        для Элемент из ДополнительныйРаздел
            Страницы.Страницы.Добавить(Элемент)
        ;
        СодержимоеРаздела.Добавить(Страницы)
    ;   
    возврат СодержимоеРаздела
;

метод ЭлементыПереключателяСтраниц(): Массив<ЭлементСпискаЗначений<Число>>
    знч ЭлементыПереключателя: Массив<ЭлементСпискаЗначений<Число>>
    пер Индекс: Число
    для СтраницаОсновногоРаздела из ОсновнойРаздел
        ЭлементыПереключателя.Добавить(новый ЭлементСпискаЗначений<Число>(
                Значение = Индекс,
                Представление = СтраницаОсновногоРаздела.Заголовок,
                ИзображениеАктивного = СтраницаОсновногоРаздела.Изображение,
                ИзображениеНеактивного = СтраницаОсновногоРаздела.Изображение))
        Индекс += 1
    ;
    для КомпонентДополнительногоРаздела из ДополнительныйРаздел
        ЭлементыПереключателя.Добавить(новый ЭлементСпискаЗначений<Число>(
                Значение = Индекс,
                Представление = КомпонентДополнительногоРаздела.Заголовок))
        Индекс += 1
    ;
    
    возврат ЭлементыПереключателя
;

метод СодержимоеТекущейСтраницы(): Массив<Компонент>
    знч СодержимоеТекущейСтраницы: Массив<Компонент>
    если ОсновнойРаздел.Размер() != 0 или ДополнительныйРаздел.Размер() != 0
        если ИндексТекущегоРаздела > ОсновнойРаздел.Граница()
            СодержимоеТекущейСтраницы.Добавить(ДополнительныйРаздел.Получить(ИндексТекущегоРаздела - ОсновнойРаздел.Размер()))
        иначе
            СодержимоеТекущейСтраницы.Добавить(ОсновнойРаздел.Получить(ИндексТекущегоРаздела))
        ;
    ;
    возврат СодержимоеТекущейСтраницы
;

метод ПереключательСтраницПриИзменении(Источник: Переключатель<Число>, Событие: СобытиеПриИзменении<Число>)
    ИндексТекущегоРаздела = Событие.НовоеЗначение
;