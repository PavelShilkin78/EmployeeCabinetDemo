
конст КОЛИЧЕСТВО_СИМВОЛОВ_В_КОЛОНКЕ = 20

метод НоваяГруппаЭлементов(): ФиксированнаяГруппа
    возврат новый ФиксированнаяГруппа(
            РастягиватьПоГоризонтали = Истина, 
            Ориентация = ОриентацияСодержимого.Горизонтальная,
            ИнтервалМеждуЭлементамиПоГоризонтали = РазмерОтступа.Половинный)
;

метод ВыбранныеЗначенияСодержимое(): Массив<Компонент>
    пер КоличествоКолонок: Число
    выбор этот.ШиринаВКолонках
    когда ШиринаВКолонках.Половинная
        КоличествоКолонок = 0.5
    когда ШиринаВКолонках.Одинарная
        КоличествоКолонок = 1
    когда ШиринаВКолонках.Двойная
        КоличествоКолонок = 2
    когда ШиринаВКолонках.Тройная
        КоличествоКолонок = 3
    когда ШиринаВКолонках.Четверная
        КоличествоКолонок = 4
    ;
    
    знч МаксимальнаяДлинаСтроки = КоличествоКолонок * КОЛИЧЕСТВО_СИМВОЛОВ_В_КОЛОНКЕ
    
    знч ВыбранныеЗначения: Массив<Группа>
    если этот.ШиринаВКолонках == Авто
        ВыбранныеЗначения.Добавить(новый АвтоматическаяГруппа())
    иначе
        ВыбранныеЗначения.Добавить(НоваяГруппаЭлементов())
    ;
    
    пер КоличествоСимволовВСтроке: Число
    для Элемент из Значение
        знч ЗаголовокЭлемента = Элемент.Представление()
        если КоличествоСимволовВСтроке != 0 и КоличествоСимволовВСтроке + ЗаголовокЭлемента.Длина() > МаксимальнаяДлинаСтроки
            КоличествоСимволовВСтроке = 0
            ВыбранныеЗначения.Добавить(НоваяГруппаЭлементов())
        иначе
            КоличествоСимволовВСтроке += ЗаголовокЭлемента.Длина()
        ;
        знч Компонент = новый КомпонентВыбора()
        Компонент.Заголовок = ЗаголовокЭлемента
        Компонент.Активность = Истина
        Компонент.ДополнительныеДанные = Элемент
        Компонент.Закрываемый = не РежимПросмотра
        Компонент.ШиринаВКолонках = ШиринаВКолонках.Одинарная
        Компонент.РастягиватьПоГоризонтали = Ложь
        Компонент.ПриНажатииЗакрытия = (КомпонентВыбора, СобытиеКомпонента) -> КомпонентВыбораПриНажатииЗакрытия(КомпонентВыбора, СобытиеКомпонента)
        Компонент.ВыравниваниеВГруппеПоВертикали = ВыравниваниеПоВертикали.Низ
        ВыбранныеЗначения.Последний().Содержимое.Добавить(Компонент)
    ;
    возврат новый Массив<Компонент>(ВыбранныеЗначения)
;

метод ВыбранныеЗначенияМинимальнаяВысота(): Число
    возврат КлиентскоеУстройство.ВидИнтерфейса == ВидИнтерфейса.Телефон ? 0 : 68
;

метод КомпонентВыбораПриНажатииЗакрытия(КомпонентВыбора: КомпонентВыбора, СобытиеКомпонента: СобытиеКомпонента)
    Значение.Удалить(КомпонентВыбора.ДополнительныеДанные как Представляемое)
    ПриИзменении(новый СобытиеСДанными<Массив<Представляемое>>(Значение))
;

метод ПолеВводаПриНажатииВвода(Источник: ПолеВвода<Представляемое?>, Событие: СобытиеКомпонента)
    ДобавитьВведенноеЗначение()
;

метод ПолеВводаПриИзменении(Источник: ПолеВвода<Представляемое?>, Событие: СобытиеПриИзменении<Представляемое?>)
    ДобавитьВведенноеЗначение()
;

метод ДобавитьВведенноеЗначение()
    если ЗначениеПолеВвода == Неопределено или Значение.Найти(ЗначениеПолеВвода) != Неопределено
        возврат 
    ;        
    Значение.Добавить(ЗначениеПолеВвода)
    ЗначениеПолеВвода = Неопределено
    ПриИзменении(новый СобытиеСДанными<Массив<Представляемое>>(Значение))
;

@ВПроекте
метод УстановитьЗначения(НовыеЗначения: Обходимое<Представляемое>)
    Значение.Очистить()
    Значение.ДобавитьВсе(НовыеЗначения)
;

@ВПроекте
метод ПолучитьЗначения(): Массив<Представляемое>
    возврат Значение
;

метод ШиринаКонтейнераВКолонках(): ШиринаВКолонках | Авто
    если РежимПросмотра
        возврат этот.ШиринаВКолонках
    иначе
        выбор этот.ШиринаВКолонках
        когда ШиринаВКолонках.Четверная
            возврат ШиринаВКолонках.Тройная
        когда ШиринаВКолонках.Тройная
            возврат ШиринаВКолонках.Двойная
        когда ШиринаВКолонках.Двойная
            возврат ШиринаВКолонках.Одинарная
        иначе
            возврат Авто
        ;
    ;
;

метод ЗаголовокПоляЦветТекста(): Цвет | Авто
    возврат БиблиотекаЦветов.Цвет(БиблиотекаЦветов.ВидыЦвета.СветлыйШрифт)
;

метод ИнтервалКонтейнера(): РазмерОтступа | Авто
    возврат РежимПросмотра ? РазмерОтступа.Отсутствует : Авто
;