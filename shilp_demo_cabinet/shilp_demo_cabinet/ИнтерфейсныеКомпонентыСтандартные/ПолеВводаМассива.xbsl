@Реализация
@ВПроекте
метод Инициализировать()
    МассивЭлементов = МассивИзОбъекта(МассивОбъект) 
    СформироватьСтрокиПоЭлементамМассива() 
    если ТипЭлементаСтруктура != Неопределено
        НовыйЭлемент = ТипЭлементаСтруктура.СоздатьЭкземпляр([])
    иначе           
        Компоненты.ВводНовогоЭлемента.ДопустимыеТипы.ДобавитьВсе(ТипыЭлементов)
    ;
;

метод СтрокаЭлементаМассиваПриИзменении(Источник: СтрокаПоляВводаМассива, Событие: СобытиеПриИзменении<Объект?>)
    ПриИзмененииЗначения(Событие)
    МассивВОбъект()
;

метод УбратьСтрокуЭлементаМассива(Источник: СтрокаПоляВводаМассива, Событие: СобытиеСДанными<Число>)
    МассивЭлементов.УдалитьПоИндексу(Событие.Данные)
    СформироватьСтрокиПоЭлементамМассива()
    МассивВОбъект()    
;

метод ДобавитьСтрокуПриНажатии(Источник: Кнопка, Событие: СобытиеПриНажатии)
    Компоненты.ВводНовогоЭлемента.РезультатПроверкиДанных = РезультатПроверкиДанных.Отсутствует
    если Компоненты.ВводНовогоЭлемента.Видимость и НовыйЭлемент == Неопределено
        Компоненты.ВводНовогоЭлемента.РезультатПроверкиДанных = РезультатПроверкиДанных.Ошибка
        возврат
    ;
    МассивЭлементов.Добавить(НовыйЭлемент)
    МассивВОбъект()
    СформироватьСтрокиПоЭлементамМассива() 
    если ТипЭлементаСтруктура != Неопределено 
        НовыйЭлемент = ТипЭлементаСтруктура.СоздатьЭкземпляр([])
    иначе
        НовыйЭлемент = Неопределено
    ;          
;

метод СформироватьСтрокиПоЭлементамМассива()
    Компоненты.СтрокиВводаМассива.Содержимое.Очистить()
    для ИндексЭлемента = 0 по МассивЭлементов.Граница()
        знч СтрокаПоляВвода: СтрокаПоляВводаМассива 
        СтрокаПоляВвода.МассивЭлементов = МассивЭлементов
        СтрокаПоляВвода.ИндексЭлемента = ИндексЭлемента
        СтрокаПоляВвода.ЭлементыСтруктура = ТипЭлементаСтруктура != Неопределено
        СтрокаПоляВвода.УбратьСтроку = &УбратьСтрокуЭлементаМассива
        СтрокаПоляВвода.ПриИзмененииЗначения = &СтрокаЭлементаМассиваПриИзменении
        Компоненты.СтрокиВводаМассива.Содержимое.Добавить(СтрокаПоляВвода)
        СтрокаПоляВвода.Инициализировать()   
    ;    
;

@НаСервере @ДоступноСКлиента
статический метод МассивИзОбъекта(Объект: Объект?): Массив<Объект>
    пер Массив: Массив<Объект>
    Массив = новый Массив<Объект>()
    если Объект != Неопределено
        для Элемент из Объект как Массив<неизвестно>
            Массив.Добавить(Элемент как Объект)
        ;
    ;
    возврат Массив    
;

метод МассивВОбъект()
    (МассивОбъект как Массив<неизвестно>).Очистить()
    для ЭлементМассива из МассивЭлементов
        (МассивОбъект как Массив<неизвестно>).Добавить(ЭлементМассива)
    ;
;