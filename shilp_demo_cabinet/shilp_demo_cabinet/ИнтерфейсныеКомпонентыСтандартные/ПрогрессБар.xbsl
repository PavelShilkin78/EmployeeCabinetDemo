@ВПроекте
перечисление ВидПрогрессБара
    ЗначенияЧислами умолчание,
    ЗначениеПроцентом,
    БезЗначений,
    ТолькоЗначение
;

метод Картинка(): ДвоичныйОбъект.Ссылка
    возврат ЗагруженнаяКартинка(ШиринаБара, ВысотаБара, Значение, МаксимальноеЗначение)
;

@НаСервере @ДоступноСКлиента
статический метод ЗагруженнаяКартинка(ШиринаБара: Число, ВысотаБара: Число, Значение: Число, МаксимальноеЗначение: Число): ДвоичныйОбъект.Ссылка
    пер КартинкаSvg = 
        "<svg viewBox='0 0 %ШиринаБара %ВысотаБара' width='%ШиринаБара' height='%ВысотаБара' xmlns='http://www.w3.org/2000/svg'>
        <defs>
            <linearGradient id='Gradient'>
                <stop offset='0%' stop-color='#32CD32'/>
                <stop offset='100%' stop-color='#3CB371'/>
            </linearGradient>
        </defs>"
    
    
    пер ШиринаЗаполнения = МаксимальноеЗначение == 0 ? 0 : (ШиринаБара * Значение/МаксимальноеЗначение).Округлить()
    КартинкаSvg += Бар(ШиринаБара, ВысотаБара, "#e0e0e0")
    если Значение != 0
        КартинкаSvg += Бар(ШиринаЗаполнения, ВысотаБара, "url(#Gradient)")
    ;
    КартинкаSvg += 
        "
        </svg>"
    возврат ОбъектноеХранилище.ЗагрузитьИзСтроки(КартинкаSvg, "image/svg+xml").Ссылка
;

@НаСервере
статический метод Бар(ШиринаБара: Число, ВысотаБара: Число, Цвет: Строка): Строка
    пер Радиус = ВысотаБара >=32 ? 6 : 4
    ШиринаБара = Макс<Число>(ШиринаБара, 2*Радиус)
    пер ШиринаБезРадиуса = ШиринаБара - Радиус
    пер ВысотаБезРадиуса = ВысотаБара - Радиус
    
    возврат 
    "
    <path d='M 0 %Радиус A %Радиус %Радиус 0 0 1 %Радиус 0 L %ШиринаБезРадиуса 0 A %Радиус %Радиус 0 0 1 %ШиринаБара %Радиус L %ШиринаБара %ВысотаБезРадиуса A %Радиус %Радиус 0 0 1 %ШиринаБезРадиуса %ВысотаБара L %Радиус %ВысотаБара A %Радиус %Радиус 0 0 1 0 %ВысотаБезРадиуса Z' fill='%Цвет'/>"
;

метод НадписьЗаголовок():Строка
    пер НадписьЗаголовок: Строка
    выбор ВидОтображения
    когда ЗначенияЧислами
        НадписьЗаголовок = "%Значение%РазделительЧисел%МаксимальноеЗначение"
    когда ЗначениеПроцентом
        пер Процент = (Значение/МаксимальноеЗначение*100).Округлить()
        НадписьЗаголовок = "%Процент%"
    когда ТолькоЗначение
        НадписьЗаголовок = "%Значение"
    ;
    возврат НадписьЗаголовок
;