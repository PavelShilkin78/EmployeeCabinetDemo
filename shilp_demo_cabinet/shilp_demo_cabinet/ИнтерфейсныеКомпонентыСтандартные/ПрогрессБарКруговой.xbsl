импорт ОбщиеСтандартные


@ВПодсистеме
@НаКлиенте @НаСервере
конст ЦВЕТ_ФОНА = "#e0e0e0"

@ВПодсистеме
@НаКлиенте @НаСервере
конст ЦВЕТ_ЗНАЧЕНИЯ = "#32CD32"

@ВПроекте
@НаСервере
@ДоступноСКлиента
статический метод ИзображениеSVG(Значение: Число, МаксимальноеЗначение: Число = 100, Размер: Число = 48, ЦветФона: Строка =
        ЦВЕТ_ФОНА, ЦветЗначения: Строка = ЦВЕТ_ЗНАЧЕНИЯ): ДвоичныйОбъект.Ссылка
    знч Прогресс = (Значение / МаксимальноеЗначение * 100).Округлить()
    знч Радиус = (Размер / 2)
    знч ТолщинаЛинии = (Радиус / 2).Округлить()
    знч Пи = МенеджерЧисел.ЧислоПи()
    знч Dasharray = 2 * Пи * Радиус
    знч Dashoffset = Dasharray * ((100 - Прогресс) / 100)
    знч ОбластьX = -(Размер / 8)
    знч ОбластьY = Размер + (Размер / 4)
    знч КоординатаЦентра = Размер / 2

    знч Картинка =
    "
    <svg xmlns='http://www.w3.org/2000/svg' version='1.1'
    width='%Размер' height='%Размер' viewBox='%ОбластьX %ОбластьX %ОбластьY %ОбластьY'   style='transform:rotate(-90deg)'>
    <circle r='%Радиус' cx='%КоординатаЦентра' cy='%КоординатаЦентра' fill='transparent' stroke='%ЦветФона' stroke-width='%{ТолщинаЛинии}px' stroke-dasharray='%Dasharray' stroke-dashoffset='0'></circle>
    <circle r='%Радиус' cx='%КоординатаЦентра' cy='%КоординатаЦентра' stroke='%ЦветЗначения' stroke-width='%{ТолщинаЛинии}px' stroke-linecap='round' stroke-dashoffset='%Dashoffset' fill='transparent' stroke-dasharray='%Dasharray'></circle>
    </svg>"

    возврат ОбъектноеХранилище.ЗагрузитьИзСтроки(Картинка, "image/svg+xml").Ссылка
;


метод ИзображениеSVG(): ДвоичныйОбъект.Ссылка
    возврат ИзображениеSVG(Значение, МаксимальноеЗначение, Размер, ЦветФонаПрогресса, ЦветЗначенияПрогресса)
;