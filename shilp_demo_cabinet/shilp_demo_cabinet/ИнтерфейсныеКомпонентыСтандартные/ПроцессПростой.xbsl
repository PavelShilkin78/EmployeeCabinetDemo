@НаКлиенте @НаСервере конст ВЫСОТА_ЭЛЕМЕНТА = 24
@НаКлиенте @НаСервере конст ПОЛЯ_ТЕКСТА = 12
@НаКлиенте @НаСервере конст ШИРИНА_ОТСТУПА = 36
@НаКлиенте @НаСервере конст ШИРИНА_ОСН_СИМВОЛА = 9
@НаКлиенте @НаСервере конст ИНТЕРВАЛ_ГОРИЗОНТАЛЬНЫЙ = 36

@НаСервере @НаКлиенте
структура ИспользуемыеЦвета
    обз знч Серый: Строка
    обз знч СветлоСерый: Строка
    обз знч ТемноСерый: Строка
;

@НаСервере @НаКлиенте
структура ПараметрыИзображения
    обз знч ШиринаКартинки: Число
    обз знч Цвета: ИспользуемыеЦвета
    обз знч Шаги: Массив<ПараметрыШага>
;

@НаСервере @НаКлиенте
структура ПараметрыШага
    обз знч Индекс: Число
    обз знч Заголовок: Строка
    обз знч Стиль: Строка
    обз знч ШиринаЭлемента: Число
    обз знч Последний: Булево
;

метод КомпонентВысота(): Число
    возврат ВЫСОТА_ЭЛЕМЕНТА
;

статический метод ПараметрыИзображения(Процесс: Массив<Строка>, ИндексТекущегоШага: Число): ПараметрыИзображения
    знч Шаги: Массив<ПараметрыШага>
    пер ШиринаКартинки: Число
    пер Индекс: Число
    для Элемент из Процесс
        знч ШиринаЭлемента = ПОЛЯ_ТЕКСТА + (Элемент.Длина() * ШИРИНА_ОСН_СИМВОЛА)
        Шаги.Добавить(
            новый ПараметрыШага(
                Индекс = Индекс,
                Заголовок = Элемент,
                Стиль = (Индекс == ИндексТекущегоШага ? "prc-active" : "prc-main"),
                ШиринаЭлемента = ШиринаЭлемента,
                Последний = Индекс == Процесс.Граница()))
        Индекс += 1
        ШиринаКартинки += ШиринаЭлемента + ШИРИНА_ОТСТУПА
    ;
    возврат новый ПараметрыИзображения(
        ШиринаКартинки = ШиринаКартинки,
        Шаги = Шаги,
        Цвета = новый ИспользуемыеЦвета(
            Серый = БиблиотекаЦветов.ЦветСтрокой(БиблиотекаЦветов.Цвет(БиблиотекаЦветов.ВидыЦвета.Серый)),
            СветлоСерый = БиблиотекаЦветов.ЦветСтрокой(БиблиотекаЦветов.Цвет(БиблиотекаЦветов.ВидыЦвета.СветлоСерый)),
            ТемноСерый = БиблиотекаЦветов.ЦветСтрокой(БиблиотекаЦветов.Цвет(БиблиотекаЦветов.ВидыЦвета.ТемноСерый))))
;

метод КартинкаШага(Шаг: ПараметрыШага, НачалоГоризонталь: Число, Цвета: ИспользуемыеЦвета): Строка
    пер Изображение =
    "
    <rect x='%НачалоГоризонталь' y='0' width='%{Шаг.ШиринаЭлемента}' height='24' rx='5' ry='5' fill='%{Цвета
        .СветлоСерый}' />
    <text x='%{НачалоГоризонталь + (Шаг.ШиринаЭлемента/2)}' 
        y='13' alignment-baseline='middle' text-anchor='middle' class='%{Шаг.Стиль}'>%{Шаг.Заголовок}</text>"

    если не Шаг.Последний
        Изображение += 
        "
        <path transform='translate(%{НачалоГоризонталь + Шаг.ШиринаЭлемента+6},0)' 
        d='M8.59,16.58L13.17,12L8.59,7.41L10,6L16,12L10,18L8.59,16.58Z' fill='%{Цвета.Серый}'/>"
    ;
    возврат Изображение
;

метод КартинкаПроцесса(Процесс: Массив<Строка>, ПараметрыИзображения: ПараметрыИзображения): Строка
    знч СтильИмпортаRoboto = ИнтерфейсныеКомпоненты.СтильИмпортаRoboto()
    пер КартинкаSvg =
    "<svg viewBox='0 0 %{ПараметрыИзображения.ШиринаКартинки} %ВЫСОТА_ЭЛЕМЕНТА' width='%{ПараметрыИзображения
        .ШиринаКартинки}' height='%ВЫСОТА_ЭЛЕМЕНТА' xmlns='http://www.w3.org/2000/svg'>
        %СтильИмпортаRoboto
        <style>   
            .prc-main {font: 12px Roboto, sans-serif;fill:%{ПараметрыИзображения.Цвета.ТемноСерый};}
            .prc-active {font: bold 12px Roboto, sans-serif;fill:%{ПараметрыИзображения.Цвета.ТемноСерый};}
        </style>"
    пер НачалоГоризонталь: Число
    для Шаг из ПараметрыИзображения.Шаги
        КартинкаSvg += КартинкаШага(Шаг, НачалоГоризонталь, ПараметрыИзображения.Цвета)
        НачалоГоризонталь += Шаг.ШиринаЭлемента + ШИРИНА_ОТСТУПА
    ;
    возврат КартинкаSvg
;

метод КомпонентСодержимое(): Строка
    если КлиентскоеУстройство.ВидИнтерфейса == ВидИнтерфейса.Телефон
        знч ТекущийЭлемент = Шаги.Получить(ТекущийШаг)
        знч АктивныеШаги = [ТекущийЭлемент]
        знч ПараметрыИзображения = ПараметрыИзображения(АктивныеШаги, 0)
        возврат КартинкаПроцесса(АктивныеШаги, ПараметрыИзображения)
    иначе
        знч ПараметрыИзображения = ПараметрыИзображения(Шаги, ТекущийШаг)
        возврат КартинкаПроцесса(Шаги, ПараметрыИзображения)
    ;
;