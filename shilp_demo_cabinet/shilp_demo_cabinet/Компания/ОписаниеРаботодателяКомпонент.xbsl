импорт ИнтерфейсныеКомпонентыСтандартные
импорт УниверсальныеМеханизмы

// МетодыЗаполненияФормы

@ВПроекте
@Реализация
метод Инициализировать()
    Прочитать(ОписаниеРаботодателя())
    ИнтерфейсныеКомпоненты.ИнициализироватьПодчиненныеКомпоненты(Компоненты)
;

@Обработчик
метод ПослеЧтения()
    Логотип = ПрисоединенныйФайл(Объект.Логотип)
;

// КонецМетодыЗаполненияФормы

// ОбработчикиКоманд

метод КомандаЗавершитьРедактирование(Команда: ОбычнаяКоманда)
    Записать()
;

метод ЛоготипИзображениеПриИзменении(Источник: ПрисоединенноеИзображениеКомпонент, Событие: СобытиеКомпонента)
    если не СобственнаяМодифицированность
        СобственнаяМодифицированность = ИзображениеЛоготипаИзменено()
    ;
;

// Конец ОбработчикиКоманд

@Обработчик
метод ПередЗаписьюОбъекта()
    если ИзображениеЛоготипаИзменено()
        Объект.Логотип = ПрименитьИзменения(
            КлючОбъекта,
            Логотип,
            Объект.Логотип
        )
    ;
;

метод ИзображениеЛоготипаИзменено(): Булево
    знч ИсходныйЛоготип = Объект.Логотип
    знч ИзображениеЗагружено = Логотип.Ссылка == Неопределено и Логотип.Хранилище != Неопределено
    знч ИзображениеУдалено = Логотип.Ссылка == Неопределено и ИсходныйЛоготип != Неопределено
    возврат ИзображениеЗагружено или ИзображениеУдалено  
;

@Обработчик
метод ПослеЗаписиОбъекта()
    СобственнаяМодифицированность = Ложь    
;

метод СохранитьДоступность(): Булево
    возврат СобственнаяМодифицированность
;

@НаСервере
конст ОШИБКА_ДОСТУПА_К_ФАЙЛАМ = "Работа с файлами не доступна"

@НаСервере @ДоступноСКлиента
статический метод ОписаниеРаботодателя(): ОписаниеРаботодателя.Ссылка
    возврат ОписаниеРаботодателя.Получить()
;

@НаСервере @ДоступноСКлиента
статический метод ПрисоединенныйФайл(Файл: Файлы.Ссылка?): ПрисоединенныйФайл
    если не Компания.ОписаниеРаботодателяИнтерактивноеРедактированиеДоступно()
        выбросить новый ИсключениеВыполнения(ОШИБКА_ДОСТУПА_К_ФАЙЛАМ)
    ;
    если Файл != Неопределено
        возврат ПрисоединенныеФайлыМенеджер.ПрисоединенныйФайл(Файл)
    иначе
        возврат новый ПрисоединенныйФайл()
    ;
;

@НаСервере @ДоступноСКлиента
статический метод ПрименитьИзменения(Владелец: ОписаниеРаботодателя.Ссылка, НовыйЛоготип: ПрисоединенныйФайл, 
        ИсходныйЛоготип: Файлы.Ссылка?): Файлы.Ссылка?
    если не Компания.ОписаниеРаботодателяИнтерактивноеРедактированиеДоступно()
        выбросить новый ИсключениеВыполнения(ОШИБКА_ДОСТУПА_К_ФАЙЛАМ)
    ;
    пер НовыйЛоготипСсылка: Файлы.Ссылка? = Неопределено
    если НовыйЛоготип.Хранилище != Неопределено
        ПрисоединенныеФайлыМенеджер.Записать(НовыйЛоготип, Владелец)
        НовыйЛоготипСсылка = НовыйЛоготип.Ссылка
    ;
    если ИсходныйЛоготип?.ЗагрузитьОбъект() != Неопределено
        Файлы.УдалитьФайл(ИсходныйЛоготип)
    ;
    возврат НовыйЛоготипСсылка
;