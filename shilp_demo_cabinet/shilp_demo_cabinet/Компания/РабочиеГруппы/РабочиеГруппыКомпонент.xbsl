импорт ОбщиеСтандартные
импорт Компания

метод РабочиеГруппыКомпонентВидимость(): Булево
    возврат не Компания.ПараметрыСеанса().ПодразделенияРуководителя.Пусто()
;

метод КоличествоСотрудниковПредставление(ДанныеЭлемента: СтрокаДинамическогоСписка): Строка
    знч Количество = ДанныеЭлемента.Данные["Количество"] как Число
    возврат "%{Количество.ВСтроку()} %{МенеджерСтрок.ТекстПоКоличеству("сотрудник", Количество)}"
;

метод ДобавитьГруппуПриНажатии(Источник: Кнопка, Событие: СобытиеПриНажатии)
    знч НоваяГруппа = новый РабочиеГруппыКлиентСервер.РабочаяГруппа()
    РедактироватьПравило(НоваяГруппа)
;

метод РабочиеГруппыПриНажатииЭлемента(Источник: Список<ДинамическийСписок>, Событие: СобытиеСДанными<СтрокаСписка<СтрокаДинамическогоСписка>>)
    пер РабочаяГруппаСсылка: РабочиеГруппы.Ссылка = Событие.Данные.ДанныеСтроки.Ключ как РабочиеГруппы.Ссылка
    РедактироватьПравило(ДанныеРабочейГруппы(РабочаяГруппаСсылка))
;

метод РедактироватьПравило(РабочаяГруппа: РабочиеГруппыКлиентСервер.РабочаяГруппа)
    знч РезультатРедактирования = ФормаРедактированияРабочейГруппы.ОткрытьВМодальномОкне(
        РабочаяГруппа = РабочаяГруппа,
        ДоступноУдаление = РабочаяГруппа.Ссылка != Неопределено)
    
    если РезультатРедактирования == Истина
        Компоненты.РабочиеГруппы.Обновить()
    ;
;

@НаСервере @ДоступноСКлиента
статический метод ДанныеРабочейГруппы(РабочаяГруппаСсылка: РабочиеГруппы.Ссылка): РабочиеГруппыКлиентСервер.РабочаяГруппа?
    знч РабочаяГруппаОбъект = РабочаяГруппаСсылка.ЗагрузитьОбъект()
    если РабочаяГруппаОбъект == Неопределено
        возврат Неопределено
    ;
    
    знч РабочаяГруппа = новый РабочиеГруппыКлиентСервер.РабочаяГруппа(
        РабочаяГруппаСсылка,
        РабочаяГруппаОбъект.Наименование,
        РабочаяГруппаОбъект.Руководитель
    )
    
    РабочаяГруппа.Сотрудники.ДобавитьВсе(РабочаяГруппаОбъект.Сотрудники)
    
    возврат РабочаяГруппа
;