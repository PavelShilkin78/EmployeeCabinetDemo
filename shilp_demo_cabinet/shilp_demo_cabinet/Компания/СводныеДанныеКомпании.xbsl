импорт СтандартноеПриложение
импорт ИнтерфейсныеКомпонентыСтандартные

@ВПодсистеме
@НаСервере @НаКлиенте
структура Данные
    пер КоличествоПодразделений: Число
    пер КоличествоСотрудников: Число
    пер КоличествоОрганизаций: Число
;

метод ИндикаторОриентация(): ОриентацияСодержимого
    возврат КлиентскоеУстройство.ВидИнтерфейса == ВидИнтерфейса.Телефон ? ОриентацияСодержимого.Вертикальная : ОриентацияСодержимого.Горизонтальная
;

метод КоличествоСотрудниковПриНажатии(): (() -> ничто)?
    возврат ПользовательСотрудник ? Неопределено : &ПерейтиКСпискуСотрудников
;

метод КоличествоПодразделенийПриНажатии(): (() -> ничто)?
    возврат ПользовательСотрудник ? Неопределено : &ПерейтиКСпискуПодразделений
;

@Обработчик
метод ПослеСоздания()
    Данные = Данные()
;

метод ПерейтиКСпискуСотрудников()
    ИнтерфейсныеКомпоненты.ОткрытьЗамещающуюФорму(
        ФормаВладелец,
        новый СтруктураПредприятияФорма(
            РежимОтображенияСписка = Компания.РежимОтображенияСписка.Сотрудники
        )
    )
;

метод ПерейтиКСпискуПодразделений()
    ИнтерфейсныеКомпоненты.ОткрытьЗамещающуюФорму(
        ФормаВладелец, 
        новый СтруктураПредприятияФорма(
            РежимОтображенияСписка = Компания.РежимОтображенияСписка.СтруктураПредприятия
        )
    )
;

@НаСервере @ДоступноСКлиента
статический метод Данные(): Данные
    знч Данные: Данные
    исп КонтекстДоступа.Привилегированный()
    область
        знч Запрос = Запрос{
            Выбрать
                Количество(Ссылка) как КоличествоОрганизаций
            Из
                ДанныеПодразделений
            Где 
                Родитель == Неопределено
                И КоличествоСотрудниковНаОсновнойРаботе > 0
            }
        пер Результат = новый ЧитаемыйМассив(Запрос.Выполнить())
        если не Результат.Пусто()
            Данные.КоличествоОрганизаций = Результат[0].КоличествоОрганизаций
        ;
    ;
    область 
        знч Запрос = Запрос{
            Выбрать
                Сумма(КоличествоСотрудниковНаОсновнойРаботе) как КоличествоСотрудников,
                Сумма(КоличествоВсехПодчиненных) Как КоличествоПодразделений
            Из
                ДанныеПодразделений
            Где
                Родитель == Неопределено
            Сгруппировать По
                Родитель}
        пер Результат = новый ЧитаемыйМассив(Запрос.Выполнить())
        если не Результат.Пусто()
            Данные.КоличествоСотрудников = Результат[0].КоличествоСотрудников
            Данные.КоличествоПодразделений = Результат[0].КоличествоПодразделений
        ;
    ;
    возврат Данные
;