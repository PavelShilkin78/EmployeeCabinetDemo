//Область РазрешенияДоступа

@Обработчик
метод ВычислитьРазрешенияДоступа(): ЧитаемаяКоллекция<РазрешениеДоступа>
    возврат КомпанияКонтрольДоступаПоставщик.ПолучитьСервис().ВычислитьРазрешенияДляКатегорийДолжности()
;

//КонецОбласти

//Область СлужебныеПроцедурыИФункции

@ВПодсистеме
метод ПровестиПервоначальноеЗаполнение()
    знч Запрос = Запрос{
        Выбрать Первые 1
            Ссылка
        Из
            КатегорииДолжности
        }
    исп РезультатЗапроса = Запрос.Выполнить()
    знч РезультатЗапросаСтрок = новый ЧитаемыйМассив(РезультатЗапроса)
    если РезультатЗапросаСтрок.Пусто()
        ЗаполнитьКатегории()    
    ;
;

метод ЗаполнитьКатегории()
    знч ОсновныеКатегории: Массив<Строка> = [
        "Руководители", // Директоры предприятий, управляющие филиалами, начальники подразделений 
        "Специалисты", // Бухгалтеры, маркетологи, экономисты, инженеры, технологи
        "Служащие (технические исполнители)", // Делопроизводители, агенты, секретари, стенографисты, чертежники
        "Рабочие" // Слесари, строители, водители, грузчики
    ]
    для НаименованиеКатегории из ОсновныеКатегории
        новый КатегорииДолжности.Объект(
            Наименование = НаименованиеКатегории    
        )
        .Записать()   
    ;
;

@ВПодсистеме
метод ПередУдалениемДополнение(Ссылка: КатегорииДолжности.Ссылка)
    исп ПривилегированныйРежим: КонтекстДоступа = КонтекстДоступа.Привилегированный()
    знч Запрос = Запрос{
        Выбрать 
            Ссылка как Должность
        Из
            Должности
        Где
            КатегорияДолжности == %Ссылка
        }
    исп РезультатЗапроса = Запрос.Выполнить()
    ПривилегированныйРежим.Закрыть()
    для РезультатЗапросаСтрока из РезультатЗапроса
        пер ДолжностьОбъект = РезультатЗапросаСтрока.Должность.ЗагрузитьОбъект(Истина) 
        ДолжностьОбъект.КатегорияДолжности = Неопределено
        ДолжностьОбъект.Записать()
    ;    
;

//КонецОбласти