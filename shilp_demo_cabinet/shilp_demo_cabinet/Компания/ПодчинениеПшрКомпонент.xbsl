
@Обработчик
метод ПослеСоздания()
    НастройкиПрименены = СтруктураПшрПрименена()
    ИспользуетсяДополнительнаяСтруктура = ИспользуетсяДополнительнаяСтруктура()
    СтруктураДополнена = ИспользуетсяДополнительнаяСтруктура
;

метод ДобавитьЭлементПриНажатии(Источник: Кнопка, Событие: СобытиеПриНажатии)
    ОткрытьФормуРедактирования(Ложь)
;

метод СтрокаПриНажатии(Источник: Список<ДинамическийСписок>, Событие: СобытиеСДанными<СтрокаСписка<СтрокаДинамическогоСписка>>) 
    ОткрытьФормуРедактирования(Истина, 
        Событие.Данные.ДанныеСтроки.Данные["ПозицияШР"] как ШтатноеРасписание.Ссылка?, 
        Событие.Данные.ДанныеСтроки.Данные["Подразделение"] как СтруктураПредприятия.Ссылка?
    )
;

метод ПрименитьНастройкиПриНажатии(Источник: Кнопка, Событие: СобытиеПриНажатии)
    ОбновитьВторичныеДанные()
    СтруктураДополнена = ИспользуетсяДополнительнаяСтруктура
    НастройкиПрименены = Истина
;

метод УдалитьСтруктуруПшр(Источник: Кнопка, Событие: СобытиеПриНажатии)
    УдалитьВсеПодчиненияПшр()
    Компоненты.НаборПодчиненийПшр.Обновить()
    НастройкиПрименены = Ложь
    ИспользуетсяДополнительнаяСтруктура = Ложь
    СтруктураДополнена = ИспользуетсяДополнительнаяСтруктура
;

метод ПредставлениеПодразделениеЗаголовок(ДанныеЭлемента: СтрокаДинамическогоСписка): Строка
    возврат (ДанныеЭлемента.Данные["Подразделение"] как СтруктураПредприятия.Ссылка).Представление()
;

метод ПредставлениеПшрЗаголовок(ДанныеЭлемента: СтрокаДинамическогоСписка): Строка
    возврат (ДанныеЭлемента.Данные["ПозицияШР"] как ШтатноеРасписание.Ссылка).Представление()
;

метод ОткрытьФормуРедактирования(РежимРедактированияВключен: Булево, ТекущаяПозицияШР : ШтатноеРасписание.Ссылка? = Неопределено,
    ТекущееПодразделение : СтруктураПредприятия.Ссылка? = Неопределено)
    пер ФормаРедактирования = новый ФормаРедактированияПодчиненияПшр(
        РежимРедактирования = РежимРедактированияВключен,
        ТекущаяПозицияШР = ТекущаяПозицияШР,
        ТекущееПодразделение = ТекущееПодразделение
    )
    
    ФормаРедактирования.Инициализировать()
    
    знч ЕстьИзменения = ФормаРедактирования.ОткрытьВМодальномОкне() ?? Ложь
    если ЕстьИзменения
        Компоненты.НаборПодчиненийПшр.Обновить()
        НастройкиПрименены = Ложь
        ИспользуетсяДополнительнаяСтруктура = ИспользуетсяДополнительнаяСтруктура()
        СтруктураДополнена = ИспользуетсяДополнительнаяСтруктура
    ;
;

@НаСервере @ДоступноСКлиента
статический метод ИспользуетсяДополнительнаяСтруктура(): Булево
    знч Запрос = Запрос{
        Выбрать первые 1 
            ПозицияШР 
        Из 
            ПодчиненностьПшрСведения
    }

    знч РезультатСтроки = новый ЧитаемыйМассив(Запрос.Выполнить())
    возврат (не РезультатСтроки.Пусто()) 
;

@НаСервере @ДоступноСКлиента
статический метод ОбновитьВторичныеДанные()
    ПодразделенияИСотрудники.СформироватьСтруктуруПодчиненияПшр()
    ПодразделенияИСотрудники.УстановитьФлагНаличияПодчиненныхЭлементов()
    ПодчиненностьПодразделений.Обновить()
    Согласования.ОбновитьСогласующихЗаявления()
;

@НаСервере @ДоступноСКлиента
статический метод СтруктураПшрПрименена(): Булево
    знч Запрос = Запрос{
        Выбрать
            ПодразделенияИСотрудники.Позиция != ПшрСведения.ПозицияШР.ЗаменитьNull(Неопределено) как ЕстьНеравные
        Поместить
            втРезультатСравнения
        Из
            ПодразделенияИСотрудники Как ПодразделенияИСотрудники
                Левое Соединение ПодчиненностьПшрСведения Как ПшрСведения
                    По ПодразделенияИСотрудники.Объект == ПшрСведения.Подразделение
        Где
            ПодразделенияИСотрудники.ТипЭлемента == ТипыЭлементовПодразделенийИСотрудников.Подразделение
        ;
        Выбрать
            втРезультатСравнения.ЕстьНеравные
        Из
            втРезультатСравнения как втРезультатСравнения 
        Где 
            втРезультатСравнения.ЕстьНеравные == Истина
    }
    
    знч РезультатСтроки = новый ЧитаемыйМассив(Запрос.Выполнить())
    возврат РезультатСтроки.Пусто()
;

@НаСервере @ДоступноСКлиента
статический метод УдалитьВсеПодчиненияПшр()
    знч НаборЗаписей = новый ПодчиненностьПшрСведения.НаборЗаписей()
    НаборЗаписей.Фильтр.Установить()
    НаборЗаписей.Записать(Истина)
;