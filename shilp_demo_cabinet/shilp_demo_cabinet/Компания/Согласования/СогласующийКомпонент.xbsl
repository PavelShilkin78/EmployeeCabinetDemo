импорт КонтрольДоступа
импорт ОбщиеСтандартные
импорт СтандартноеПриложение
импорт ИнтерфейсныеКомпонентыСтандартные

метод МиниатюраАдресКартинки(ДанныеЭлемента: СогласующиеСтрока): ДвоичныйОбъект.Ссылка?
    возврат ДанныеЭлемента.Миниатюра == Неопределено ? СтандартноеПриложение.ПараметрыСеанса().АватарПоУмолчанию : ДанныеЭлемента.Миниатюра
;

метод УчастникЗаголовок(ДанныеЭлемента: СогласующиеСтрока): Строка
    пер Участник: Строка
    если СтрокаТекущегоПользователя(ДанныеЭлемента)
        пер Результат = ДанныеЭлемента.Результат
        если Результат == РезультатыСогласования.НаПодписанииВСервисеПодписания
            Участник = "Отправлено в сервис подписания"
        иначе если ЭтоДокументДляОзнакомления
            Участник = ДанныеЭлемента.РольПодписанта != Согласования.РольПодписантаОрганизация() ?
                "Вы ознакомились" : "Вы отправили"
        иначе если ДанныеЭлемента.НомерШагаСогласования == 0
            и ДанныеЭлемента.РольПодписанта != Согласования.РольПодписантаИсполнитель()
            
            Участник = "Вы отправили"
        иначе если Результат == РезультатыСогласования.Согласовано
            Участник = "Вы согласовали"
        иначе
            Участник = "Вы отказали"
        ;
    иначе
        Участник = ДанныеЭлемента.УчастникСогласованияСтрокой
    ;
    если Участник.Пусто()
        Участник = "Исполнитель не известен"
    ;
    возврат Участник
;

метод СтрокаТекущегоПользователя(ДанныеЭлемента: СогласующиеСтрока): Булево
    возврат КонтрольДоступа.ПараметрыСеанса().ФизлицоТекущегоПользователя == ДанныеЭлемента.УчастникСогласования
;

метод РезультатВидимость(ДанныеЭлемента: СогласующиеСтрока): Булево
    возврат не РезультатЗаголовок(ДанныеЭлемента).Пусто()
;

метод РезультатЗаголовок(ДанныеЭлемента: СогласующиеСтрока): Строка
    пер Результат: Строка
    если не СтрокаТекущегоПользователя(ДанныеЭлемента)
        если ДанныеЭлемента.НомерШагаСогласования == 0
            и ДанныеЭлемента.РольПодписанта == Согласования.РольПодписантаСотрудник()
            
            Результат = "Отправлено"
        иначе если ДанныеЭлемента.РольПодписанта == Согласования.РольПодписантаОрганизация()
            Результат = "Подписано"
        иначе
            Результат = ДанныеЭлемента.Результат.Представление()
        ;
    ;
    возврат Результат
;

метод ДатаЗаголовок(ДанныеЭлемента: СогласующиеСтрока): Строка
    возврат МенеджерДат.ФорматМомента(ДанныеЭлемента.Дата, "дд.ММ.гггг", ПривязкаКТекущейДате = Истина)
;

метод ВремяЗаголовок(ДанныеЭлемента: СогласующиеСтрока): Строка
    возврат МенеджерДат.ФорматМомента(ДанныеЭлемента.Дата, "ЧЧ:мм")
;

метод КомментарийВидимость(ДанныеЭлемента: СогласующиеСтрока): Булево
    возврат не ДанныеЭлемента.Комментарий.Пусто()
;

метод ЦветФона(ДанныеЭлемента: СогласующиеСтрока): Цвет | Авто
    если ДанныеЭлемента.Результат == РезультатыСогласования.НаСогласовании
        возврат БиблиотекаЦветов.Цвет(БиблиотекаЦветов.ВидыЦвета.СветлоСерый, 0.3)
    ;
    
    возврат БиблиотекаЦветов.Цвет(БиблиотекаЦветов.ВидыЦвета.ФонОкнаПриложения)
;