импорт ФизическиеЛица

@Обработчик
метод ПослеСоздания()
    ЗаполнитьЭлементыМеню()
;

метод ТабличнаяЧастьИсточникДанные(): Массив<Объект?>
    пер Данные: Массив<Объект?>
    Данные.ДобавитьВсе(ШагСогласования.Согласующие)
    возврат Данные
;

метод ДобавитьПриНажатии(Источник: Кнопка, Событие: СобытиеПриНажатии)
    Добавить.Открыть(Компоненты.ДобавитьСогласующего)
;

метод УдалитьСогласующегоПриНажатии(Источник: СтандартнаяКолонкаТаблицы<Объект?>, Событие: СобытиеПриНажатии, ДанныеЭлемента: Объект?)
    ШагСогласования.Согласующие.Удалить(ДанныеЭлемента как РолиПодписантов.Ссылка|ФизическиеЛица.Ссылка|Неопределено)
;

метод ДобавитьПриВыбореЭлемента(Компонент: ВсплывающееМеню, СобытиеСДанными: СобытиеСДанными<ЭлементМеню>)
    пер РольСогласующего = СобытиеСДанными.Данные.ДополнительныеПараметры как РолиПодписантов.Ссылка
    Добавить.Закрыть()
    
    если РольСогласующего == Согласования.РольПодписантаСотрудник()
        ВыборСогласующего = Истина
    иначе
        ШагСогласования.Согласующие.Добавить(РольСогласующего)
    ;
;

метод СогласующийПриИзменении(Источник: ПолеВвода<ФизическиеЛица.Ссылка?>, Событие: СобытиеПриИзменении<неизвестно>)
    если Согласующий == Неопределено
        возврат
    ;
    ВыборСогласующего = Ложь
    ШагСогласования.Согласующие.Добавить(Согласующий)
    Согласующий = Неопределено
;

метод ТипШагаСогласованияВидимость(): Булево
    возврат ШагСогласования.Согласующие.Размер() > 1
;

метод ЗаполнитьЭлементыМеню()
    для ДоступнаяРоль из ДоступныеРолиПодписантов()
        ДобавитьЭлементМеню(ДоступнаяРоль.Значение, ДоступнаяРоль.Ключ)
    ;
;

метод ДобавитьЭлементМеню(Заголовок: Строка, РольПодписанта: РолиПодписантов.Ссылка)
    знч ЭлементМеню = новый ЭлементМеню()
    ЭлементМеню.Заголовок = Заголовок
    ЭлементМеню.ДополнительныеПараметры = РольПодписанта
    
    Компоненты.Добавить.Элементы.Добавить(ЭлементМеню)
;

@НаСервере @ДоступноСКлиента
статический метод ДоступныеРолиПодписантов(): ЧитаемоеСоответствие<РолиПодписантов.Ссылка, Строка>
    знч ДоступныеРоли: Соответствие<РолиПодписантов.Ссылка, Строка>
    знч РолиПодписантовПриложения = Согласования.РолиПодписантовПриложения()
    знч Запрос = Запрос{
        Выбрать
            Ссылка,
            Наименование
        Из
            РолиПодписантов
        Где
            Используется
            и Ссылка В (%РолиПодписантовПриложения)}
    исп Результат = Запрос.Выполнить()
    для РезультатСтрока из Результат
        ДоступныеРоли.Вставить(РезультатСтрока.Ссылка, РезультатСтрока.Наименование)
    ;
    
    возврат ДоступныеРоли
;