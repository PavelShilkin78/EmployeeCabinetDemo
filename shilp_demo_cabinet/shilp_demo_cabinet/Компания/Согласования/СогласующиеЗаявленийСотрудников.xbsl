импорт КонтрольДоступа

//Область РазрешенияДоступа

@Обработчик
метод ВычислитьРазрешенияДоступа(): Массив<РазрешениеДоступа>
    возврат КонтрольДоступа.РегистрСведенийРедактированиеАдминистратору()
;

//КонецОбласти

//Область Обновление
@ВПодсистеме
метод ОбновитьВидСогласуемыхЗаявленийИРольСогласующего(ВидыСогласуемыхЗаявлений: ЧитаемоеСоответствие<ВидыСогласуемыхЗаявленийУстарело, ВидыСогласуемыхЗаявлений.Ссылка>, 
    РолиПодписантов: ЧитаемоеСоответствие<РолиПодписантовУстарело, РолиПодписантов.Ссылка>)
    
    знч Запрос = Запрос{
        Выбрать
            Сотрудник,
            ШагСогласования,
            ИдентификаторЗаписи,
            ВидСогласуемогоЗаявленияУстарело,
            ТипШага,
            Согласующий,
            ПервичныйСогласующий,
            СпособЗамещения,
            РольСогласующегоУстарело,
            ТребуемаяПодпись
        Из
            СогласующиеЗаявленийСотрудников
        Где
            ВидСогласуемогоЗаявления == Неопределено
            или РольСогласующего == Неопределено
    }
    
    исп Результат = Запрос.Выполнить()
    для РезультатСтрока из Результат
        знч НаборЗаписей = новый СогласующиеЗаявленийСотрудников.НаборЗаписей()
        НаборЗаписей.Фильтр.Сотрудник.Установить(РезультатСтрока.Сотрудник)
        НаборЗаписей.Фильтр.ВидСогласуемогоЗаявленияУстарело.Установить(РезультатСтрока.ВидСогласуемогоЗаявленияУстарело)
        НаборЗаписей.Фильтр.ШагСогласования.Установить(РезультатСтрока.ШагСогласования)
        НаборЗаписей.Фильтр.ИдентификаторЗаписи.Установить(РезультатСтрока.ИдентификаторЗаписи)
        
        НаборЗаписей.ДобавитьЗапись(
            Сотрудник = РезультатСтрока.Сотрудник,
            ШагСогласования = РезультатСтрока.ШагСогласования,
            ИдентификаторЗаписи = РезультатСтрока.ИдентификаторЗаписи,
            ТипШага = РезультатСтрока.ТипШага,
            Согласующий = РезультатСтрока.Согласующий,
            ПервичныйСогласующий = РезультатСтрока.ПервичныйСогласующий,
            СпособЗамещения = РезультатСтрока.СпособЗамещения,
            ТребуемаяПодпись = РезультатСтрока.ТребуемаяПодпись,
            РольСогласующегоУстарело = РезультатСтрока.РольСогласующегоУстарело,
            РольСогласующего = РолиПодписантов.ПолучитьИлиУмолчание(РезультатСтрока.РольСогласующегоУстарело),
            ВидСогласуемогоЗаявленияУстарело = РезультатСтрока.ВидСогласуемогоЗаявленияУстарело,
            ВидСогласуемогоЗаявления = ВидыСогласуемыхЗаявлений.ПолучитьИлиУмолчание(РезультатСтрока.ВидСогласуемогоЗаявленияУстарело)
        )
        
        НаборЗаписей.Записать()
    ;
;

//КонецОбласти

@ВПодсистеме
метод УдалитьЗаписиВидовСогласуемыхЗаявлений(ВидСогласуемогоЗаявления: ВидыСогласуемыхЗаявлений.Ссылка)
    знч НаборЗаписей = новый СогласующиеЗаявленийСотрудников.НаборЗаписей()
    НаборЗаписей.Фильтр.ВидСогласуемогоЗаявления.Установить(ВидСогласуемогоЗаявления)
    НаборЗаписей.Записать()
;