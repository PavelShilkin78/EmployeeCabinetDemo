импорт КонтрольДоступа

//Область РазрешенияДоступа

@Обработчик
метод ВычислитьРазрешенияДоступа(): Массив<РазрешениеДоступа>
    возврат КонтрольДоступа.РегистрСведенийРедактированиеВсем()
;

@Обработчик
метод ВычислитьРазрешенияДоступаДляОбъектов(Записи: ЧитаемыйМассив<ЗаместителиПриСогласовании.ДанныеРасчетаРазрешений>): ЧитаемоеСоответствие<ЗаместителиПриСогласовании.ДанныеРасчетаРазрешений, ЧитаемаяКоллекция<РазрешениеДоступа>>
    знч Разрешения: Соответствие<ЗаместителиПриСогласовании.ДанныеРасчетаРазрешений, ЧитаемаяКоллекция<РазрешениеДоступа>>
    для Элемент из Записи
        знч РазрешенияЭлемента: Множество<РазрешениеДоступа>
        РазрешенияЭлемента.ДобавитьВсе(КонтрольДоступа.РедактированиеДанныхРегистровПерсонально([Элемент.Согласующий]))
        Разрешения.Вставить(Элемент, РазрешенияЭлемента)
    ;
    возврат Разрешения
;

//КонецОбласти

//Область Обновление
@ВПодсистеме
метод ОбновитьВидСогласуемыхЗаявлений(ВидыСогласуемыхЗаявлений: ЧитаемоеСоответствие<ВидыСогласуемыхЗаявленийУстарело, ВидыСогласуемыхЗаявлений.Ссылка>)
    знч Запрос = Запрос{
        Выбрать
            Согласующий,
            ВидСогласуемогоЗаявленияУстарело,
            Заместитель,
            СпособЗамещения
        Из
            ЗаместителиПриСогласовании
        Где
            ВидСогласуемогоЗаявления == Неопределено
    }
    
    исп Результат = Запрос.Выполнить()
    для РезультатСтрока из Результат
        знч НаборЗаписей = новый ЗаместителиПриСогласовании.НаборЗаписей()
        НаборЗаписей.Фильтр.Согласующий.Установить(РезультатСтрока.Согласующий)
        НаборЗаписей.Фильтр.ВидСогласуемогоЗаявленияУстарело.Установить(РезультатСтрока.ВидСогласуемогоЗаявленияУстарело)
        НаборЗаписей.ДобавитьЗапись(
            Согласующий = РезультатСтрока.Согласующий,
            ВидСогласуемогоЗаявленияУстарело = РезультатСтрока.ВидСогласуемогоЗаявленияУстарело,
            Заместитель = РезультатСтрока.Заместитель,
            СпособЗамещения = РезультатСтрока.СпособЗамещения,
            ВидСогласуемогоЗаявления = ВидыСогласуемыхЗаявлений.ПолучитьИлиУмолчание(РезультатСтрока.ВидСогласуемогоЗаявленияУстарело)
        )
        
        НаборЗаписей.Записать()
    ;
;

//КонецОбласти

@ВПодсистеме
метод УдалитьЗаписиВидовСогласуемыхЗаявлений(ВидСогласуемогоЗаявления: ВидыСогласуемыхЗаявлений.Ссылка)
    знч НаборЗаписей = новый ЗаместителиПриСогласовании.НаборЗаписей()
    НаборЗаписей.Фильтр.ВидСогласуемогоЗаявления.Установить(ВидСогласуемогоЗаявления)
    НаборЗаписей.Записать()
;

@ВПодсистеме
метод СкопироватьНастройкиВида(ЦелевойВид: ВидыСогласуемыхЗаявлений.Ссылка, ИсходныйВид: ВидыСогласуемыхЗаявлений.Ссылка)
    знч НаборЗаписейЗаместителиПриСогласовании = новый ЗаместителиПриСогласовании.НаборЗаписей()
    НаборЗаписейЗаместителиПриСогласовании.Фильтр.ВидСогласуемогоЗаявления.Установить(ИсходныйВид)
    НаборЗаписейЗаместителиПриСогласовании.Прочитать()
    
    знч НовыйНаборЗаписейЗаместителиПриСогласовании = новый ЗаместителиПриСогласовании.НаборЗаписей()
    НовыйНаборЗаписейЗаместителиПриСогласовании.Фильтр.ВидСогласуемогоЗаявления.Установить(ЦелевойВид)
    
    для ЗаписьНабора из НаборЗаписейЗаместителиПриСогласовании
        НовыйНаборЗаписейЗаместителиПриСогласовании.ДобавитьЗапись(
            ВидСогласуемогоЗаявления = ЦелевойВид,
            Согласующий              = ЗаписьНабора.Согласующий,
            Заместитель              = ЗаписьНабора.Заместитель,
            СпособЗамещения          = ЗаписьНабора.СпособЗамещения)
    ;
    НовыйНаборЗаписейЗаместителиПриСогласовании.Записать()
;