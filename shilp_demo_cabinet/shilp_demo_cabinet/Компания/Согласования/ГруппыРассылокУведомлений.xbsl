импорт ИнтерфейсныеКомпонентыСтандартные
импорт ФизическиеЛица
импорт Компания
импорт Уведомления

@ВПроекте
перечисление РежимОтображения
    ГлобальнаяНастройка умолчание,
    НастройкаРуководителя,
    НастройкаСотрудника
;

@ВПодсистеме
@НаКлиенте @НаСервере
перечисление ГруппыПодчиненных
    Непосредственные умолчание,
    ДвухУровней,
    Все
;

@Обработчик
метод ПослеСоздания()
    если РежимОтображения == РежимОтображения.НастройкаРуководителя и Руководитель == Неопределено
        возврат
    ;
    Инициализировать(Руководитель)
;

@ВПроекте
метод Инициализировать(Руководитель: ФизическиеЛица.Ссылка? = Неопределено)
    пер УровеньУведомления: Число?
    если Руководитель != Неопределено
        УровеньУведомления = УровеньУведомления(Руководитель)
        этот.Руководитель = Руководитель
    иначе
        УровеньУведомления = УровеньУведомленияСистемы()
    ;
    если УровеньУведомления != Неопределено
        ТекущаяГруппаРассылок = ГруппаРассылок(УровеньУведомления)
        АктивнаяГруппаРассылок = ТекущаяГруппаРассылок
    ;
;

метод ПрименитьНастройки(Источник: Кнопка, Событие: СобытиеПриНажатии)
    знч УровеньУведомления = УровеньУведомления(АктивнаяГруппаРассылок)
    пер СообщениеУспех = ""
    если РежимОтображения == РежимОтображения.ГлобальнаяНастройка
        УстановитьУровеньУведомлений(УровеньУведомления)
        СообщениеУспех = "Настройки успешно изменены для всех руководителей компании"
    иначе
        УстановитьУровеньУведомлений(УровеньУведомления, Руководитель)
        СообщениеУспех = "Ваши настройки рассылки уведомлений вступили в силу"
    ;
    СтандартныеФормы.ПоказатьУведомление("Настройки уведомлений", СообщениеУспех, ОценкаИнформации.Положительная)
    ТекущаяГруппаРассылок = АктивнаяГруппаРассылок
;

метод ГруппаРассылок(Уровень: Число): ГруппыПодчиненных
    выбор Уровень
    когда 0
        возврат ГруппыПодчиненных.Непосредственные
    когда 1
        возврат ГруппыПодчиненных.ДвухУровней
    иначе
        возврат ГруппыПодчиненных.Все
    ;
;

метод УровеньУведомления(ГруппыПодчиненных: ГруппыПодчиненных): Число
    выбор ГруппыПодчиненных
    когда Непосредственные
        возврат 0
    когда ДвухУровней
        возврат 1
    иначе
        возврат 10
    ;
;

@НаСервере @ДоступноСКлиента
статический метод УстановитьУровеньУведомлений(НовыйУровень: Число)
    ЗаписатьУровеньУведомленияСистемы(НовыйУровень)
    знч ДанныеПодразделений = ПодразделенияИСотрудники.ДанныеПодразделений()
    ИзменитьУровеньУведомлений(НовыйУровень, ДанныеПодразделений)
;

@НаСервере @ДоступноСКлиента
статический метод УстановитьУровеньУведомлений(НовыйУровень: Число, Руководитель: ФизическиеЛица.Ссылка)
    знч ДанныеПодразделений = ПодразделенияИСотрудники.ДанныеПодразделений(Руководитель)
    ИзменитьУровеньУведомлений(НовыйУровень, ДанныеПодразделений)
;

@НаСервере
статический метод ИзменитьУровеньУведомлений(НовыйУровень: Число, ДанныеПодразделений: Множество<ПодразделенияИСотрудники.Ссылка>)
    исп КонтекстДоступа.Привилегированный()
    для ДанныеПодразделения из ДанныеПодразделений
        пер Подразделение = ДанныеПодразделения.ЗагрузитьОбъект()
        Подразделение.УровеньУведомления = НовыйУровень
        Подразделение.Записать()
    ;
;

@НаСервере @ДоступноСКлиента
статический метод ЗаписатьУровеньУведомленияСистемы(УровеньУведомления: Число)
    знч Запрос = Запрос{
        Выбрать
            Ссылка
        Из 
            НастройкиУведомлений}

    исп РезультатЗапроса = Запрос.Выполнить()
    для СтрокаРезультата из РезультатЗапроса
        пер Настройки = СтрокаРезультата.Ссылка.ЗагрузитьОбъект()
        Настройки.УровеньУведомления = УровеньУведомления
        Настройки.Записать()        
    ;
;

@НаСервере @ДоступноСКлиента
статический метод УровеньУведомленияСистемы(): Число?
    знч Запрос = Запрос{
        Выбрать Первые 1
            Настройки.УровеньУведомления как Уровень
        Из
            НастройкиУведомлений Как Настройки
    }
    знч Результат = новый ЧитаемыйМассив(Запрос.Выполнить())
    возврат Результат.ПервыйИлиУмолчание()?.Уровень
;

@НаСервере @ДоступноСКлиента
статический метод УровеньУведомления(Руководитель: ФизическиеЛица.Ссылка): Число?
    знч Запрос = Запрос{
        Выбрать
            КомпанияПодразделенияИСотрудники.УровеньУведомления как Уровень
        Из
            ПодразделенияИСотрудники Как КомпанияПодразделенияИСотрудники
            Внутреннее соединение СтруктураПредприятия как Структура
            по Структура.Ссылка == КомпанияПодразделенияИСотрудники.Объект
        Где
            Структура.Руководитель == %Руководитель
    }
    знч Результат = новый ЧитаемыйМассив(Запрос.Выполнить())
    если не Результат.Пусто()
        возврат Результат.ПервыйИлиУмолчание().Уровень
    ;
    возврат Неопределено
;