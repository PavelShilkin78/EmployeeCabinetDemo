импорт ОбменДанными

@Обработчик
@ВыполнятьПриЗагрузкеДанных
метод ПередЗаписью(ПредыдущееЗначение: ПравилаСогласования.Данные, ПараметрыЗаписи: ПравилаСогласования.ПараметрыЗаписи)
    если ФизическоеЛицо != Неопределено и Подразделение != Неопределено
        выбросить новый ИсключениеВыполнения("Физическое лицо и поздразделение не могут быть заполнены одновременно")
    ;
    ЗаполнитьВторичныеДанные()
;

@Обработчик
@ВыполнятьПриЗагрузкеДанных
метод ПослеЗаписи(До: ПравилаСогласования.Данные, ПараметрыЗаписи: ПравилаСогласования.ПараметрыЗаписи)  
    исп КонтекстДоступа.Привилегированный()
    если Компания.ПолучитьФункциональныеОпции().РасширенныеРолиСогласующих    
        ОбменДанными.ЗарегистрироватьОтправляемыйДокумент(Ссылка, Истина)
        знч ХарактерИзменения = Действие == ДействияСПравиломСогласования.Удалить ? ХарактерыИзменений.Удаление : ХарактерыИзменений.Изменение
        пер ТипСогласуемогоДокумента: ТипыСогласуемыхДокументов.Ссылка?
        для Сервис из СогласованияПользователь.ПолучитьСервисы()
            ТипСогласуемогоДокумента = Сервис.ТипСогласуемогоДокумента(ВидСогласуемогоЗаявления)
            если ТипСогласуемогоДокумента != Неопределено
                прервать
            ;
        ;
        РегистрацияИзменений.ЗарегистрироватьИзменение(Ссылка, ТипСогласуемогоДокумента, ХарактерИзменения)
    ; 
;

@Обработчик
@ВыполнятьПриЗагрузкеДанных
метод ПередУдалением(ПараметрыУдаления: ПравилаСогласования.ПараметрыУдаления)
    ШагиПравилСогласования.УдалитьЗаписиПравилаСогласования(Ссылка)
;

метод ЗаполнитьВторичныеДанные()
                
    КоличествоШагов = 0
    ПредставлениеСогласующих = ""
    
    знч Запрос = Запрос{
        Выбрать
            ШагСогласования,
            РольСогласующего,
            Согласующий
        Из
            ШагиПравилСогласования
        Где
            ПравилоСогласования == %Ссылка
        Упорядочить по
            ШагСогласования}
            
    исп Результат = Запрос.Выполнить()
    знч Согласующие: Массив<Строка>
    для РезультатСтрока из Результат
        КоличествоШагов = РезультатСтрока.ШагСогласования
        Согласующие.Добавить(РезультатСтрока.Согласующий == Неопределено ?
            РезультатСтрока.РольСогласующего.Представление() : РезультатСтрока.Согласующий.Представление()) 
    ;
    если Согласующие.Размер() < 3
        ПредставлениеСогласующих = Строки.Соединить(Согласующие, "\n")
    иначе
        ПредставлениеСогласующих =
            "%{Согласующие[0]}
            и еще %{Согласующие.Размер() - 1} согласующих"
    ;
    
    если ФизическоеЛицо != Неопределено
        ПодразделениеФизическоеЛицо = ФизическоеЛицо.Представление()
    иначе если Подразделение != Неопределено
        ПодразделениеФизическоеЛицо = Подразделение.Представление()
    ;
    
    если ПодразделениеФизическоеЛицо.Пусто()
        Наименование = ПредставлениеСогласующих.Заменить("\n", ", ")
    иначе
        пер НаименованиеПравила = "Правило согласования %ПодразделениеФизическоеЛицо"
        Наименование = НаименованиеПравила.Подстрока(0, НаименованиеПравила.Длина() > 200 ? 200 : НаименованиеПравила.Длина())
    ;
;