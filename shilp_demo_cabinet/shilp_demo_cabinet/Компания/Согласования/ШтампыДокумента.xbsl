импорт ИнтерфейсныеКомпонентыСтандартные
импорт КлиентСервисаПодписания
импорт ФизическиеЛица

@ВПроекте
@Реализация
метод Инициализировать()
    ПрочитатьДанныеПодписей()
;

@ВПроекте
метод Обновить()
    ПрочитатьДанныеПодписей()
;

метод ШтампОрганизацииВидимость(): Булево
    возврат ПодписьОрганизации != Неопределено
;

метод ШтампРуководителяВидимость(): Булево
    возврат ПодписьРуководителя != Неопределено
;

метод ШтампИсполнителяВидимость(): Булево
    возврат ПодписьИсполнителя != Неопределено
;

метод ШтампСотрудникаВидимость(): Булево
    возврат ПодписьФизическогоЛица != Неопределено
;

метод ПрочитатьДанныеПодписей()
    если Документ == Неопределено
        возврат
    ;
    ИнформацияОПодписанииОрганизации = ""
    ИнформацияОПодписанииРуководителем = ""
    ИнформацияОПодписанииИсполнителем = ""
    ИнформацияОПодписанииСотрудником = ""
    пер ДанныеПодписей = ДанныеПодписей(Документ, ФизическоеЛицо)
    ПодписьОрганизации     = ДанныеПодписей.ПодписьОрганизации?.Подпись ?? Неопределено
    ПодписьРуководителя    = ДанныеПодписей.ПодписьРуководителя?.Подпись ?? Неопределено
    ПодписьИсполнителя     = ДанныеПодписей.ПодписьИсполнителя?.Подпись ?? Неопределено
    ПодписьФизическогоЛица = ДанныеПодписей.ПодписьФизическогоЛица?.Подпись ?? Неопределено
    КлиентСервисаПодписания.ДополнитьФормыДаннымиПодписейНаСогласовании(этот, ДанныеПодписей)
    ИнтерфейсныеКомпоненты.ИнициализироватьПодчиненныеКомпоненты(Компоненты)
;

метод СкачатьАрхивПриНажатии(Источник: Надпись, Событие: СобытиеПриНажатии)
    Согласования.ВыгрузитьАрхивПрисоединенныхФайлов(Документ)
;

метод СкачатьАрхивВидимость(): Булево
    возврат ШтампОрганизацииВидимость()
        или ШтампРуководителяВидимость()
        или ШтампИсполнителяВидимость()
        или ШтампСотрудникаВидимость()
;

@НаСервере @ДоступноСКлиента
статический метод ДанныеПодписей(Документ: неизвестно, ФизическоеЛицо: ФизическиеЛица.Ссылка?): Согласования.ДанныеПодписейДокумента
    пер ДанныеПодписей = новый Согласования.ДанныеПодписейДокумента()
    пер ПодписиДокумента = СогласованияДокументов.ДанныеПодписейДокумента(Документ)
    для ДанныеПодписи из ПодписиДокумента
        если ДанныеПодписи.РольПодписанта == Согласования.РольПодписантаОрганизация()
            и ДанныеПодписей.ПодписьОрганизации == Неопределено
            
            ДанныеПодписей.ПодписьОрганизации = ДанныеПодписи
        иначе если ДанныеПодписи.РольПодписанта == Согласования.РольПодписантаИсполнитель()
            или ДанныеПодписи.РольПодписанта == Согласования.РольПодписантаОрганизация()
            
            ДанныеПодписей.ПодписьИсполнителя = ДанныеПодписи
        ; 
        если ДанныеПодписи.РольПодписанта == Согласования.РольПодписантаНепосредственныйРуководитель()
            ДанныеПодписей.ПодписьРуководителя = ДанныеПодписи
        ; 
        если ДанныеПодписи.РольПодписанта == Согласования.РольПодписантаСотрудник()
            и ДанныеПодписи.УчастникСогласования == ФизическоеЛицо
            
            ДанныеПодписей.ПодписьФизическогоЛица = ДанныеПодписи
        ;
    ;
    возврат ДанныеПодписей 
;