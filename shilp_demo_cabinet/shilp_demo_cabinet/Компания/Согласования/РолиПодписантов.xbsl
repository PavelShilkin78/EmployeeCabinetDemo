импорт КонтрольДоступа

//Область РазрешенияДоступа

@Обработчик
метод ВычислитьРазрешенияДоступа(): Массив<РазрешениеДоступа>
    возврат КонтрольДоступа.РазрешенияДоступаТаблицыСервисныхДанныхПриложения()
;

//КонецОбласти

@ВПодсистеме
метод ЗаполнитьПредопределенныеРолиПодписантов()
    Согласования.ЗаполнитьПредопределенныеРолиПодписантов()
    для Сервис из СогласованияПользователь.ПолучитьСервисы()
        Сервис.ЗаполнитьПредопределенныеРолиПодписантов()
    ;
;

@ВПроекте
метод ДобавитьПредопределеннуюРольПодписанта(Наименование: Строка, ЗначениеJson: Строка, Используется: Булево, ИмяПредопределенныхДанных: Строка,
    РольПодписантаУстарело: РолиПодписантовУстарело? = Неопределено)
    
    если ИмяПредопределенныхДанных.Пусто()
        возврат
    ;
    
    исп Транзакция = Транзакции.Начать()
    знч Запрос = Запрос{
        Выбрать
            Ссылка
        из
            РолиПодписантов
        Где
            ИмяПредопределенныхДанных == %ИмяПредопределенныхДанных}
    исп Результат = Запрос.Выполнить()
    знч РольПодписантаСсылка = Результат.ПервыйИлиУмолчание()?.Ссылка
    пер РольПодписанта: РолиПодписантов.Объект?
    если РольПодписантаСсылка == Неопределено
        РольПодписанта = новый РолиПодписантов.Объект()
    иначе
        РольПодписанта = РольПодписантаСсылка.ЗагрузитьОбъект(Истина)
    ;
    
    РольПодписанта.Наименование = Наименование
    РольПодписанта.ЗначениеJson = ЗначениеJson
    РольПодписанта.Используется = Используется
    РольПодписанта.ИмяПредопределенныхДанных = ИмяПредопределенныхДанных
    РольПодписанта.РольПодписантаУстарело = РольПодписантаУстарело
    РольПодписанта.Записать()
    Транзакция.Фиксировать()
;

@ВПроекте
метод НайтиПредопределенное(ИмяПредопределенныхДанных: Строка): РолиПодписантов.Ссылка?
    если ИмяПредопределенныхДанных.Пусто()
        возврат Неопределено
    ;
    
    знч Запрос = Запрос{
        Выбрать
            Ссылка
        из
            РолиПодписантов
        Где
            ИмяПредопределенныхДанных == %ИмяПредопределенныхДанных}
    исп Результат = Запрос.Выполнить()
    возврат Результат.ПервыйИлиУмолчание()?.Ссылка
;

@ВПроекте
метод ИзменитьИспользуемостьРолиПодписанта(РольПодписантаСсылка: РолиПодписантов.Ссылка, Используется: Булево)
    исп Транзакция = Транзакции.Начать()
    знч РольПодписанта = РольПодписантаСсылка.ЗагрузитьОбъект(Истина)
    РольПодписанта.Используется = Используется
    РольПодписанта.Записать()
    Транзакция.Фиксировать()
;

@ВПодсистеме
метод СоответствиеРолейПодписантов(): ЧитаемоеСоответствие<РолиПодписантовУстарело, РолиПодписантов.Ссылка>
    знч РолиПодписантов: Соответствие<РолиПодписантовУстарело, РолиПодписантов.Ссылка>
    знч Запрос = Запрос{
        Выбрать
            Ссылка,
            РольПодписантаУстарело
        Из
            РолиПодписантов
        Где
            РольПодписантаУстарело != Неопределено}
            
    исп Результат = Запрос.Выполнить()
    для РезультатСтрока из Результат
        РолиПодписантов.Вставить(РезультатСтрока.РольПодписантаУстарело, РезультатСтрока.Ссылка)
    ;
    
    возврат РолиПодписантов
;