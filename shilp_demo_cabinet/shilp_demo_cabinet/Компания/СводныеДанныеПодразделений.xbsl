импорт ИнтерфейсныеКомпонентыСтандартные
импорт ОбщиеСтандартные
импорт ФизическиеЛица
импорт СтандартноеПриложение

@ВПроекте
@Реализация
метод Инициализировать()
    ИнтерфейсныеКомпоненты.ИнициализироватьТаблицу(Таблица = Компоненты.СписокПодразделений, РазмерСтраницы = 12)
    ИнтерфейсныеКомпоненты.ИнициализироватьПодчиненныеКомпоненты(Компоненты)
;

@ВПроекте
метод ОбновитьСводныеДанные()
    этот.Компоненты.СписокПодразделений.Обновить()
;

метод ПолучитьШиринуКолонкиКоличества(): Число
    возврат ОсновнойИнтерфейс.РаботаНаУзкомУстройстве() ? 88 : 120
;

метод ПодразделенияПриНажатииСтроки(Источник: Список<ДинамическийСписок>, Событие: СобытиеСДанными<СтрокаСписка<СтрокаДинамическогоСписка>>)
    пер ДанныеСтроки: ЧитаемоеСоответствие<Строка, Объект?> = Событие.Данные.ДанныеСтроки.Данные
    знч Подразделение = ДанныеСтроки["Объект"] как СтруктураПредприятия.Ссылка?

    если Подразделение != Неопределено
        КарточкаПодразделения.ОткрытьВМодальномОкне(
            Подразделение = Подразделение
        )
    ;
;

метод РуководительЗаголовок(ДанныеЭлемента: СтрокаДинамическогоСписка): Строка
    возврат МенеджерСущности.ПредставлениеСсылки(
            ДанныеЭлемента.Данные["Руководитель"] как ФизическиеЛица.Ссылка?, "Позиция не известна")
;

метод ПояснениеКоличестваСотрудников(ДанныеЭлемента: СтрокаДинамическогоСписка): Строка
    пер Пояснение = ""
    пер КолСотрудников = ДанныеЭлемента.Данные["КоличествоСотрудниковНаОсновнойРаботе"] как Число
    если КолСотрудников != 0
        Пояснение += МенеджерСтрок.ТекстПоКоличеству("Сотрудник", КолСотрудников)
    ;
    пер КолПодразделений = ДанныеЭлемента.Данные["КоличествоВсехПодчиненных"] как Число
    если КолПодразделений != 0
        Пояснение += "\нв %КолПодразделений " + (КолПодразделений % 10 == 1 ? "подразделении" : "подразделениях")
    ;
    если Пояснение.Пусто()
        Пояснение = "Нет сотрудников"
    ;
    возврат Пояснение
;

метод ЦветПоУровнюИерархии(УровеньИерархии: Число): Строка
    пер Альфа: Число
    выбор УровеньИерархии
    когда 0
        Альфа = 1
    когда 1
        Альфа = 0.7
    когда 2
        Альфа = 0.4
    иначе 
        Альфа = 0.2
    ;
    возврат БиблиотекаЦветов.ЦветСтрокой(БиблиотекаЦветов.ВидыЦвета.ТемноСерый, Альфа)
;