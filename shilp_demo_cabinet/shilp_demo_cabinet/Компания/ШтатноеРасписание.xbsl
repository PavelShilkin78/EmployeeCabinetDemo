импорт ОбщиеСтандартные
импорт ВебApi
импорт КонтрольДоступа

//Область РазрешенияДоступа

@Обработчик
метод ВычислитьРазрешенияДоступа(): Массив<РазрешениеДоступа>
    возврат КонтрольДоступа.РазрешенияДоступаТаблицыКонтентДанныхАбонентаЧтениеВсем()
;

//КонецОбласти

//Область JSON

@ВПроекте
структура ДанныеJson
    обз знч ID: Строка
    обз знч name: Строка?
    обз знч employerID: Строка
    обз знч divisionID: Строка
    обз знч positionID: Строка
;

@ВПроекте
метод ТипДанныхJson(ВерсияФорматаОбмена: Версия?): Тип
    если ВерсияФорматаОбмена == Неопределено
        возврат Тип<ДанныеJson>
    иначе если ВерсияФорматаОбмена >= Версия{1.0}
        возврат Тип<ДанныеJson>
    ;
;

@ВПроекте
метод ТипКоллекцииДанныхJson(ВерсияФорматаОбмена: Версия?): Тип
    если ВерсияФорматаОбмена == Неопределено
        возврат Тип<Массив<ДанныеJson>>
    иначе если ВерсияФорматаОбмена >= Версия{1.0}
        возврат Тип<Массив<ДанныеJson>>
    ;
;

@ВПроекте
метод ТипОбъектаJson(): Строка
    возврат "staffListPositions"
;

@ВПроекте
метод ЗагрузитьОбъектИзJson(ДанныеJson: ДанныеJson, Сервисы: ВебApiСервер.СервисыЗагрузкиJson)

    исп Транзакции.Начать() 

    знч Позиция = Сервисы.ПрочитатьОбъект(ДанныеJson.ID, &ШтатноеРасписание.Объект.новый(Ууид), Тип<ШтатноеРасписание.Ссылка>) как ШтатноеРасписание.Объект
    Позиция.ЗаполнитьПоДаннымJson(ДанныеJson, Сервисы)
    Сервисы.ЗаписатьОбъект(новый ВебApiСервер.ПараметрыЗаписиВходящихДанных(Позиция, ДанныеJson.ID), Сервисы.ИнструментыМенеджераОбъекта)
;

@ВПодсистеме
метод ВнешняяСсылка(Идентификатор: Строка): ВебApiСервер.ВнешняяСсылка
    возврат новый ВебApiСервер.ВнешняяСсылка(Идентификатор, (Ууид) -> ШтатноеРасписание.ПолучитьСсылку(Ууид))
;

метод ДанныеПозицийШтатногоРасписания(Позиции: ЧитаемаяКоллекция<ШтатноеРасписание.Ссылка>): 
    ЧитаемоеСоответствие<ШтатноеРасписание.Ссылка, ЧитаемоеСоответствие<Строка, Объект?>>

    знч ДанныеПозицийШтатногоРасписания: Соответствие<ШтатноеРасписание.Ссылка, ЧитаемоеСоответствие<Строка, Объект?>>

    знч Запрос = Запрос{
        Выбрать 
            Ссылка,
            Наименование,
            МестоВСтруктуреПредприятия,
            Организация,
            Должность
        Из 
            ШтатноеРасписание
        Где
            Ссылка В (%Позиции)}
    исп Результат = Запрос.Выполнить() 
    для РезультатСтрока из Результат
        ДанныеПозицийШтатногоРасписания.Вставить(
            РезультатСтрока.Ссылка, 
            МенеджерЗапросов.СтрокаРезультатаЗапросаВСоответствие(РезультатСтрока, Результат))
    ;
    возврат ДанныеПозицийШтатногоРасписания
;

//КонецОбласти

@ВПроекте
@НаСервере @ДоступноСКлиента
метод ДолжностиПодразделения(Подразделение: СтруктураПредприятия.Ссылка?): Массив<Должности.Ссылка>
    если Подразделение == Неопределено
        возврат []
    ;
    знч Запрос = Запрос{
        ВЫБРАТЬ РАЗЛИЧНЫЕ
            Должность
        ИЗ 
            ШтатноеРасписание
        ГДЕ
            МестоВСтруктуреПредприятия == %Подразделение
            И Должность != Неопределено
    }
    исп РезультатЗапроса = Запрос.Выполнить()
    возврат РезультатЗапроса.Преобразовать(СтрокаРезультата -> СтрокаРезультата.Должность!)
;