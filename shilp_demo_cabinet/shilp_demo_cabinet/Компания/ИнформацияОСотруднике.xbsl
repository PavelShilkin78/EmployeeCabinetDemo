импорт ИнтерфейсныеКомпонентыСтандартные
импорт Компания
импорт ОбщиеСтандартные 
импорт ФизическиеЛица
импорт СтандартноеПриложение

@ВПроекте
@Реализация
метод Инициализировать()
    если ФизическоеЛицо == Неопределено
        возврат
    ;
    Данные = ПолучитьДанныеКарточки(ФизическоеЛицо!)
    если Данные.Фотография == Неопределено
        Данные.Фотография = СтандартноеПриложение.ПараметрыСеанса().АватарПоУмолчанию
    ;
;

метод ВыравниваниеПоГоризонтали(): ВыравниваниеПоГоризонтали | Авто
    возврат КлиентскоеУстройство.ВидИнтерфейса == ВидИнтерфейса.Телефон ? ВыравниваниеПоГоризонтали.Центр : ВыравниваниеПоГоризонтали.Начало
;

метод ФотографияРазмер(): Число
    возврат (КлиентскоеУстройство.ВидИнтерфейса == ВидИнтерфейса.Телефон ? 15 : 20) * Метрики.РАЗМЕР_ПОЛУИНТЕРВАЛА
;

метод ИнформацияОСотрудникеГруппировка(): ОриентацияСодержимого
    возврат КлиентскоеУстройство.ВидИнтерфейса == ВидИнтерфейса.Телефон ? ОриентацияСодержимого.Вертикальная : ОриентацияСодержимого.Горизонтальная
;

@НаСервере @ДоступноСКлиента
статический метод ПолучитьДанныеКарточки(ФизическоеЛицо: ФизическиеЛица.Ссылка?): ДанныеСотрудника
    пер ДанныеКарточки: ДанныеСотрудника
    если ФизическоеЛицо != Неопределено
        знч ЗапрашиваемыеДанные: ФизЛица.ЗапрашиваемыеДанныеФизическихЛиц
        ЗапрашиваемыеДанные.ЛичныеДанные = Истина
        пер ДанныеФизическогоЛица = ФизЛица.ДанныеФизическогоЛица(ФизическоеЛицо, ЗапрашиваемыеДанные)
        ДанныеКарточки.ФИО = ФизЛица.ДанныеФизическогоЛицаВФИО(ДанныеФизическогоЛица).ПредставлениеИмени(ФизЛица.ВариантПредставленияИмени.ФамилияИмяОтчество)
        если ДанныеФизическогоЛица.ЛичныеДанные.Фотография == Неопределено
            ДанныеКарточки.Фотография = ФизическиеЛица.ФотографияИзФайла(
                ДанныеФизическогоЛица.ЛичныеДанные.Миниатюра)
        иначе
            ДанныеКарточки.Фотография = ФизическиеЛица.ФотографияИзФайла(
                ДанныеФизическогоЛица.ЛичныеДанные.Фотография)
        ;
        пер РабочиеМеста: ЧитаемыйМассив<Сотрудники.Ссылка> = Сотрудники.СотрудникиФизическогоЛица(ФизическоеЛицо!)
        если не РабочиеМеста.Пусто()
            пер ДанныеРабочегоМеста: ЧитаемоеСоответствие<Строка, неизвестно> = МенеджерСущности.ЗначенияРеквизитовОбъекта(РабочиеМеста[0], ["Должность", "МестоВСтруктуреПредприятия", "Организация"])
            ДанныеКарточки.Должность = ДанныеРабочегоМеста["Должность"].Представление()
            ДанныеКарточки.МестоВСтруктуреПредприятия = ДанныеРабочегоМеста["МестоВСтруктуреПредприятия"].Представление()
            ДанныеКарточки.Организация = ДанныеРабочегоМеста["Организация"].Представление()
        ;
    ;
    возврат ДанныеКарточки
;