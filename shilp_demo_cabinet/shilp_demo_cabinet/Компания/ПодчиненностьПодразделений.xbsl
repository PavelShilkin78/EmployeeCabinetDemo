импорт КонтрольДоступа

//Область РазрешенияДоступа

@Обработчик
метод ВычислитьРазрешенияДоступа(): Массив<РазрешениеДоступа>
    возврат КонтрольДоступа.РегистрСведенийРедактированиеАдминистратору()
;

//КонецОбласти

@ВПроекте
метод Обновить(Подразделения: ЧитаемыйМассив<СтруктураПредприятия.Ссылка>? = Неопределено)
	знч ОбновляемыеПодразделения: Множество<СтруктураПредприятия.Ссылка>
    знч РодителиПодразделений: Соответствие<СтруктураПредприятия.Ссылка, СтруктураПредприятия.Ссылка>
    
    если Подразделения != Неопределено
        ОбновляемыеПодразделения.ДобавитьВсе(ОбновляемыеПодразделения(Подразделения))
    ;
    знч Запрос = Запрос{
		Выбрать
			Ссылка как Ссылка,
			Родитель как Родитель
		Из
			СтруктураПредприятия
    }
	исп Результат = Запрос.Выполнить()
    
	для РезультатСтрока из Результат
		если РезультатСтрока.Родитель != Неопределено
            РодителиПодразделений.Вставить(РезультатСтрока.Ссылка, РезультатСтрока.Родитель)
        ;
        если Подразделения == Неопределено
            ОбновляемыеПодразделения.Добавить(РезультатСтрока.Ссылка)
        ;
	;
    
    ОчиститьВышестоящиеПозиции()
    пер ВышестоящиеПозиции = ВышестоящиеПозицииПодразделений(ОбновляемыеПодразделения)
       
	для Подразделение из ОбновляемыеПодразделения
		пер Уровень = 0
		знч НаборЗаписей = новый ПодчиненностьПодразделений.НаборЗаписей()
		НаборЗаписей.Фильтр.Подразделение.Установить(Подразделение)
		НаборЗаписей.Добавить(
            ПодчиненностьОтПодразделения(Уровень, Подразделение, Подразделение)
        )
        пер Родитель = РодителиПодразделений.ПолучитьИлиУмолчание(Подразделение)
        пер ВышестоящаяПозиция = ВышестоящиеПозиции.ПолучитьИлиУмолчание(Подразделение)
		пока Родитель != Неопределено
			Уровень += 1
            если ВышестоящаяПозиция != Неопределено
                НаборЗаписей.Добавить(
                    ПодчиненностьОтПозиции(Уровень, Подразделение, ВышестоящаяПозиция)
                )
                Уровень += 1
            ;
            НаборЗаписей.Добавить(
                ПодчиненностьОтПодразделения(Уровень, Подразделение, Родитель)
            )
            ВышестоящаяПозиция = ВышестоящиеПозиции.ПолучитьИлиУмолчание(Родитель)
            Родитель = РодителиПодразделений.ПолучитьИлиУмолчание(Родитель)
		;
		НаборЗаписей.Записать(Истина)
	;
;
метод ПодчиненностьОтПодразделения(Уровень: Число, Подразделение: СтруктураПредприятия.Ссылка, ВышестоящееПодразделение: СтруктураПредприятия.Ссылка): ПодчиненностьПодразделений.Запись
    пер Запись = ЗаписьПодчиненностиПодразделения(Уровень, Подразделение)
    Запись.ВышестоящееПодразделение = ВышестоящееПодразделение
    возврат Запись
;

метод ПодчиненностьОтПозиции(Уровень: Число, Подразделение: СтруктураПредприятия.Ссылка, ВышестоящаяПозиция: ШтатноеРасписание.Ссылка): ПодчиненностьПодразделений.Запись
    пер Запись = ЗаписьПодчиненностиПодразделения(Уровень, Подразделение)
    Запись.ВышестоящаяПозиция = ВышестоящаяПозиция
    возврат Запись
;

метод ЗаписьПодчиненностиПодразделения(Уровень: Число, Подразделение: СтруктураПредприятия.Ссылка): ПодчиненностьПодразделений.Запись
    пер Запись = новый ПодчиненностьПодразделений.Запись()
    Запись.Уровень = Уровень
    Запись.Подразделение = Подразделение
    возврат Запись
;

метод ОчиститьВышестоящиеПозиции()
    знч НаборЗаписей = новый ПодчиненностьПодразделений.НаборЗаписей()
    НаборЗаписей.Фильтр.ВышестоящееПодразделение.Установить(Неопределено)
    НаборЗаписей.Очистить()
;

метод ВышестоящиеПозицииПодразделений(ОбновляемыеПодразделения: Множество<СтруктураПредприятия.Ссылка>): Соответствие<СтруктураПредприятия.Ссылка, ШтатноеРасписание.Ссылка>
    пер ВышестоящиеПозиции: Соответствие<СтруктураПредприятия.Ссылка, ШтатноеРасписание.Ссылка>
    знч Запрос = Запрос{
        Выбрать
            Подразделение,
            ПозицияШР
        Из 
            ПодчиненностьПшрСведения
        Где 
            Подразделение в (%ОбновляемыеПодразделения)
    }
    исп Результат = Запрос.Выполнить()
    для РезультатСтрока из Результат
        ВышестоящиеПозиции.Вставить(РезультатСтрока.Подразделение, РезультатСтрока.ПозицияШР)
    ;
    возврат ВышестоящиеПозиции
;


метод ОбновляемыеПодразделения(Родители: ЧитаемыйМассив<СтруктураПредприятия.Ссылка>): Множество<СтруктураПредприятия.Ссылка>
    знч ОбновляемыеПодразделения = новый Множество<СтруктураПредприятия.Ссылка>(Родители)
    пер Подразделения = новый Множество<СтруктураПредприятия.Ссылка>(Родители)
    
    пока не Подразделения.Пусто()
        знч Запрос = Запрос{
            Выбрать
                Ссылка
            Из
                СтруктураПредприятия
            Где
                Родитель В (%Подразделения)
        }
        исп Результат = Запрос.Выполнить()
        Подразделения.Очистить()
        для РезультатСтрока из Результат
            ОбновляемыеПодразделения.Добавить(РезультатСтрока.Ссылка)
            Подразделения.Добавить(РезультатСтрока.Ссылка)
        ;
    ;
    
    возврат ОбновляемыеПодразделения
;