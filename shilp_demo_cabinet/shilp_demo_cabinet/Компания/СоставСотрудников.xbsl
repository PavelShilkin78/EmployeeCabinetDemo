импорт ФизическиеЛица
импорт ИнтерфейсныеКомпонентыСтандартные

@НаКлиенте @НаСервере
структура СводныеДанныеСотрудников
    обз пер Возраст: Число
    обз пер Пол: ПолФизическогоЛица
    обз пер Количество: Число
;

метод СоставСотрудниковОриентация(): ОриентацияСодержимого
    возврат КлиентскоеУстройство.ШиринаОкнаПриложения < 464 ? ОриентацияСодержимого.Вертикальная : ОриентацияСодержимого.Горизонтальная
;

@Обработчик
метод ПослеСоздания()
    Обновить()
;

@ВПроекте
метод Обновить()
    пер СводныеДанныеПоСотрудникам = СводныеДанныеПоСотрудникам()
    СоставПоПолу = ДанныеДиаграммыПол(СводныеДанныеПоСотрудникам)
    СоставПоВозрасту = ДанныеДиаграммыВозраст(СводныеДанныеПоСотрудникам)
;

метод ДанныеДиаграммыПол(ДанныеПоСотрудникам: Множество<СводныеДанныеСотрудников>): Множество<КольцеваяДиаграмма.ЗначениеДиаграммы>
    знч ДанныеДиаграммыПол: Множество<КольцеваяДиаграмма.ЗначениеДиаграммы>
    знч КоличествоПоПолу = ДанныеПоПолу(ДанныеПоСотрудникам)
    для ПолКоличество из КоличествоПоПолу
        ДанныеДиаграммыПол.Добавить(новый КольцеваяДиаграмма.ЗначениеДиаграммы(ПолКоличество.Значение, ФизЛица.ПолСокращенно(ПолКоличество.Ключ)))
    ;
    возврат ДанныеДиаграммыПол
;

метод ДанныеДиаграммыВозраст(ДанныеПоСотрудникам: Множество<СводныеДанныеСотрудников>): Множество<КольцеваяДиаграмма.ЗначениеДиаграммы>
    знч ДанныеДиаграммыВозраст: Множество<КольцеваяДиаграмма.ЗначениеДиаграммы>
    знч КоличествоПоВозрасту = ДанныеПоВозрасту(ДанныеПоСотрудникам)
    для ВозрастКоличество из КоличествоПоВозрасту
        ДанныеДиаграммыВозраст.Добавить(новый КольцеваяДиаграмма.ЗначениеДиаграммы(ВозрастКоличество.Значение, ВозрастКоличество.Ключ.Представление()))
    ;
    возврат ДанныеДиаграммыВозраст
;

метод ДанныеПоПолу(ДанныеПоСотрудникам: Множество<СводныеДанныеСотрудников>): Соответствие<ПолФизическогоЛица, Число>
    пер ДанныеПоПолу = новый Соответствие<ПолФизическогоЛица, Число>()
    ДанныеПоПолу.Вставить(ПолФизическогоЛица.Мужской, 0)
    ДанныеПоПолу.Вставить(ПолФизическогоЛица.Женский, 0)
    для Данные из ДанныеПоСотрудникам
        ДанныеПоПолу[Данные.Пол] += Данные.Количество
    ;
    возврат ДанныеПоПолу
;

метод ДанныеПоВозрасту(ДанныеПоСотрудникам: Множество<СводныеДанныеСотрудников>): Соответствие<ФизЛица.ДиапазоныВозраста, Число>
    пер ДанныеПоВозрасту = новый Соответствие<ФизЛица.ДиапазоныВозраста, Число>()
    для Данные из ДанныеПоСотрудникам
        знч Ключ = ФизЛица.ВозрастнойДиапазон(Данные.Возраст)
        если не ДанныеПоВозрасту.СодержитКлюч(Ключ)
            ДанныеПоВозрасту.Вставить(Ключ, Данные.Количество)
        иначе
            ДанныеПоВозрасту[Ключ] += Данные.Количество
        ;
    ;
    возврат ДанныеПоВозрасту
;

@НаСервере @ДоступноСКлиента
статический метод СводныеДанныеПоСотрудникам(): Множество<СводныеДанныеСотрудников>
        знч Запрос = Запрос{
        Выбрать
            ФизЛица.Пол,
            ФизЛицаДанные.ДатаРождения.ЗаменитьNull(Дата{}).Год.ЗаменитьNull(0) как ГодРождения,
            Количество(ФизЛица.Ссылка) как Количество
        Из
            ПодразделенияИСотрудники как Сотрудники
            Внутреннее Соединение ФизическиеЛица как ФизЛица
                По Сотрудники.ФизическоеЛицо == ФизЛица.Ссылка
            Внутреннее Соединение ЛичныеДанныеФизическихЛиц как ФизЛицаДанные
                По Сотрудники.ФизическоеЛицо == ФизЛицаДанные.Владелец
        Где
            Сотрудники.ТипЭлемента == ТипыЭлементовПодразделенийИСотрудников.Сотрудник
            И Сотрудники.ОсновноеМестоРаботы
        Сгруппировать по
            ФизЛица.Пол,  ФизЛицаДанные.ДатаРождения.ЗаменитьNull(Дата{}).Год
    }
    исп ПривилегированныйРежим: КонтекстДоступа = КонтекстДоступа.Привилегированный()
    пер Результат = новый ЧитаемыйМассив(Запрос.Выполнить())
    ПривилегированныйРежим.Закрыть()
    пер СводныеДанные = новый Множество<СводныеДанныеСотрудников>()
    если не Результат.Пусто()
        для РезультатСтрока из Результат
            СводныеДанные.Добавить(
                новый СводныеДанныеСотрудников(
                    Возраст = ДатаВремя.Сейчас().Год - РезультатСтрока.ГодРождения,
                    Пол = РезультатСтрока.Пол,
                    Количество = РезультатСтрока.Количество
                )
            )
        ;
    ;
    возврат СводныеДанные
;