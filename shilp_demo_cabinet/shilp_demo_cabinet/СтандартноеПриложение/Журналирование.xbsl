@ВПроекте
@НаСервере
метод ЗарегистрироватьОшибку(
            Ошибка: Исключение,
            Важность: ВажностьСобытияЖурналаСобытий = ВажностьСобытияЖурналаСобытий.Обычная,
            Характер: ХарактерОшибки = ХарактерОшибки.ДляПоддержки)

    новый ОшибкаПриложения(
        ТипОшибки = Ошибка.ПолучитьТип().ВСтроку(),
        ОписаниеОшибки = Ошибка.Описание,
        Информация = Ошибка.Информация(),
        Важность = Важность,
        ХарактерОшибки = Характер
    ).Записать()
;

@ВПроекте
@НаСервере
метод ЗарегистрироватьОшибку(
        Тип: Строка,
        Описание: Строка? = Неопределено,
        Причина: Исключение? = Неопределено,
        Важность: ВажностьСобытияЖурналаСобытий = ВажностьСобытияЖурналаСобытий.Обычная,
        Характер: ХарактерОшибки = ХарактерОшибки.ДляПоддержки)

    новый ОшибкаПриложения(
        ТипОшибки = Тип,
        ОписаниеОшибки = Описание ?? Тип,
        Информация = Причина?.Информация(),
        Важность = Важность,
        ХарактерОшибки = Характер
    ).Записать()
;

@ВПроекте
@НаСервере
метод ЗарегистрироватьСобытие(Тип: Строка, Описание: Строка? = Неопределено,
        Важность: ВажностьСобытияЖурналаСобытий = ВажностьСобытияЖурналаСобытий.Обычная)

    новый СобытиеПриложения(
        ТипСобытия = Тип,
        ОписаниеСобытия = Описание ?? Тип,
        Важность = Важность
    ).Записать()
;

@ВПроекте
@НаСервере
метод ЗарегистрироватьHttpЗапрос(Тип: Строка, Описание: Строка? = Неопределено,
        Запрос: ЗапросHttp, ТелоЗапроса: Строка? = Неопределено,
        Ответ: ОтветHttp? = Неопределено, ТелоОтвета: Строка? = Неопределено,
        ВключатьЗаголовкиЗапроса: Булево = Ложь, ВключатьЗаголовкиОтвета: Булево = Ложь,
        Важность: ВажностьСобытияЖурналаСобытий = ВажностьСобытияЖурналаСобытий.Обычная)

    новый СобытиеHttpЗапрос(
        ТипЗапроса = Тип,
        ОписаниеЗапроса = Описание ?? Тип,
        ВходящийЗапрос = Ложь,
        Url = Запрос.АбсолютныйUrl,
        МетодHttp = Запрос.МетодHttp,
        ЗаголовкиЗапроса = ВключатьЗаголовкиЗапроса ? ЗаголовкиHttpВСтроку(Запрос.Заголовки) : Неопределено,
        ТелоЗапроса = ТелоЗапроса,
        КодОтвета = Ответ != Неопределено ? Ответ!.КодСостояния : Неопределено,
        СостояниеОтвета = Ответ != Неопределено ? Ответ!.Причина : Неопределено,
        ЗаголовкиОтвета = ВключатьЗаголовкиОтвета и Ответ != Неопределено ? ЗаголовкиHttpВСтроку(Ответ!.Заголовки) : Неопределено,
        ТелоОтвета = ТелоОтвета,
        Важность = Важность
    ).Записать()
;

@ВПроекте
@НаСервере
метод ЗарегистрироватьHttpЗапрос(Тип: Строка, Описание: Строка? = Неопределено,
        Запрос: HttpСервисЗапрос, ТелоЗапроса: Строка? = Неопределено, ТелоОтвета: Строка? = Неопределено,
        ВключатьЗаголовкиЗапроса: Булево = Ложь, ВключатьЗаголовкиОтвета: Булево = Ложь,
        Важность: ВажностьСобытияЖурналаСобытий = ВажностьСобытияЖурналаСобытий.Обычная)

    новый СобытиеHttpЗапрос(
        ТипЗапроса = Тип,
        ОписаниеЗапроса = Описание ?? Тип,
        ВходящийЗапрос = Истина,
        Url = Запрос.ПутьКРесурсу,
        МетодHttp = Запрос.Метод,
        ЗаголовкиЗапроса = ВключатьЗаголовкиЗапроса ? ЗаголовкиHttpВСтроку(Запрос.Заголовки) : Неопределено,
        ТелоЗапроса = ТелоЗапроса,
        КодОтвета = Запрос.Ответ.КодСтатуса,
        СостояниеОтвета = Запрос.Ответ.Причина,
        ЗаголовкиОтвета = ВключатьЗаголовкиОтвета? ЗаголовкиHttpВСтроку(Запрос.Ответ.Заголовки) : Неопределено,
        ТелоОтвета = ТелоОтвета,
        Важность = Важность
    ).Записать()
;

@НаСервере
метод ЗаголовкиHttpВСтроку(Заголовки: ЧитаемыеЗаголовкиHttp): Строка
    возврат Заголовки.Имена().Преобразовать(Имя->"%Имя:%{Заголовки.ПолучитьВсе(Имя).Соединить(",")}").Соединить(";")
;