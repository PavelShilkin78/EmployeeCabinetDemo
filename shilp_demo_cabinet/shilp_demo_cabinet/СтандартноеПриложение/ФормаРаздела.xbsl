импорт СтандартноеПриложение

@Обработчик
метод ПослеСоздания()
    пер Виджеты = ОсновнойИнтерфейс.Виджеты()
    Виджеты = КомпоновщикОсновногоИнтерфейса.ПолучитьСервис().СкомпоноватьВиджеты(Виджеты)
    для Виджет из Виджеты
        знч Компонент = новый ВиджетРаздела(ОписаниеВиджета = Виджет)
        Компоненты.ГлавнаяГруппа.Содержимое.Добавить(Компонент)
    ;
    СтандартноеПриложение.ФормыРазделовСтандартногоПриложения().Главное = этот
    ДополнительныеКоманды = ФормаРазделаДополнительныеКоманды()
;

метод ГлавнаяГруппаМинимальнаяШиринаКолонок(): Число
    возврат ОсновнойИнтерфейс.МинимальнаяШиринаПанелейПодразделов()
;

метод ГлавнаяГруппаШиринаКолонок(): Число
    возврат ОсновнойИнтерфейс.МаксимальнаяШиринаПанелейПодразделов()
;

метод ФормаРазделаМаксимальнаяШирина(): Число
    возврат Содержимое.МаксимальнаяШирина
;

метод ФормаРазделаМинимальнаяШирина(): Число
    возврат Содержимое.МинимальнаяШирина
;

метод ФормаРазделаДополнительныеКоманды(): ФрагментКомандногоИнтерфейса<Команда>?
    пер ФормаРазделаДополнительныеКоманды: ФрагментКомандногоИнтерфейса<Команда>?
    знч Сервисы = ОсновнойИнтерфейсПоставщик.ПолучитьСервисы()
    если не Сервисы.Пусто()
        ФормаРазделаДополнительныеКоманды = новый ФрагментКомандногоИнтерфейса<Команда>()
        для Сервис из Сервисы
            ФормаРазделаДополнительныеКоманды.Элементы.ДобавитьВсе(Сервис.ДополнительныеКомандыПанелиГлавное())
        ;
    ;
    возврат ФормаРазделаДополнительныеКоманды
;