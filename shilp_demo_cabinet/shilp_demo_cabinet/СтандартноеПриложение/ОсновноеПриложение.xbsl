импорт ОбщиеСтандартные
импорт ИнтерфейсныеКомпонентыСтандартные
импорт КонтрольДоступаСтандартные
импорт СтандартноеПриложение

@Обработчик
метод ПослеСоздания()
    пер МобильныйИнтерфейс: Булево = ОсновнойИнтерфейс.ЭтоМобильныйИнтерфейс()
    пер СмещениеКлиента: Число = МенеджерДат.ТекущееСмещениеВМинутах()
    
    если КлиентскоеУстройство.ВидИнтерфейса == ВидИнтерфейса.Компьютер
        (КлиентскоеПриложение как ОсновноеПриложение).ВидимостьИнформацииОПриложении = МенеджерПравДоступа.ЭтоАдминистраторПриложения()
    ;

    // Прежде всего получаем параметры сеанса
    ХранилищеПараметровСеанса = ПараметрыСеанса.ИнициализироватьПараметрыСеанса(МобильныйИнтерфейс)
    
    ИнициироватьПриложение(СмещениеКлиента, МобильныйИнтерфейс)
    ИнициализироватьКомандныйИнтерфейс()
    знч ОбработчикиПослеСоздания = ПослеСозданияОбработчик.ПолучитьСервисы()
    для Обработчик из ОбработчикиПослеСоздания
        Обработчик.ПослеСоздания()
    ;
    
    если КлиентскоеУстройство.ВидИнтерфейса == ВидИнтерфейса.Компьютер
        ПоложениеЗаголовка = ПоложениеЗаголовкаПриложения.Горизонтально
    иначе
        ПоложениеЗаголовка = ПоложениеЗаголовкаПриложения.НеОтображать
    ;
    Видимость = Истина
    
    МенеджерГлобальныхКлиентскихСобытий.ПодключитьОбработчик(метод (Источник, Параметры) ->
        выбор Источник
        когда СтандартноеПриложение.ТРЕБУЕТСЯ_ОБНОВЛЕНИЕ_ПАРАМЕТРОВ_СЕАНСА
            ХранилищеПараметровСеанса = ПараметрыСеанса.ИнициализироватьПараметрыСеанса(МобильныйИнтерфейс)
        ;  
    ;)
;

@ВПроекте
@НаКлиенте
метод ИнициализироватьКомандныйИнтерфейс(ВыполнитьКомандуНавигации: Булево = Ложь, ВходУжеВыполнен: Булево = Ложь)
    пер ДополнительныеКоманды: Массив<Команда>
    пер КомандыПанелиДействий: Массив<Команда>
    пер ШиринаРазделов: Число
    

    ЗаполнитьРазделыПриложенияСведения(ВходУжеВыполнен)
    
    // очищаем текущие команды
    КомандныйИнтерфейсОсновной.Элементы.Очистить()
    КомандныйИнтерфейсДополнительный.Элементы.Очистить()
    ПанельДействий.Элементы.Очистить()
    
    для Раздел из РазделыПриложенияСведения
        если Раздел.НавигационнаяКомандаРаздела != Неопределено
            выбор Раздел.РазмещениеРаздела
            когда ОсновнаяНавигация
                КомандныйИнтерфейсОсновной.Элементы.Добавить(Раздел.НавигационнаяКомандаРаздела!)
                ШиринаРазделов = ШиринаРазделов < Раздел.ШиринаДляПанелиНавигации ? Раздел.ШиринаДляПанелиНавигации : ШиринаРазделов
            когда ДополнительнаяНавигация
                ДополнительныеКоманды.Добавить(Раздел.НавигационнаяКомандаРаздела как НавигационнаяКоманда)
            когда ПанельДействий 
                ПанельДействий.Элементы.Добавить(Раздел.НавигационнаяКомандаРаздела!)
            ;
        ;
        
        если Раздел.Ид == ОсновнойИнтерфейс.ИД_РАЗДЕЛА_ГЛАВНОЕ
            ОсобоеОформлениеПервогоРаздела = (КлиентскоеУстройство.ВидИнтерфейса != ВидИнтерфейса.Телефон)
        ;
    ;
    ЗаполнитьДополнительныеКоманды(ДополнительныеКоманды)
    пер КомандыГруппы: ЧитаемыйМассив<Команда> = ДополнительныеКоманды
    пер ГруппаДополнительныхКоманд = новый ГруппаКомандногоИнтерфейса<Команда>("", КомпоновщикОсновногоИнтерфейса.ПолучитьСервис().Аватар(), КомандыГруппы)
    КомандныйИнтерфейсДополнительный.Элементы.Добавить(ГруппаДополнительныхКоманд)

    ЗаполнитьКомандыПанелиДействий(КомандыПанелиДействий)
    ПанельДействий.Элементы.ДобавитьВсе(КомандыПанелиДействий)
    
    если КлиентскоеУстройство.ВидИнтерфейса == ВидИнтерфейса.Компьютер
        СтандартноеПриложение.ПараметрыСеанса().НастройкиИнтерфейса.ШиринаПанелиНавигации = НулеваяШиринаПанелейНавигации() + ШиринаРазделов
    ;
    если ВыполнитьКомандуНавигации
        пер Команда: Команда?
        для Элемент из КомандныйИнтерфейсОсновной.Элементы
            Команда = ПолучитьКоманду(Элемент)
            если Команда != Неопределено
                Команда.Выполнить()
                прервать
            ;
        ;
    ;
;

метод ПолучитьДополнительныйКомандныйИнтерфейс(): ГруппаКомандногоИнтерфейса<Команда>?
    если КомандныйИнтерфейсДополнительный.Элементы.Размер() > 0
        возврат КомандныйИнтерфейсДополнительный.Элементы[0] как ГруппаКомандногоИнтерфейса<Команда>
    ;
    возврат Неопределено
;

метод ПолучитьКоманду(ЭлементКомандногоИнтерфейса: Команда|ФрагментКомандногоИнтерфейса<Команда>|ГруппаКомандногоИнтерфейса<Команда>): Команда?
    пер Команда: Команда?
    выбор 
    когда ЭлементКомандногоИнтерфейса это Команда
        Команда = ЭлементКомандногоИнтерфейса как Команда
    когда ЭлементКомандногоИнтерфейса это ФрагментКомандногоИнтерфейса<Команда>
        Команда = ПолучитьКоманду((ЭлементКомандногоИнтерфейса как ФрагментКомандногоИнтерфейса<Команда>).Элементы[0])
    когда ЭлементКомандногоИнтерфейса это ГруппаКомандногоИнтерфейса<Команда>
        Команда = ПолучитьКоманду((ЭлементКомандногоИнтерфейса как ГруппаКомандногоИнтерфейса<Команда>).Элементы[0])
    ;
    возврат Команда
;

метод НулеваяШиринаПанелейНавигации(): Число
    если КлиентскоеУстройство.ВидИнтерфейса == ВидИнтерфейса.Компьютер
        возврат (6 + 4 + 6 + 6) * Метрики.РАЗМЕР_ПОЛУИНТЕРВАЛА  // 2 * 24 - отступы окна приложения, 2 * 16 - отступы перед областью размещения форм, 48 - размер правой панели, 2 * 24 - отступы области размещения форм
    иначе
        возврат 4 * Метрики.РАЗМЕР_ПОЛУИНТЕРВАЛА  // 2 * 16 - отступы окна приложения
    ;  
;

метод ЗаполнитьРазделыПриложенияСведения(ВходУжеВыполнен: Булево)
    РазделыПриложенияСведения.Очистить()
    знч Сервисы = ОсновнойИнтерфейсПоставщик.ПолучитьСервисы()
    для Сервис из Сервисы
        РазделыПриложенияСведения.ДобавитьВсе(Сервис.РазделыПриложения())
    ;
    РазделыПриложенияСведения = КомпоновщикОсновногоИнтерфейса.ПолучитьСервис().СкомпоноватьРазделы(РазделыПриложенияСведения, ВходУжеВыполнен)
;

метод ЗаполнитьДополнительныеКоманды(ДополнительныеКоманды: Массив<Команда>)
    знч Сервисы = ОсновнойИнтерфейсПоставщик.ПолучитьСервисы()
    для Сервис из Сервисы
        ДополнительныеКоманды.ДобавитьВсе(Сервис.ДополнительныеКоманды())
    ;
    КомпоновщикОсновногоИнтерфейса.ПолучитьСервис().СкомпоноватьДополнительныеКоманды(ДополнительныеКоманды)
;

метод ЗаполнитьКомандыПанелиДействий(КомандыПанелиДействий: Массив<Команда>)
    знч Сервисы = ОсновнойИнтерфейсПоставщик.ПолучитьСервисы()
    для Сервис из Сервисы
        КомандыПанелиДействий.ДобавитьВсе(Сервис.КомандыПанелиДействий())
    ;
    КомпоновщикОсновногоИнтерфейса.ПолучитьСервис().СкомпоноватьКомандыПанелиДействий(КомандыПанелиДействий)
;

@НаСервере @НаКлиенте
исключение ЗапускПриложенияОтменен
    обз пер ПричинаОтмены: Строка
;

@НаСервере @ДоступноСКлиента
статический метод ИнициироватьПриложение(СмещениеКлиента: Число, МобильныйИнтерфейс: Булево)
    знч ОбработчикиВременнойЗоны = УчетВременнойЗоны.ПолучитьСервисы()
    для Обработчик из ОбработчикиВременнойЗоны
        Обработчик.ПриИзменении(СмещениеКлиента)
    ;
    знч ИмяСобытия: Строка = МобильныйИнтерфейс ? "ВходСМобильногоУстройства" : "ВходЧерезВеб"
    знч Описание: Строка = МобильныйИнтерфейс ? "Успешный вход с мобильного устройства" : "Успешный вход через Веб"
    Журналирование.ЗарегистрироватьСобытие(ИмяСобытия, Описание, ВажностьСобытияЖурналаСобытий.Низкая)
;