импорт СтандартноеПриложение

@ВПодсистеме конст ШИРИНА_КНОПКИ_ВСЕ_ФУНКЦИИ = 200

метод НадписьЗначение(): Строка
    возврат ОписаниеКоманды.Представление
;

метод ЭлементПанелиВсеФункцииВидимость(): Булево
    возврат ВидимостьКоманды(ОписаниеКоманды.Представление)
;    

метод НадписьПриНажатии(Источник: Надпись, Событие: СобытиеПриНажатии)
    если ОписаниеКоманды.Команда != Неопределено
        ОписаниеКоманды.Команда.Выполнить()
    иначе
        ОткрытьФормуСписка()
    ;
;

метод ОткрытьФормуСписка()
    знч КомандаСоздания = новый ОбычнаяКоманда(Обработчик = &ОткрытьФормуНового, Представление = "Создать")
    знч Форма = ФормаСписка()
    знч Таблица = (Форма.КомпонентТаблицы как Таблица<ДинамическийСписок>)
    Таблица.Команды = новый ФрагментКомандногоИнтерфейса<Команда>([КомандаСоздания])
    Таблица.ПриНажатииСтроки = &ОткрытьФормуОбъекта
    знч ИмяТаблицы = Таблица.Источник.ОсновнаяТаблица.Таблица
    знч НекликабельныеПоля = НекликабельныеПоля(ИмяТаблицы)
    пер Индекс = Таблица.Колонки.Граница()
    пока Индекс >= 0
        пер Колонка = Таблица.Колонки[Индекс] как СтандартнаяКолонкаТаблицы<СтрокаДинамическогоСписка>
        если НекликабельныеПоля.Содержит(Колонка.ПолеЗначения)
            Таблица.Колонки.Удалить(Колонка)
            
            пер НоваяКолонка = новый СтандартнаяКолонкаТаблицы<СтрокаДинамическогоСписка>(
                ПолеЗначения = Колонка.ПолеЗначения,
                Заголовок = Колонка.Заголовок,
                ОбрабатыватьНажатие = Ложь
            )
            Таблица.Колонки.Вставить(Индекс, НоваяКолонка)
        ;
        Индекс = Индекс - 1
    ;  
    Форма.Открыть()
;
@НаСервере @ДоступноСКлиента
статический метод НекликабельныеПоля(ИмяТаблицы: Строка): Массив<Строка>
    знч НекликабельныеПоля: Массив<Строка>
    пер ОтражениеСущности: ОтражениеСущности?
    для Подсистема из Проект.ПолучитьПодсистемы()
        ОтражениеСущности = ОтражениеСущностиПодсистемы(Подсистема, ИмяТаблицы)
        если ОтражениеСущности != Неопределено
            для Поле из ОтражениеСущности.ОсновнаяТаблица.Поля
                если Поле.Тип.Типы.Содержит(Тип<Тип>)
                    НекликабельныеПоля.Добавить(Поле.Имя.Русский)
                    НекликабельныеПоля.Добавить(Поле.Имя.Английский)
                ;
            ;
            прервать
        ;
    ;
    возврат НекликабельныеПоля
;

@НаСервере
статический метод ОтражениеСущностиПодсистемы(Пакет: ОтражениеПакетаПроекта, ИмяТаблицы: Строка): ОтражениеСущности?
    пер ОтражениеСущности: ОтражениеСущности?
    для ЭлементПроекта из Пакет.Элементы
        если ЭлементПроекта это ОтражениеСущности и не (ЭлементПроекта это ОтражениеЭлементаПроектаКонтрактСущности)
            знч ПолныеИменаТаблицы: Массив<Строка>
            знч ПолноеИмя = (ЭлементПроекта как ОтражениеСущности).ОсновнаяТаблица.ПолноеИмя
            ПолныеИменаТаблицы.Добавить("%{ПолноеИмя.ПространствоИмен.Русский}::%{ПолноеИмя.Имя.Русский}")
            ПолныеИменаТаблицы.Добавить("%{ПолноеИмя.ПространствоИмен.Английский}::%{ПолноеИмя.Имя.Английский}")
            если ПолныеИменаТаблицы.Содержит(ИмяТаблицы)
                ОтражениеСущности = ЭлементПроекта как ОтражениеСущности
                прервать
            ;
        ;
    ;
    если ОтражениеСущности == Неопределено
        для ПодчиненныйПакет из Пакет.Пакеты
            ОтражениеСущности = ОтражениеСущностиПодсистемы(ПодчиненныйПакет, ИмяТаблицы)
            если ОтражениеСущности != Неопределено
                прервать
            ;
        ;
    ;
    возврат ОтражениеСущности
;

метод ОткрытьФормуОбъекта(Источник: Список<ДинамическийСписок>, Событие: СобытиеСДанными<СтрокаСписка<СтрокаДинамическогоСписка>>)
    знч ФормаОбъекта = ФормаОбъекта()
    ФормаОбъекта.Прочитать(Событие.Данные.ДанныеСтроки.Ключ)
    ФормаОбъекта.ОткрытьВМодальномОкне()
;

метод КопироватьОбъект()
;

метод ОткрытьФормуНового(Команда: ОбычнаяКоманда)
    знч ФормаОбъекта = ФормаОбъекта()
    ФормаОбъекта.ОткрытьВМодальномОкне()
;

метод ФормаОбъекта(): неизвестно
    возврат ОписаниеКоманды.СоздательФормыОбъекта()
;

метод ФормаСписка(): ФормаСписка
    возврат ОписаниеКоманды.СоздательФормыСписка()
;