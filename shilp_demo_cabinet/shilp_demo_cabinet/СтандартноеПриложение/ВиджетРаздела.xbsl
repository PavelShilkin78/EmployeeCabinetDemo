импорт СтандартноеПриложение
импорт ИнтерфейсныеКомпонентыСтандартные

@Обработчик
метод ПослеСоздания()
    пер ГруппаКоманды: ФиксированнаяГруппа = Компоненты.СодержимоеИКоманда
    ГруппаКоманды.Содержимое.Добавить(ОписаниеВиджета.Компонент)
    если ОписаниеВиджета.Команда != Неопределено
        знч Кнопка = новый Кнопка()
        Кнопка.Вид = ВидКнопки.Дополнительная
        Кнопка.Команды = ОписаниеВиджета.Команда
        Кнопка.ВыравниваниеВГруппеПоГоризонтали = ВыравниваниеПоГоризонтали.Конец
        Кнопка.ВыравниваниеВГруппеПоВертикали = ВыравниваниеПоВертикали.Низ
        ГруппаКоманды.Содержимое.Добавить(Кнопка)
    ;
;

метод ВиджетРазделаГиперссылка(): Булево
    возврат КомандаВыполняемаяПриНажатии() != Неопределено
;

метод ВиджетРазделаПриНажатии(Источник: Карточка, Событие: СобытиеПриНажатии)
    СтатистикаИспользованияПриложения.ЗарегистрироватьЦель("Раздел%{ОписаниеВиджета.Ид}ИзКарточки")
    знч Команда = КомандаВыполняемаяПриНажатии()
    если Команда != Неопределено
        Команда.Выполнить()
    ;
;

метод КомандаВыполняемаяПриНажатии(): Команда?
    пер Команда: Команда?
    если ОписаниеВиджета.НавигационнаяКоманда != Неопределено
        Команда = ОписаниеВиджета.НавигационнаяКоманда
    иначе
        знч ОписаниеРаздела: ОсновнойИнтерфейс.ОписаниеРаздела? = ОсновнойИнтерфейс.ОписаниеРазделаПоИдентификатору(ОписаниеВиджета.ИдРаздела)
        если ОписаниеРаздела != Неопределено и ОписаниеРаздела.НавигационнаяКомандаРаздела это Команда
            Команда = (ОписаниеРаздела.НавигационнаяКомандаРаздела как Команда)
        ;
    ;
    возврат Команда
;

метод КартинкаИзображение(): ДвоичныйОбъект.Ссылка?
    пер Картинка: ДвоичныйОбъект.Ссылка?
    если ОписаниеВиджета.Пиктограмма != Неопределено
        Картинка = ОписаниеВиджета.Пиктограмма
    иначе
        знч ОписаниеРаздела: ОсновнойИнтерфейс.ОписаниеРаздела? = ОсновнойИнтерфейс.ОписаниеРазделаПоИдентификатору(ОписаниеВиджета.ИдРаздела)
        если ОписаниеРаздела != Неопределено
            Картинка = ОписаниеРаздела.Пиктограмма
        ;
    ;
    возврат Картинка
;

метод КартинкаЦвет(): Цвет?
    пер ВидЦвета: БиблиотекаЦветов.ВидыЦвета
    если ОписаниеВиджета.ЦветПиктограммы != Неопределено
        ВидЦвета = ОписаниеВиджета.ЦветПиктограммы
    иначе
        знч ОписаниеРаздела: ОсновнойИнтерфейс.ОписаниеРаздела? = ОсновнойИнтерфейс.ОписаниеРазделаПоИдентификатору(ОписаниеВиджета.ИдРаздела)
        если ОписаниеРаздела != Неопределено и ОписаниеРаздела.ЦветПиктограммы != Неопределено
            ВидЦвета = ОписаниеРаздела.ЦветПиктограммы
        ;
    ;
    возврат БиблиотекаЦветов.Цвет(ВидЦвета)
;