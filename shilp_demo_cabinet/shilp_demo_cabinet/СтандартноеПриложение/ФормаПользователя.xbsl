импорт КонтрольДоступаСтандартные
импорт ИнтерфейсныеКомпонентыСтандартные

@ВПодсистеме
структура Утверждение
    пер Ключ: Строка
    пер Значение: Строка
;

метод УдалитьОбработчик(Команда: ОбычнаяКоманда)
    Удалить(ДанныеПользователя.Ссылка)
;

метод Утверждения(): Массив<Утверждение>
    знч Утверждения: Массив<Утверждение>
    для Элемент из ДанныеПользователя.Утверждения
        Утверждения.Добавить(новый Утверждение(Элемент.Ключ, Элемент.Значение))
    ;
    возврат Утверждения
;

метод ЗаписатьИЗакрытьОбработчик(Команда: ОбычнаяКоманда)
    Записать()
    Закрыть()
;

метод ЗаписатьОбработчик(Команда: ОбычнаяКоманда)
    Записать()
;

метод УдалитьТокенОбработчик(Команда: КомандаСПараметром<МенеджерПравДоступа.ТокенДоступаПользователя>, Параметр: МенеджерПравДоступа.ТокенДоступаПользователя)
    УдалитьТокен(ДанныеПользователя.Ссылка, Параметр.Ид)
    ДанныеПользователя.ТокеныДоступа.Удалить(Параметр)
;

метод УдалитьУтверждениеОбработчик(Команда: КомандаСПараметром<Утверждение>, Параметр: Утверждение)
    Компоненты.Утверждения.Источник.Данные.Удалить(Параметр)
;

@НаСервере @ДоступноСКлиента
статический метод УдалитьТокен(Пользователь: Пользователи.Ссылка, ИдТокена: Строка)
    знч ПользовательОбъект = Пользователь.ЗагрузитьОбъект()
    ПользователиСервиса.УдалитьДанныеЗапросаТокенаДоступа(ПользовательОбъект.Ид, ИдТокена)
;

метод Записать()
    знч НовыеУтверждения: Массив<Утверждение>
    НовыеУтверждения.ДобавитьВсе(Компоненты.Утверждения.Источник.Данные)
    ДанныеПользователя.Утверждения.Очистить()
    для Элемент из НовыеУтверждения
        ДанныеПользователя.Утверждения.Вставить(Элемент.Ключ, Элемент.Значение)
    ;
    Записать(ДанныеПользователя)
;

@НаСервере @ДоступноСКлиента
статический метод Удалить(Пользователь: Пользователи.Ссылка)
    знч ПользовательОбъект: Пользователи.Объект? = Пользователь.ЗагрузитьОбъект()
    Пользователи.Отключить(Пользователь)
    ПользователиСервиса.Удалить(ПользовательОбъект.Ид)
;

@НаСервере @ДоступноСКлиента
статический метод Записать(ДанныеПользователя: СтандартноеПриложение.ДанныеПользователя)
    если ДанныеПользователя.Ссылка == Неопределено
        знч Описание = новый ОписаниеПользователя(
            СпискиПользователей.ПолучитьСписокПоУмолчанию().Ид,
            ДанныеПользователя.Представление, 
            ДанныеПользователя.Представление)
        ПользователиСервиса.Создать(Описание)
        знч Пользователь = Пользователи.Подключить(Описание.Ид)
        Пользователи.УстановитьУтверждения(Пользователь.Ссылка, ДанныеПользователя.Утверждения)
    иначе
        знч ПользовательОбъект = ДанныеПользователя.Ссылка.ЗагрузитьОбъект()
        если ПользовательОбъект.Представление != ДанныеПользователя.Представление
            пер Описание = ПользователиСервиса.Найти(ПользовательОбъект.Ид)
            Описание = Описание.СПредставлением(ДанныеПользователя.Представление)
            Описание = Описание.СЛогином(ДанныеПользователя.Представление)
            ПользователиСервиса.Изменить(Описание)
            Пользователи.Переподключить(ПользовательОбъект.Ид, ДанныеПользователя.Ссылка.Ид)
        ;
        если ПользовательОбъект.Утверждения != ДанныеПользователя.Утверждения
            Пользователи.УстановитьУтверждения(ДанныеПользователя.Ссылка, ДанныеПользователя.Утверждения)
        ;
    ;
;

метод СформироватьТокенОбработчик(Команда: ОбычнаяКоманда)
    если ДанныеПользователя.Ссылка == Неопределено
        СтандартныеФормы.ПоказатьУведомление("Не выполнено", "Необходимо сначала записать пользователя", ОценкаИнформации.Предупреждающая)
        возврат
    ;
    знч Полномочия: МенеджерПравДоступа.ТокенДоступаПользователя = УстановитьТокенДоступа(ДанныеПользователя.Ссылка, ДанныеПользователя.Представление)
    СтандартныеФормы.ПоказатьУведомление("Токен установлен", "Идентификатор: %{Полномочия.Ид}%{"\н"}Секрет: %{Полномочия.Секрет}")
    ДанныеПользователя.ТокеныДоступа = ТокеныДоступа(ДанныеПользователя.Ссылка)
;

@НаСервере @ДоступноСКлиента
статический метод УстановитьТокенДоступа(Ссылка: Пользователи.Ссылка, ПредставлениеПользователя: Строка): МенеджерПравДоступа.ТокенДоступаПользователя
    знч ИдПользователя: ИдПользователя = Ссылка.ЗагрузитьОбъект().Ид
    знч НовыйТокен: ДанныеЗапросаТокенаДоступа = ПользователиСервиса.СгенерироватьДанныеЗапросаТокенаДоступа(ИдПользователя, ПредставлениеПользователя)
    возврат новый МенеджерПравДоступа.ТокенДоступаПользователя(Ид = НовыйТокен.Ид, Секрет = НовыйТокен.Секрет, Описание = ПредставлениеПользователя)
;

@НаСервере @ДоступноСКлиента
статический метод ТокеныДоступа(Пользователь: Пользователи.Ссылка): Массив<МенеджерПравДоступа.ТокенДоступаПользователя>
    знч ПользовательОбъект = Пользователь.ЗагрузитьОбъект()
    знч Токены: Массив<МенеджерПравДоступа.ТокенДоступаПользователя>
    для ТокенДоступа из ПользователиСервиса.ПолучитьОписанияЗапросовТокеновДоступа(ПользовательОбъект.Ид)
        Токены.Добавить(новый МенеджерПравДоступа.ТокенДоступаПользователя(Ид = ТокенДоступа.Ид, Описание = ТокенДоступа.Описание))
    ;
    возврат Токены
;
