импорт ИнтерфейсныеКомпонентыСтандартные

@ВПроекте
структура ОписаниеРаздела
    обз знч Ид: Строка
    обз знч Имя: Строка 
    знч Пиктограмма: ДвоичныйОбъект.Ссылка? 
    знч ЦветПиктограммы: БиблиотекаЦветов.ВидыЦвета?
    знч ШиринаДляПанелиНавигации: Число
    знч НавигационнаяКомандаРаздела: НавигационнаяКоманда|ФрагментКомандногоИнтерфейса<Команда>?
    знч РазмещениеРаздела: РазмещениеРаздела
    пер Приоритет: Число
    @ИменованныеПараметры
    конструктор
;

@ВПроекте
структура ОписаниеВиджета
    обз знч Ид: Строка
    обз знч ИдРаздела: Строка
    обз знч Имя: Строка
    обз знч Компонент: Компонент
    знч НавигационнаяКоманда: НавигационнаяКоманда?
    знч Команда: Команда|ГруппаКомандногоИнтерфейса<Команда>|?
    знч Пиктограмма: ДвоичныйОбъект.Ссылка? 
    знч ЦветПиктограммы: БиблиотекаЦветов.ВидыЦвета?
    пер Приоритет: Число
    @ИменованныеПараметры
    конструктор
;

@ВПроекте
структура ОписательГруппыКомандВсеФункции
    знч Наименование: Строка = "Прочее"
    знч Приоритет: Число = 999
;

@ВПроекте
метод ГруппаКомандАдминистрирование(): ОписательГруппыКомандВсеФункции
    возврат новый ОписательГруппыКомандВсеФункции(
        Наименование = "Администрирование",
        Приоритет = 1)
;

@ВПроекте
структура ОписательКомандыВсеФункции
    знч Группа: ОсновнойИнтерфейс.ОписательГруппыКомандВсеФункции
    знч Команда: НавигационнаяКоманда?
    знч СоздательФормыСписка: (()->ФормаСписка)|?
    знч СоздательФормыОбъекта: (()->неизвестно)|?
    обз знч Представление: Строка
;

@ВПроекте
метод ВиджетРазделаМинимальнаяШирина(): Число
    знч ШиринаВПолуинтрвалах = КлиентскоеУстройство.ВидИнтерфейса == ВидИнтерфейса.Телефон ? 34 : 45
    возврат ШиринаВПолуинтрвалах * Метрики.РАЗМЕР_ПОЛУИНТЕРВАЛА
;

@ВПроекте
метод ВиджетРазделаМаксимальнаяШирина(): Число
    знч ШиринаВПолуинтрвалах = 60
    возврат ШиринаВПолуинтрвалах * Метрики.РАЗМЕР_ПОЛУИНТЕРВАЛА
;

@ВПодсистеме
метод КомандыВсеФункции(): Массив<ОсновнойИнтерфейс.ОписательКомандыВсеФункции>
    знч ВсеФункции: Массив<ОсновнойИнтерфейс.ОписательКомандыВсеФункции>
    знч Сервисы = ОсновнойИнтерфейсПоставщик.ПолучитьСервисы()
    для Сервис из Сервисы
        ВсеФункции.ДобавитьВсе(Сервис.КомандыВсеФункции())
    ;
    возврат ВсеФункции
;

@ВПроекте
метод ШиринаОсновногоОкнаПриложения(): Число
    возврат КлиентскоеУстройство.ШиринаОкнаПриложения - СтандартноеПриложение.ПараметрыСеанса().НастройкиИнтерфейса.ШиринаПанелиНавигации
;

@ВПроекте
метод ОписаниеРазделаПоИдентификатору(Ид: Строка): ОписаниеРаздела?
    пер ОписаниеРаздела: ОписаниеРаздела?
    для Описание из (КлиентскоеПриложение как ОсновноеПриложение).РазделыПриложенияСведения
        если Описание.Ид == Ид
            ОписаниеРаздела = Описание
        ;
    ;
    возврат ОписаниеРаздела
;

@ВПроекте
конст ИД_РАЗДЕЛА_ВСЕ_ФУНКЦИИ = "ВсеФункции"

@ВПроекте
конст ИД_РАЗДЕЛА_ГЛАВНОЕ: Строка = "Главное"

@ВПроекте
конст ИД_РАЗДЕЛА_НАСТРОЙКИ = "Настройки"

@ВПроекте
конст РАЗМЕР_ИНДИКАТОРА_ДЛЯ_ВИДЖЕТА = 48

@ВПроекте
метод ЭтоМобильныйИнтерфейс(): Булево
    возврат (КлиентскоеУстройство.ВидИнтерфейса == ВидИнтерфейса.Телефон или КлиентскоеУстройство.ВидИнтерфейса == ВидИнтерфейса.Планшет)
;

@ВПроекте
метод МаксимальнаяШиринаПанелейПодразделов(): Число
    пер ШиринаОсновногоОкна = ШиринаОсновногоОкна()
    пер ШиринаПанели: Число = 100 * Метрики.РАЗМЕР_ПОЛУИНТЕРВАЛА
    возврат (ШиринаОсновногоОкна < ШиринаПанели ? ШиринаОсновногоОкна : ШиринаПанели)
;

@ВПроекте
метод ШиринаОсновногоОкна(): Число
    возврат КлиентскоеУстройство.ШиринаОкнаПриложения - СтандартноеПриложение.ПараметрыСеанса().НастройкиИнтерфейса.ШиринаПанелиНавигации
;

@ВПроекте
метод МинимальнаяШиринаПанелейПодразделов(): Число
    пер МинимальнаяШиринаПолуинтервалы: Число
    если КлиентскоеУстройство.ВидИнтерфейса == ВидИнтерфейса.Телефон
        МинимальнаяШиринаПолуинтервалы = 36
    иначе
        МинимальнаяШиринаПолуинтервалы = 60
    ;      
    возврат МинимальнаяШиринаПолуинтервалы * Метрики.РАЗМЕР_ПОЛУИНТЕРВАЛА
;

@ВПроекте
метод МаксимальнаяШиринаФормыДокумента(): Число
    возврат 100 * Метрики.РАЗМЕР_ПОЛУИНТЕРВАЛА 
;

@ВПроекте
метод МинимальнаяШиринаФормыДокумента(): Число
    пер МинимальнаяШиринаПолуинтервалы: Число
    если КлиентскоеУстройство.ВидИнтерфейса == ВидИнтерфейса.Телефон
        МинимальнаяШиринаПолуинтервалы = 36
    иначе
        МинимальнаяШиринаПолуинтервалы = 40
    ;      
    возврат МинимальнаяШиринаПолуинтервалы * Метрики.РАЗМЕР_ПОЛУИНТЕРВАЛА
;

@ВПроекте
метод ВысотаФормыДокумента(ПредпочтительнаяВысотаВПолуинтервалах: Число): Число
    пер ВысотаФормыДокумента: Число
    если КлиентскоеУстройство.ВидИнтерфейса != ВидИнтерфейса.Телефон
        пер МаксимальнаяВысота: Число = КлиентскоеУстройство.ВысотаОкнаПриложения * 0.9
        МаксимальнаяВысота = (МаксимальнаяВысота/Метрики.РАЗМЕР_ПОЛУИНТЕРВАЛА).Округлить() * Метрики.РАЗМЕР_ПОЛУИНТЕРВАЛА
        пер ПредпочтительнаяВысота: Число = ПредпочтительнаяВысотаВПолуинтервалах * Метрики.РАЗМЕР_ПОЛУИНТЕРВАЛА
        ВысотаФормыДокумента = ПредпочтительнаяВысота > МаксимальнаяВысота ? МаксимальнаяВысота : ПредпочтительнаяВысота
    ;
    возврат ВысотаФормыДокумента
;

@ВПроекте
метод РаботаНаУзкомУстройстве(): Булево
    пер ГраничнаяШирина: Число = КлиентскоеУстройство.ШиринаОкнаПриложения
    ГраничнаяШирина -= СтандартноеПриложение.ПараметрыСеанса().НастройкиИнтерфейса.ШиринаПанелиНавигации
    ГраничнаяШирина -= 10 * Метрики.РАЗМЕР_ПОЛУИНТЕРВАЛА  // 2 * 24 - отступы основного окна, 32 - двойной интервал между панелями 
    возврат 2 * МинимальнаяШиринаПанелейПодразделов() >= ГраничнаяШирина  
;

@ВПроекте
метод Виджеты(): Массив<ОсновнойИнтерфейс.ОписаниеВиджета>
    знч Виджеты: Массив<ОсновнойИнтерфейс.ОписаниеВиджета>
    знч Сервисы = ОсновнойИнтерфейсПоставщик.ПолучитьСервисы()
    для Сервис из Сервисы
        Виджеты.ДобавитьВсе(Сервис.Виджеты())
    ;
    возврат Виджеты
;

@ВПроекте
метод ЛоготипПриложения(): ДвоичныйОбъект.Ссылка?
    пер Лого: ДвоичныйОбъект.Ссылка?
    если КлиентскоеУстройство.ВидИнтерфейса != ВидИнтерфейса.Телефон
        Лого = СтандартноеПриложение.ПараметрыСеанса().Логотип
    ;
    возврат Лого
;
