@ВПодсистеме
перечисление РежимОтображения
    Список умолчание,
    Группы
;

метод ОсновнаяГруппаСодержимое(): Массив<Компонент>
    знч ОсновнаяГруппаСодержимое: Массив<Компонент>
    пер ВсеКоманды = ОсновнойИнтерфейс.КомандыВсеФункции()
    пер ТекущаяГруппа: МатричнаяГруппа?
    
    выбор РежимОтображения 
    когда Список
        ВсеКоманды = ВсеКоманды.СортироватьПо(ОписаниеКоманды -> ОписаниеКоманды.Представление)
        ТекущаяГруппа = КонтейнерКоманд()
        ОсновнаяГруппаСодержимое.Добавить(ТекущаяГруппа)
        для ОписаниеКоманды из ВсеКоманды
            ТекущаяГруппа.Содержимое.Добавить(КомандаВсеФункции(ОписаниеКоманды))
        ;
    когда Группы
        ВсеКоманды = ВсеКоманды.Сортировать(&КомпараторОписателейКоманд)
        пер ОписаниеТекущейГруппы: ОсновнойИнтерфейс.ОписательГруппыКомандВсеФункции?
        для ОписаниеКоманды из ВсеКоманды
            если ОписаниеТекущейГруппы != ОписаниеКоманды.Группа
                ОписаниеТекущейГруппы = ОписаниеКоманды.Группа
                ТекущаяГруппа = КонтейнерКоманд()
                
                знч ГруппаСЗаголовком = новый ФиксированнаяГруппа()
                ГруппаСЗаголовком.Заголовок = ОписаниеТекущейГруппы.Наименование
                ГруппаСЗаголовком.Содержимое.Добавить(ТекущаяГруппа)
                ОсновнаяГруппаСодержимое.Добавить(ГруппаСЗаголовком)
            ;
            ТекущаяГруппа.Содержимое.Добавить(КомандаВсеФункции(ОписаниеКоманды))
        ;
    ;
    возврат ОсновнаяГруппаСодержимое
;

метод КонтейнерКоманд(): МатричнаяГруппа
    знч КонтейнерКоманд = новый МатричнаяГруппа()
    КонтейнерКоманд.РастягиватьПоГоризонтали = Истина
    КонтейнерКоманд.ОписаниеАвтоматическихКолонок = новый КолонкаМатричнойГруппы(
            РастягиватьПоГоризонтали = Истина,
            МинимальнаяШирина = ЭлементПанелиВсеФункции.ШИРИНА_КНОПКИ_ВСЕ_ФУНКЦИИ,
            МаксимальнаяШирина = ЭлементПанелиВсеФункции.ШИРИНА_КНОПКИ_ВСЕ_ФУНКЦИИ)
    КонтейнерКоманд.АвтоЗаполнение = АвтоЗаполнениеМатричнойГруппы.ДобавлятьКолонкиИСтроки
    возврат КонтейнерКоманд
;

метод КомандаВсеФункции(ОписаниеКоманды: ОсновнойИнтерфейс.ОписательКомандыВсеФункции): ЭлементПанелиВсеФункции
    знч КомандаВсеФункции = новый ЭлементПанелиВсеФункции()
    КомандаВсеФункции.ОписаниеКоманды = ОписаниеКоманды
    КомандаВсеФункции.ВидимостьКоманды = метод(НазваниеКоманды: Строка) -> 
        возврат НазваниеКоманды.Содержит(СтрокаПоиска, Истина);
    возврат КомандаВсеФункции
;

@Обработчик
метод ПослеСоздания()
    СтандартноеПриложение.ФормыРазделовСтандартногоПриложения().ВсеФункции = этот
;

метод КомпараторОписателейКоманд(Первый: ОсновнойИнтерфейс.ОписательКомандыВсеФункции, Второй: ОсновнойИнтерфейс.ОписательКомандыВсеФункции): Число
    если Первый.Группа.Приоритет > Второй.Группа.Приоритет
        возврат 1
    иначе если Первый.Группа.Приоритет < Второй.Группа.Приоритет
        возврат -1
    иначе
        если Первый.Группа.Наименование > Второй.Группа.Наименование
            возврат 1
        иначе если Первый.Группа.Наименование < Второй.Группа.Наименование
            возврат -1
        иначе
            если Первый.Представление > Второй.Представление
                возврат 1
            иначе если Первый.Представление < Второй.Представление
                возврат -1
            иначе
                возврат 0
            ;
        ;
    ;
;

метод ПереключениеСписокГруппы(Команда: ПереключаемаяКоманда)
    РежимОтображения = Команда.Активна ? РежимОтображения.Группы : РежимОтображения.Список
;