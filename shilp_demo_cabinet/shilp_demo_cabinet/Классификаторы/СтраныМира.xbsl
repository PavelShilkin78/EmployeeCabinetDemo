
структура ЭлементКлассификатора
    знч Наименование: Строка
    знч КодАльфа2: Строка
    знч КодАльфа3: Строка
    знч Код: Строка
    знч НаименованиеПолное: Строка
    знч УчастникЕАЭС: Булево
    знч МеждународноеНаименование: Строка
;

@ВПроекте
@НаСервере
метод ЗаполнитьПоКлассификатору()
    знч Классификатор = СериализацияJson.ПрочитатьОбъект(
        Ресурс{Оксм.json}.ОткрытьПотокЧтения(),
        Тип<Массив<ЭлементКлассификатора>>
    )
    
    для Элемент из Классификатор
        пер СправочникОбъект: СтраныМира.Объект
        знч НайденныйЭлемент = СтраныМира.НайтиПоКоду(Элемент.Код)
        если НайденныйЭлемент == Неопределено
            СправочникОбъект = новый СтраныМира.Объект()
        иначе
            СправочникОбъект = НайденныйЭлемент.ЗагрузитьОбъект()
        ;
        
        СправочникОбъект.Наименование = Элемент.Наименование
        СправочникОбъект.ЦифровойКод = Элемент.Код
        СправочникОбъект.НаименованиеПолное = Элемент.НаименованиеПолное
        СправочникОбъект.КодАльфа2 = Элемент.КодАльфа2
        СправочникОбъект.КодАльфа3 = Элемент.КодАльфа3
        СправочникОбъект.УчастникЕАЭС = Элемент.УчастникЕАЭС
        СправочникОбъект.МеждународноеНаименование = Элемент.МеждународноеНаименование
        
        СправочникОбъект.Записать()
    ;
;

@ВПроекте
@НаСервере
метод СвойстваЭлемента(Страна: СтраныМира.Ссылка): ЭлементКлассификатора?
    знч Запрос = Запрос{
        Выбрать
            Наименование,
            ЦифровойКод,
            НаименованиеПолное,
            КодАльфа2,
            КодАльфа3,
            УчастникЕАЭС,
            МеждународноеНаименование
        Из
            СтраныМира
        Где
            Ссылка == %Страна}
    исп Результат = Запрос.Выполнить()
    если не Результат.Пусто()
        знч СтрокаРезультата = Результат.Единственный()
        возврат новый ЭлементКлассификатора(
            Наименование                = СтрокаРезультата.Наименование,
            Код                         = СтрокаРезультата.ЦифровойКод,
            НаименованиеПолное          = СтрокаРезультата.НаименованиеПолное,
            КодАльфа2                   = СтрокаРезультата.КодАльфа2,
            КодАльфа3                   = СтрокаРезультата.КодАльфа3,
            УчастникЕАЭС                = СтрокаРезультата.УчастникЕАЭС,
            МеждународноеНаименование   = СтрокаРезультата.МеждународноеНаименование
        )
    ;
    возврат Неопределено
;

@ВПроекте
@НаСервере
метод НайтиПоКоду(Код: Строка): СтраныМира.Ссылка?
    возврат Запрос{
        ВЫБРАТЬ
            Ссылка
        ИЗ
            СтраныМира
        ГДЕ
            ЦифровойКод == %Код
    }.Выполнить().ЕдинственныйИлиУмолчание()?.Ссылка
;

@ВПроекте
@НаСервере @ДоступноСКлиента
статический метод Россия(): СтраныМира.Ссылка?
    возврат СтраныМира.НайтиПоКоду("643")
;