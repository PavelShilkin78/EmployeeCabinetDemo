импорт ВебApi
импорт КонтрольДоступа
импорт ОбщиеСтандартные

//Область РазрешенияДоступа

@Обработчик
метод ВычислитьРазрешенияДоступа(): Массив<РазрешениеДоступа>
    возврат КонтрольДоступа.РазрешенияДоступаТаблицыКонтентДанныхАбонентаЧтениеВсем()
;

//КонецОбласти

//Область JSON

@ВПроекте
структура ДанныеJson
    обз знч ID: Строка
    обз знч name: Строка
    обз знч size : Число
    обз пер body: Строка
;

@ВПроекте
структура ДанныеJson_V3_0
    обз знч ID: Строка
    обз знч name: Строка
    обз знч size : Число
    обз пер body: Строка
    обз пер signatures: Массив<ПодписиДокументов.ДанныеJson_V3_0>?
;

@ВПроекте
метод ТипДанныхJson(ВерсияФорматаОбмена: Версия?): Тип
    если ВерсияФорматаОбмена >= Версия{3.0}
        возврат Тип<ДанныеJson_V3_0>
    иначе если ВерсияФорматаОбмена >= Версия{2.0}
        возврат Тип<ДанныеJson>
    ;
;

@ВПроекте
метод ТипКоллекцииДанныхJson(ВерсияФорматаОбмена: Версия?): Тип
    если ВерсияФорматаОбмена >= Версия{3.0}
        возврат Тип<Массив<ДанныеJson_V3_0>>
    иначе если ВерсияФорматаОбмена >= Версия{2.0}
        возврат Тип<Массив<ДанныеJson>>
    ;
;

@ВПроекте
метод ТипОбъектаJson(): Строка
    возврат "powerOfAttorneys"
;

@ВПроекте
метод ЗагрузитьОбъектИзJson(ДанныеJson: ДанныеJson, Сервисы: ВебApiСервер.СервисыЗагрузкиJson)

    исп Транзакции.Начать() 

    знч МашиночитаемыеДоверенность = Сервисы.ПрочитатьОбъект(ДанныеJson.ID,
        &МашиночитаемыеДоверенности.Объект.новый(Ууид), Тип<МашиночитаемыеДоверенности.Ссылка>) как МашиночитаемыеДоверенности.Объект
    МашиночитаемыеДоверенность.ЗаполнитьПоДаннымJson(ДанныеJson, Сервисы)
    Сервисы.ЗаписатьОбъект(новый ВебApiСервер.ПараметрыЗаписиВходящихДанных(МашиночитаемыеДоверенность, ДанныеJson.ID), Сервисы.ИнструментыМенеджераОбъекта)
    МенеджерСущности.УдалитьПодчиненныеОбъекты(МашиночитаемыеДоверенность.Ссылка, Тип<ПодписиДокументов>, "Документ")
;

@ВПроекте
метод ЗагрузитьОбъектИзJson(ДанныеJson: ДанныеJson_V3_0, Сервисы: ВебApiСервер.СервисыЗагрузкиJson)

    исп Транзакции.Начать() 

    знч МЧД = Сервисы.ПрочитатьОбъект(ДанныеJson.ID,
        &МашиночитаемыеДоверенности.Объект.новый(Ууид), Тип<МашиночитаемыеДоверенности.Ссылка>) как МашиночитаемыеДоверенности.Объект
    МЧД.ЗаполнитьПоДаннымJson(ДанныеJson, Сервисы)
    Сервисы.ЗаписатьОбъект(новый ВебApiСервер.ПараметрыЗаписиВходящихДанных(МЧД, ДанныеJson.ID), Сервисы.ИнструментыМенеджераОбъекта)
    если ДанныеJson.signatures != Неопределено
        ПодписиДокументов.ЗагрузитьОбъектыИзJson(МЧД.Ссылка, ДанныеJson.signatures,  Сервисы)
    ;
;

@ВПроекте
метод ВнешняяСсылка(Идентификатор: Строка): ВебApiСервер.ВнешняяСсылка
    возврат новый ВебApiСервер.ВнешняяСсылка(Идентификатор, (Ууид) -> МашиночитаемыеДоверенности.ПолучитьСсылку(Ууид))
;

//КонецОбласти

@ВПроекте
метод ДанныеПодписей(Доверенность: МашиночитаемыеДоверенности.Ссылка): Массив<ПодписиДокументов.ДанныеПодписиДокумента>
    возврат ПодписиДокументов.ДанныеПодписейДокумента(Доверенность)
;