импорт КонтрольДоступа
импорт УниверсальныеМеханизмы
импорт ФизическиеЛица

//Область РазрешенияДоступа

@Обработчик
метод ВычислитьРазрешенияДоступа(): Массив<РазрешениеДоступа>
    возврат [КонтрольДоступа.ПолныйДоступАутентифицированным(этот)]
;

@Обработчик
метод ВычислитьРазрешенияДоступаДляОбъектов(Элементы: ЧитаемыйМассив<ПравилаЭлектронныхПодписей.ДанныеРасчетаРазрешений>): ЧитаемоеСоответствие<ПравилаЭлектронныхПодписей.ДанныеРасчетаРазрешений, ЧитаемаяКоллекция<РазрешениеДоступа>>
    знч Разрешения: Соответствие<ПравилаЭлектронныхПодписей.ДанныеРасчетаРазрешений, ЧитаемаяКоллекция<РазрешениеДоступа>>
    для Элемент из Элементы
        знч РазрешенияЭлемента: Множество<РазрешениеДоступа>
        РазрешенияЭлемента.Добавить(КонтрольДоступа.ПолныйДоступАдминистраторуАбонента(этот))
        если Элемент.Владелец это ФизическиеЛица.Ссылка
            РазрешенияЭлемента.ДобавитьВсе(КонтрольДоступа.ЧтениеРедактированиеДанныхРегистровПерсонально(
                [Элемент.Владелец как ФизическиеЛица.Ссылка]))
        ;
        Разрешения.Вставить(Элемент, РазрешенияЭлемента)
    ;
    
    возврат Разрешения
;

//КонецОбласти

@ВПроекте
метод ЗаписатьПравило(Владелец: ВладелецПравилаЭлектронныхПодписей.Ссылка | ФизическиеЛица.Ссылка?, ВидЭлектроннойПодписи: ВидыЭлектронныхПодписей, ВариантИспользованияЭлектроннойПодписи: ВариантыИспользованияЭлектроннойПодписи)
    
    пер НаборЗаписейПравилЭлектронныхПодписей: ПравилаЭлектронныхПодписей.НаборЗаписей
    НаборЗаписейПравилЭлектронныхПодписей.Фильтр.Владелец.Установить(Владелец)
    НаборЗаписейПравилЭлектронныхПодписей.ДобавитьЗапись(
        Владелец = Владелец,
        ВидЭлектроннойПодписи = ВидЭлектроннойПодписи,
        ВариантИспользованияЭлектроннойПодписи = ВариантИспользованияЭлектроннойПодписи
    )
    НаборЗаписейПравилЭлектронныхПодписей.Записать(Истина)
;