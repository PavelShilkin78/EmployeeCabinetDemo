импорт СтандартноеПриложение

@ВПодсистеме
структура ПараметрыПриложенияСистемыВзаимодействия
    пер ИдПриложения: Ууид
    пер ОткрытыйКлючПриложения: Строка
    пер ОткрытыйКлючСВ: Строка
    пер ЗакрытыйКлючСВ: Строка
;

@ВПодсистеме
@НаСервере @ДоступноСКлиента
метод СсылкаНаНастройкиСистемыВзаимодействия(): НастройкиСистемыВзаимодействия.Ссылка?
    возврат Обсуждения.СсылкаНаНастройкиСистемыВзаимодействия()  
;

@ВПодсистеме
@НаСервере @ДоступноСКлиента
метод ЗапроситьКодАктивации(АдресСервераСВ: Строка, АдресПочты: Строка): Булево
    пер Возврат: Булево
    попытка
        исп ПривилегированныйРежим = КонтекстДоступа.Привилегированный() 
        Возврат = УправлениеПриложениямиВзаимодействия.ЗапроситьКодАктивации(АдресПочты)
        ПривилегированныйРежим.Закрыть()    
    поймать Исключение: ИсключениеСистемыВзаимодействия
        Журналирование.ЗарегистрироватьОшибку(Исключение)
        Возврат = Ложь
    ;
    возврат Возврат
;

@ВПодсистеме
@НаСервере @ДоступноСКлиента
метод НовоеПриложение(КодАктивации: Строка, АдресПочты: Строка, АдресСервераСВ: Строка): ПараметрыПриложенияСистемыВзаимодействия
    попытка        
        пер ПараметрыТекПользователя = Обсуждения.ПараметрыТекущегоПользователя()
        пер КлючиИБ = новый КлючиПриложенияВзаимодействия()
        исп ПривилегированныйРежим = КонтекстДоступа.Привилегированный() 
        пер ПараметрыПриложения = УправлениеПриложениямиВзаимодействия.СоздатьПриложениеВзаимодействия(
            КлючиИБ,
            ПараметрыТекПользователя.УуидПользователя,
            ПараметрыТекПользователя.ИмяПользователя,
            ПараметрыТекПользователя.ПолноеИмяПользователя,
            КодАктивации,
            АдресПочты,
            Проект.Представление,
            АдресСервераСВ
        )

        пер ПараметрыПриложенияСистемыВзаимодействия = новый ПараметрыПриложенияСистемыВзаимодействия()
        ПараметрыПриложенияСистемыВзаимодействия.ИдПриложения = ПараметрыПриложения.ИдПриложения
        ПараметрыПриложенияСистемыВзаимодействия.ОткрытыйКлючПриложения = ПараметрыПриложения.ОткрытыйКлюч
        ПараметрыПриложенияСистемыВзаимодействия.ОткрытыйКлючСВ = новый Строка(КлючиИБ.ОткрытыйКлюч)
        ПараметрыПриложенияСистемыВзаимодействия.ЗакрытыйКлючСВ = новый Строка(КлючиИБ.ЗакрытыйКлюч)
        
        
        знч ДанныеРегистрации = новый ДанныеРегистрацииПриложенияВзаимодействия(
                                Сервер = АдресСервераСВ,
                                ИдПриложения = ПараметрыПриложенияСистемыВзаимодействия.ИдПриложения,
                                КлючиПриложения = новый КлючиПриложенияВзаимодействия(ПараметрыПриложенияСистемыВзаимодействия.ОткрытыйКлючСВ, ПараметрыПриложенияСистемыВзаимодействия.ЗакрытыйКлючСВ),
                                ОткрытыйКлючСервера = ПараметрыПриложенияСистемыВзаимодействия.ОткрытыйКлючПриложения)
        СистемаВзаимодействия.УстановитьДанныеРегистрацииПриложения(ДанныеРегистрации)
        
        ПривилегированныйРежим.Закрыть()
        возврат ПараметрыПриложенияСистемыВзаимодействия        
    поймать Исключение: ИсключениеСистемыВзаимодействия
        выбросить Исключение
    ;
;

@ВПодсистеме
@НаСервере @ДоступноСКлиента
метод ДобавленаНастройкаСистемыВзаимодействия(): Булево
    возврат Обсуждения.СсылкаНаНастройкиСистемыВзаимодействия() != Неопределено   
;

@ВПодсистеме
@НаСервере @ДоступноСКлиента
метод АдресСервераСистемыВзаимодействия(): Строка
    возврат Обсуждения.АдресСервераСистемыВзаимодействия()    
;