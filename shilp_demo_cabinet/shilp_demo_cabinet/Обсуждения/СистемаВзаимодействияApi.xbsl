импорт ВебApi
импорт Обсуждения
импорт СтандартноеПриложение

структура ДанныеПриложенияСистемыВзаимодействия
    пер ID: Строка
    пер name: Строка
;

@ВПодсистеме
метод ОбновитьНастройкуСистемыВзаимодействия(ЗапросHttp: HttpСервисЗапрос, ФорматОбмена: Версия? = Неопределено)
    ВебApiСервер.ЗагрузитьОбъект(ЗапросHttp, Тип<НастройкиСистемыВзаимодействия.Ссылка>, 
    новый ВебApiСервер.ИнструментыМенеджераОбъекта(
        (Объект: Сущность.Объект) -> (Объект как НастройкиСистемыВзаимодействия.Объект).Записать()
    ), ФорматОбмена)
;

@ВПодсистеме
метод ПолучитьПриложениеСистемыВзаимодействия(ЗапросHttp: HttpСервисЗапрос)
    попытка
        пер НастройкаСистемыВзаимодействия = Обсуждения.НастройкаСистемыВзаимодействия()
        если НастройкаСистемыВзаимодействия == Неопределено или НастройкаСистемыВзаимодействия.ИдПриложения == Неопределено
            СервисыHttp.ЗаполнитьОтвет(ЗапросHttp, СервисыHttp.КодыСостояний.НеНайдено_404)
            возврат
        ;
        знч ДанныеПриложенияСистемыВзаимодействияJson = новый ДанныеПриложенияСистемыВзаимодействия(
            НастройкаСистемыВзаимодействия.ИдПриложения.ВСтроку(),
            ИнформацияОПриложении.ПолучитьСервис().НазваниеПриложения())

        СервисыHttp.УпаковатьДанныеJson(ЗапросHttp, ДанныеПриложенияСистемыВзаимодействияJson)
    поймать Ошибка: Исключение
        СервисыHttp.ЗаполнитьОтвет(ЗапросHttp, Ошибка)
    ;
;