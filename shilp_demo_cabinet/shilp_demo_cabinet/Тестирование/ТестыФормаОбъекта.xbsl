импорт Анкетирование
импорт ИнтерфейсныеКомпонентыСтандартные

@Обработчик
метод ПослеЧтения()
    
    ШаблонОбъект = ШаблонОбъект(Объект.ШаблонАнкеты, Объект.ЭтоНовый())
     
    если не Объект.ЭтоНовый()
        ЗаполнитьПравильныеОтветы()
        если Анкетирование.ПоШаблонуСозданыАнкеты(Объект.ШаблонАнкеты)
            ТолькоПросмотр = Истина
        ;
    ;
    
    если КопируемыйТест != Неопределено
        КопируемыйОбъект = КопируемыйОбъект(КопируемыйТест!)
        КопируемыйШаблонАнкеты = КопируемыйОбъект.ШаблонАнкеты
    ;
    
    Компоненты.Конструктор.Инициализировать()
    
    СкопироватьПравильныеОтветы()
    СкопироватьИтоговыеОценки()
    
    если не ДобавленыВкладкиТеста
        пер СтраницыКонструктора = Компоненты.Конструктор.Компоненты().Страницы
        пер СтраницаПравильныеОтветы = новый ПравильныеОтветы(ВопросыШаблона = ДанныеКонструктора, ПравильныеВарианты = ПравильныеВарианты, ТолькоПросмотр = ТолькоПросмотр)
        пер СтраницаОценка = новый Оценка(ВопросыШаблона = ДанныеКонструктора, ПравильныеВарианты = ПравильныеВарианты, ИтоговыеОценки = Объект.ИтоговыеОценки, ТолькоПросмотр = ТолькоПросмотр)
        СтраницыКонструктора.Страницы.Добавить(СтраницаПравильныеОтветы)
        СтраницыКонструктора.Страницы.Добавить(СтраницаОценка)
        ДобавленыВкладкиТеста = Истина
    ;

;

метод СкопироватьПравильныеОтветы()
    
    если КопируемыйОбъект == Неопределено
        возврат
    ;
    
    для КопируемыйПравильныйОтвет из КопируемыйОбъект.ОценкиОтветов
        знч ПравильныйВариант = новый ПравильныйВариант()
        ПравильныйВариант.ИдВопроса = ИсторияИдентификаторов.Вопросы.ПолучитьИлиУмолчание(КопируемыйПравильныйОтвет.Вопрос.Ид)
        ПравильныйВариант.ИдВарианта = ИсторияИдентификаторов.Варианты.ПолучитьИлиУмолчание(КопируемыйПравильныйОтвет.ПравильныйОтветВариантОтвета.Ид)
        ПравильныйВариант.КоличествоБаллов = КопируемыйПравильныйОтвет.КоличествоБаллов
        ПравильныеВарианты.Добавить(ПравильныйВариант)
    ;
;

метод СкопироватьИтоговыеОценки()
    
    если КопируемыйОбъект == Неопределено
        возврат
    ;
    
    для КопируемаяОценка из КопируемыйОбъект.ИтоговыеОценки
        знч ИтоговаяОценка = новый ИтоговаяОценка()    
        ИтоговаяОценка.ИдОценки = новый Ууид()
        ИтоговаяОценка.МинимальноеКоличествоБаллов = КопируемаяОценка.МинимальноеКоличествоБаллов
        ИтоговаяОценка.Наименование = КопируемаяОценка.Наименование
        ИтоговаяОценка.Описание = КопируемаяОценка.Описание
        ИтоговаяОценка.СмайликОценки = КопируемаяОценка.СмайликОценки
        Объект.ИтоговыеОценки.Добавить(ИтоговаяОценка)
    ;
;

метод ПросмотрОбработчик(Команда: ОбычнаяКоманда)
    Компоненты.Конструктор.Просмотр()
;

метод КонструкторСохранениеШаблона(Источник: КонструкторШаблонаКомпонент, Событие: СобытиеСДанными<Анкетирование.СтруктураЗаписиШаблона>)
    пер СтруктураЗаписиШаблона = Событие.Данные
    СтруктураЗаписиШаблона.ДанныеШаблонаАнкет.ИдПодсистемыПоставщика = Тестирование.ИД_ПОДСИСТЕМЫ
    
    Анкетирование.ЗаписатьШаблон(СтруктураЗаписиШаблона)
    СобственнаяМодифицированность = Ложь
    Объект.Наименование = СтруктураЗаписиШаблона.ДанныеШаблонаАнкет.Наименование
    Объект.ШаблонАнкеты = СтруктураЗаписиШаблона.ДанныеШаблонаАнкет.Ссылка
    Объект.КоличествоБаллов = Тестирование.МаксимальныйБалл(ПравильныеВарианты)
    ЗаполнитьОценкиОтветов()
    Записать()
    СтандартныеФормы.ПоказатьУведомление("Сохранение выполнено!", "Тест успешно сохранен", ОценкаИнформации.Положительная)
;

метод ЗаполнитьПравильныеОтветы()
    ПравильныеВарианты.Очистить()
    пер ОценкиОтветов = Объект.ОценкиОтветов
    для Оценка из ОценкиОтветов
        ПравильныеВарианты.Добавить(новый ПравильныйВариант(Оценка.Вопрос.Ид, Оценка.ПравильныйОтветВариантОтвета.Ид, Оценка.КоличествоБаллов))    
    ; 
;

метод ЗаполнитьОценкиОтветов()
    пер ОценкиОтветов = Объект.ОценкиОтветов
    ОценкиОтветов.Очистить()
    для Вариант из ПравильныеВарианты
        ОценкиОтветов.Добавить(новый ОценкаОтвета(Вопрос = ВопросПоИд(Вариант.ИдВопроса), ПравильныйОтветВариантОтвета = ВариантПоИд(Вариант.ИдВарианта), КоличествоБаллов = Вариант.КоличествоБаллов))
    ;
;

@НаСервере @ДоступноСКлиента
статический метод ВопросПоИд(ИдВопроса: Ууид?): ВопросыШаблоновАнкет.Ссылка?
    если ИдВопроса == Неопределено
        возврат Неопределено
    ;

    возврат ВопросыШаблоновАнкет.ПолучитьСсылку(ИдВопроса)
;

@НаСервере @ДоступноСКлиента
статический метод ВариантПоИд(ИдВарианта: Ууид?): ВариантыОтветов.Ссылка?
    если ИдВарианта == Неопределено
        возврат Неопределено
    ;

    возврат ВариантыОтветов.ПолучитьСсылку(ИдВарианта)
;

@НаСервере @ДоступноСКлиента
статический метод ШаблонОбъект(ШаблонСсылка: ШаблоныАнкет.Ссылка?, ЭтоНовый: Булево): ШаблоныАнкет.Объект

    если ЭтоНовый
        возврат новый ШаблоныАнкет.Объект()
    иначе
        возврат ШаблонСсылка.ЗагрузитьОбъект()
    ;
    
;

@НаСервере @ДоступноСКлиента
статический метод КопируемыйОбъект(КопируемыйТест: Тесты.Ссылка): Тесты.Объект
    возврат КопируемыйТест.ЗагрузитьОбъект()
;