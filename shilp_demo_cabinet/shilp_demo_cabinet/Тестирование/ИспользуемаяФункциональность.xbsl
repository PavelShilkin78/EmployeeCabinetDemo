импорт КонтрольДоступа

//Область РазрешенияДоступа

@Обработчик
метод ВычислитьРазрешенияДоступа(): Массив<РазрешениеДоступа>
    возврат КонтрольДоступа.РегистрСведенийРедактированиеАдминистраторуЗагружаемый()
;

//КонецОбласти

@ВПодсистеме
метод ФункциональныеОпции(): Тестирование.ФункциональныеОпции
    пер ФункциональныеОпции: Тестирование.ФункциональныеОпции
    знч Запрос = Запрос{
        Выбрать 
            Функциональность, 
            Использование
        Из ИспользуемаяФункциональность}
    исп Результат = Запрос.Выполнить()
    для РезультатСтрока из Результат
        УстановитьЗначение(ФункциональныеОпции, РезультатСтрока.Функциональность, РезультатСтрока.Использование)
    ;
    возврат ФункциональныеОпции
;

метод УстановитьЗначение(ФункциональныеОпции: Тестирование.ФункциональныеОпции, 
    Функциональность: Функциональность, Значение: Булево)
    
    выбор Функциональность
    когда ИспользованиеПодсистемы
        ФункциональныеОпции.ИспользованиеПодсистемы = Значение
    ;
;

@ВПодсистеме
метод ЗаписатьФункциональныеОпции(ФункциональныеОпции: Тестирование.ФункциональныеОпции) 
    ЗаписатьЗначение(Функциональность.ИспользованиеПодсистемы, ФункциональныеОпции.ИспользованиеПодсистемы)
;

метод ЗаписатьЗначение(Функциональность: Функциональность, Значение: Булево?)
    если Значение == Неопределено
        возврат
    ;
    
    знч НаборЗаписей = новый ИспользуемаяФункциональность.НаборЗаписей()
		НаборЗаписей.Фильтр.Функциональность.Установить(Функциональность)
		НаборЗаписей.ДобавитьЗапись(
            Функциональность = Функциональность,
            Использование = Значение
        )
    НаборЗаписей.Записать()      
;

@ВПодсистеме
метод ПодсистемаИспользуется(): Булево
    знч Запрос = Запрос{
        Выбрать Первые 1
            Использование
        Из ИспользуемаяФункциональность
        Где Функциональность == %{Функциональность.ИспользованиеПодсистемы}}
    исп Результат = Запрос.Выполнить()
    возврат Результат.ЕдинственныйИлиУмолчание()?.Использование ?? Ложь
;