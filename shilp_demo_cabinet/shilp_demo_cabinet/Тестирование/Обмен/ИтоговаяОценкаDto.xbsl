@ВПодсистеме
структура ИтоговаяОценка_2_0
    @JsonСвойство("name")
    обз знч Наименование: Строка
    @JsonСвойство("ID")
    пер Ид: Строка?
    @JsonСвойство("description")
    пер Описание: Строка?
    @JsonСвойство("minPoints")
    пер МинимальноеКоличествоБаллов: Число?
    @JsonСвойство("markSmile")
    пер СмайликОценки: ВидыСмайликовОценкиJson?  
;

перечисление ВидыСмайликовОценкиJson
    noSelect,
    happy,
    smile,
    sad,
    neutral    
;

конст ВИДЫ_СМАЙЛИКОВ_ОЦЕНКИ_JSON = {
    ВидыСмайликовОценкиJson.noSelect: ВидыСмайликовОценки.НеВыбран,
    ВидыСмайликовОценкиJson.happy: ВидыСмайликовОценки.Радость,
    ВидыСмайликовОценкиJson.smile: ВидыСмайликовОценки.Улыбка,
    ВидыСмайликовОценкиJson.sad: ВидыСмайликовОценки.Грусть,
    ВидыСмайликовОценкиJson.neutral: ВидыСмайликовОценки.Нейтральный       
}

метод ВидыСмайликовОценкиВJson(ВидСмайликаОценки: ВидыСмайликовОценки): ВидыСмайликовОценкиJson?
    для КлючИЗначение из ВИДЫ_СМАЙЛИКОВ_ОЦЕНКИ_JSON
        если КлючИЗначение.Значение == ВидСмайликаОценки
            возврат КлючИЗначение.Ключ
        ;     
    ;    
;

метод ВидыСмайликовОценкиИзJson(ВидСмайликаОценкиJson: ВидыСмайликовОценкиJson): ВидыСмайликовОценки
    возврат ВИДЫ_СМАЙЛИКОВ_ОЦЕНКИ_JSON[ВидСмайликаОценкиJson]
;

@ВПодсистеме
метод ЗаполнитьПоJson(СериализованнаяИтоговаяОценка: ИтоговаяОценка_2_0): ИтоговаяОценка
    знч ИтоговаяОценка: ИтоговаяОценка
    ИтоговаяОценка.Наименование = СериализованнаяИтоговаяОценка.Наименование
    если СериализованнаяИтоговаяОценка.Ид != Неопределено
        ИтоговаяОценка.ИдОценки = новый Ууид(СериализованнаяИтоговаяОценка.Ид)
    иначе
        ИтоговаяОценка.ИдОценки = Неопределено
    ;
    ИтоговаяОценка.Описание = СериализованнаяИтоговаяОценка.Описание ?? ""
    ИтоговаяОценка.МинимальноеКоличествоБаллов = СериализованнаяИтоговаяОценка.МинимальноеКоличествоБаллов ?? 0
    если СериализованнаяИтоговаяОценка.СмайликОценки != Неопределено
        ИтоговаяОценка.СмайликОценки = ВидыСмайликовОценкиИзJson(СериализованнаяИтоговаяОценка.СмайликОценки)
    иначе
        ИтоговаяОценка.СмайликОценки = ВидыСмайликовОценки.НеВыбран
    ;
    
    возврат ИтоговаяОценка
;

@ВПодсистеме
метод ПолучитьJson_V2_0(ИтоговаяОценка: ИтоговаяОценка): ИтоговаяОценка_2_0

    пер СериализованнаяИтоговаяОценка = новый ИтоговаяОценка_2_0(ИтоговаяОценка.Наименование)
    
    если ИтоговаяОценка.ИдОценки == Неопределено
        СериализованнаяИтоговаяОценка.Ид = Неопределено  
    иначе
        СериализованнаяИтоговаяОценка.Ид = ИтоговаяОценка.ИдОценки.ВСтроку()
    ;
    СериализованнаяИтоговаяОценка.Описание = ИтоговаяОценка.Описание
    СериализованнаяИтоговаяОценка.МинимальноеКоличествоБаллов = ИтоговаяОценка.МинимальноеКоличествоБаллов 
    СериализованнаяИтоговаяОценка.СмайликОценки = ВидыСмайликовОценкиВJson(ИтоговаяОценка.СмайликОценки)
    возврат СериализованнаяИтоговаяОценка
;