импорт Анкетирование
импорт ИнтерфейсныеКомпонентыСтандартные

@Обработчик
метод ПослеСоздания()
    ИзмениласьПравильностьОтвета.ПодключитьОбработчик((метод (ИдВопроса: Ууид, ИдВарианта: Ууид, Правильный: Булево, КоличествоБаллов: Число) ->
        если не Правильный
            УдалитьВариант(ИдВарианта)
        возврат
        ;
        ИзменитьПравильныйОтвет(ИдВопроса, ИдВарианта, Правильный, КоличествоБаллов)    
    ;))
;

метод ИзменитьПравильныйОтвет(ИдВопроса: Ууид, ИдВарианта: Ууид, Правильный: Булево, КоличествоБаллов: Число)
    
    пер НайденныйВариант = НайтиВариант(ИдВарианта)
    
    если НайденныйВариант == Неопределено
        ДобавитьВариант(ИдВопроса, ИдВарианта, КоличествоБаллов)
    иначе
        ИзменитьВариант(НайденныйВариант, КоличествоБаллов)
    ;

;

метод ДобавитьВариант(ИдВопроса: Ууид, ИдВарианта: Ууид, КоличествоБаллов: Число)
    ПравильныеВарианты.Добавить(новый ПравильныйВариант(ИдВопроса, ИдВарианта, КоличествоБаллов))
;

метод ИзменитьВариант(НайденныйВариант: ПравильныйВариант, КоличествоБаллов: Число)
    НайденныйВариант.КоличествоБаллов = КоличествоБаллов
;

метод НайтиВариант(ИдВарианта: Ууид): ПравильныйВариант?
    
    пер НайденныйВариант: ПравильныйВариант?
    для Вариант из ПравильныеВарианты
        если Вариант.ИдВарианта == ИдВарианта
            НайденныйВариант = Вариант    
        ;
    ;
    
    возврат НайденныйВариант
;

метод УдалитьВариант(ИдВарианта: Ууид)
    пер Удаляемые: Массив<ПравильныйВариант>
    для Вариант из ПравильныеВарианты
        если Вариант.ИдВарианта == ИдВарианта
            Удаляемые.Добавить(Вариант)
        ;
    ;
    
    ПравильныеВарианты.УдалитьВсе(Удаляемые)
    
;

метод ПравильныеОтветыМаксимальнаяШирина(): Число
    возврат 135 * Метрики.РАЗМЕР_ПОЛУИНТЕРВАЛА
;

метод УстановкаОценокПослеСозданияСтроки(Источник: Список<ИсточникДанныхМассив<ДанныеЭлементаКонструктора>>, Событие: СобытиеСДанными<СтрокаСписка<ДанныеЭлементаКонструктора>>)
    пер ДанныеВарианта = Событие.Данные как ВыборВариантаОтветаСтрока
    ДанныеВарианта.ПравильныеВарианты = ПравильныеВарианты
    ДанныеВарианта.Инициализировать()
;

метод ПравильныеОтветыДанные(): Массив<ДанныеЭлементаКонструктора>
    пер Данные: Массив<ДанныеЭлементаКонструктора>
    Данные.ДобавитьВсе(ВопросыШаблона)
    возврат Данные
;