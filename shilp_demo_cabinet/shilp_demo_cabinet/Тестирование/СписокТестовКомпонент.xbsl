импорт Анкетирование
импорт ИнтерфейсныеКомпонентыСтандартные
импорт СтандартноеПриложение

@Обработчик
метод ПослеСоздания()
    ОбновленСписокШаблонов.ПодключитьОбработчик((метод () ->
        Компоненты.Тесты.Обновить()
    ;))
;

метод ТестыПриНажатииСтроки(Источник: Список<ДинамическийСписок>, Событие: СобытиеСДанными<СтрокаСписка<СтрокаДинамическогоСписка>>)
    ПриВыбореТеста(новый СобытиеСДанными<Тесты.Ссылка>(Событие.Данные.ДанныеСтроки.Данные["Тест"] как Тесты.Ссылка))
;

метод ОтправитьПриНажатии(Команда: ОбычнаяКоманда, ДанныеСтроки: Объект?)
    знч Тест = (ДанныеСтроки как СтрокаДинамическогоСписка).Данные["Тест"] как Тесты.Ссылка?
    знч НаименованиеШаблона = (ДанныеСтроки как СтрокаДинамическогоСписка).Данные["Наименование"] как Строка
    ФормаСозданияТеста.ОткрытьВМодальномОкне(Тест = Тест, НаименованиеТеста = НаименованиеШаблона)
;

метод КопироватьПриНажатии(Команда: ОбычнаяКоманда, ДанныеСтроки: Объект?)
    знч КопируемыйТест = (ДанныеСтроки как СтрокаДинамическогоСписка).Данные["Тест"] как Тесты.Ссылка?
    ИнтерфейсныеКомпоненты.ОткрытьЗамещающуюФорму(СтандартноеПриложение.ФормыРазделовСтандартногоПриложения().Настройки, новый ТестыФормаОбъекта(КопируемыйТест = КопируемыйТест))
;

метод УдалитьПриНажатии(Команда: ОбычнаяКоманда, ДанныеСтроки: Объект?)
    знч Шаблон = (ДанныеСтроки как СтрокаДинамическогоСписка).Данные["Шаблон"] как ШаблоныАнкет.Ссылка?
    
    если Шаблон == Неопределено
        возврат
    ;
    
    если Анкетирование.ПоШаблонуСозданыАнкеты(Шаблон!)
        СтандартныеФормы.ПоказатьУведомление("Ошибка", "Удаление невозможно. По этому шаблону уже выполнялось тестирование.", ОценкаИнформации.Отрицательная)
        возврат
    ;
    
    знч Ответ = ЗапросДействия.ОткрытьВМодальномОкне(ОжидатьЗакрытия = Истина, Текст = "Вы уверены, что хотите удалить тест?")
    если Ответ != КнопкиМодальныхДиалогов.Да
        возврат
    ;
    
    УдалитьТест((ДанныеСтроки как СтрокаДинамическогоСписка).Данные["Тест"] как Тесты.Ссылка)
    Компоненты.Тесты.Обновить()
;

метод МенюСтрокиВидимость(): Булево
    возврат Тестирование.ЕстьПолномочиеРедактированиеТестов()
;

@НаСервере @ДоступноСКлиента
статический метод УдалитьТест(Тест: Тесты.Ссылка)
    Тест.ЗагрузитьОбъект().Удалить()
;