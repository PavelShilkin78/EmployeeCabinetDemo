импорт Анкетирование
импорт ОбщиеСтандартные
импорт УниверсальныеМеханизмы
импорт ИнтерфейсныеКомпонентыСтандартные

@Обработчик
метод ПослеЧтения()
    
    ДанныеТеста = Тестирование.ДанныеТеста(Объект.Тест)
    Анкета = Объект.Анкета
    пер РеквизитыАнкеты = МенеджерСущности.ЗначенияРеквизитовОбъекта(Анкета, ["Статус", "Шаблон", "Респондент"])
          
    ДанныеАнкеты = Анкетирование.ДанныеАнкеты(Анкета,
                                             РеквизитыАнкеты["Статус"] как СтатусыАнкет,
                                             ДанныеТеста.Шаблон)
    
    ЗаполнитьСтруктуруДанныхШаблона(РеквизитыАнкеты["Шаблон"] как ШаблоныАнкет.Ссылка)

    если не ДанныеШаблона.ОтображатьЗавершение
        ДанныеШаблона.ОтображатьЗавершение = Истина
        ДанныеШаблона.ЗаголовокЗавершения = "Результаты теста"
    ;
    ТекущийПользовательЯвляетсяРеспондентом = УниверсальныеМеханизмы.ПараметрыСеанса().РеспондентыТекущегоПользователя.Содержит(РеквизитыАнкеты["Респондент"] как РеспондентАнкетирования.Ссылка)
    ДанныеШаблона.Разделы = Анкетирование.РазделыШаблона(ДанныеТеста.Шаблон)
    
    ЗаголовокКнопкиСохранения = ТекущийПользовательЯвляетсяРеспондентом ? "Отправить" : "Завершить просмотр"
    
    если Объект.Статус == СостоянияТестирования.Завершено
        ДанныеШаблона.ТекущийРаздел= ДанныеШаблона.Разделы.Последний()
        Компоненты.Анкета.ОтображатьЗавершение = Истина
        если не ОценкаДобавлена
            пер ОценкаЗаТест = Тестирование.ОценкаЗаТест(ДанныеТеста, ДанныеАнкеты)
            Тестирование.ОтобразитьОценку(Компоненты.Анкета, ДанныеТеста, ОценкаЗаТест)    
        ;
    иначе если ДанныеШаблона.Разделы.Размер() > 0
        ДанныеШаблона.ТекущийРаздел= ДанныеШаблона.Разделы[0]
    ;
    
    ИнтерфейсныеКомпоненты.ИнициализироватьПодчиненныеКомпоненты(Компоненты)

    ЧтениеДанныхЗавершено = Истина
;


метод СохранитьРезультатТестирования(Источник: АнкетаКомпонент, Событие: СобытиеСДанными<СтатусыАнкет>)
    
    Объект.ПредставлениеРезультата = ДанныеШаблона.Наименование
    выбор Событие.Данные
    когда Заполняется
        Объект.Статус = СостоянияТестирования.Выполняется
    когда Заполнена
        ОценкаДобавлена = Истина
        знч ОценкаЗаТест = Тестирование.ОценкаЗаТест(ДанныеТеста, ДанныеАнкеты)
        Тестирование.ОтобразитьОценку(Источник, ДанныеТеста, ОценкаЗаТест)
        Объект.Статус = СостоянияТестирования.Завершено
        Объект.КоличествоБаллов = ОценкаЗаТест.НабраноБаллов
        Объект.ИтоговаяОценка = ОценкаЗаТест.ИтоговаяОценка    
    ;
    СохранитьАнкету(Анкета, ДанныеАнкеты, Событие.Данные)
    
    Записать()
;

@НаСервере @ДоступноСКлиента
статический метод СохранитьАнкету(Анкета: Анкеты.Ссылка?, ДанныеАнкеты: Массив<ДанныеЭлементаКонструктора>, Статус: СтатусыАнкет)
    Анкетирование.УдалитьПредыдущиеОтветы(Анкета)
    Анкетирование.СохранитьОтветы(Анкета, ДанныеАнкеты)
    пер АнкетаОбъект = Анкета.ЗагрузитьОбъект()
    АнкетаОбъект.Статус = Статус
    АнкетаОбъект.Записать()
;

метод АнкетаЗакрытиеКомпонента(Источник: АнкетаКомпонент, Событие: неизвестно)
    Закрыть()
    ЗакрытиеАнкеты.Оповестить(этот)
;

метод ЗаполнитьСтруктуруДанныхШаблона(Шаблон: ШаблоныАнкет.Ссылка?)
    пер РеквизитыШаблона = МенеджерСущности.ЗначенияРеквизитовОбъекта(Шаблон, ["Наименование", "КартинкаПриветствия", "ТекстПриветствия", "КоличествоРазделов", "Приветствие", "ЗаголовокПриветствия", "Завершение", "ЗаголовокЗавершения", "ТекстЗавершения", "КартинкаЗавершения"])    
    ДанныеШаблона.Ссылка = Шаблон
    ДанныеШаблона.Наименование = РеквизитыШаблона["Наименование"]
    ДанныеШаблона.ЗаголовокПриветствия = РеквизитыШаблона["ЗаголовокПриветствия"]
    ДанныеШаблона.КартинкаПриветствия = РеквизитыШаблона["КартинкаПриветствия"]
    ДанныеШаблона.ТекстПриветствия = РеквизитыШаблона["ТекстПриветствия"]
    ДанныеШаблона.ЗаголовокЗавершения = РеквизитыШаблона["ЗаголовокЗавершения"]
    ДанныеШаблона.КартинкаЗавершения= РеквизитыШаблона["КартинкаЗавершения"]
    ДанныеШаблона.ТекстЗавершения = РеквизитыШаблона["ТекстЗавершения"]
    ДанныеШаблона.КоличествоРазделов = РеквизитыШаблона["КоличествоРазделов"]
    ДанныеШаблона.ОтображатьПриветствие = РеквизитыШаблона["Приветствие"]
    ДанныеШаблона.ОтображатьЗавершение = РеквизитыШаблона["Завершение"]
;