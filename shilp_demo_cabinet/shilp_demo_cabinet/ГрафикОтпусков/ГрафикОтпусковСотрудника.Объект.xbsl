импорт ФизическиеЛица

//Область ОбработчикиСобытий

@Обработчик
@ВыполнятьПриЗагрузкеДанных
метод ПриЗаполнении()
    ЗаполнитьМоментСоздания()
;

@Обработчик
@ВыполнятьПриЗагрузкеДанных
метод ПередЗаписью(ПрежнееЗначение: ГрафикОтпусковСотрудника.Данные, ПараметрыЗаписи: ГрафикОтпусковСотрудника.ПараметрыЗаписи)
    ЗаполнитьПредставление()
    ЗаполнитьМоментСоздания()
;

@Обработчик
@ВыполнятьПриЗагрузкеДанных
метод ПередУдалением(ПараметрыУдаления: ГрафикОтпусковСотрудника.ПараметрыУдаления)
    исп Транзакции.Начать()
    знч Запрос = Запрос{
        Выбрать 
            ПериодыОтпуска.Ссылка Как ПериодОтпуска
        Из 
            ГрафикОтпусковСотрудника Как ГрафикОтпусковСотрудника
            Внутреннее Соединение ПериодыОтпуска Как ПериодыОтпуска 
                По ГрафикОтпусковСотрудника.ФизическоеЛицо == ПериодыОтпуска.ФизическоеЛицо
                    и ГрафикОтпусковСотрудника.Ссылка == %Ссылка
            Внутреннее соединение ГрафикОтпусковСтруктурнойЕдиницы КАК ГрафикОтпусковСтруктурнойЕдиницы
                Внутреннее соединение ГрафикОтпусковПредприятия КАК ГрафикОтпусковПредприятияСсылка
                    ПО ГрафикОтпусковСтруктурнойЕдиницы.ГрафикОтпусковПредприятия == ГрафикОтпусковПредприятияСсылка.Ссылка
                ПО ГрафикОтпусковСотрудника.ГрафикОтпусковПодразделения == ГрафикОтпусковСтруктурнойЕдиницы.Ссылка  
                    и ГрафикОтпусковСотрудника.Ссылка == %Ссылка
        Где
            ПериодыОтпуска.ДатаНачала.Год == ГрафикОтпусковПредприятия.Год                
        }
    исп Результат = Запрос.Выполнить() 
    для РезультатСтрока из Результат
        пер ПериодОтпуска = РезультатСтрока.ПериодОтпуска.ЗагрузитьОбъект(Истина)
        ПериодОтпуска.Состояние = СостояниеГрафикаОтпусков.Черновик
        ПериодОтпуска.Записать()
    ;    
;

//КонецОбласти

//Область СлужебныеПроцедурыИФункции

метод ЗаполнитьПредставление()
    ПредставлениеДокумента = "Сбор графика отпусков %{ФизическоеЛицо.Представление()}"
;

метод ЗаполнитьМоментСоздания()
    если МоментСоздания == Неопределено
        МоментСоздания = Момент.Сейчас()
    ;
;

//КонецОбласти