импорт ЛичныеКабинетыПользователей
импорт ГрафикОтпусков

метод КолонкаЗначокЗаголовок(): Строка
    пер ДатаМесяц = Дата{1980-01-01}.ДобавитьМесяцы(НомерМесяца-1)
    возврат ДатаМесяц.Представление("МММ")
;

метод КолонкаЗначокПредставление(ДанныеСтроки: СборГрафиковОтпусков.ДанныеСоставленияГрафикаОтпусков): Строка
    если ФормФактор == ФормФакторГрафикаОтпусков.Полный
        возврат ДанныеСтроки.ЗапланированоПоМесяцам[НомерМесяца-1].ПредставлениеЗначкаМесяц        
    иначе если ФормФактор == ФормФакторГрафикаОтпусков.Одинарный
        возврат ДанныеСтроки.ЗапланированоПредставлениеОбщее
    иначе если НомерМесяца == 3
        возврат НомерМесяца - 1 <= ДанныеСтроки.ЗапланированоПоПериодам.Граница() ? ДанныеСтроки.ЗапланированоПоПериодам[НомерМесяца-1].ПредставлениеЗначкаОстаток : ""
    иначе
        возврат НомерМесяца - 1 <= ДанныеСтроки.ЗапланированоПоПериодам.Граница() ? ДанныеСтроки.ЗапланированоПоПериодам[НомерМесяца-1].ПредставлениеЗначкаПериод : ""
    ;
; 

метод ЗначокВидимость(ДанныеСтроки: СборГрафиковОтпусков.ДанныеСоставленияГрафикаОтпусков): Булево
    если не ОтображатьЗначки
            или ДанныеСтроки.ОтпускНеПланирует
            или не ДанныеСтроки.ЛичныйКабинетСоздан
        возврат Ложь
    ;
    если ФормФактор == ФормФакторГрафикаОтпусков.Полный
        возврат ДанныеСтроки.ЗапланированоПоМесяцам[НомерМесяца-1].КоличествоДнейВМесяце > 0
    иначе если ФормФактор == ФормФакторГрафикаОтпусков.Одинарный 
        возврат НомерМесяца == 1 и ДанныеСтроки.ЗапланированоПоПериодам.Размер() > 0
    иначе
        возврат НомерМесяца - 1 <= ДанныеСтроки.ЗапланированоПоПериодам.Граница() ? ДанныеСтроки.ЗапланированоПоПериодам[НомерМесяца-1].КоличествоДнейВПериоде > 0 : Ложь
    ;
; 

метод СтилиОформленияЗначокМесяц(ДанныеСтроки: СборГрафиковОтпусков.ДанныеСоставленияГрафикаОтпусков): ОценкаИнформации
    возврат СборГрафиковОтпусков.КоннотацияПоСостоянию(ТекущееСостояниеЗапланированногоОтпуска())
;

метод СогласоватьПриНажатии(Источник: Надпись, Событие: СобытиеПриНажатии)
    если (ДанныеСтроки как СборГрафиковОтпусков.ДанныеСоставленияГрафикаОтпусков).ОтпускНеПланирует
        или (ДанныеСтроки как СборГрафиковОтпусков.ДанныеСоставленияГрафикаОтпусков).ПервичноеСостояниеКампанииПодразделения == СостояниеГрафикаОтпусков.Утверждено
        возврат
    ;
    если (ДанныеСтроки как СборГрафиковОтпусков.ДанныеСоставленияГрафикаОтпусков).ОтветственныйГрафикаСтруктурнойЕдиницы != ЛичныеКабинетыПользователей.ПараметрыСеанса().ТекущееФизическоеЛицо
        возврат
    ;
    СборГрафиковОтпусков.ЗаполнитьПунктыМенюСогласования(Согласовать, ТекущееСостояниеЗапланированногоОтпуска())
    Согласовать.Открыть()
;

метод ТекущееСостояниеЗапланированногоОтпуска(): СостояниеГрафикаОтпусков?
    пер СостояниеЗапланированногоОтпуска: СостояниеГрафикаОтпусков? 
    если ФормФактор == ФормФакторГрафикаОтпусков.Полный
        СостояниеЗапланированногоОтпуска = (ДанныеСтроки как СборГрафиковОтпусков.ДанныеСоставленияГрафикаОтпусков).ЗапланированоПоМесяцам[НомерМесяца-1].СостояниеЗапланированногоОтпуска
    иначе если ФормФактор == ФормФакторГрафикаОтпусков.Одинарный или НомерМесяца == 3
        СостояниеЗапланированногоОтпуска = (ДанныеСтроки как СборГрафиковОтпусков.ДанныеСоставленияГрафикаОтпусков).ЗапланированоСостояниеОбщее
    иначе
        СостояниеЗапланированногоОтпуска = НомерМесяца - 1 <= (ДанныеСтроки как СборГрафиковОтпусков.ДанныеСоставленияГрафикаОтпусков).ЗапланированоПоПериодам.Граница() ? (ДанныеСтроки как СборГрафиковОтпусков.ДанныеСоставленияГрафикаОтпусков).ЗапланированоПоПериодам[НомерМесяца-1].СостояниеЗапланированногоОтпуска : Неопределено
    ;
    возврат СостояниеЗапланированногоОтпуска
;

метод СогласоватьПриВыбореЭлемента(Компонент: ВсплывающееМеню, СобытиеСДанными: СобытиеСДанными<ЭлементМеню>)
    пер СостояниеСогласования = СобытиеСДанными.Данные.ДополнительныеПараметры как СостояниеГрафикаОтпусков
    Согласовать.Закрыть()
    пер ТекущиеДанные = ДанныеСтроки как СборГрафиковОтпусков.ДанныеСоставленияГрафикаОтпусков
    пер ПериодыОтпуска = новый Массив<ПериодыОтпуска.Ссылка>()
    если ФормФактор == ФормФакторГрафикаОтпусков.Полный
        ПериодыОтпуска.Добавить(ТекущиеДанные.ЗапланированоПоМесяцам[НомерМесяца-1].ПериодОтпускаСсылка)
    иначе если ФормФактор == ФормФакторГрафикаОтпусков.Одинарный
        для Период из ТекущиеДанные.ЗапланированоПоПериодам
            ПериодыОтпуска.Добавить(Период.ПериодОтпускаСсылка)
        ;        
    иначе если НомерМесяца == 3  
        пер ИндексПериода = НомерМесяца - 1
        пер КоличествоПериодов = ТекущиеДанные.ЗапланированоПоПериодам.Граница()
        пока ИндексПериода <= КоличествоПериодов
            ПериодыОтпуска.Добавить(ТекущиеДанные.ЗапланированоПоПериодам[ИндексПериода].ПериодОтпускаСсылка)
            ИндексПериода += 1
        ;
    иначе
        если НомерМесяца - 1 <= ТекущиеДанные.ЗапланированоПоПериодам.Граница()
            ПериодыОтпуска.Добавить(ТекущиеДанные.ЗапланированоПоПериодам[НомерМесяца - 1].ПериодОтпускаСсылка)
        ;
    ; 
    если не ПериодыОтпуска.Пусто()
        СборГрафиковОтпусков.ИзменитьСостояниеПериодовОтпуска(
            СостояниеСогласования, 
            ПериодыОтпуска)
        ИзмененЗапланированныйОтпускФизическогоЛица.Оповестить()
    ;
;
