импорт ВебApi
импорт ФизическиеЛица

//Область РазрешенияДоступа
@Обработчик
метод ВычислитьРазрешенияДоступа(): Массив<РазрешениеДоступа>
    возврат ГрафикОтпусковПоставщикОграничений.ПолучитьСервис().ПериодыОтпускаРазрешения()
;

@Обработчик
метод ВычислитьРазрешенияДоступаДляОбъектов(Элементы: ЧитаемыйМассив<ПериодыОтпуска.ДанныеРасчетаРазрешений>): ЧитаемоеСоответствие<ПериодыОтпуска.ДанныеРасчетаРазрешений, ЧитаемаяКоллекция<РазрешениеДоступа>>
    возврат ГрафикОтпусковПоставщикОграничений.ПолучитьСервис().ПериодыОтпускаРазрешенияДляОбъектов(Элементы)
;

//КонецОбласти

//Область СлужебныйПрограммныйИнтерфейс

@ВПроекте
структура ПериодОтпуска
    @JsonСвойство("personID")
    обз знч ФизическоеЛицо: Строка 
    @JsonСвойство("startDate")
    обз знч ДатаНачала: Дата
    @JsonСвойство("endDate")
    обз знч ДатаОкончания: Дата
;

@ВПодсистеме
метод ДанныеПериодовВJson(Ссылки: ЧитаемаяКоллекция<ПериодыОтпуска.Ссылка>, Сервисы: ВебApiСервер.СервисыУпаковкиJson): Массив<ПериодОтпуска>
    знч Данные: Массив<ПериодОтпуска>
    для Период из Ссылки
        Данные.Добавить(ДанныеПериодаВJson(Период, Сервисы))        
    ;
    возврат Данные
;
    
метод ДанныеПериодаВJson(ПериодОтпуска: ПериодыОтпуска.Ссылка, Сервисы: ВебApiСервер.СервисыУпаковкиJson): ПериодОтпуска
    исп ПривилегированныйРежим: КонтекстДоступа = КонтекстДоступа.Привилегированный()
    знч ПериодОтпускаОбъект = ПериодОтпуска.ЗагрузитьОбъект(Ложь)
    ПривилегированныйРежим.Закрыть()
    знч ДанныеВJson = новый ПериодОтпуска(
        Сервисы.ПолучитьИдентификатор(ПериодОтпускаОбъект.ФизическоеЛицо),
        ПериодОтпускаОбъект.ДатаНачала,
        ПериодОтпускаОбъект.ДатаОкончания
    )     
    возврат ДанныеВJson    
;   

@ВПроекте
метод ЗагрузитьОбъектИзJson(ДанныеJson: ПериодОтпуска, Сервисы: ВебApiСервер.СервисыЗагрузкиJson)
    пер ПериодОтпуска: ПериодыОтпуска.Объект
    ПериодОтпуска.ФизическоеЛицо = Сервисы.ПолучитьСсылку(ФизическиеЛица.ВнешняяСсылка(ДанныеJson.ФизическоеЛицо)) как ФизическиеЛица.Ссылка
    ПериодОтпуска.ДатаНачала = ДанныеJson.ДатаНачала
    ПериодОтпуска.ДатаОкончания = ДанныеJson.ДатаОкончания
    ПериодОтпуска.Состояние = СостояниеГрафикаОтпусков.Утверждено   
    ПериодОтпуска.Записать() 
;

//КонецОбласти