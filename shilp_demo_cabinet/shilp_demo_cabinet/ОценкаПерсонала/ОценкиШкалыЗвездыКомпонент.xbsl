импорт ИнтерфейсныеКомпонентыСтандартные

@Обработчик
метод ПослеСоздания()
    ВывестиОтображениеЗвезд()    
    ОбновитьОтображениеЗвезд() 
;

метод ВывестиОтображениеЗвезд()
    пер ОценкиШкалы: Массив<ОценкиШкалы> = 
        ОценкиШкалы(ШкалаОценки)
    КоличествоУровнейШкалы = ОценкиШкалы.Размер()
    // картинки звезд
    для ОценкаШкалы из ОценкиШкалы
        пер КомпонентЗвезда = новый Картинка()
        КомпонентЗвезда.Изображение = ОценкаЗвездаИзображение(
            ДанныеСтроки, 
            ОценкаШкалы.Уровень
        )        
        КомпонентЗвезда.Цвет = БиблиотекаЦветов.Цвет(БиблиотекаЦветов.ВидыЦвета.КнопкаПоУмолчанию)
        если не РежимПросмотра
            КомпонентЗвезда.ОбрабатыватьНажатие = Истина
            КомпонентЗвезда.ПриНажатии = 
                (Источник, Событие) -> ПриНажатииКартинки(Источник, Событие, ДанныеСтроки)
        ;
        Компоненты.ОценкаЗвездыГруппа.Содержимое.Добавить(КомпонентЗвезда)
    ;
    // картинка сброс
    если не РежимПросмотра
        пер КомпонентКрестик = новый Картинка()
        КомпонентКрестик.Изображение = Ресурс{Крестик.svg}.Ссылка
        КомпонентКрестик.Цвет = БиблиотекаЦветов.Цвет(БиблиотекаЦветов.ВидыЦвета.Серый)
        КомпонентКрестик.Ширина = 64
        КомпонентКрестик.ВыравниваниеВГруппеПоГоризонтали = ВыравниваниеПоГоризонтали.Конец
        КомпонентКрестик.ОбрабатыватьНажатие = Истина
        КомпонентКрестик.ПриНажатии = 
            (Источник, Событие) -> ПриНажатииКартинки(Источник, Событие, ДанныеСтроки)
        Компоненты.ОценкаЗвездыГруппа.Содержимое.Добавить(КомпонентКрестик)
    ;
;

метод ОбновитьОтображениеЗвезд()
    пер Уровень: Число
    для Звезда из Компоненты.ОценкаЗвездыГруппа.Содержимое
        Уровень += 1
        если КоличествоУровнейШкалы >= Уровень
            (Звезда как Картинка).Изображение = ОценкаЗвездаИзображение(ДанныеСтроки, Уровень)
        ; 
    ;
;

метод ОценкаЗвездаИзображение(ДанныеСтроки: Объект?, Уровень: Число): ДвоичныйОбъект.Ссылка? 
    если ДанныеСтроки == Неопределено
        возврат Ресурс{ЗвездаКонтур.svg}.Ссылка    
    иначе
        возврат Уровень <= (ДанныеСтроки как КомпетенцииЭкземпляраОценкиПоМетоду360).Оценка
                ? Ресурс{Звезда.svg}.Ссылка : Ресурс{ЗвездаКонтур.svg}.Ссылка
    ;
;

метод ПриНажатииКартинки(Источник: Картинка, Событие: СобытиеПриНажатии, ДанныеСтроки: КомпетенцииЭкземпляраОценкиПоМетоду360)
    пер ПорядокКартинки: Число = Компоненты.ОценкаЗвездыГруппа.Содержимое.Найти(Источник) + 1
    ДанныеСтроки.Оценка = КоличествоУровнейШкалы >= ПорядокКартинки ? ПорядокКартинки : 0
    ОбновитьОтображениеЗвезд()
;

@НаСервере @ДоступноСКлиента
статический метод ОценкиШкалы(ШкалаОценки: ШкалыОценок.Ссылка?): Массив<ОценкиШкалы>
    пер ОценкиШкалы: Массив<ОценкиШкалы>
    если ШкалаОценки != Неопределено        
        пер ШкалаОценкиОбъект = ШкалаОценки.ЗагрузитьОбъект(Ложь)
        ОценкиШкалы = ШкалаОценкиОбъект.Оценки.ПодМассив(0)
    ;
    возврат ОценкиШкалы 
;
