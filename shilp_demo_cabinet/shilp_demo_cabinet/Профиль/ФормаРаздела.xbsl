импорт ИнтерфейсныеКомпонентыСтандартные
импорт СтандартноеПриложение
импорт ВладельцыКабинетов
импорт Криптография
импорт ЛичныеКабинетыПользователей
импорт ФизическиеЛица
импорт Компания

@ВПроекте
@Реализация
метод Заголовок(): Строка
    возврат ""
;

@ВПроекте
@Реализация
метод КомандыСоздания(): Команда|ГруппаКомандногоИнтерфейса<Команда>|Неопределено
    возврат Неопределено
;

@ВПроекте
@Реализация
метод ДополнительныеКоманды(): ФрагментКомандногоИнтерфейса<Команда>?
    возврат ДополнительныеКомандыФормы
;

@ВПроекте
@Реализация
метод ОсновнаяКоманда(): Команда?
    возврат Неопределено
;

@ВПроекте
@Реализация
метод ОбычныеКоманды(): ФрагментКомандногоИнтерфейса<Команда>?
    возврат Неопределено
;

@Обработчик
метод ПослеСоздания()
    ФизическоеЛицо = ЛичныеКабинетыПользователей.ПараметрыСеанса().ТекущееФизическоеЛицо
    ФизическоеЛицоОбъект = ЗагрузитьФизическоеЛицо(ФизическоеЛицо)
    ИнтерфейсныеКомпоненты.ИнициализироватьПодчиненныеКомпоненты(Компоненты)
    ВизитнаяКарточка = ВизитнаяКарточкаНаСервере(ФизическоеЛицо)
;

метод QRПриНажатии(Команда: ОбычнаяКоманда)
    знч ВизитнаяКарточкаФорма = новый ВизитнаяКарточкаФорма(QRКод = ВизитнаяКарточка)
    ИнтерфейсныеКомпоненты.ОткрытьЗамещающуюФорму(этот, ВизитнаяКарточкаФорма)
;

метод ЗаглушкаЭлектроннаяПодписьВидимость(): Булево
    возврат (не ЭлектроннаяПодписьВидимость() и Криптография.ПараметрыСеанса().ИспользованиеКЭДО)
;

метод ЭлектроннаяПодписьВидимость(): Булево
    возврат Криптография.ПараметрыСеанса().ИспользованиеКЭДО
        и (ВладельцыКабинетов.ДоступноПодписаниеЭлектроннойПодписью(ФизическоеЛицо)
            или ВладельцыКабинетов.ПараметрыСеанса().ИспользуетКэдо)
;

метод ЭлектроннаяПодписьКомпонентВидимостьКнопкиУдалить(): Булево
    возврат не ВладельцыКабинетов.ПараметрыСеанса().ИспользуетКэдо
;

метод ФормаРазделаМаксимальнаяШирина(): Число
    возврат Содержимое.МаксимальнаяШирина
;

метод ФормаРазделаМинимальнаяШирина(): Число
    возврат Содержимое.МинимальнаяШирина
;

метод ПанельМинимальнаяШирина(): Число
    возврат ОсновнойИнтерфейс.МинимальнаяШиринаПанелейПодразделов()
;

метод ПанельМаксимальнаяШирина(): Число
    возврат ОсновнойИнтерфейс.МаксимальнаяШиринаПанелейПодразделов()
;

метод ГлавнаяГруппаУстановленнаяШиринаОбластиРазмещения(): Число
    возврат ОсновнойИнтерфейс.ШиринаОсновногоОкнаПриложения()
;

@НаСервере @ДоступноСКлиента
статический метод ЗагрузитьФизическоеЛицо(ФизическоеЛицо: ФизическиеЛица.Ссылка): ФизическиеЛица.Объект
    возврат ФизическоеЛицо.ЗагрузитьОбъект()    
;

@НаСервере @ДоступноСКлиента
статический метод ВизитнаяКарточкаНаСервере(ФизическоеЛицо: ФизическиеЛица.Ссылка): ДвоичныйОбъект.Ссылка
    знч ЗапрашиваемыеДанные: ФизЛица.ЗапрашиваемыеДанныеФизическихЛиц
    ЗапрашиваемыеДанные.РабочаяКонтактнаяИнформация = Истина
    ЗапрашиваемыеДанные.ЛичныеДанные = Истина
    пер ДанныеФизическогоЛица = ФизЛица.ДанныеФизическогоЛица(ФизическоеЛицо, ЗапрашиваемыеДанные)
    знч ДанныеСотрудника = Сотрудники.ДанныеОсновногоСотрудника(ФизическоеЛицо)
    пер ПредставлениеДолжности = ""
    если ДанныеСотрудника != Неопределено
        ПредставлениеДолжности = Сотрудники.ПредставлениеРабочегоМеста(
            ДанныеСотрудника.Позиция,
            ДанныеСотрудника.Должность,
            ДанныеСотрудника.МестоВСтруктуреПредприятия,
            ДанныеСотрудника.Организация,
            Ложь)
    ;
    знч СсылкаОписаниеРаботодателя = ОписаниеРаботодателя.Получить()
    знч ДанныеОписанияРаботодателя = ОписаниеРаботодателя.ДанныеОписанияРаботодателя([СсылкаОписаниеРаботодателя])
    пер ПредставлениеОрганизации = ""
    пер Адрес = ""
    пер Вебсайт = ""
    если ДанныеОписанияРаботодателя.СодержитКлюч(СсылкаОписаниеРаботодателя)
        знч ОписаниеРаботодателя = ДанныеОписанияРаботодателя.Получить(СсылкаОписаниеРаботодателя)
        ПредставлениеОрганизации = ОписаниеРаботодателя.Получить("Наименование") как Строка
        Адрес = ОписаниеРаботодателя.Получить("Локация") как Строка
        знч СайтКомпании = ОписаниеРаботодателя.Получить("СайтКомпании")
        если СайтКомпании это Строка
            Вебсайт = СайтКомпании как Строка
        ;
    ;
    знч Генератор = новый ГенераторQrКода(200, 200, УровеньКоррекцииQrКода.Низкий, ФорматИзображения.Png, Истина)
    знч БайтыКартинки = Генератор.КодироватьВизитку(
        ДанныеФизическогоЛица.ЛичныеДанные.ФИО.ПредставлениеИмени(),
        ДанныеФизическогоЛица.РабочаяКонтактнаяИнформация.МобильныйТелефон,
        ДанныеФизическогоЛица.РабочаяКонтактнаяИнформация.ЭлектроннаяПочта,
        ПредставлениеДолжности,
        ПредставлениеОрганизации,
        Адрес,
        Вебсайт,
        "")
    возврат ОбъектноеХранилище.ЗагрузитьИзБайт(БайтыКартинки, "image/png").Ссылка
;

метод ВизитнаяКарточкаВыравниваниеВГруппеПоГоризонтали(): ВыравниваниеПоГоризонтали | Авто
    возврат КлиентскоеУстройство.ВидИнтерфейса == ВидИнтерфейса.Телефон ? ВыравниваниеПоГоризонтали.Центр : Авто
;