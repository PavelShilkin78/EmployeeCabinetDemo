импорт УниверсальныеМеханизмы
импорт ОбщиеСтандартные

@ВПодсистеме
@НаСервере @НаКлиенте
перечисление ОшибкиПроверкиЗаполнения
    ПортНеЗаполнено,
    УчетнаяЗаписьНеЗаполнена,
    ПарольНеЗаполнено,
    СерверНеЗаполнено
;

метод ПроверитьЗаполнение()
    знч Ошибки = новый Множество<ОшибкиПроверкиЗаполнения>()
    если СерверПочты.Пусто()
        Ошибки.Добавить(ОшибкиПроверкиЗаполнения.СерверНеЗаполнено)   
    ;
    если УчетнаяЗапись.Пусто()
        Ошибки.Добавить(ОшибкиПроверкиЗаполнения.УчетнаяЗаписьНеЗаполнена)   
    ;
    если ПарольУчетнойЗаписи.Пусто()
        Ошибки.Добавить(ОшибкиПроверкиЗаполнения.ПарольНеЗаполнено)   
    ;
    если ПортСервераПочты == 0
        Ошибки.Добавить(ОшибкиПроверкиЗаполнения.ПортНеЗаполнено)   
    ;
    если Ошибки.Размер() > 0
        выбросить новый ПроверкаЗаполнения.ИсключениеПроверкиЗаполнения("При проверке заполнения выявлены ошибки", Ошибки = Ошибки)
    ;   
;

@Обработчик
метод ПередЗаписью(ПрежнееЗначение: УчетныеЗаписиПочты.Данные, ПараметрыЗаписи: УчетныеЗаписиПочты.ПараметрыЗаписи)
    если ЭтоНовый()
        ЗаполнитьПредставление()
    ;
    ПроверитьЗаполнение()
;

@Обработчик
метод ПослеЗаписи(До: УчетныеЗаписиПочты.Данные, ПараметрыЗаписи: УчетныеЗаписиПочты.ПараметрыЗаписи)
    исп КонтекстДоступа.Привилегированный()
    если СборПочты и Протокол != ПротоколыПочты.Smtp
        попытка 
            СбросПротоколаСбораПочты(этот.ВладелецПочтыПодсистема, этот.ВладелецПочтыОбъект, этот.Протокол)
        поймать Исключение: неизвестно
            пер УчетнаяЗаписьОбъект = Ссылка.ЗагрузитьОбъект()
            УчетнаяЗаписьОбъект.СборПочты = Ложь
            УчетнаяЗаписьОбъект.Записать()
            выбросить новый ИсключениеВыполнения("Сбор почты для учетной записи по протоколу %{До.Протокол.Представление()} не установлен")
        ;
    ;
;

метод ЗаполнитьПредставление()
    если Наименование.Пусто()
        Наименование = "%{АдресЭлектроннойПочты}"
        СтрокаПоиска = этот.ВладелецПочтыОбъект.Представление()
    ;
;

метод СбросПротоколаСбораПочты(Подсистема: Строка, Владелец: ВладелецЭлектроннойПочты.Ссылка?, Протокол: ПротоколыПочты)
    знч Запрос = Запрос{
        Выбрать
            Ссылка,
            Протокол
        Из 
            УчетныеЗаписиПочты
        Где 
            Протокол != ПротоколыПочты.Smtp
            И ВладелецПочтыОбъект == %Владелец
            И ВладелецПочтыПодсистема == %Подсистема
    }
    пер Результат = новый ЧитаемыйМассив(Запрос.Выполнить())
    если не Результат.Пусто()
        для РезультатСтрока из Результат
            если РезультатСтрока.Протокол != Протокол
                пер УчетнаяЗаписьОбъект = РезультатСтрока.Ссылка.ЗагрузитьОбъект()
                УчетнаяЗаписьОбъект.СборПочты = Ложь
                УчетнаяЗаписьОбъект.Записать()
            ;
        ;
    ;
;