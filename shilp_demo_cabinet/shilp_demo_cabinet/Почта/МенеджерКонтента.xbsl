импорт УниверсальныеМеханизмы
импорт ОбщиеСтандартные

@ВПодсистеме
метод ИзвлечьТекстовоеСодержимое(СодержимоеHtml: Строка): Строка
    пер Содержимое = СодержимоеHtml
                    .Заменить("</div>", "\н")
                    .Заменить("<div>", "")
                    .Заменить("<div", "\н<div")
                    .Заменить("</p>", "\н")
                    .Заменить("<br />", "\н")
                    .Заменить("<br>", "\н")
                    .Заменить("<BR>", "\н")
                    .Заменить("<BR />", "\н")
                    .Заменить("<p>", "")
                    .Заменить("<p", "\н<p")
                    
    Содержимое = УтилитыHtml.ИзвлечьТекстовоеСодержимое(Содержимое)
    возврат Содержимое
;

@ВПодсистеме
метод Содержание(ЧастиПисьмаПоВиду: ЧитаемыйМассив<ЧастьТекстаПисьма>): Соответствие<ФорматыПочтовогоСообщения, Строка>
    пер СодержаниеПоФорматам = новый Соответствие<ФорматыПочтовогоСообщения, Строка>()
    для Содержание из ЧастиПисьмаПоВиду
        знч ФорматСодержимого = КонструкторПисьма.ПолучитьФорматСодержимого(Содержание)
        СодержаниеПоФорматам.Вставить(ФорматСодержимого, Содержание.Текст)
    ;
    возврат СодержаниеПоФорматам
;

@ВПодсистеме
метод СоздатьПрисоединенныйФайл(ФайлДляЗагрузки: ВложениеПисьма): ПрисоединенныйФайл
    пер НовыйПрисоединенныйФайл = новый ПрисоединенныйФайл()
    НовыйПрисоединенныйФайл.Наименование = ФайлДляЗагрузки.Название
    НовыйПрисоединенныйФайл.Расширение = ФайлДляЗагрузки.Название.Содержит(".") ? ФайлДляЗагрузки.Название.Разделить(".")[1] : ""
    если не НовыйПрисоединенныйФайл.Расширение.Пусто()
        НовыйПрисоединенныйФайл.Наименование = НовыйПрисоединенныйФайл.Наименование.Заменить(
            "." + НовыйПрисоединенныйФайл.Расширение, "")
    ;
    НовыйПрисоединенныйФайл.АдресКартинкиРасширения = ПрисоединенныеФайлыМенеджер.АдресКартинкиРасширения(НовыйПрисоединенныйФайл.Расширение)
    НовыйПрисоединенныйФайл.Размер = ФайлДляЗагрузки.Данные.Размер().ВБайтах()
    НовыйПрисоединенныйФайл.ДатаСоздания = МенеджерДат.ТекущаяДатаВремя()
    пер Хранилище = ОбъектноеХранилище.ЗагрузитьИзБайт(ФайлДляЗагрузки.Данные)
    НовыйПрисоединенныйФайл.Хранилище = Хранилище.Ссылка

    возврат НовыйПрисоединенныйФайл
;
