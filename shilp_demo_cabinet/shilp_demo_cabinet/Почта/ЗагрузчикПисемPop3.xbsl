импорт Стд::ЭлектроннаяПочта
импорт СтандартноеПриложение

@Реализация
@ВПодсистеме
метод АктуальныйЗагрузчик(Подключение: ПодключениеПочтовогоСервера): Булево
    возврат Подключение это ПодключениеPop3
;

@Реализация
@ВПодсистеме
метод СинхронизироватьЗаголовки(Подключение: ПодключениеПочтовогоСервера, Фильтр: ФильтрЗаголовков)
    исп Соединение = ПолучитьСоединение(Подключение)
    если Соединение == Неопределено
        возврат
    ;
    для Заголовок из Соединение.ПолучитьЗаголовкиПисем()
        Фильтр.Применить(Заголовок.Заголовки)
        если Фильтр.УсловияВыполнены
            пер Входящее = ЭлектронноеПисьмо.Создать(Заголовок)
            Входящее.Записать()
        ;
    ;
;

@Реализация
@ВПодсистеме
метод Выгрузить(Подключение: ПодключениеПочтовогоСервера, Идентификатор: Объект, ОтметитьНаУдаление: Булево = Ложь): Письмо?
    исп Соединение = ПолучитьСоединение(Подключение)
    если Соединение != Неопределено
        для Заголовок из Соединение.ПолучитьЗаголовкиПисем()
            если Заголовок.Заголовки.ИдСообщения != Неопределено
                и Заголовок.Заголовки.ИдСообщения.Сравнить(Идентификатор.ВСтроку(), Истина) == 0
                знч ПолученоОтСервера = Соединение.ПолучитьПисьмо(Заголовок.Индекс)
                если ОтметитьНаУдаление
                    Соединение.ОтметитьНаУдаление([ПолученоОтСервера.Индекс])
                ;
                возврат ПолученоОтСервера.Письмо              
            ;
        ;
    ;
    возврат Неопределено
;

@Реализация
@ВПодсистеме
метод Заполнить(Подключение: ПодключениеПочтовогоСервера, Письма: Множество<ЭлектронноеПисьмо.Ссылка>)
    исп Соединение = ПолучитьСоединение(Подключение)
    если Соединение == Неопределено
        возврат
    ;
    пер ЗаголовкиПоИд = новый Соответствие<Строка, ЗаголовкиПисьмаВСоединении>()
    для Заголовок из Соединение.ПолучитьЗаголовкиПисем()
        если Заголовок.Заголовки.ИдСообщения != Неопределено
            ЗаголовкиПоИд.Вставить(Заголовок.Заголовки.ИдСообщения!.ВНижнийРегистр(), Заголовок)
        ;
    ;
    для ПисьмоСсылка из Письма
        знч ПисьмоОбъект = ПисьмоСсылка.ЗагрузитьОбъект()
        попытка 
            СборПисем.ПередЗагрузкой(ПисьмоОбъект)
        поймать Ошибка: Исключение
            Журналирование.ЗарегистрироватьОшибку(Ошибка)
        ;
        знч ИдСообщения = ПисьмоОбъект.ИдСообщения.ВНижнийРегистр()
        если ЗаголовкиПоИд.Ключи().Содержит(ИдСообщения)
            знч Индекс = ЗаголовкиПоИд.Получить(ИдСообщения).Индекс
            пер НайденноеПисьмо = Соединение.ПолучитьПисьмо(Индекс).Письмо
            попытка 
                ПисьмоОбъект.Заполнить(НайденноеПисьмо)
                СборПисем.ПослеЗаполнения(ПисьмоОбъект)
                ПисьмоОбъект.Записать()
                СборПисем.ПослеСохранения(ПисьмоОбъект)
            поймать Ошибка: Исключение
                Журналирование.ЗарегистрироватьОшибку(Ошибка)
            ;
        ;
    ;
;

@Реализация
@ВПодсистеме
метод Заполнить(Подключение: ПодключениеПочтовогоСервера, Письмо: ЭлектронноеПисьмо.Ссылка)
    исп Соединение = ПолучитьСоединение(Подключение)
    если Соединение == Неопределено
        возврат
    ;
    знч ПисьмоОбъект = Письмо.ЗагрузитьОбъект()
    
    для Заголовок из Соединение.ПолучитьЗаголовкиПисем()
        если Заголовок.Заголовки.ИдСообщения != Неопределено
            и Заголовок.Заголовки.ИдСообщения.Сравнить(ПисьмоОбъект.ИдСообщения, Истина) == 0
            пер НайденноеПисьмо = Соединение.ПолучитьПисьмо(Заголовок.Индекс).Письмо
            ПисьмоОбъект.Заполнить(НайденноеПисьмо)
            ПисьмоОбъект.Записать()
        ;
    ;
;

@ВПодсистеме
метод ПолучитьСоединение(Подключение: ПодключениеПочтовогоСервера): СоединениеPop3?
    пер Соединение: СоединениеPop3?
    попытка
        Соединение = новый СоединениеPop3(Подключение.Параметры() как ПараметрыПодключенияPop3)
    поймать Ошибка: Исключение
        Журналирование.ЗарегистрироватьОшибку(Ошибка)
        возврат Неопределено
    ;
    если не Соединение.Подключено()
        пер ТекстОшибки = Почта.ТекстОшибки(ТипыОшибок.СоединениеНеУстановленоPop3, Подключение.СформироватьПредставление())
        Журналирование.ЗарегистрироватьОшибку("ОшибкаПочты", ТекстОшибки, Важность = ВажностьСобытияЖурналаСобытий.Высокая)
        возврат Неопределено
    ;
    возврат Соединение
;