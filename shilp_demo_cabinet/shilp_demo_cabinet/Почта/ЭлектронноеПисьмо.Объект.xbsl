импорт ОбщиеСтандартные
импорт УниверсальныеМеханизмы

@ВПроекте
метод СодержаниеПоФорматам(): Соответствие<ФорматыПочтовогоСообщения, Строка>
    пер СодержаниеПоФорматам = новый Соответствие<ФорматыПочтовогоСообщения, Строка>()
    
    СодержаниеПоФорматам.Вставить(ФорматСодержимого, Содержание)
    если ФорматСодержимого != ФорматыПочтовогоСообщения.ПростойТекст
        СодержаниеПоФорматам.ВставитьЕслиОтсутствует(ФорматыПочтовогоСообщения.ПростойТекст, СодержаниеТекст)
    ;
    возврат СодержаниеПоФорматам
;

@ВПроекте
метод ТекстовоеСодержание(МаксКоличествоСтрок: Число, МаксКоличествоСимоволов: Число): Строка
    пер Результат = ""
    пер СчетчикСимволов = 0
    пер ВсеСтроки = СодержаниеПоФорматам().Получить(ФорматыПочтовогоСообщения.ПростойТекст).Разделить("\н")
    для Индекс = 0 по Мин(ВсеСтроки.Граница(), МаксКоличествоСтрок)
        знч ТекущаяСтрока = ВсеСтроки[Индекс].Сократить()
        СчетчикСимволов += ТекущаяСтрока.Длина()
        если ТекущаяСтрока.Длина() > 0
            Результат += ТекущаяСтрока + "\н"
        ;
    ;
    если СчетчикСимволов > МаксКоличествоСимоволов
        Результат = Результат.Подстрока(0, МаксКоличествоСимоволов) + "..."
    ;
    возврат Результат
;

@ВПодсистеме
метод СохранитьВложения(Вложения: ЧитаемыйМассив<ВложениеПисьма>)
    если не Вложения.Пусто()
        пер ВсеВложения = новый Массив<ПрисоединенныйФайл>()
        для Вложение из Вложения
            ВсеВложения.Добавить(
                МенеджерКонтента.СоздатьПрисоединенныйФайл(Вложение)
            )
        ;
        ПрисоединенныеФайлыМенеджер.Записать(
            новый ПрисоединенныеФайлы(этот.Ссылка, ВсеВложения)
        )
    ;
;

@ВПодсистеме
метод Заполнить(ЗагруженноеПисьмо: Письмо)
    Статус = СтатусыПисьма.Получено
    ЕстьВложения = ЗагруженноеПисьмо.Вложения.Размер() > 0
    Размер = ЗагруженноеПисьмо.ВБайты().Размер().ВБайтах()
    Содержание = ЗагруженноеПисьмо.ПолучитьТекстКакСтроку()
    ИдИсходногоСобщения = КонструкторПисьма.ИдИсходногоСобщения(ЗагруженноеПисьмо)
    
    пер СодержаниеЧасти = МенеджерКонтента.Содержание(ЗагруженноеПисьмо.ПолучитьТекст())
    ФорматСодержимого = ФорматыПочтовогоСообщения.ПростойТекст
    
    если СодержаниеЧасти.СодержитКлюч(ФорматСодержимого)
        СодержаниеТекст = СодержаниеЧасти.Получить(ФорматСодержимого)
    ;
    если СодержаниеЧасти.СодержитКлюч(ФорматыПочтовогоСообщения.Rtf)
        ФорматСодержимого = ФорматыПочтовогоСообщения.Rtf
    ;
    если СодержаниеЧасти.СодержитКлюч(ФорматыПочтовогоСообщения.Html)
        ФорматСодержимого = ФорматыПочтовогоСообщения.Html
    ;
    СохранитьВложения(ЗагруженноеПисьмо.Вложения)
;

@Обработчик
метод ПриЗаполнении()
    Дата = МенеджерДат.ТекущаяДатаВремя().ВДатаВремя(ЧасовойПояс.Текущий())
    Статус = СтатусыПисьма.Новое
;

@Обработчик
метод ПередЗаписью(До: ЭлектронноеПисьмо.Данные, ПараметрыЗаписи: ЭлектронноеПисьмо.ПараметрыЗаписи)
    если ЭтоНовый()
        ЗаполнитьПредставление()
    ;
    если ФорматСодержимого != Неопределено и ФорматСодержимого != ФорматыПочтовогоСообщения.ПростойТекст
        если СодержаниеТекст.Пусто()
            СодержаниеТекст = МенеджерКонтента.ИзвлечьТекстовоеСодержимое(Содержание)
        ;
    ;
;

метод ЗаполнитьПредставление()
    если Наименование.Пусто()
        Наименование = "ЭлектронноеПисьмо(%{Дата.Представление()})"
    ;
;