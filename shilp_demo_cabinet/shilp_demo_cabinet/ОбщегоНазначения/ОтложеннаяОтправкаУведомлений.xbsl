импорт КонтрольДоступа

//Область РазрешенияДоступа

@Обработчик
метод ВычислитьРазрешенияДоступа(): Массив<РазрешениеДоступа>
    возврат КонтрольДоступа.РазрешенияДоступаТаблицыСервисныхДанныхАбонента()
;

//КонецОбласти

@ВПодсистеме
метод УстановитьНастройкиОтложеннойОтправкиУведомлений(ИспользоватьОтложеннуюОтправку: Булево, Задержка: Число)

    пер Настройки = новый ОтложеннаяОтправкаУведомлений.Объект()
    
    знч Запрос = Запрос{
        Выбрать 
            Ссылка
        Из 
            ОтложеннаяОтправкаУведомлений}
        
    исп РезультатЗапроса = Запрос.Выполнить() 
    знч РезультатЗапросаСтроки = новый ЧитаемыйМассив(РезультатЗапроса)
    если (не РезультатЗапросаСтроки.Пусто())
        Настройки = РезультатЗапросаСтроки[0].Ссылка.ЗагрузитьОбъект()
    ;
    
    Настройки.ИспользоватьОтложеннуюОтправку = ИспользоватьОтложеннуюОтправку
    Настройки.Задержка = Задержка
    Настройки.Записать()
;

@ВПодсистеме
метод Настройки(): ОбщегоНазначения.НастройкиОтложеннойОтправки
    пер Настройки = новый ОбщегоНазначения.НастройкиОтложеннойОтправки(Ложь, 0)
    знч Запрос = Запрос{
        Выбрать 
            Ссылка, 
            ИспользоватьОтложеннуюОтправку, 
            Задержка
        Из 
            ОтложеннаяОтправкаУведомлений}
        
    исп РезультатЗапроса = Запрос.Выполнить() 
    знч РезультатЗапросаСтроки = новый ЧитаемыйМассив(РезультатЗапроса)
    если (не РезультатЗапросаСтроки.Пусто())
        Настройки.ИспользоватьОтложеннуюОтправку = РезультатЗапросаСтроки[0].ИспользоватьОтложеннуюОтправку
        Настройки.Задержка = РезультатЗапросаСтроки[0].Задержка
    ;
    возврат Настройки
;