импорт КонтрольДоступа

//Область РазрешенияДоступа

@Обработчик
метод ВычислитьРазрешенияДоступа(): Массив<РазрешениеДоступа>
    возврат КонтрольДоступа.РегистрСведенийРедактированиеАдминистратору()
;

//КонецОбласти

@ВПодсистеме
метод ФункциональныеОпции(): ОбщегоНазначения.ФункциональныеОпции
    пер ФункциональныеОпции: ОбщегоНазначения.ФункциональныеОпции
    знч Запрос = Запрос{
        Выбрать 
            Функциональность, 
            Использование
        Из ИспользуемаяФункциональность}
    исп Результат = Запрос.Выполнить()
    для РезультатСтрока из Результат
        УстановитьЗначение(ФункциональныеОпции, РезультатСтрока.Функциональность, РезультатСтрока.Использование)
    ;
    возврат ФункциональныеОпции
;

метод УстановитьЗначение(ФункциональныеОпции: ОбщегоНазначения.ФункциональныеОпции, 
    Функциональность: Функциональность, Значение: Булево)
    
    выбор Функциональность
    когда ФайлДляВыводаНаПринтер
        ФункциональныеОпции.ФайлДляВыводаНаПринтер = Значение
    когда СканДокумента
        ФункциональныеОпции.СканДокумента = Значение
    когда ФайлСЭлектроннойПодписью
        ФункциональныеОпции.ФайлСЭлектроннойПодписью = Значение
    когда ВБумажномВиде
        ФункциональныеОпции.ВБумажномВиде = Значение
    когда КопияНаПочту
        ФункциональныеОпции.КопияНаПочту = Значение    
    ;
;

@ВПодсистеме
метод ЗаписатьФункциональныеОпции(ФункциональныеОпции: ОбщегоНазначения.ФункциональныеОпции)  
    ЗаписатьЗначение(Функциональность.ФайлДляВыводаНаПринтер, ФункциональныеОпции.ФайлДляВыводаНаПринтер)  
    ЗаписатьЗначение(Функциональность.СканДокумента, ФункциональныеОпции.СканДокумента)  
    ЗаписатьЗначение(Функциональность.ФайлСЭлектроннойПодписью, ФункциональныеОпции.ФайлСЭлектроннойПодписью)  
    ЗаписатьЗначение(Функциональность.ВБумажномВиде, ФункциональныеОпции.ВБумажномВиде)
    ЗаписатьЗначение(Функциональность.КопияНаПочту, ФункциональныеОпции.КопияНаПочту)            
;

метод ЗаписатьЗначение(Функциональность: Функциональность, Значение: Булево?)
    
    если Значение == Неопределено
        возврат
    ;
    
    знч НаборЗаписей = новый ИспользуемаяФункциональность.НаборЗаписей()
		НаборЗаписей.Фильтр.Функциональность.Установить(Функциональность)
		НаборЗаписей.ДобавитьЗапись(
            Функциональность = Функциональность,
            Использование = Значение
        )
    НаборЗаписей.Записать()
    
;