импорт ОбщиеСтандартные

метод УстановленныйРежимТолькоЧтение(): Булево
    возврат ТолькоЧтение
;

метод ТабличнаяЧастьИсточник(): ИсточникДанныхМассив<ЗаместителиСтрока>
    пер Источник = новый ИсточникДанныхМассив<ЗаместителиСтрока>()
    Источник.Данные.ДобавитьВсе(ТаблицаЗаместители.Заместители)
    возврат Источник
;

// Область РаботаСТабличнойЧастью

метод ДобавитьЗаместителяПриНажатии(Источник: Кнопка, Событие: СобытиеПриНажатии)
    СтатистикаИспользованияПриложения.ЗарегистрироватьЦель("ДобавитьЗаместителя%ИдентификаторСтатистики")
    пер ДанныеНовойСтроки = новый ЗаместителиСтрока()
    ДанныеНовойСтроки.Модифицированность = Истина
    ОткрытьФормуЗаместителя(ДанныеНовойСтроки)
;

метод УдалитьЗаместителя(ДанныеЭлемента: ЗаместителиСтрока)
    пер Индекс: Число? = ТаблицаЗаместители.Заместители.Найти(ДанныеЭлемента)
    если Индекс != Неопределено
        пер ДанныеЗаявления = ТаблицаЗаместители.Заместители.Получить(Индекс!) 
        если ДанныеЗаявления.Ссылка == Неопределено
            ТаблицаЗаместители.Заместители.УдалитьПоИндексу(Индекс!)
        иначе
            ДанныеЗаявления.Удален = Истина
            СобственнаяМодифицированность = Истина
            если ДействиеПриИзменении != Неопределено
                ДействиеПриИзменении()
            ;
        ;
    ;
;

// КонецОбласти РаботаСТабличнойЧастью

метод ТаблицаСЗаголовкомВидимость(): Булево
    возврат ПоказыватьЗаголовок или ТабличнаяЧастьВидимость()
;

метод ТабличнаяЧастьВидимость(): Булево
    для ЭлементДанных из ТаблицаЗаместители.Заместители
        если ЭлементДанных.Удален
            продолжить
        ;
        возврат Истина
    ;
    возврат Ложь
;

метод ТабличнаяЧастьПриНажатииЭлемента(Источник: Список<ИсточникДанныхМассив<ЗаместителиСтрока>>, Событие: СобытиеСДанными<СтрокаСписка<ЗаместителиСтрока>>)
    ОткрытьФормуЗаместителя(Событие.Данные.ДанныеСтроки)
;

метод ВозможноРедактирование(): Булево
    возврат не ТолькоЧтение
;

метод ДобавитьЗаместителяЗаголовок(): Строка
    возврат (ТаблицаЗаместители.Заместители.Размер() == 0 ? "Меня замещает..." : "Добавить заместителя")
;

метод ОткрытьФормуЗаместителя(ДанныеСтроки: ЗаместителиСтрока)
    пер ФормаЗаместителя = новый ЗаместительФорма(
        ТолькоЧтение = ТолькоЧтение,
        ИсходныеДанные = ДанныеСтроки,
        ТаблицаЗаместители = ТаблицаЗаместители)
    ФормаЗаместителя.Инициализировать()
    пер ИзмененныеДанныеСтроки = ФормаЗаместителя.ОткрытьВМодальномОкне()
    если ИзмененныеДанныеСтроки != Неопределено
        если не КоллекцияСодержитЗаместителя(ИзмененныеДанныеСтроки)
            ТаблицаЗаместители.Заместители.Добавить(ИзмененныеДанныеСтроки)
            если ДействиеПриИзменении != Неопределено
                ДействиеПриИзменении()
            ;
        ;
    ;
    СобственнаяМодифицированность = Истина
;

метод КоллекцияСодержитЗаместителя(ДанныеСтроки: ЗаместителиСтрока): Булево

    для ДанныеЗаместителя из ТаблицаЗаместители.Заместители
        если ДанныеЗаместителя.Заместитель == ДанныеСтроки.Заместитель
            возврат Истина
        ;
    ;
    возврат Ложь 
;

метод ЗаместительЗаголовок(ДанныеЭлемента: ЗаместителиСтрока): Строка
    возврат МенеджерСущности.ПредставлениеСсылки(ДанныеЭлемента.Заместитель)
;

метод ЗаместителиКомпонентСтрокаВидимость(ДанныеЭлемента: ЗаместителиСтрока): Булево
    возврат не ДанныеЭлемента.Удален
;

метод ЗаместителиКомпонентСтрокаУдалитьПриНажатии(Источник: СтандартнаяКолонкаТаблицы<ЗаместителиСтрока>, Событие: СобытиеПриНажатии, ДанныеЭлемента: ЗаместителиСтрока)
    УдалитьЗаместителя(ДанныеЭлемента)
;

метод КомментарийГоризонтальноеПоложениеСодержимого(): ВыравниваниеПоГоризонтали | Авто
    возврат РежимПросмотра ? ВыравниваниеПоГоризонтали.Конец : Авто
;