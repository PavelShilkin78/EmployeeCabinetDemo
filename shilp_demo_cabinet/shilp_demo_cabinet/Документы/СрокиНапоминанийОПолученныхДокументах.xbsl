импорт КонтрольДоступа

//Область РазрешенияДоступа

@Обработчик
метод ВычислитьРазрешенияДоступа(): Массив<РазрешениеДоступа>
    возврат КонтрольДоступа.РазрешенияДоступаТаблицыСервисныхДанныхАбонентаЧтениеВсем()
;

//КонецОбласти

//Область СлужебныйПрограммныйИнтерфейс

@ВПодсистеме
метод СрокиНапоминаний(): Массив<НапоминанияОПолученныхДокументахСтрока>
    
    пер СрокиНапоминаний = новый Массив<НапоминанияОПолученныхДокументахСтрока>()
    
    знч Запрос = Запрос{
        Выбрать
            Ссылка,
            КоличествоДней
        Из 
            СрокиНапоминанийОПолученныхДокументах
        Упорядочить По
            КоличествоДней}
    
    исп Результат = Запрос.Выполнить() 
    для РезультатСтрока из Результат
        пер СрокНапоминания: НапоминанияОПолученныхДокументахСтрока
        СрокНапоминания.Ссылка = РезультатСтрока.Ссылка
        СрокНапоминания.КоличествоДней = РезультатСтрока.КоличествоДней
        СрокиНапоминаний.Добавить(СрокНапоминания)
    ;
    
    возврат СрокиНапоминаний
    
;

@ВПодсистеме
метод НачальноеЗаполнение()
    
    знч Запрос = Запрос{
        Выбрать Первые 1
            Ссылка
        Из 
            СрокиНапоминанийОПолученныхДокументах}
    исп Результат = Запрос.Выполнить() 
    знч РезультатСтроки = новый ЧитаемыйМассив(Результат)
    если (не РезультатСтроки.Пусто())
        возврат
    ;
    
    пер СрокНапоминания = новый СрокиНапоминанийОПолученныхДокументах.Объект()
    СрокНапоминания.КоличествоДней = 5
    СрокНапоминания.Записать()
    
    СрокНапоминания = новый СрокиНапоминанийОПолученныхДокументах.Объект()
    СрокНапоминания.КоличествоДней = 8
    СрокНапоминания.Записать()
    
    СрокНапоминания = новый СрокиНапоминанийОПолученныхДокументах.Объект()
    СрокНапоминания.КоличествоДней = 10
    СрокНапоминания.Записать()
    
;

//КонецОбласти