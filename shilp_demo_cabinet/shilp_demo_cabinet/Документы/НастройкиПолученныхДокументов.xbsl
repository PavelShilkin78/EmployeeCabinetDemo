импорт КонтрольДоступа

//Область РазрешенияДоступа

@Обработчик
метод ВычислитьРазрешенияДоступа(): Массив<РазрешениеДоступа>
    возврат КонтрольДоступа.РазрешенияДоступаТаблицыСервисныхДанныхАбонентаЧтениеВсем()
;

//КонецОбласти

//Область СлужебныеПроцедурыИФункции

@ВПодсистеме
метод НастройкиПолученныхДокументов(): ПолученныеДокументы.НапоминанияОПолученныхДокументах
    
    пер Настройки = новый ПолученныеДокументы.НапоминанияОПолученныхДокументах(Ложь, Ложь)
    
    знч Запрос = Запрос{
        Выбрать Первые 1
            НапоминатьОПолученныхДокументах,
            НапоминатьОПолученныхДокументахПодчиненных
        Из 
            НастройкиПолученныхДокументов}

    исп РезультатЗапроса = Запрос.Выполнить() 
    знч РезультатЗапросаСтроки = новый ЧитаемыйМассив(РезультатЗапроса)
    если (не РезультатЗапросаСтроки.Пусто())
        Настройки.НапоминатьОПолученныхДокументах = РезультатЗапросаСтроки[0].НапоминатьОПолученныхДокументах
        Настройки.НапоминатьОПолученныхДокументахПодчиненных = РезультатЗапросаСтроки[0].НапоминатьОПолученныхДокументахПодчиненных
    ;
    
    возврат Настройки
    
;

@ВПодсистеме
метод ЗаписатьНастройкиПолученныхДокументов(НастройкиНапоминаний: ПолученныеДокументы.НапоминанияОПолученныхДокументах)
    
    пер Настройки = новый НастройкиПолученныхДокументов.Объект()
    
    знч Запрос = Запрос{
        Выбрать Первые 1
            Ссылка
        Из 
            НастройкиПолученныхДокументов}

    исп РезультатЗапроса = Запрос.Выполнить() 
    знч РезультатЗапросаСтроки = новый ЧитаемыйМассив(РезультатЗапроса)
    если (не РезультатЗапросаСтроки.Пусто())
        Настройки = РезультатЗапросаСтроки[0].Ссылка.ЗагрузитьОбъект()
    ;
    
    Настройки.НапоминатьОПолученныхДокументах = НастройкиНапоминаний.НапоминатьОПолученныхДокументах
    Настройки.НапоминатьОПолученныхДокументахПодчиненных = НастройкиНапоминаний.НапоминатьОПолученныхДокументахПодчиненных
    Настройки.Записать()
;

//КонецОбласти