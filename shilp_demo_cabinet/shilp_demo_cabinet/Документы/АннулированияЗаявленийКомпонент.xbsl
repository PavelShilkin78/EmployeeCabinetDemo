импорт ВебApi
импорт ИнтерфейсныеКомпонентыСтандартные
импорт КонтрольДоступа
импорт ОбменДанными
импорт ОбщегоНазначения
импорт Компания::Согласования
импорт УниверсальныеМеханизмы
импорт ОбщиеСтандартные
импорт ФизическиеЛица
импорт ЛичныеКабинетыПользователей
импорт УниверсальныеМеханизмы::ШаблоныДокументов

// Контракт ДокументСотрудникаКомпонент

@ВПроекте
@Реализация
метод УстановитьТекущееЗаявление(Заявление: Объект)
    если Заявление это АннулированияЗаявлений.Объект
        Объект = Заявление как АннулированияЗаявлений.Объект
        ПрочитатьОбъект()
    ;
;

// Конец ДокументСотрудникаКомпонент

метод ПрочитатьОбъект()
    ДанныеДокумента = ДополнительныеДанныеДокумента(
        Объект.ЭтоНовый(), 
        Объект.Ссылка,
        Объект.ФизическоеЛицо,
        ЛичныеКабинетыПользователей.ПараметрыСеанса().ТекущееФизическоеЛицо
    )
    
    пер МаксимальныйШагСогласования: Число
    пер ТекущийШагСогласования: Число
    для Элемент из ДанныеДокумента.ТабличныеЧасти.ТаблицаСогласующие.Согласующие
        если Элемент.Результат == РезультатыСогласования.НаСогласовании
            продолжить
        ;
        МаксимальныйШагСогласования = Макс(МаксимальныйШагСогласования, Элемент.НомерШагаСогласования)
        если Элемент.УчастникСогласования == ЛичныеКабинетыПользователей.ПараметрыСеанса().ТекущееФизическоеЛицо
            ПодписанТекущимФизическимЛицом = Истина
            ТекущийШагСогласования = Элемент.НомерШагаСогласования
        ;
    ;
    
    ПодписанПослеАвтора = ТекущийШагСогласования < МаксимальныйШагСогласования
  
    РедактированиеЗапрещено = ОбщегоНазначения.РедактированиеЗапрещено(
        ДанныеДокумента.Состояние, 
        Объект.ФизическоеЛицо, 
        ЛичныеКабинетыПользователей.ПараметрыСеанса().ТекущееФизическоеЛицо
    )
      
    если не РежимПросмотра
        ОтслеживатьИзменениеДанных(Компоненты.ПрисоединенныеФайлы, Истина)
        ОтслеживатьИзменениеДанных(Компоненты.ДополнительныеРеквизиты, Истина)
        ЗавершеноЧтениеДанных(новый СобытиеКомпонента())
    ;
    ИнтерфейсныеКомпоненты.ИнициализироватьПодчиненныеКомпоненты(Компоненты)
    
    если Объект.ЭтоНовый()
        Компоненты.ДополнительныеРеквизиты.СобственнаяМодифицированность = не ДанныеДокумента.ТабличныеЧасти.ДополнительныеРеквизиты.Реквизиты.Пусто()
    иначе
        Компоненты.ПрисоединенныеФайлы.СобственнаяМодифицированность = Ложь
        Компоненты.ДополнительныеРеквизиты.СобственнаяМодифицированность = Ложь
        СобственнаяМодифицированность = Ложь
    ;
    
    если АннулируемыйДокумент == Неопределено
        АннулируемыйДокумент = Аннулирование.АннулируемыйДокумент(Объект.Ссылка)
    ;
;

метод ПрочитатьДополнительныеДанныеДокумента()
    ДанныеДокумента = ДополнительныеДанныеДокумента(
        Объект.ЭтоНовый(),
        Объект.Ссылка,
        Объект.ФизическоеЛицо,
        ЛичныеКабинетыПользователей.ПараметрыСеанса().ТекущееФизическоеЛицо
    )
;

метод ОсновноеСодержаниеФормыТолькоЧтение(): Булево
    возврат РежимСогласования или РедактированиеЗапрещено 
;

метод ПояснениеЗаголовок(): Строка
    пер Пояснение: Строка
    если ВозможноРедактирование()
        Пояснение = "Вы можете отменить согласованное заявление"
    иначе
        пер ДатаОтправки: Момент? = Объект.МоментСоздания
        если ДанныеДокумента.Состояние != Неопределено 
            пер ВремяОтправки: Строка
            если МенеджерДат.НачалоДня(ДатаОтправки) == МенеджерДат.НачалоДня(МенеджерДат.ТекущаяДатаВремя())
                 ВремяОтправки = "сегодня, в " + МенеджерДат.ФорматМомента(ДатаОтправки, "ЧЧ:мм")
            иначе если МенеджерДат.НачалоДня(МенеджерДат.ДобавитьДни(ДатаОтправки, 1)) == МенеджерДат.НачалоДня(МенеджерДат.ТекущаяДатаВремя())
                 ВремяОтправки = "вчера, в " + МенеджерДат.ФорматМомента(ДатаОтправки, "ЧЧ:мм")
            иначе
                ВремяОтправки = МенеджерДат.ФорматМомента(ДатаОтправки, "дд ММММ гггг")
            ;
            пер СтатусЗапроса: Строка
            если ДанныеДокумента.Состояние == СостоянияДокумента.Выполнен
                СтатусЗапроса = " и уже согласовано."
            иначе если ДанныеДокумента.Состояние == СостоянияДокумента.Подготовлен
                СтатусЗапроса = ", но еще находится в обработке."
            иначе если ДанныеДокумента.Состояние == СостоянияДокумента.ПолученОтказ
                СтатусЗапроса = ", но по нему получен отказ. Причины можно уточнить в бухгалтерии."
            ;
            Пояснение = "Заявление на отмену было отправлено %ВремяОтправки%СтатусЗапроса."
        ;
    ;
    возврат Пояснение
;

@ВПодсистеме
метод МодифицированныеТабличныеЧасти(): ТабличныеЧасти
    пер ТабличныеЧасти: ТабличныеЧасти
    если Компоненты.ПрисоединенныеФайлы.СобственнаяМодифицированность
        ОбщегоНазначения.ОчиститьКлиентскиеДанные(ДанныеДокумента.ТабличныеЧасти.ТаблицаФайлы)    
        ТабличныеЧасти.ТаблицаФайлы = ДанныеДокумента.ТабличныеЧасти.ТаблицаФайлы
    ;
    если Компоненты.ДополнительныеРеквизиты.СобственнаяМодифицированность
        ТабличныеЧасти.ДополнительныеРеквизиты = ДанныеДокумента.ТабличныеЧасти.ДополнительныеРеквизиты
    ;
    возврат ТабличныеЧасти
;

метод АннулируемыйДокументЗаголовок(): Строка
    
    знч Сервисы = АннулируемыйИнтерфейсПоставщик.ПолучитьСервисы()
    знч МетодыПредставленияАннулируемого: Соответствие<Тип, (АннулируемыйДокумент.Ссылка) -> Строка?>
    для Сервис из Сервисы
        МетодыПредставленияАннулируемого.ВставитьВсе(Сервис.МетодыПредставленияАннулируемого())
    ;
    
    знч МетодПредставленияАннулируемого = МетодыПредставленияАннулируемого.Получить(АннулируемыйДокумент.ПолучитьТип())
    
    возврат "Отменяется ${МетодПредставленияАннулируемого(АннулируемыйДокумент)}"
    
;

метод ДополнительныеРеквизитыТаблицаПараметров(): РеквизитыШаблона
    пер Таблица: РеквизитыШаблона
    если ДанныеДокумента?.ТабличныеЧасти.ДополнительныеРеквизиты != Неопределено
        Таблица = ДанныеДокумента?.ТабличныеЧасти.ДополнительныеРеквизиты
    ;
    возврат Таблица 
;

метод ДополнительныеРеквизитыТолькоЧтение(): Булево
    возврат не ВозможноРедактирование()
;

метод ВозможноРедактирование(): Булево
    возврат ДанныеДокумента.Состояние == СостоянияДокумента.Черновик или ДанныеДокумента.Состояние == Неопределено
;


@НаСервере @НаКлиенте
@ВПодсистеме
структура ДополнительныеДанныеДокумента
    пер Состояние: СостоянияДокумента?
    пер ДатаСостояния: Момент?
    пер ФизическоеЛицоЗаголовок: Строка
    пер ФизическоеЛицо: ФизическиеЛица.Ссылка?
    пер ТабличныеЧасти: ТабличныеЧасти
    пер Отправлен: Булево
;

@НаСервере @НаКлиенте
@ВПодсистеме
структура ТабличныеЧасти
    пер ТаблицаФайлы: ПрисоединенныеФайлы?
    пер ТаблицаСогласующие: Согласующие?
    пер ДополнительныеРеквизиты: РеквизитыШаблона?
;

@НаСервере @ДоступноСКлиента
статический метод ДополнительныеДанныеДокумента(ЭтоНовый: Булево, Ссылка: АннулированияЗаявлений.Ссылка, ФизическоеЛицо: ФизическиеЛица.Ссылка?, ТекущееФизическоеЛицо: ФизическиеЛица.Ссылка?): ДополнительныеДанныеДокумента
    
    если ТекущееФизическоеЛицо == Неопределено
        ТекущееФизическоеЛицо = ПользователиСистемы.ТекущееФизическоеЛицо()
    ;
        
    пер ДополнительныеДанныеДокумента: ДополнительныеДанныеДокумента
    ДополнительныеДанныеДокумента.ФизическоеЛицо = ТекущееФизическоеЛицо 
    ДополнительныеДанныеДокумента.ФизическоеЛицоЗаголовок = МенеджерСущности.ПредставлениеСсылки(ФизическоеЛицо)
    
    если не ЭтоНовый
        ДополнительныеДанныеДокумента.ТабличныеЧасти = ДанныеТабличныхЧастей(Ссылка)
        ДополнительныеДанныеДокумента.Отправлен = ЭтоОтправляемыйДокумент(Ссылка)
    иначе
        ДополнительныеДанныеДокумента.ТабличныеЧасти = ДанныеТабличныхЧастейПоУмолчанию(Ссылка)
    ;
    ЗаполнитьДанныеСостояния(Ссылка, ДополнительныеДанныеДокумента)

    возврат ДополнительныеДанныеДокумента
;

@НаСервере
статический метод ЭтоОтправляемыйДокумент(Ссылка: АннулированияЗаявлений.Ссылка): Булево
    возврат ОбменДанными.ЭтоОтправляемыйДокумент(Ссылка)
;

@НаСервере
статический метод ДанныеТабличныхЧастей(Владелец: АннулированияЗаявлений.Ссылка): ТабличныеЧасти
    пер ДанныеТабличныхЧастей: ТабличныеЧасти
    ДанныеТабличныхЧастей.ТаблицаФайлы = ПрисоединенныеФайлыМенеджер.ПрисоединенныеФайлы(Владелец)
    ДанныеТабличныхЧастей.ТаблицаСогласующие = Согласования.Согласующие(Владелец)
    ДанныеТабличныхЧастей.ДополнительныеРеквизиты = ШаблоныДокументовМенеджер.РеквизитыШаблона(
        Владелец, УниверсальныеМеханизмы.Ид(Документы.ИД_ПОДСИСТЕМЫ, Документы.ШАБЛОН_АННУЛИРОВАНИЯ_ЗАЯВЛЕНИЯ))
    возврат ДанныеТабличныхЧастей
;

@НаСервере
статический метод ЗаполнитьДанныеСостояния(Ссылка: АннулированияЗаявлений.Ссылка, ДополнительныеДанныеДокумента: ДополнительныеДанныеДокумента)
    пер ДанныеСостояния = СостДокументов.ДанныеСостоянияДокумента(Ссылка)
    ДополнительныеДанныеДокумента.Состояние = ДанныеСостояния.Состояние
    ДополнительныеДанныеДокумента.ДатаСостояния = ДанныеСостояния.Дата
;
 
@НаСервере
статический метод ДанныеТабличныхЧастейПоУмолчанию(Заявление: АннулированияЗаявлений.Ссылка): ТабличныеЧасти
    пер ДанныеТабличныхЧастей: ТабличныеЧасти
    пер ТаблицаФайлы: ПрисоединенныеФайлы
    ТаблицаФайлы.Владелец = Заявление
    пер Согласующие: Согласующие
    Согласующие.Владелец = Заявление
    ДанныеТабличныхЧастей.ТаблицаФайлы = ТаблицаФайлы
    ДанныеТабличныхЧастей.ТаблицаСогласующие = Согласующие
    ДанныеТабличныхЧастей.ДополнительныеРеквизиты = ШаблоныДокументовМенеджер.РеквизитыШаблона(
        Неопределено, УниверсальныеМеханизмы.Ид(Документы.ИД_ПОДСИСТЕМЫ, Документы.ШАБЛОН_АННУЛИРОВАНИЯ_ЗАЯВЛЕНИЯ))
    ДанныеТабличныхЧастей.ДополнительныеРеквизиты.Владелец = Заявление
    возврат ДанныеТабличныхЧастей
;

// Контракт ВладелецПрисоединенныеФайлыКомпонент
@ВПроекте
@Реализация
метод ПрисоединенныеФайлыНачатаЗагрузкаФайлов(Источник: ПрисоединенныеФайлыКомпонент, Событие: неизвестно, ИдКомпонента: Строка = "")
    ИзменениеСостоянияЗагрукиФайлов(новый СобытиеСДанными(Истина))
;

@ВПроекте
@Реализация
метод ПрисоединенныеФайлыЗавершенаЗагрузкаФайлов(Источник: ПрисоединенныеФайлыКомпонент, Событие: неизвестно, ИдКомпонента: Строка = "")
    ИзменениеСостоянияЗагрукиФайлов(новый СобытиеСДанными(Ложь))
;
// Конец ВладелецПрисоединенныеФайлыКомпонент