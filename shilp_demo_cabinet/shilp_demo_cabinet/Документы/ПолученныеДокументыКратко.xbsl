импорт ИнтерфейсныеКомпонентыСтандартные
импорт Криптография
импорт ОбщегоНазначения
импорт ОбщиеСтандартные
импорт УниверсальныеМеханизмы
импорт ФизическиеЛица
импорт ВладельцыКабинетов
импорт Компания::Согласования
импорт ЛичныеКабинетыПользователей
импорт Зарплата

@Обработчик
метод ПослеСоздания()
    ОбновитьДанныеФормы()
    
    МенеджерГлобальныхКлиентскихСобытий.ПодключитьОбработчик(метод (Источник, Параметры) ->
        выбор Источник
        когда Согласования.ДОКУМЕНТ_СОГЛАСОВАН,
            Зарплата.ПОДПИСАН_РАСЧЕТНЫЙ_ЛИСТ
            
            ПослеИзмененияКоличестваНесогласованныхДокументов()
        когда ОбщегоНазначения.СОГЛАСИЕ_КЭДО_ПОДПИСАНО
            ПодключитесьККэдоВидимость = СогласиеНаПрисоединениеККэдоКлиент.ТребуетсяПодписатьСогласия()
        когда ФизЛица.ТРЕБУЕТСЯ_ОБНОВЛЕНИЕ_ДАННЫХ_ФИЗИЧЕСКОГО_ЛИЦА
            КоличествоНеподписанныхДокументов = КоличествоНеподписанныхДокументов(ЛичныеКабинетыПользователей.ПараметрыСеанса().ТекущееФизическоеЛицо)
        когда Криптография.ОБНОВЛЕНА_ЭЛЕКТРОННАЯ_ПОДПИСЬ
            ОбновитьДанныеФормы()
        ;
        возврат
    ;)
;

метод ОбновитьДанныеФормы()
    знч ПараметрыСеансаВладельцыКабинетов = ВладельцыКабинетов.ПараметрыСеанса()
    пер ДанныеЗаполнения = ДанныеЗаполнения(
        ПараметрыСеансаВладельцыКабинетов.ФизическоеЛицо,
        ПараметрыСеансаВладельцыКабинетов.ИспользуетКэдо,
        Криптография.ПараметрыСеанса().ВидЭлектроннойПодписи)
    КоличествоНеподписанныхДокументов = ДанныеЗаполнения.КоличествоНеподписанныхДокументов
    ПодключитесьККэдоВидимость = ДанныеЗаполнения.ЕстьНеподписанныеСогласия и Криптография.ПараметрыСеанса().ИспользованиеКЭДО
    ПамяткаКэдоВидимость = ДанныеЗаполнения.ПамяткаКэдоВидимость
;

метод ДокументыПояснение(): Строка
    пер ДокументыПояснение: Строка
    если КоличествоНеподписанныхДокументов == 0
        ДокументыПояснение = "Нет документов, с которыми необходимо ознакомиться."
    иначе если КоличествоНеподписанныхДокументов == 1
        ДокументыПояснение = "Вам нужно ознакомиться с одним документом."
    иначе
        ДокументыПояснение = "Вам нужно ознакомиться с несколькими документами."
    ;
    возврат ДокументыПояснение
;

метод ПослеИзмененияКоличестваНесогласованныхДокументов()
    КоличествоНеподписанныхДокументов = КоличествоНеподписанныхДокументов(ЛичныеКабинетыПользователей.ПараметрыСеанса().ТекущееФизическоеЛицо)
;

метод НадписьПодключитесьККэдоЗаголовок(): Строка
    если КоличествоНеподписанныхДокументов == 0
        возврат СогласиеНаПрисоединениеККэдоКлиентСервер.ТекстПриглашенияККэдо()   
    иначе
        возврат "Но перед этим подпишите согласие на подключение к кадровому электронному документообороту."
    ;
;

метод ОранжевыйЦветКоннотации(): Цвет
    возврат БиблиотекаЦветов.Цвет(БиблиотекаЦветов.ВидыЦвета.Оранжевый)
;

метод КомандаПодключитьсяВидимость(): Булево
    возврат СогласиеНаПрисоединениеККэдоКлиент.ТребуетсяПодписатьСогласия()
;

метод КомандаПодключитьсяПриНажатии(Источник: Кнопка, Событие: СобытиеПриНажатии)
    КомплектСогласиеНаПрисоединениеККэдоФорма.ОткрытьВМодальномОкне()
;

метод ПамяткаКэдоТекст(): Строка
    возврат СогласиеНаПрисоединениеККэдоКлиентСервер.ТекстПамяткаКэдо(ВладельцыКабинетов.ПараметрыСеанса().ИспользуетКэдо)    
;

метод ПриИзмененииДанныхФизическогоЛица()
    КоличествоНеподписанныхДокументов = КоличествоНеподписанныхДокументов(ЛичныеКабинетыПользователей.ПараметрыСеанса().ТекущееФизическоеЛицо)
    ПодключитесьККэдоВидимость = СогласиеНаПрисоединениеККэдоКлиент.ТребуетсяПодписатьСогласия()
;

@НаСервере @НаКлиенте
структура ДанныеЗаполнения
    пер КоличествоНеподписанныхДокументов: Число
    пер ЕстьНеподписанныеСогласия: Булево
    пер ПамяткаКэдоВидимость: Булево
;

@НаСервере @ДоступноСКлиента
статический метод ДанныеЗаполнения(ФизическоеЛицо: ФизическиеЛица.Ссылка, ИспользуетКэдо: Булево,
    ВидЭлектроннойПодписи: ВидыЭлектронныхПодписей): ДанныеЗаполнения
    
    пер ДанныеЗаполнения: ДанныеЗаполнения
    ДанныеЗаполнения.КоличествоНеподписанныхДокументов = ДокументыНаПодпись.КоличествоНеподписанныхДокументов(ФизическоеЛицо)
    ДанныеЗаполнения.КоличествоНеподписанныхДокументов += Согласования.КоличествоДокументовНаСогласовании(ФизическоеЛицо)
    ДанныеЗаполнения.ЕстьНеподписанныеСогласия = СогласиеНаПрисоединениеККэдоСервер.ЕстьНеподписанныеСогласия(ФизическоеЛицо)                                                        
    если ВидЭлектроннойПодписи == ВидыЭлектронныхПодписей.УНЭПИнформационнойСистемы
        
        ДанныеЗаполнения.ПамяткаКэдоВидимость = не ДанныеЗаполнения.ЕстьНеподписанныеСогласия 
            и (СогласиеНаПрисоединениеККэдоСервер.ПамяткаКэдоВидимость()
                или ИспользуетКэдо и (не Криптография.ЕстьДействующийСертификат(ФизическоеЛицо)
                    или не Криптография.КорневойСертификатДействет(ФизическоеЛицо)))
    иначе если ВидЭлектроннойПодписи == ВидыЭлектронныхПодписей.УнэпАстралПлатформа
        
        ДанныеЗаполнения.ПамяткаКэдоВидимость = не ДанныеЗаполнения.ЕстьНеподписанныеСогласия 
            и ИспользуетКэдо и не Криптография.ЕстьДействующийСертификатСервисаПодписния(ФизическоеЛицо, ВидЭлектроннойПодписи)
    иначе
        ДанныеЗаполнения.ПамяткаКэдоВидимость = Ложь
    ;

    возврат ДанныеЗаполнения
;

@НаСервере @ДоступноСКлиента
статический метод КоличествоНеподписанныхДокументов(ФизическоеЛицо: ФизическиеЛица.Ссылка): Число
    возврат ДокументыНаПодпись.КоличествоНеподписанныхДокументов(ФизическоеЛицо) + Согласования.КоличествоДокументовНаСогласовании(ФизическоеЛицо)
;