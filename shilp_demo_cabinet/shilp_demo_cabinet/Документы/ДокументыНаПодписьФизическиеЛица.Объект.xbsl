@Обработчик
@ВыполнятьПриЗагрузкеДанных
метод ПослеЗаписи(ПредыдущееЗначение: ДокументыНаПодписьФизическиеЛица.Данные, ПараметрыЗаписи: ДокументыНаПодписьФизическиеЛица.ПараметрыЗаписи)
    если ПараметрыЗаписи.РежимЗагрузкиДанных
        возврат
    ;
    
    ПересчитатьРазрешенияДоступаДокументовНаПодпись(ПредыдущееЗначение)
;

@Обработчик
@ВыполнятьПриЗагрузкеДанных
метод ПослеУдаления(ПараметрыУдаления: ДокументыНаПодписьФизическиеЛица.ПараметрыУдаления)
    если ПараметрыУдаления.РежимЗагрузкиДанных
        возврат
    ;
    
    ДокументыНаПодпись
        .ПересчитатьРазрешенияДоступаДляОбъектов([новый ДокументыНаПодпись.ДанныеРасчетаРазрешений(Документ)])   
;

метод ПересчитатьРазрешенияДоступаДокументовНаПодпись(ПредыдущееЗначение: ДокументыНаПодписьФизическиеЛица.Данные)
    знч ДокументыКПересчетуРазрешений: Массив<ДокументыНаПодпись.ДанныеРасчетаРазрешений>
    если (ЭтоНовый()
        или ПредыдущееЗначение.ФизическоеЛицо != ФизическоеЛицо
        или ПредыдущееЗначение.Документ != Документ)
        и Документ != Неопределено
        
        ДокументыКПересчетуРазрешений.Добавить(новый ДокументыНаПодпись.ДанныеРасчетаРазрешений(Документ))
    ;
    
    если ПредыдущееЗначение.Документ != Документ
        и ПредыдущееЗначение.Документ != Неопределено
        
        ДокументыКПересчетуРазрешений.Добавить(новый ДокументыНаПодпись.ДанныеРасчетаРазрешений(ПредыдущееЗначение.Документ))
    ;
    
    если ДокументыКПересчетуРазрешений.Пусто()
        возврат
    ;
    
    ДокументыНаПодпись
        .ПересчитатьРазрешенияДоступаДляОбъектов(ДокументыКПересчетуРазрешений)    
;