импорт ВладельцыКабинетов
импорт Компания::Согласования
импорт КонтрольДоступа
импорт Криптография
импорт ОбменДанными
импорт ОбщиеСтандартные
импорт УниверсальныеМеханизмы

//Область ОбработчикиСобытий

@Обработчик
@ВыполнятьПриЗагрузкеДанных
метод ПриЗаполнении()
    ФизическоеЛицо = ПользователиСистемы.ТекущееФизическоеЛицо()
    если ФизическоеЛицо == Неопределено
        возврат
    ;
    пер ЧасовойПояс: ЧасовойПояс = ВладельцыКабинетов.ЧасовойПоясФизическогоЛица(ФизическоеЛицо)
    МоментСоздания = Момент.Сейчас()
    Дата = Дата.Сейчас(ЧасовойПояс)
;

@Обработчик
@ВыполнятьПриЗагрузкеДанных
метод ПередЗаписью(ПредыдущееЗначение: АннулированияЗаявлений.Данные, ПараметрыЗаписи: АннулированияЗаявлений.ПараметрыЗаписи)
    ЗаполнитьМесяцСоздания()
    ЗаполнитьПредставление(ПараметрыЗаписи.АннулируемыйДокумент)
;

@Обработчик
@ВыполнятьПриЗагрузкеДанных
метод ПослеЗаписи(ПредыдущееЗначение: АннулированияЗаявлений.Данные, ПараметрыЗаписи: АннулированияЗаявлений.ПараметрыЗаписи)
    исп КонтекстДоступа.Привилегированный()
    пер ТребуетсяРегистрацияИзменений = РегистрацияИзменений.ТребуетсяРегистрацияИзмененийОбъекта(этот, ПредыдущееЗначение)
    АннулированияЗаявлений.ПослеЗаписиДополнение(Ссылка, ФизическоеЛицо, ТребуетсяРегистрацияИзменений)
;

@Обработчик
@ВыполнятьПриЗагрузкеДанных
метод ПередУдалением(ПараметрыУдаления: АннулированияЗаявлений.ПараметрыУдаления)
    МенеджерСущности.УдалитьПодчиненныеОбъекты(Ссылка, Тип<СогласованияДокументов>, "Документ")
    МенеджерСущности.УдалитьПодчиненныеОбъекты(Ссылка, Тип<ПодписиДокументов>, "Документ")
    Аннулирование.УдалитьАннулирование(Ссылка)
;

@ВПодсистеме
метод ЗаписатьАннулирование(АннулируемыйДокумент: АннулируемыйДокумент.Ссылка)
   Аннулирование.ЗаписатьАннулирование(ФизическоеЛицо, Ссылка, АннулируемыйДокумент)
;
//КонецОбласти

//Область СлужебныеПроцедурыИФункции

метод ЗаполнитьПредставление(АннулируемыйДокумент: АннулируемыйДокумент.Ссылка)
    знч ПредставлениеАннулируемого = АннулируемыйДокумент.Представление()
    ПредставлениеДокумента = "Отзыв %ПредставлениеАннулируемого"
;


метод ЗаполнитьМесяцСоздания()
    МесяцСоздания = Дата.НачалоМесяца()
;

//КонецОбласти