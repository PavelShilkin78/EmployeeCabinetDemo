импорт Компания::Согласования
импорт ФизическиеЛица

@ВПодсистеме
метод ЗаполнитьИнформациюОНовыхДокументах(Объект: ДокументыНаПодпись.Ссылка? = Неопределено)
    пер НаборыПолучателей = новый Соответствие<ДокументыНаПодпись.Ссылка, Массив<ФизическиеЛица.Ссылка>>()
    знч ИспользуетсяФильтрПоОбъекту = (Объект != Неопределено)
    знч Запрос = Запрос{
        Выбрать Различные
            ДокументыНаПодписьФизическиеЛица.Документ Как Документ,
            ДокументыНаПодписьФизическиеЛица.ФизическоеЛицо Как ФизическоеЛицо
        Из
            ДокументыНаПодписьФизическиеЛица Как ДокументыНаПодписьФизическиеЛица
            Левое Соединение СогласованияДокументов Как СогласованияДокументов
            По ДокументыНаПодписьФизическиеЛица.Документ == СогласованияДокументов.Документ
                И ДокументыНаПодписьФизическиеЛица.ФизическоеЛицо == СогласованияДокументов.УчастникСогласования
        Где
            СогласованияДокументов.УчастникСогласования Есть Null и
            (не %ИспользуетсяФильтрПоОбъекту или ДокументыНаПодписьФизическиеЛица.Документ == %Объект)
        Упорядочить По
            Документ}
    исп Результат = Запрос.Выполнить()
    для СтрокаРезультата из Результат
        пер Получатели = НаборыПолучателей.ПолучитьИлиУмолчание(СтрокаРезультата.Документ, новый Массив<ФизическиеЛица.Ссылка>())
        Получатели.Добавить(СтрокаРезультата.ФизическоеЛицо)
        НаборыПолучателей.Вставить(СтрокаРезультата.Документ, Получатели)
    ;
    для ПолучателиДокумента из НаборыПолучателей
        знч НаборЗаписей = новый НовыеДокументы.НаборЗаписей()
        НаборЗаписей.Фильтр.Объект.Установить(ПолучателиДокумента.Ключ)
        для Получатель из ПолучателиДокумента.Значение
            НаборЗаписей.ДобавитьЗапись(
                Объект = ПолучателиДокумента.Ключ,
                ФизическоеЛицо = Получатель)
        ;
        НаборЗаписей.Записать()      
    ;
;

@ВПодсистеме
метод ПриУдаленииОбъекта(Объект: ДокументыНаПодпись.Ссылка)
    знч НаборЗаписей = новый НовыеДокументы.НаборЗаписей()
    НаборЗаписей.Фильтр.Объект.Установить(Объект)
    НаборЗаписей.Записать()
;

@ВПодсистеме
метод ПриЗаписиОбъекта(Объект: ДокументыНаПодпись.Ссылка)
    ЗаполнитьИнформациюОНовыхДокументах(Объект)
;

@ВПодсистеме
метод ЗарегистрироватьОзнакомлениесОбъектом(Объект: ДокументыНаПодпись.Ссылка, ФизическоеЛицо: ФизическиеЛица.Ссылка)
    знч НаборЗаписей = новый НовыеДокументы.НаборЗаписей()
    НаборЗаписей.Фильтр.Объект.Установить(Объект)
    НаборЗаписей.Фильтр.ФизическоеЛицо.Установить(ФизическоеЛицо)
    НаборЗаписей.Записать()
;

@ВПодсистеме
метод ЭтоНовыйДокумент(Объект: ДокументыНаПодпись.Ссылка, ФизическоеЛицо: ФизическиеЛица.Ссылка): Булево
    знч Запрос = Запрос{
        Выбрать Первые 1
            Истина Как ЭтоНовыйДокумент
        Из
            НовыеДокументы
        Где
            Объект == %Объект
            и ФизическоеЛицо == %ФизическоеЛицо}
    исп Результат = Запрос.Выполнить()
    если Результат.ЕдинственныйИлиУмолчание() != Неопределено
        возврат Истина
    ;
    возврат Ложь
;