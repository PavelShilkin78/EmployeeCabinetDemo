импорт УниверсальныеМеханизмы
импорт ФизическиеЛица

@ВПроекте
@НаСервере @ДоступноСКлиента
метод АннулирующийДокумент(АннулируемыйДокумент: АннулируемыйДокумент.Ссылка, ТолькоАктивные = Ложь): АннулирующийДокумент.Ссылка?
    знч Запрос = Запрос{
        Выбрать
            АннулирующийДокумент         
        ИЗ
            Аннулирование
        ГДЕ 
            АннулируемыйДокумент == %АннулируемыйДокумент
            И ((%ТолькоАктивные И Активно == Истина) Или Не %ТолькоАктивные)  
            
    }
    
    возврат Запрос.Выполнить().ПервыйИлиУмолчание()?.АннулирующийДокумент
;   

@ВПроекте
@НаСервере @ДоступноСКлиента
метод АннулируемыйДокумент(АннулирующийДокумент: АннулирующийДокумент.Ссылка, ТолькоАктивные = Ложь): АннулируемыйДокумент.Ссылка?
    знч Запрос = Запрос{
        Выбрать
            АннулируемыйДокумент
        ИЗ
            Аннулирование
        ГДЕ
            АннулирующийДокумент == %АннулирующийДокумент
            И ((%ТолькоАктивные И Активно == Истина) Или Не %ТолькоАктивные)  
    }
    
    возврат Запрос.Выполнить().ПервыйИлиУмолчание()?.АннулируемыйДокумент
;

@ВПроекте
@НаСервере @ДоступноСКлиента
метод АннулируемыеДокументы(АннулирующиеДокументы: ЧитаемыйМассив<АннулирующийДокумент.Ссылка>, ТолькоАктивные = Ложь):  Массив<АннулирующиеАннулируемыеДокументы>
    
    знч Запрос = Запрос{
        Выбрать
            АннулирующийДокумент,
            АннулируемыйДокумент
        Породить АннулирующиеАннулируемыеДокументы
        ИЗ
            Аннулирование
        ГДЕ
            АннулирующийДокумент В (%АннулирующиеДокументы)
            И ((%ТолькоАктивные И Активно == Истина) Или Не %ТолькоАктивные)
    }
    
    исп Результат = Запрос.Выполнить()
    
    возврат Результат.ВМассив()
       
;

@ВПроекте
@НаСервере
метод ЗаписатьАннулирование(ФизическоеЛицо: ФизическиеЛица.Ссылка, 
    АннулирующийДокумент: АннулирующийДокумент.Ссылка, АннулируемыйДокумент: АннулируемыйДокумент.Ссылка)

    знч Набор = новый Аннулирование.НаборЗаписей()
    Набор.Фильтр.Установить(
        ФизическоеЛицо = ФизическоеЛицо,
        АннулирующийДокумент = АннулирующийДокумент
    )
    Набор.ДобавитьЗапись(
        АннулируемыйДокумент = АннулируемыйДокумент

    )
    Набор.Записать()  
;

@ВПроекте
метод УдалитьАннулирование(АннулирующийДокумент: АннулирующийДокумент.Ссылка)
    знч Запрос = Запрос{
        ВЫБРАТЬ 
            Аннулирование.ФизическоеЛицо КАК ФизическоеЛицо
        ИЗ 
            Аннулирование КАК Аннулирование
        ГДЕ 
            Аннулирование.АннулирующийДокумент == %АннулирующийДокумент
    }
     
    исп Результат = Запрос.Выполнить()
    для Элемент из Результат
        знч Набор = новый Аннулирование.НаборЗаписей()
        Набор.Фильтр.Установить(
            ФизическоеЛицо = Элемент.ФизическоеЛицо,
            АннулирующийДокумент = АннулирующийДокумент
        ) 
        Набор.Записать()    
    ;
;

@ВПроекте
метод ИзменитьАктивностьАннулирования(АннулирующийДокумент: АннулирующийДокумент.Ссылка, Активность: Булево)
    знч Запрос = Запрос{
        ВЫБРАТЬ 
            Аннулирование.ФизическоеЛицо КАК ФизическоеЛицо
        ИЗ 
            Аннулирование КАК Аннулирование
        ГДЕ 
            Аннулирование.АннулирующийДокумент == %АннулирующийДокумент
    }
     
    исп Результат = Запрос.Выполнить()
    для Элемент из Результат
        знч Набор = новый Аннулирование.НаборЗаписей()
        Набор.Фильтр.Установить(
            ФизическоеЛицо = Элемент.ФизическоеЛицо,
            АннулирующийДокумент = АннулирующийДокумент)
        Набор.Прочитать()
        если не Набор.Пусто()
            Набор[0].Активно = Активность
        ;
        Набор.Записать()
    ;
;

@ВПроекте
метод ПриИзмененииСогласования(АннулирующийДокумент: АннулирующийДокумент.Ссылка, 
    ТекущееСостояние: СостоянияДокумента, ПредыдущееСостояние: СостоянияДокумента)
    если ТекущееСостояние == СостоянияДокумента.ПолученОтказ
        или ТекущееСостояние == СостоянияДокумента.Черновик
        или (ТекущееСостояние == СостоянияДокумента.Аннулирован и ПредыдущееСостояние == СостоянияДокумента.Подготовлен)
        ИзменитьАктивностьАннулирования(АннулирующийДокумент, Ложь)
    ;
;