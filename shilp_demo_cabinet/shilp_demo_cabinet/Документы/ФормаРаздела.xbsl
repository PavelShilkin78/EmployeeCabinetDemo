импорт ИнтерфейсныеКомпонентыСтандартные
импорт ОбщегоНазначения
импорт ОбщиеСтандартные
импорт ЛичныеКабинетыПользователей
импорт ВладельцыКабинетов
импорт Компания::Согласования
импорт Криптография
импорт УниверсальныеМеханизмы
импорт ФизическиеЛица

@ВПодсистеме
перечисление ВидыДокументов
    ДокументыНаПодпись умолчание,
    ДокументыНаСогласование,
    ДокументыСотрудника
;

@Обработчик
метод ПослеСоздания()
    ИнтерфейсныеКомпоненты.ИнициализироватьПодчиненныеКомпоненты(Компоненты)
    ОбновитьВидимостьЭлементов() 
            
    МенеджерГлобальныхКлиентскихСобытий.ПодключитьОбработчик(метод (Источник, Параметры) ->
        выбор Источник 
        когда ОбщегоНазначения.СОГЛАСИЕ_КЭДО_ПОДПИСАНО
            ОбновитьВидимостьЭлементов()
        когда Криптография.ОБНОВЛЕНА_ЭЛЕКТРОННАЯ_ПОДПИСЬ
            ОбновитьВидимостьЭлементов()
        ;
        возврат
    ;)
;

метод ОбновитьВидимостьЭлементов()
    знч ПараметрыСеансаВладельцыКабинетов = ВладельцыКабинетов.ПараметрыСеанса()
    знч ДанныеЗаполнения = ДанныеЗаполнения(
        ПараметрыСеансаВладельцыКабинетов.ФизическоеЛицо,
        ПараметрыСеансаВладельцыКабинетов.ИспользуетКэдо,
        Криптография.ПараметрыСеанса().ВидЭлектроннойПодписи)
    КомандаПодключитесьККэдоВидимость =  ДанныеЗаполнения.ЕстьНеподписанныеСогласия и Криптография.ПараметрыСеанса().ИспользованиеКЭДО
    ПамяткаКэдоВидимость = ДанныеЗаполнения.ПамяткаКэдоВидимость    
;

метод ФормаРазделаМаксимальнаяШирина(): Число
    возврат Содержимое.МаксимальнаяШирина
;

метод ФормаРазделаМинимальнаяШирина(): Число
    возврат Содержимое.МинимальнаяШирина
;

метод КомандаПодключитесьККэдоЗаголовок(): Строка
    возврат СогласиеНаПрисоединениеККэдоКлиентСервер.ТекстПриглашенияККэдоРасширенный(ВладельцыКабинетов.ПараметрыСеанса().ФункциональныеОпции.ПолучениеСогласияКэдоУнэп)
;

метод КомандаПодключитесьККэдоПриНажатии(Источник: Надпись, Событие: СобытиеПриНажатии)
    КомплектСогласиеНаПрисоединениеККэдоФорма.ОткрытьВМодальномОкне()  
;

метод ПамяткаКэдоЗаголовок(): Строка
    возврат СогласиеНаПрисоединениеККэдоКлиентСервер.ТекстПамяткаКэдо(ВладельцыКабинетов.ПараметрыСеанса().ИспользуетКэдо)
;

метод ДокументыНаПодписьВидимость(): Булево
    возврат ВидДокумента == ВидыДокументов.ДокументыНаПодпись
;

метод ДокументыНаСогласованиеВидимость(): Булево
    возврат ВидДокумента == ВидыДокументов.ДокументыНаСогласование
;

метод ДокументыСотрудниковВидимость(): Булево
    возврат ВидДокумента == ВидыДокументов.ДокументыСотрудника
;

метод ФормаРазделаКомандыСоздания(): Команда|ГруппаКомандногоИнтерфейса<Команда>? 
    если ВидДокумента == ВидыДокументов.ДокументыСотрудника
        возврат Компоненты.ДокументыСотрудников.КомандыСоздания
    иначе
        возврат Неопределено
    ;    
;

метод ПереключательВидовДокументовЭлементы(): Массив<ЭлементСпискаЗначений<ВидыДокументов>>
    пер ЭлементыВидовДокументов: Массив<ЭлементСпискаЗначений<ВидыДокументов>>
    
    пер ЭлементДокументовСотрудника = новый ЭлементСпискаЗначений<ВидыДокументов>(
        Представление = "Мои документы",
        Значение = ВидыДокументов.ДокументыСотрудника)
    ЭлементыВидовДокументов.Добавить(ЭлементДокументовСотрудника)
    
    пер ЭлементДокументыНаПодпись = новый ЭлементСпискаЗначений<ВидыДокументов>(
        Представление = "Подписать",
        Значение = ВидыДокументов.ДокументыНаПодпись)
    ЭлементыВидовДокументов.Добавить(ЭлементДокументыНаПодпись)
    
    если УчаствуетВСогласованиях
        пер ЭлементДокументыНаСогласование = новый ЭлементСпискаЗначений<ВидыДокументов>(
            Представление = КлиентскоеУстройство.ШиринаОкнаПриложения < 360 ? "Согласов." : "Согласовать",
            Значение = ВидыДокументов.ДокументыНаСогласование)
        ЭлементыВидовДокументов.Добавить(ЭлементДокументыНаСогласование)
    ;
    возврат ЭлементыВидовДокументов
;

метод УчаствуетВСогласованиях(): Булево
    знч ЭтоСогласователь = Согласования.ПараметрыСеанса().ЭтоСогласователь
    знч БылСогласователемРанее = не Согласования.ДокументыСогласованные(ЛичныеКабинетыПользователей.ПараметрыСеанса().ТекущееФизическоеЛицо).Пусто()
    возврат ЭтоСогласователь или БылСогласователемРанее    
;

@НаСервере @НаКлиенте
структура ДанныеЗаполнения
    пер ЕстьНеподписанныеСогласия: Булево
    пер ПамяткаКэдоВидимость: Булево
;

@НаСервере @ДоступноСКлиента
статический метод ДанныеЗаполнения(ФизическоеЛицо: ФизическиеЛица.Ссылка, ИспользуетКэдо: Булево,
    ВидЭлектроннойПодписи: ВидыЭлектронныхПодписей): ДанныеЗаполнения
    
    пер ДанныеЗаполнения: ДанныеЗаполнения
    ДанныеЗаполнения.ЕстьНеподписанныеСогласия = СогласиеНаПрисоединениеККэдоСервер.ЕстьНеподписанныеСогласия(ФизическоеЛицо)
    если ВидЭлектроннойПодписи == ВидыЭлектронныхПодписей.УНЭПИнформационнойСистемы
        
        ДанныеЗаполнения.ПамяткаКэдоВидимость = не ДанныеЗаполнения.ЕстьНеподписанныеСогласия
            и (СогласиеНаПрисоединениеККэдоСервер.ПамяткаКэдоВидимость()
                или ИспользуетКэдо и (не Криптография.ЕстьДействующийСертификат(ФизическоеЛицо)
                    или не Криптография.КорневойСертификатДействет(ФизическоеЛицо)))
    иначе если ВидЭлектроннойПодписи == ВидыЭлектронныхПодписей.УнэпАстралПлатформа
        
        ДанныеЗаполнения.ПамяткаКэдоВидимость = не ДанныеЗаполнения.ЕстьНеподписанныеСогласия
            и (СогласиеНаПрисоединениеККэдоСервер.ПамяткаКэдоВидимость()
                или ИспользуетКэдо и не Криптография.ЕстьДействующийСертификатСервисаПодписния(ФизическоеЛицо, ВидЭлектроннойПодписи))
    иначе
        ДанныеЗаполнения.ПамяткаКэдоВидимость = Ложь
    ;
    
    возврат ДанныеЗаполнения
;