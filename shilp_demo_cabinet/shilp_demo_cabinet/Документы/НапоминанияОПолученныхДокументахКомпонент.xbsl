импорт ОбщиеСтандартные

@Обработчик
метод ПослеСоздания()
    СрокиНапоминаний = СрокиНапоминаний()
;

метод ТабличнаяЧастьИсточникДанные(): Массив<НапоминанияОПолученныхДокументахСтрока>
    возврат СрокиНапоминаний
;

метод ДобавитьПриНажатии(Источник: Кнопка, Событие: СобытиеПриНажатии)
    пер ДанныеНовойСтроки = новый НапоминанияОПолученныхДокументахСтрока()
    ДанныеНовойСтроки.Модифицированность = Истина
    ОткрытьФормуСрокаНапоминания(ДанныеНовойСтроки)
;

метод ДобавитьДоступность(): Булево
    возврат не этот.ТолькоЧтение
;

метод ТабличнаяЧастьДоступность(): Булево
    возврат не этот.ТолькоЧтение
;
метод УдалитьСрокНапоминания(Источник: СтандартнаяКолонкаТаблицы<НапоминанияОПолученныхДокументахСтрока>, Событие: СобытиеПриНажатии, ДанныеСтроки: НапоминанияОПолученныхДокументахСтрока)
    пер Индекс: Число? = СрокиНапоминаний.Найти(ДанныеСтроки)
    если Индекс != Неопределено
        пер Строка = СрокиНапоминаний.Получить(Индекс) 
        Строка.Удален = Истина
        СохранитьСрокНапоминания(Строка)
        СрокиНапоминаний.УдалитьПоИндексу(Индекс)
    ;
;

метод ТабличнаяЧастьПриНажатииЭлемента(Источник: Список<ИсточникДанныхМассив<НапоминанияОПолученныхДокументахСтрока>>, Событие: СобытиеСДанными<СтрокаСписка<НапоминанияОПолученныхДокументахСтрока>>)
    ОткрытьФормуСрокаНапоминания(Событие.Данные.ДанныеСтроки)
;

метод ОткрытьФормуСрокаНапоминания(ДанныеСтроки: НапоминанияОПолученныхДокументахСтрока)
    пер КоличествоДнейИзмененное = СрокНапоминанияОПолученныхДокументахФорма.ОткрытьВМодальномОкне(
            КоличествоДней = ДанныеСтроки.КоличествоДней,
            ИсходныеСрокиНапоминания = КонвертироватьВКоличествоДней(СрокиНапоминаний))
        
    если КоличествоДнейИзмененное != Неопределено и КоличествоДнейИзмененное != ДанныеСтроки.КоличествоДней
        ДанныеСтроки.КоличествоДней = КоличествоДнейИзмененное
        если не КоллекцияСодержитСрокНапоминания(ДанныеСтроки)
            СрокиНапоминаний.Добавить(ДанныеСтроки)
            СохранитьСрокНапоминания(ДанныеСтроки)
        иначе
            ДанныеСтроки.Модифицированность = Истина
            СохранитьСрокНапоминания(ДанныеСтроки)
        ;
    ;
;

метод КонвертироватьВКоличествоДней(СрокиНапоминаний: Массив<НапоминанияОПолученныхДокументахСтрока>): Массив<Число>
    пер СрокиНапоминанийДни = новый Массив<Число>()
    для СрокНапомнинания из СрокиНапоминаний
        СрокиНапоминанийДни.Добавить(СрокНапомнинания.КоличествоДней)
    ;
    возврат СрокиНапоминанийДни
;

метод КоллекцияСодержитСрокНапоминания(ДанныеСтроки: НапоминанияОПолученныхДокументахСтрока): Булево

    для СрокНапоминания из СрокиНапоминаний
        если СрокНапоминания.КоличествоДней == ДанныеСтроки.КоличествоДней
            возврат Истина
        ;
    ;
    возврат Ложь
;

метод СохранитьСрокНапоминания(ДанныеСтроки: НапоминанияОПолученныхДокументахСтрока)
    ДанныеСтроки.Ссылка = ЗаписатьСрокНапоминания(ДанныеСтроки)
    ДанныеСтроки.Модифицированность = Ложь
;

метод КоличествоДнейЗаголовок(ДанныеЭлемента: НапоминанияОПолученныхДокументахСтрока): Строка
    пер КоличествоДнейСтрокой = ДанныеЭлемента.КоличествоДней.ВСтроку() + " " + МенеджерСтрок.ТекстПоКоличеству("день", ДанныеЭлемента.КоличествоДней)
    возврат  "Через %КоличествоДнейСтрокой после публикации"
;

@НаСервере @ДоступноСКлиента
статический метод СрокиНапоминаний(): Массив<НапоминанияОПолученныхДокументахСтрока>
    возврат СрокиНапоминанийОПолученныхДокументах.СрокиНапоминаний()
;

@НаСервере @ДоступноСКлиента
статический метод ЗаписатьСрокНапоминания(СрокНапоминания: НапоминанияОПолученныхДокументахСтрока): СрокиНапоминанийОПолученныхДокументах.Ссылка?
    
    пер СрокНапоминанияСсылка: СрокиНапоминанийОПолученныхДокументах.Ссылка? = СрокНапоминания.Ссылка
    если СрокНапоминания.Удален
        пер СрокНапоминанияОбъект = СрокНапоминания.Ссылка.ЗагрузитьОбъект()
        если СрокНапоминанияОбъект != Неопределено
            СрокНапоминанияОбъект.Удалить()
        ;
    иначе если СрокНапоминания.Модифицированность и СрокНапоминания.Ссылка == Неопределено
        пер СрокНапоминанияОбъект = новый СрокиНапоминанийОПолученныхДокументах.Объект()
        СрокНапоминанияОбъект.КоличествоДней = СрокНапоминания.КоличествоДней
        СрокНапоминанияОбъект.Записать()
        СрокНапоминанияСсылка = СрокНапоминанияОбъект.Ссылка
    иначе если СрокНапоминания.Модифицированность
        пер СрокНапоминанияОбъект = СрокНапоминания.Ссылка.ЗагрузитьОбъект()
        СрокНапоминанияОбъект.КоличествоДней = СрокНапоминания.КоличествоДней
        СрокНапоминанияОбъект.Записать()
    ;
    
    возврат СрокНапоминанияСсылка    
    
;