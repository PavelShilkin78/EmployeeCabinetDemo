импорт ОбщиеСтандартные
импорт УниверсальныеМеханизмы
импорт Криптография
импорт ЛичныеКабинетыПользователей
импорт ФизическиеЛица

метод СогласиеНаПрисоединениеККэдоЗаголовок(): Строка
    возврат "Согласие на присоединение к КЭДО"
;

метод ОтбираемыеСостояния(): Массив<СостоянияДокумента>?
    пер ОтбираемыеСостояния = новый Массив<СостоянияДокумента>()
    если ВыводитьЗавершенные == Ложь        
        ОтбираемыеСостояния.Добавить(СостоянияДокумента.Подготовлен)            
    ; 
    возврат ОтбираемыеСостояния   
;

метод СогласияВидимость(): Булево
    возврат Криптография.ПараметрыСеанса().ИспользованиеКЭДО
;

// Контракт ВладелецСостояниеДокументовКомпонент

@ВПроекте
@Реализация
метод ПредставленияДокументов(): ЧитаемаяКоллекция<СостояниеДокументаКомпонентДанных>
    знч ФизическоеЛицо = ЛичныеКабинетыПользователей.ПараметрыСеанса().ТекущееФизическоеЛицо
    возврат ПредставленияСогласийФизлица(ФизическоеЛицо)
;

@ВПроекте
@Реализация
метод ПриНажатии(Документ: ДокументИмеющийСостояние.Ссылка, ИдКомпонента: Строка = "")
    если Документ это СогласиеНаПрисоединениеККэдо.Ссылка
        СогласиеНаПрисоединениеККэдоФормаДокумента.ОткрытьВМодальномОкне(КлючОбъекта = Документ как СогласиеНаПрисоединениеККэдо.Ссылка)
    ;
;

@ВПроекте
@Реализация
метод ТипыОтображаемыхДокументов(): Массив<Тип>
    возврат []
;

// Конец ВладелецСостояниеДокументовКомпонент

@НаСервере 
структура ДанныеЗаявления
    обз пер ДатаДокумента: Дата
    обз пер Представление: Строка
;

@НаСервере @ДоступноСКлиента
статический метод ПредставленияСогласийФизлица(ФизическоеЛицо: ФизическиеЛица.Ссылка?): ЧитаемаяКоллекция<СостояниеДокументаКомпонентДанных>
    пер ПредставленияСогласийФизлица: Массив<СостояниеДокументаКомпонентДанных>
    знч ЗаявленияФизлица: Массив<ДокументИмеющийСостояние.Ссылка>
    знч ДанныеЗаявлений: Соответствие<ДокументИмеющийСостояние.Ссылка, ДанныеЗаявления>
    исп РезульатЗапроса = Запрос{Выбрать
            Ссылка Как Документ,
            Дата Как ДатаДокумента,
            ПредставлениеДокумента Как Представление
            Из СогласиеНаПрисоединениеККэдо
            Где ФизическоеЛицо == %ФизическоеЛицо}.Выполнить()
    для СтрокаРезультата из РезульатЗапроса
        ЗаявленияФизлица.Добавить(СтрокаРезультата.Документ)
        ДанныеЗаявлений.Вставить(СтрокаРезультата.Документ, новый ДанныеЗаявления(СтрокаРезультата.ДатаДокумента, СтрокаРезультата.Представление))
    ;
    ПредставленияСогласийФизлица = СостДокументов.СостоянияДокументов(ЗаявленияФизлица)
    для Компонент из ПредставленияСогласийФизлица
        Компонент.ДатаДокумента = ДанныеЗаявлений[Компонент.Документ].ДатаДокумента
        Компонент.Представление = ДанныеЗаявлений[Компонент.Документ].Представление
        Компонент.ДополнительноеПредставление = МенеджерДат.ФорматДаты(ДанныеЗаявлений[Компонент.Документ].ДатаДокумента)
    ;
    возврат ПредставленияСогласийФизлица
;