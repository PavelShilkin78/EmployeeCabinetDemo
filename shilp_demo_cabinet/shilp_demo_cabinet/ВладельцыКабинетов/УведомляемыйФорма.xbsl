импорт ИнтерфейсныеКомпонентыСтандартные
импорт ФизическиеЛица
импорт ЛичныеКабинетыПользователей
импорт Компания

@ВПодсистеме
статический метод Показать(Уведомляемый: Уведомляемый, ТаблицаУведомляемые: Уведомляемые, ТолькоЧтение: Булево = Ложь): Булево
    знч ФормаУведомляемого = новый УведомляемыйФорма(
        ТолькоЧтение = ТолькоЧтение,
        ИсходныеДанные = Уведомляемый,
        ТаблицаУведомляемые = ТаблицаУведомляемые)
    ФормаУведомляемого.Инициализировать()
    возврат ФормаУведомляемого.ОткрытьВМодальномОкне() ?? Ложь
;

метод Инициализировать()
    СкопироватьДанные(РедактируемыеДанные, ИсходныеДанные)
;

метод УведомляемыйПриИзменении(Источник: ПолеВвода<ФизическиеЛица.Ссылка?>, Событие: СобытиеПриИзменении<ФизическиеЛица.Ссылка?>)
    ПроверитьЗаполнение()
;

метод ПанельОсновнаяЗаписатьИЗакрыть(Источник: КоманднаяПанельФорм, Событие: СобытиеКомпонента)
    если не ПроверитьЗаполнение()
        возврат
    ;
    СкопироватьДанные(ИсходныеДанные, РедактируемыеДанные)
    Закрыть(Истина)
;

метод ПроверитьЗаполнение(): Булево
    пер РезультатПроверки = Истина
    УведомляемыйСообщениеОбОшибкеВвода = ""
    если Компоненты.Уведомляемый.Значение == Неопределено
        РезультатПроверки = Ложь
        УведомляемыйСообщениеОбОшибкеВвода = "Не выбран уведомляемый"
    иначе
        для ДанныеУведомляемого из ТаблицаУведомляемые.Уведомляемые
            если Компоненты.Уведомляемый.Значение == ДанныеУведомляемого.Уведомляемый
                РезультатПроверки = Ложь
                УведомляемыйСообщениеОбОшибкеВвода = "Сотрудник уже был выбран ранее"
                прервать
            ;
        ;
    ;
    возврат РезультатПроверки
;

метод ПанельОсновнаяСохранитьЧерновик(Источник: КоманднаяПанельФорм, Событие: СобытиеКомпонента)
    Закрыть(Ложь)
;

метод СкопироватьДанные(Приемник: Уведомляемый, Источник: Уведомляемый)
    Приемник.Уведомляемый = Источник.Уведомляемый
;

метод УведомляемыйФормаВысота(): Число
    возврат 30 * Метрики.РАЗМЕР_ПОЛУИНТЕРВАЛА
;

метод УведомляемыйРезультатПроверкиДанных(): РезультатПроверкиДанных
    возврат УведомляемыйСообщениеОбОшибкеВвода.Пусто() ? РезультатПроверкиДанных.Отсутствует : РезультатПроверкиДанных.Ошибка    
;

метод ПанельОсновнаяИспользованиеЗаписатьИЗакрыть(): Булево
    возврат не ТолькоЧтение
;

метод УведомляемыйНастройкиВводаСсылок(): НастройкиВводаСсылок
    возврат Компания.НастройкиВводаВсеФизическиеЛицаСотрудникиКроме(
        ЛичныеКабинетыПользователей.ПараметрыСеанса().ТекущееФизическоеЛицо)
;