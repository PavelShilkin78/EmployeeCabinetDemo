импорт СтандартноеПриложение
импорт ОбщиеСтандартные
импорт ФизическиеЛица
импорт ЛичныеКабинетыПользователей
импорт ОбщегоНазначения

@Обработчик
метод ПослеСоздания()
    пер ДанныеУведомлений: ДанныеУведомлений =
        ДанныеУведомлений(ЛичныеКабинетыПользователей.ПараметрыСеанса().ТекущееФизическоеЛицо!,
        ТаблицаЗаместители.Заместители, ТаблицаУведомляемые.Уведомляемые)
    Руководитель = ДанныеУведомлений.Руководитель
    Заместители = ДанныеУведомлений.Заместители
    Коллеги = ДанныеУведомлений.Коллеги
;

метод РуководительВидимость(): Булево
    возврат не Руководитель.Пусто()
;

метод ПредставлениеКоллегВидимость(): Булево
    возврат не Коллеги.Пусто()
;

метод ЗаместителиВидимость(): Булево
    возврат не Заместители.Пусто()
;

метод ГотовоПриНажатии(Источник: Кнопка, Событие: СобытиеПриНажатии)
    Закрыть()
;

метод УведомляемыеФормаМаксимальнаяШирина(): Число
    возврат ОсновнойИнтерфейс.МаксимальнаяШиринаФормыДокумента()
;

метод УведомляемыеФормаМинимальнаяШирина(): Число
    возврат ОсновнойИнтерфейс.МинимальнаяШиринаФормыДокумента()
;

метод УведомляемыеЗаголовокСписка(): Строка
    пер ЗаголовокСписка: Строка
    если ЕстьСтандартныеУведомляемые()
        ЗаголовокСписка = "А также..."
    ;
    возврат ЗаголовокСписка
;

метод ЕстьСтандартныеУведомляемые(): Булево
    возврат РуководительВидимость() или
            ПредставлениеКоллегВидимость() или
            ЗаместителиВидимость() 
;

@НаСервере @НаКлиенте
структура ДанныеУведомлений
    пер Руководитель: Строка
    пер Заместители: Строка
    пер Коллеги: Строка
;

@НаСервере @ДоступноСКлиента
статический метод ДанныеУведомлений(ФизическоеЛицо: ФизическиеЛица.Ссылка, ЗаместителиФормы:  Массив<ЗаместителиСтрока>,
    УведомляемыеФормы: Массив<Уведомляемый>): ДанныеУведомлений
    
    пер ДанныеУведомлений: ДанныеУведомлений
    пер УведомляемыеФизическогоЛица: УведомляемыеМенеджер.УведомляемыеОбОтсутствии =
        УведомляемыеМенеджер.УведомляемыеОбОтсутствииФизическогоЛица(
            ФизическоеЛицо, ЗаместителиФормы, УведомляемыеФормы)

    пер Уведомляемые = новый Множество<ФизическиеЛица.Ссылка>()
    если УведомляемыеФизическогоЛица.Руководитель != Неопределено
        Уведомляемые.Добавить(УведомляемыеФизическогоЛица.Руководитель)
    ;
    Уведомляемые.ДобавитьВсе(УведомляемыеФизическогоЛица.Заместители)
    если не Уведомляемые.Пусто()
        знч ЗапрашиваемыеДанные: ФизЛица.ЗапрашиваемыеДанныеФизическихЛиц
        ЗапрашиваемыеДанные.ЛичныеДанные = Истина
        знч ДанныеФизическихЛиц = ФизЛица.ДанныеФизическихЛиц(Уведомляемые, ЗапрашиваемыеДанные)
        если УведомляемыеФизическогоЛица.Руководитель != Неопределено
            ДанныеУведомлений.Руководитель = СоставляющиеФИО(ДанныеФизическихЛиц, УведомляемыеФизическогоЛица.Руководитель).ПредставлениеИмени(
                ФизЛица.ВариантПредставленияИмени.ФамилияИОРаздельно)
        ;
        знч КоличествоЗаместителей = УведомляемыеФизическогоЛица.Заместители.Размер()
        если КоличествоЗаместителей > 0
            пер ЗаместителиСтрокой: Строка
            знч ФИОФизическихЛиц: Соответствие<ФизическиеЛица.Ссылка, ФизЛица.ФИО>
            для Заместитель из УведомляемыеФизическогоЛица.Заместители
                ФИОФизическихЛиц.Вставить(Заместитель, СоставляющиеФИО(ДанныеФизическихЛиц, Заместитель))
            ;
            знч ФИОПервогоЗаместителя = ФИОФизическихЛиц[УведомляемыеФизическогоЛица.Заместители[0]].ПредставлениеИмени(
                ФизЛица.ВариантПредставленияИмени.ФамилияИОРаздельно)
            если КоличествоЗаместителей == 1
                ЗаместителиСтрокой = ФИОПервогоЗаместителя
            иначе если КоличествоЗаместителей == 2
                пер ФИОВторогоЗаместителя: Строка = ФИОФизическихЛиц[УведомляемыеФизическогоЛица.Заместители[1]].ПредставлениеИмени(
                    ФизЛица.ВариантПредставленияИмени.ФамилияИОРаздельно)
                ЗаместителиСтрокой = "%ФИОПервогоЗаместителя, %ФИОВторогоЗаместителя"
            иначе
                пер ЕщеЗаместители = "%{(КоличествоЗаместителей - 1).ВСтроку()} %{МенеджерСтрок.ТекстПоКоличеству("человек", КоличествоЗаместителей - 1)}"
                ЗаместителиСтрокой = "%ФИОПервогоЗаместителя и еще %ЕщеЗаместители"
            ;
            ДанныеУведомлений.Заместители = ЗаместителиСтрокой
        ;
    ;                
    пер Коллеги = УведомляемыеФизическогоЛица.Коллеги
    если не Коллеги.Пусто()
        ДанныеУведомлений.Коллеги = "%{Коллеги.Размер().ВСтроку()} %{МенеджерСтрок.ТекстПоКоличеству("коллега", Коллеги.Размер())}" 
    ; 
    возврат ДанныеУведомлений
;

@НаСервере
статический метод СоставляющиеФИО(
        ДанныеФизическихЛиц: ЧитаемоеСоответствие<ФизическиеЛица.Ссылка, ФизЛица.ДанныеФизлица>,
        ФизическоеЛицо: ФизическиеЛица.Ссылка): ФизЛица.ФИО
        
    пер ФИО: ФизЛица.ФИО
    если ДанныеФизическихЛиц.СодержитКлюч(ФизическоеЛицо)
        ФИО = ФизЛица.ДанныеФизическогоЛицаВФИО(ДанныеФизическихЛиц[ФизическоеЛицо])
    ;
    возврат ФИО 
;