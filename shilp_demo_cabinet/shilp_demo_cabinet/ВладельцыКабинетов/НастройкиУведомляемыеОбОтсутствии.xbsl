импорт ФизическиеЛица
импорт КонтрольДоступа

@Обработчик
метод ВычислитьРазрешенияДоступа(): ЧитаемаяКоллекция<РазрешениеДоступа>
    возврат [КонтрольДоступа.ПолныйДоступАутентифицированным(этот)]
;

@Обработчик
метод ВычислитьРазрешенияДоступаДляОбъектов(Записи: ЧитаемыйМассив<НастройкиУведомляемыеОбОтсутствии.ДанныеРасчетаРазрешений>): ЧитаемоеСоответствие<НастройкиУведомляемыеОбОтсутствии.ДанныеРасчетаРазрешений, ЧитаемаяКоллекция<РазрешениеДоступа>>
    знч Разрешения: Соответствие<НастройкиУведомляемыеОбОтсутствии.ДанныеРасчетаРазрешений, ЧитаемаяКоллекция<РазрешениеДоступа>>
    для Запись из Записи
        знч РазрешенияЭлемента: Множество<РазрешениеДоступа>
        РазрешенияЭлемента.ДобавитьВсе(КонтрольДоступа.ЧтениеРедактированиеДанныхРегистровПерсонально([Запись.Владелец]))
        Разрешения.Вставить(Запись, РазрешенияЭлемента)
    ;
    возврат Разрешения
;

@ВПодсистеме
метод Прочитать(Владелец: ФизическиеЛица.Ссылка): Уведомляемые
    знч НаборЗаписей = новый НастройкиУведомляемыеОбОтсутствии.НаборЗаписей()
    НаборЗаписей.Фильтр.Установить(Владелец = Владелец)
    НаборЗаписей.Прочитать()
    возврат новый Уведомляемые(
        Владелец = Владелец,
        Уведомляемые = НаборЗаписей.Преобразовать(З -> новый Уведомляемый(
            Уведомляемый = З.Уведомляемый
        ))        
    )
;

@ВПодсистеме
метод Записать(Уведомляемые: Уведомляемые)
    знч Владелец = Уведомляемые.Владелец как ФизическиеЛица.Ссылка
    знч НаборЗаписей = новый НастройкиУведомляемыеОбОтсутствии.НаборЗаписей()
    НаборЗаписей.Фильтр.Установить(Владелец = Владелец)
    для Уведомляемый из Уведомляемые.Уведомляемые
        НаборЗаписей.Добавить(новый НастройкиУведомляемыеОбОтсутствии.Запись(
            Владелец = Владелец,
            Уведомляемый = Уведомляемый.Уведомляемый
        ))
    ;
    НаборЗаписей.Записать()
;

@ВПодсистеме
метод ПеренестиДанные()
    знч Запрос = Запрос{
        ВЫБРАТЬ РАЗЛИЧНЫЕ
            Владелец,
            Уведомляемый 
        ИЗ
            УведомляемыеОбОтсутствииУстарело
        ГДЕ
            Владелец != Неопределено
            И Уведомляемый != Неопределено
    }
    знч НаборЗаписей = новый НастройкиУведомляемыеОбОтсутствии.НаборЗаписей()
    НаборЗаписей.Фильтр.Установить()
    исп РезультатЗапроса = Запрос.Выполнить()
    для СтрокаРезультат из РезультатЗапроса
        НаборЗаписей.Добавить(новый НастройкиУведомляемыеОбОтсутствии.Запись(
            Владелец = СтрокаРезультат.Владелец,
            Уведомляемый = СтрокаРезультат.Уведомляемый
        ))    
    ;
    НаборЗаписей.Записать()
;