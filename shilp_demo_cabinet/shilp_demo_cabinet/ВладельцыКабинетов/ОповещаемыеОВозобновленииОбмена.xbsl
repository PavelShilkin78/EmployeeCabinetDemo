импорт ФизическиеЛица
импорт ВладельцыКабинетов
импорт КонтрольДоступа
импорт ОбменДанными
импорт ОбщиеСтандартные
импорт Уведомления

//Область РазрешенияДоступа

@Обработчик
метод ВычислитьРазрешенияДоступа(): Массив<РазрешениеДоступа>
    возврат КонтрольДоступа.РазрешенияДоступаТаблицыБезПраваУдаления()
;

//КонецОбласти

//Область СлужебныйПрограммныйИнтерфейс

@ВПодсистеме
метод ОтправитьУведомленияОВозобновленииОбмена(ИнформационнаяСистема: ИнформационныеСистемы.Ссылка)
    
    знч Запрос = Запрос{
        Выбрать
            Ссылка,
            ФизическоеЛицо
        Из
            ОповещаемыеОВозобновленииОбмена
        Где
            ИнформационнаяСистема == %ИнформационнаяСистема}
    

    пер СсылкиФизическихЛиц: Множество<ФизическиеЛица.Ссылка>
    исп Результат = Запрос.Выполнить() 
    знч РезультатСтроки = новый ЧитаемыйМассив(Результат)
    если (не РезультатСтроки.Пусто())
        СсылкиФизическихЛиц.Добавить(РезультатСтроки[0].ФизическоеЛицо)
        РезультатСтроки[0].Ссылка.ЗагрузитьОбъект().Удалить()
    ;
    знч Запрашиваемыеданные: ВладельцыКабинетов.ЗапрашиваемыеДанныеВладельцевКабинетов
    Запрашиваемыеданные.Настройки = Истина
    Запрашиваемыеданные.ЗапрашиваемыеДанныеФизическихЛиц.ЛичныеДанные = Истина
    знч ДанныеФизическихЛиц = ВладельцыКабинетов.ДанныеВладельцевКабинетов(СсылкиФизическихЛиц, Запрашиваемыеданные)
    
    для ФизическоеЛицо из СсылкиФизическихЛиц
        пер ПредпочтительныйСпособУведомления = ДанныеФизическихЛиц[ФизическоеЛицо].Настройки.ПредпочтительныйСпособУведомления
        если ПредпочтительныйСпособУведомления != СпособыУведомления.EMail
            и ПредпочтительныйСпособУведомления != СпособыУведомления.Push
            продолжить
        ;
        знч СервисУведомлений = РассылкаУведомлений.ПолучитьСервис()
        если СервисУведомлений != Неопределено 
            знч Обращение = ДанныеФизическихЛиц[ФизическоеЛицо].ДанныеФизлица.ЛичныеДанные.ФИО.ПредставлениеИмени(ФизЛица.ВариантПредставленияИмени.ИмяОтчество)
            СервисУведомлений.ОтправитьУведомлениеПолучателю(
                ФизическоеЛицо, 
                новый РассылкаУведомлений.ТекстыУведомления(
                    "Возобновлена передача данных в кадровую службу", 
                    "Возобновлена передача данных в кадровую службу", 
                    "%Обращение!
                    Возобновлена передача данных в кадровую службу. Оформленные вами документы отправлены."))
        ;
    ;
;

@ВПодсистеме
метод ДобавитьОповещаемого(ФизическоеЛицо: ФизическиеЛица.Ссылка, ИнформационнаяСистема: ИнформационныеСистемы.Ссылка)
    знч Запрос = Запрос{
        Выбрать Первые 1
            Ссылка
         Из
            ОповещаемыеОВозобновленииОбмена
         Где
            ФизическоеЛицо == %ФизическоеЛицо
            И ИнформационнаяСистема == %ИнформационнаяСистема}
    исп Результат = Запрос.Выполнить() 
    знч РезультатСтроки = новый ЧитаемыйМассив(Результат)
    если (не РезультатСтроки.Пусто())
        возврат
    ;
    пер Оповещаемый = новый ОповещаемыеОВозобновленииОбмена.Объект()
    Оповещаемый.ФизическоеЛицо = ФизическоеЛицо
    Оповещаемый.ИнформационнаяСистема = ИнформационнаяСистема
    Оповещаемый.Записать()
;
//КонецОбласти