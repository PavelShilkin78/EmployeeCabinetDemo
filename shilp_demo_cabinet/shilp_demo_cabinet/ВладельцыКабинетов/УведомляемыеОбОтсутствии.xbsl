импорт КонтрольДоступа
импорт ОбщегоНазначения

@Обработчик
метод ВычислитьРазрешенияДоступа(): ЧитаемаяКоллекция<РазрешениеДоступа>
    возврат [КонтрольДоступа.ПолныйДоступАутентифицированным(этот)]
;

@Обработчик
метод ВычислитьРазрешенияДоступаДляОбъектов(Записи: ЧитаемыйМассив<УведомляемыеОбОтсутствии.ДанныеРасчетаРазрешений>): ЧитаемоеСоответствие<УведомляемыеОбОтсутствии.ДанныеРасчетаРазрешений, ЧитаемаяКоллекция<РазрешениеДоступа>>
    знч Разрешения: Соответствие<УведомляемыеОбОтсутствии.ДанныеРасчетаРазрешений, ЧитаемаяКоллекция<РазрешениеДоступа>>
    для Запись из Записи
        Разрешения.Вставить(Запись, КонтрольДоступа.КопироватьРазрешенияДоступаОбъектаДляРегистра(Запись.Документ))
    ;
    возврат Разрешения
;

@ВПодсистеме
метод Прочитать(Документ: ДокументОтсутствияСУведомляемыми.Ссылка): Уведомляемые
    знч НаборЗаписей = новый УведомляемыеОбОтсутствии.НаборЗаписей()
    НаборЗаписей.Фильтр.Установить(Документ = Документ)
    НаборЗаписей.Прочитать()
    возврат новый Уведомляемые(
        Владелец = Документ,
        Уведомляемые = НаборЗаписей.Преобразовать(З -> новый Уведомляемый(
            Уведомляемый = З.Уведомляемый
        ))        
    )    
;

@ВПодсистеме
метод Записать(Уведомляемые: Уведомляемые)
    знч Документ = Уведомляемые.Владелец как ДокументОтсутствияСУведомляемыми.Ссылка
    знч НаборЗаписей = новый УведомляемыеОбОтсутствии.НаборЗаписей()
    НаборЗаписей.Фильтр.Установить(Документ = Документ)
    для Уведомляемый из Уведомляемые.Уведомляемые
        НаборЗаписей.Добавить(новый УведомляемыеОбОтсутствии.Запись(
            Документ = Документ,
            Уведомляемый = Уведомляемый.Уведомляемый
        ))
    ;
    НаборЗаписей.Записать()    
;

@ВПодсистеме
метод ПеренестиДанные()
    исп РезультатЗапроса = ОбщегоНазначения.ПолучитьДанныеУведомляемыеОбОтсутствии()
    знч НаборЗаписей = новый УведомляемыеОбОтсутствии.НаборЗаписей()
    НаборЗаписей.Фильтр.Установить()
    для СтрокаРезультата из РезультатЗапроса
        НаборЗаписей.Добавить(новый УведомляемыеОбОтсутствии.Запись(
            Документ = (СтрокаРезультата.Документ как ДокументОтсутствияСУведомляемыми.Ссылка),
            Уведомляемый = СтрокаРезультата.Уведомляемый
        ))    
    ;
    НаборЗаписей.Записать()
;