импорт ОбщиеСтандартные

метод ТабличнаяЧастьИсточникДанные(): Массив<Уведомляемый>
    пер Данные: Массив<Уведомляемый>
    Данные.ДобавитьВсе(ТаблицаУведомляемые.Уведомляемые)
    возврат Данные
;

// Область РаботаСТабличнойЧастью

метод ДобавитьУведомляемогоПриНажатии(Источник: Кнопка, Событие: СобытиеПриНажатии)
    СтатистикаИспользованияПриложения.ЗарегистрироватьЦель("ДобавитьУведомляемого%ИдентификаторСтатистики")
    ОткрытьФормуУведомляемого(новый Уведомляемый(), Добавление = Истина)
;

метод УдалитьУведомляемого(ДанныеЭлемента: Уведомляемый)
    ТаблицаУведомляемые.Уведомляемые.Удалить(ДанныеЭлемента)
    СохранитьУведомляемых()
;

// КонецОбласти РаботаСТабличнойЧастью

метод ТаблицаСЗаголовкомВидимость(): Булево
    возврат ПоказыватьЗаголовок или ТабличнаяЧастьВидимость()
;

метод ТабличнаяЧастьВидимость(): Булево
    возврат не ТаблицаУведомляемые.Уведомляемые.Пусто()
;

метод ТабличнаяЧастьПриНажатииЭлемента(Источник: Список<ИсточникДанныхМассив<Уведомляемый>>, Событие: СобытиеСДанными<СтрокаСписка<Уведомляемый>>)
    ОткрытьФормуУведомляемого(Событие.Данные.ДанныеСтроки)
;

метод ВозможноРедактирование(): Булево
    возврат не ТолькоЧтение
;

метод ОткрытьФормуУведомляемого(ДанныеСтроки: Уведомляемый, Добавление: Булево = Ложь)
    знч БылиИзменения = УведомляемыйФорма.Показать(ДанныеСтроки, ТаблицаУведомляемые, ТолькоЧтение)
    если БылиИзменения
        если Добавление
            ТаблицаУведомляемые.Уведомляемые.Добавить(ДанныеСтроки)
        ;
        СохранитьУведомляемых()   
    ;
;

метод КоллекцияСодержитУведомляемого(ДанныеСтроки: Уведомляемый): Булево
    возврат не ТаблицаУведомляемые.Уведомляемые.Фильтровать(У -> У.Уведомляемый == ДанныеСтроки.Уведомляемый).Пусто()
;

метод СохранитьУведомляемых()
    если ДействиеПриИзменении != Неопределено
        ДействиеПриИзменении()
    ;
;

метод УведомляемыйЗаголовок(ДанныеЭлемента: Уведомляемый): Строка
    возврат МенеджерСущности.ПредставлениеСсылки(ДанныеЭлемента.Уведомляемый)
;

метод УведомляемыеКомпонентСтрокаУдалитьПриНажатии(Источник: СтандартнаяКолонкаТаблицы<Уведомляемый>, Событие: СобытиеПриНажатии, ДанныеЭлемента: Уведомляемый)
    УдалитьУведомляемого(ДанныеЭлемента)
;
