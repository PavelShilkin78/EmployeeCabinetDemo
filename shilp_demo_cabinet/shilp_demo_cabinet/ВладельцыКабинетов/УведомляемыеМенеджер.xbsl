импорт ОбщиеСтандартные
импорт ФизическиеЛица
импорт Компания
импорт ОбщегоНазначения

@ВПроекте
структура УведомляемыеОбОтсутствии
    пер Руководитель: ФизическиеЛица.Ссылка?
    пер Заместители: Массив<ФизическиеЛица.Ссылка>
    пер Уведомляемые: Массив<ФизическиеЛица.Ссылка>
    пер Коллеги: Массив<ФизическиеЛица.Ссылка>
;

@ВПроекте
метод УведомляемыеПоУмолчанию(Документ: ДокументОтсутствияСУведомляемыми.Ссылка, Владелец: ФизическиеЛица.Ссылка): Уведомляемые
    знч Уведомляемые = НастройкиУведомляемыеОбОтсутствии.Прочитать(Владелец)    
    Уведомляемые.Владелец = Документ
    возврат Уведомляемые
;

@ВПроекте
метод Уведомляемые(Документ: ДокументОтсутствияСУведомляемыми.Ссылка): Уведомляемые
    возврат УведомляемыеОбОтсутствии.Прочитать(Документ)
;

@ВПодсистеме
метод УведомляемыеОбОтсутствииФизическогоЛица(ФизическоеЛицо: ФизическиеЛица.Ссылка,
    Заместители:  Массив<ЗаместителиСтрока>, Уведомляемые: Массив<Уведомляемый>): УведомляемыеОбОтсутствии
    
    пер УведомляемыеОбОтсутствии = новый УведомляемыеОбОтсутствии()
    пер Руководитель = ОрганизационнаяСтруктура.НепосредственныйРуководительФизическогоЛица(ФизическоеЛицо)
    УведомляемыеОбОтсутствии.Руководитель = Руководитель
    
    для ОписаниеЗаместителя из Заместители
        УведомляемыеОбОтсутствии.Заместители.Добавить(ОписаниеЗаместителя.Заместитель)
    ;
    для ОписаниеУведомляемого из Уведомляемые
        УведомляемыеОбОтсутствии.Уведомляемые.Добавить(ОписаниеУведомляемого.Уведомляемый)
    ;
    пер Сотрудники = Сотрудники.СотрудникиФизическогоЛица(ФизическоеЛицо)
    если не Сотрудники.Пусто()
        пер Подразделение = МенеджерСущности.ЗначениеРеквизитаОбъекта(Сотрудники[0], "МестоВСтруктуреПредприятия")
        пер Коллеги = новый Массив<ФизическиеЛица.Ссылка>(ОрганизационнаяСтруктура.ФизическиеЛицаПодразделения(Подразделение))
        если Руководитель != Неопределено и Коллеги.Содержит(Руководитель)
            Коллеги.Удалить(Руководитель)
        ;
        если Коллеги.Содержит(ФизическоеЛицо)
            Коллеги.Удалить(ФизическоеЛицо)
        ;
        для Заместитель из УведомляемыеОбОтсутствии.Заместители
            если Коллеги.Содержит(Заместитель)
                Коллеги.Удалить(Заместитель)
            ;
        ;
        для Уведомляемый из УведомляемыеОбОтсутствии.Уведомляемые
            если Коллеги.Содержит(Уведомляемый)
                Коллеги.Удалить(Уведомляемый)
            ;
        ;
        УведомляемыеОбОтсутствии.Коллеги.ДобавитьВсе(Коллеги)
    ;
    возврат УведомляемыеОбОтсутствии
;

@ВПроекте 
метод Записать(Уведомляемые: Уведомляемые)
    УведомляемыеОбОтсутствии.Записать(Уведомляемые)
;