импорт СтандартноеПриложение
импорт ИнтерфейсныеКомпонентыСтандартные
импорт Криптография
импорт ОбменДанными
импорт ОбщегоНазначения
импорт Компания::Согласования
импорт УниверсальныеМеханизмы
импорт ФизическиеЛица
импорт УниверсальныеМеханизмы::ШаблоныДокументов
импорт Компания

// Методы заполнения формы

метод ЗапросИзмененияЛичныхДанныхФормаДокументаВысота(): Число
    возврат ОсновнойИнтерфейс.ВысотаФормыДокумента(100)
;

метод ПанельОсновнаяЗаголовокКнопкиЗаписатьИЗакрыть(): Строка
    возврат Компоненты.Содержимое.ВозможноРедактирование()  и не Компоненты.Содержимое.РежимПросмотра ? "Отправить" : "Закрыть" 
;

метод ПанельОсновнаяИспользованиеСохранитьЧерновик(): Булево
    возврат Компоненты.Содержимое.ВозможноРедактирование() и не Компоненты.Содержимое.РежимПросмотра
;

метод ОсновнойКомпонентПанели(): Компонент
    возврат Компоненты.Содержимое
;

// Конец

// Обработчики событий формы

@Обработчик
метод ПослеЧтения()
    Компоненты.Содержимое.УстановитьТекущееЗаявление(Объект)
    УстановитьРежимПросмотра()
    ИнтерфейсныеКомпоненты.ИнициализироватьПодчиненныеКомпоненты(Компоненты)
;

метод ПанельОсновнаяСохранитьЧерновик(Источник: КоманднаяПанельФорм, Событие: СобытиеКомпонента)
    ЗаписатьИЗакрыть(Ложь)
;

метод ПанельОсновнаяЗаписатьИЗакрыть(Источник: КоманднаяПанельФорм, Событие: СобытиеКомпонента)
    ЗаписатьИЗакрыть(Истина)
;

метод СодержимоеЗавершеноЧтениеДанных(Источник: ЗапросИзмененияЛичныхДанныхКомпонент, Событие: неизвестно)
    Компоненты.ГлавнаяГруппа.ЧтениеДанныхЗавершено = Истина 
;

метод СодержимоеИзменениеСостоянияЗагрузкиФайлов(Источник: ЗапросИзмененияЛичныхДанныхКомпонент, Событие: неизвестно)
    УстановитьДоступностьКоманд(не Событие.Данные)
;

// Конец

// Прочее

метод ЗаписатьИЗакрыть(ОтправитьНаСогласование: Булево)
    если не Компоненты.Содержимое.ПроверитьЗаполнение()
        возврат
    ;
    если не Компоненты.Содержимое.ВозможноРедактирование()
        или Компоненты.Содержимое.РежимПросмотра
        
        Закрыть()
        возврат
    ;
    если ОтправитьНаСогласование
        и не Криптография.ВвестиПарольЗакрытогоКлюча(
            Объект.ФизическоеЛицо, Объект.Ссылка)
        
        возврат
    ;
    
    // Признак ЭтоНовый получаем до записи 
    пер ЭтоНовый: Булево = ЭтоНовый()
    пер Состояние: СостоянияДокумента 
    пер ИзмененноеФото = Компоненты.Содержимое.ПрисоединенноеФото
    пер ИзменениеФото = Объект.ИзменениеФото
    
    если ОтправитьНаСогласование
        
        пер ИзменилисьФамилияИлиИмя = Компоненты.Содержимое.ИзменилисьФамилияИлиИмя
        пер ИзменилсяДокумент = Компоненты.Содержимое.ИзменилсяДокумент
        пер ИзменилсяНомерМобильного = Компоненты.Содержимое.ИзменилсяНомерМобильного
        пер ИзменилсяАдресЭлектроннойПочты = Компоненты.Содержимое.ИзменилсяАдресЭлектроннойПочты
        пер ИзменилсяСНИЛС = Компоненты.Содержимое.ИзменилсяСНИЛС
        пер ИзменилсяИНН = Компоненты.Содержимое.ИзменилсяИНН
        пер ИзменилсяАдрес = Компоненты.Содержимое.ИзменилсяАдрес
        
        пер ИзменениеФИО = Объект.ИзменениеФИО
        пер ИзменениеДокумента = Объект.ИзменениеДокумента
        пер ИзменениеМобильногоТелефона = Объект.ИзменениеМобильногоТелефона
        пер ИзменениеАдресаЭлектроннойПочты = Объект.ИзменениеАдресаЭлектроннойПочты
        пер ИзменениеСНИЛС = Объект.ИзменениеСНИЛС
        пер ИзменениеИНН = Объект.ИзменениеИНН
        пер ИзменениеАдреса = Объект.ИзменениеАдреса
        пер Комментарий = Объект.Комментарий
        выбор 
        когда не ИзменениеФИО и не ИзменениеДокумента и не ИзменениеМобильногоТелефона и не ИзменениеАдресаЭлектроннойПочты и не ИзменениеАдреса и не ИзменениеСНИЛС и не ИзменениеИНН и не ИзменениеФото и Комментарий.Пусто()
            выбросить новый ОбщегоНазначения.ОшибкаПроверкиЗаполненияФормы("Для оформления запроса необходимо уточнить какие именно данные изменились или описать необходимые изменения в комментарии")
        когда ИзменениеФИО и 
            Объект.Фамилия.Пусто() и 
            Объект.Имя.Пусто() и 
            Объект.Отчество.Пусто()
            выбросить новый ОбщегоНазначения.ОшибкаПроверкиЗаполненияФормы("Не заполнены фамилия имя и отчество. Снимите флажок \"%ИзменилисьФамилияИлиИмя\" или заполните изменения")
        когда ИзменениеДокумента и 
            Объект.ВидУдостоверенияЛичности == Неопределено и 
            Объект.ДокументСерия.Пусто() и 
            Объект.ДокументНомер.Пусто() и 
            Объект.ДокументДатаВыдачи == Неопределено и
            Объект.ДокументВыдавшийОрган.Пусто() и 
            Объект.ДокументКодВыдавшегоОргана.Пусто()
            выбросить новый ОбщегоНазначения.ОшибкаПроверкиЗаполненияФормы("Не заполнены данные документа. Снимите флажок \"%ИзменилсяДокумент\" или заполните изменения")
        когда ИзменениеМобильногоТелефона и 
            Объект.МобильныйТелефон.Пусто()
            выбросить новый ОбщегоНазначения.ОшибкаПроверкиЗаполненияФормы("Не заполнен мобильный телефон. Снимите флажок \"%ИзменилсяНомерМобильного\" или впишите номер телефона")
        когда ИзменениеАдресаЭлектроннойПочты и 
            Объект.ЭлектроннаяПочта.Пусто()
            выбросить новый ОбщегоНазначения.ОшибкаПроверкиЗаполненияФормы("Не заполнен e-mail. Снимите флажок \"%ИзменилсяАдресЭлектроннойПочты\" или впишите адрес электронной почты")
        когда ИзменениеСНИЛС и 
            Объект.СНИЛС.Пусто()
            выбросить новый ОбщегоНазначения.ОшибкаПроверкиЗаполненияФормы("Не заполнен СНИЛС. Снимите флажок \"%ИзменилсяСНИЛС\" или впишите номер СНИЛС")
        когда ИзменениеИНН и 
            Объект.ИНН.Пусто()
            выбросить новый ОбщегоНазначения.ОшибкаПроверкиЗаполненияФормы("Не заполнен ИНН. Снимите флажок \"%ИзменилсяИНН\" или впишите номер ИНН")   
        когда ИзменениеАдреса и 
            Объект.АдресРегистрации.Пусто() и 
            Объект.АдресМестаПроживания.Пусто()
            выбросить новый ОбщегоНазначения.ОшибкаПроверкиЗаполненияФормы("Не заполнены адреса. Снимите флажок \"%ИзменилсяАдрес\" или заполните изменения")
        когда ИзменениеФото и ИзмененноеФото == Неопределено
            выбросить новый ОбщегоНазначения.ОшибкаПроверкиЗаполненияФормы("Не добавлено изменённое фото. Снимите флажок \"Изменилось фото\" или заполните изменения")
        ;
        Состояние = СостоянияДокумента.Подготовлен
    ;
        
    пер ТабличныеЧасти = Компоненты.Содержимое.МодифицированныеТабличныеЧасти()
    
    пер ДанныеДокумента = новый ДанныеДокумента()
        
    ДанныеДокумента.Ссылка = Объект.Ссылка
    ДанныеДокумента.Дата = Объект.Дата
    ДанныеДокумента.ФизическоеЛицо = Объект.ФизическоеЛицо
    ДанныеДокумента.ИзменениеФИО = Объект.ИзменениеФИО
    ДанныеДокумента.Фамилия = Объект.Фамилия
    ДанныеДокумента.Имя = Объект.Имя
    ДанныеДокумента.Отчество = Объект.Отчество
    ДанныеДокумента.ИзменениеДокумента = Объект.ИзменениеДокумента
    ДанныеДокумента.ВидУдостоверенияЛичности = Объект.ВидУдостоверенияЛичности
    ДанныеДокумента.ДокументСерия = Объект.ДокументСерия
    ДанныеДокумента.ДокументНомер = Объект.ДокументНомер
    ДанныеДокумента.ДокументДатаВыдачи = Объект.ДокументДатаВыдачи
    ДанныеДокумента.ДокументВыдавшийОрган = Объект.ДокументВыдавшийОрган
    ДанныеДокумента.ДокументКодВыдавшегоОргана = Объект.ДокументКодВыдавшегоОргана
    ДанныеДокумента.ИзменениеМобильногоТелефона = Объект.ИзменениеМобильногоТелефона
    ДанныеДокумента.МобильныйТелефон = Объект.МобильныйТелефон
    ДанныеДокумента.ИзменениеАдресаЭлектроннойПочты = Объект.ИзменениеАдресаЭлектроннойПочты
    ДанныеДокумента.ЭлектроннаяПочта = Объект.ЭлектроннаяПочта   
    ДанныеДокумента.ИзменениеСНИЛС = Объект.ИзменениеСНИЛС
    ДанныеДокумента.СНИЛС = Объект.СНИЛС
    ДанныеДокумента.ИзменениеИНН = Объект.ИзменениеИНН
    ДанныеДокумента.ИНН = Объект.ИНН  
    ДанныеДокумента.ИзменениеАдреса = Объект.ИзменениеАдреса
    ДанныеДокумента.АдресРегистрации = Объект.АдресРегистрации
    ДанныеДокумента.АдресМестаПроживания = Объект.АдресМестаПроживания
    ДанныеДокумента.ИзменениеФото = ИзменениеФото
    ДанныеДокумента.ПрисоединенноеФото = ИзмененноеФото
    ДанныеДокумента.Комментарий = Объект.Комментарий
    
    пер СостояниеОбмена: ОбменДанными.СостояниеОбмена
    знч РезультатПодписания = Записать(
            ЭтоНовый(), 
            ДанныеДокумента, 
            ТабличныеЧасти, 
            ОтправитьНаСогласование,
            Криптография.ПараметрыСеанса().ПарольЗакрытогоКлючаПользователя, 
            ВладельцыКабинетов.ПараметрыСеанса().ИнформационныеСистемы, СостояниеОбмена)
    если РезультатПодписания.ЕстьОшибки
        СтандартныеФормы.ПоказатьУведомление(
            "Ошибка подписания документа",
            РезультатПодписания.ТекстОшибки,
            ОценкаИнформации.Отрицательная
        )
        возврат
    ;

    если ОтправитьНаСогласование
        если Криптография.УстановитьПодписьВСервисеПодписания(
            Объект.Ссылка, ДанныеДокумента.ФизическоеЛицо, Согласования.РольПодписантаСотрудник()) == Ложь
            
            Прочитать(Объект.Ссылка)
            возврат
        ;
    ;
    Компоненты.Содержимое.СброситьМодифицированностьТабличныхЧастей()
    если РассчитаннаяМодифицированность
        СобственнаяМодифицированность = Ложь
    ;

    // Опровещение выполянем перед закрытием - когда уже записаны все данные, сопровождающие запись документа
    знч ДанныеСостояния = СостояниеДокументаКомпонентДанных.Создать()
    ДанныеСостояния.Документ = ДанныеДокумента.Ссылка
    ДанныеСостояния.ДатаДокумента = ДанныеДокумента.Дата
    ДанныеСостояния.Состояние = Состояние
    ДанныеСостояния.ДополнительноеПредставление = СостДокументов.ПредставлениеПериодаДокумента(ДатаДокумента = ДанныеДокумента.Дата)
    ИзмененоСостояниеДокумента.Оповестить(ДанныеСостояния, ЭтоНовый)
    если ОтправитьНаСогласование 
        пер ЗаголовокУведомления = "Запрос подготовлен"
        пер ТекстУведомления = "Запрос на изменение личных данных отправлен в кадровую службу. После его обработки данные будут изменены."
        пер КоннотацияУведомления: ОценкаИнформации = ОценкаИнформации.Обычная
        если СостояниеОбмена.ОповеститьОбОтсутствииОбмена
             ЗаголовокУведомления = "Приостановлена отправка запроса"
             ТекстУведомления = "Запрос на изменение личных данных сейчас не может быть отправлен. Запрос будет отправлен в кадровую службу, как только появится такая возможность." 
             если СостояниеОбмена.ОповеститьСотрудникаОВозобновленииОбмена
                ТекстУведомления += " Вы получите уведомление после его отправки."
             ;
             КоннотацияУведомления = ОценкаИнформации.Предупреждающая
        ;
        СтандартныеФормы.ПоказатьУведомление(ЗаголовокУведомления, ТекстУведомления, КоннотацияУведомления)
    ;
    Закрыть()
;

метод УстановитьДоступностьКоманд(Доступность: Булево)
    пер ГлавнаяГруппа: КорневойКомпонентМодальнойФормы = Компоненты.ГлавнаяГруппа
    для КоманднаяПанель из ГлавнаяГруппа.КомандныеПанели
         КоманднаяПанель.Доступность = Доступность
    ;
;

метод ОшибкаПриВыполненииКоманды(Источник: КоманднаяПанельФорм, Событие: СобытиеСДанными<Исключение>)
    Криптография.СброситьПарольЗакрытогоКлючаПриОшибкеВводаПароля(Событие.Данные)
    Компоненты.ГлавнаяГруппа.ПоказатьОшибку(
        ОбщегоНазначения.ОписаниеОшибкиПриДействииСДокументом(Событие.Данные, Объект.ФизическоеЛицо))
;

метод ВключеноИзменениеРасширенногоНабораДанных(): Булево    
    возврат ВключеноИзменениеРасширенногоНабораДанныхСервер()
;

@НаСервере @НаКлиенте
структура ДанныеДокумента
    пер Ссылка: ЗапросИзмененияЛичныхДанных.Ссылка?
    пер Дата: Дата?
    пер ФизическоеЛицо: ФизическиеЛица.Ссылка?
    пер ИзменениеФИО: Булево
    пер Фамилия: Строка
    пер Имя: Строка
    пер Отчество: Строка
    пер ИзменениеДокумента: Булево
    пер ВидУдостоверенияЛичности: ДокументыУдостоверяющиеЛичность?
    пер ДокументСерия: Строка
    пер ДокументНомер: Строка
    пер ДокументДатаВыдачи: Дата?
    пер ДокументВыдавшийОрган: Строка
    пер ДокументКодВыдавшегоОргана: Строка
    пер ИзменениеМобильногоТелефона: Булево
    пер МобильныйТелефон: Строка
    пер ИзменениеАдресаЭлектроннойПочты: Булево
    пер ЭлектроннаяПочта: Строка
    пер ИзменениеСНИЛС: Булево
    пер СНИЛС: Строка
    пер ИзменениеИНН: Булево
    пер ИНН: Строка    
    пер ИзменениеАдреса: Булево
    пер АдресРегистрации: Строка
    пер АдресМестаПроживания: Строка
    пер Комментарий: Строка
    пер ИзменениеФото: Булево
    пер ПрисоединенноеФото: ПрисоединенныйФайл?
;

@НаСервере @ДоступноСКлиента
статический метод Записать(ЭтоНовый: Булево, 
        ДанныеДокумента: ДанныеДокумента, 
        ТабличныеЧасти: ЗапросИзмененияЛичныхДанныхКомпонент.ТабличныеЧасти, 
        ОтправитьНаСогласование: Булево,
        ПарольЗакрытогоКлюча: Строка?, 
        ИнформационныеСистемы: Соответствие<ИнформационныеСистемы.Ссылка, ОбменДанными.НастройкиИнформационнойСистемы>, 
        СостояниеОбмена: ОбменДанными.СостояниеОбмена): Криптография.РезультатПодписания
    
    пер Заявление: ЗапросИзмененияЛичныхДанных.Объект?
    пер ЗаявлениеСсылка: ЗапросИзмененияЛичныхДанных.Ссылка? = ДанныеДокумента.Ссылка
    
    если ЭтоНовый
        Заявление = новый ЗапросИзмененияЛичныхДанных.Объект(ЗаявлениеСсылка.Ид).Заполнить()
    иначе
        Заявление = ЗаявлениеСсылка.ЗагрузитьОбъект()
    ;
    
    Заявление!.Дата = ДанныеДокумента.Дата
    Заявление!.ФизическоеЛицо = ДанныеДокумента.ФизическоеЛицо
    Заявление!.ИзменениеФИО = ДанныеДокумента.ИзменениеФИО
    Заявление!.Фамилия = ДанныеДокумента.Фамилия
    Заявление!.Имя = ДанныеДокумента.Имя
    Заявление!.Отчество = ДанныеДокумента.Отчество
    Заявление!.ИзменениеДокумента = ДанныеДокумента.ИзменениеДокумента
    Заявление!.ВидУдостоверенияЛичности = ДанныеДокумента.ВидУдостоверенияЛичности
    Заявление!.ДокументСерия = ДанныеДокумента.ДокументСерия
    Заявление!.ДокументНомер = ДанныеДокумента.ДокументНомер
    Заявление!.ДокументДатаВыдачи = ДанныеДокумента.ДокументДатаВыдачи
    Заявление!.ДокументВыдавшийОрган = ДанныеДокумента.ДокументВыдавшийОрган
    Заявление!.ДокументКодВыдавшегоОргана = ДанныеДокумента.ДокументКодВыдавшегоОргана
    Заявление!.ИзменениеМобильногоТелефона = ДанныеДокумента.ИзменениеМобильногоТелефона
    Заявление!.МобильныйТелефон = ДанныеДокумента.МобильныйТелефон
    Заявление!.ИзменениеАдресаЭлектроннойПочты = ДанныеДокумента.ИзменениеАдресаЭлектроннойПочты
    Заявление!.ЭлектроннаяПочта = ДанныеДокумента.ЭлектроннаяПочта
    Заявление!.ИзменениеСНИЛС = ДанныеДокумента.ИзменениеСНИЛС
    Заявление!.СНИЛС = ДанныеДокумента.СНИЛС
    Заявление!.ИзменениеИНН = ДанныеДокумента.ИзменениеИНН
    Заявление!.ИНН = ДанныеДокумента.ИНН
    Заявление!.ИзменениеАдреса = ДанныеДокумента.ИзменениеАдреса
    Заявление!.АдресРегистрации = ДанныеДокумента.АдресРегистрации
    Заявление!.АдресМестаПроживания = ДанныеДокумента.АдресМестаПроживания
    Заявление!.ИзменениеФото = ДанныеДокумента.ИзменениеФото
    
    Заявление!.Комментарий = ДанныеДокумента.Комментарий
    исп Транзакции.Начать()
    Заявление.Записать()
    Заявление!.Фото = ДанныеДокумента.ПрисоединенноеФото == Неопределено ? Неопределено : ПрисоединенныеФайлыМенеджер.Записать(ДанныеДокумента.ПрисоединенноеФото, ЗаявлениеСсылка)
    
    Заявление.Записать()
    
    если не ТабличныеЧасти.ТаблицаФайлы == Неопределено
        ПрисоединенныеФайлыМенеджер.Записать(ТабличныеЧасти.ТаблицаФайлы)
    ;
    ШаблоныДокументовМенеджер.СохранитьРеквизиты(ТабличныеЧасти.ДополнительныеРеквизиты)
    ПредставленияДокументов.СформироватьПредставленияДокумента(ЗаявлениеСсылка, Истина)
    пер РезультатПодписания: Криптография.РезультатПодписания
    если ОтправитьНаСогласование
        знч ДанныеСогласующего = новый Согласования.ДанныеСогласующего(ДанныеДокумента.ФизическоеЛицо, Согласования.РольПодписантаСотрудник())
        РезультатПодписания = Согласования.ПодписатьИУстановитьСостояниеДокумента(ЗаявлениеСсылка, ДанныеСогласующего, ПарольЗакрытогоКлюча, СостоянияДокумента.Подготовлен, ДанныеДокумента.ФизическоеЛицо)
        если РезультатПодписания.ЕстьОшибки
            возврат РезультатПодписания
        ;
        СостояниеОбмена = ВладельцыКабинетов.ПроверитьСостояниеОбмена(ДанныеДокумента.ФизическоеЛицо)
    ;
    возврат РезультатПодписания
;

@НаСервере @ДоступноСКлиента
статический метод ВключеноИзменениеРасширенногоНабораДанныхСервер(): Булево
       
    знч ФункциональныеОпции = ВладельцыКабинетов.ПолучитьФункциональныеОпции()    
    возврат ФункциональныеОпции.РасширенныйЗапросНаИзменениеЛичныхДанных   
;

метод УстановитьРежимПросмотра()
    если не Компания.ПараметрыСеанса().ТекущийПользовательСотрудник
        Компоненты.Содержимое.РежимПросмотра = Истина
    ;
;

// Конец