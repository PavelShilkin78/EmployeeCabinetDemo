импорт ВебApi
импорт УниверсальныеМеханизмы
импорт ЛичныеКабинетыПользователей
импорт ФизическиеЛица
импорт УниверсальныеМеханизмы::ШаблоныДокументов

@ВПодсистеме
метод ОбновитьСостояниеЗапросовИзмененияЛичныхДанных(ЗапросHttp: HttpСервисЗапрос, ФорматОбмена: Версия? = Неопределено)
    ВебApiСервер.ЗагрузитьОбъект(ЗапросHttp, Тип<СостояниеДокументов.Ссылка>,
        [новый СервисыHttp.ОжидаемыйПараметр("requestID", Истина, СервисыHttp.ТипыПараметраUrl.Строка),
        новый СервисыHttp.ОжидаемыйПараметр("status", Истина, СервисыHttp.ТипыПараметраUrl.Строка)], 
    новый ВебApiСервер.ИнструментыМенеджераОбъекта(
        (Объект: Сущность.Объект) -> (Объект как СостояниеДокументов.Объект).Записать()
    ), ФорматОбмена)
;

@ВПодсистеме
метод ОбновитьДоступныеФункцииФизическихЛиц(ЗапросHttp: HttpСервисЗапрос, ФорматОбмена: Версия? = Неопределено)
    ВебApiСервер.ЗагрузитьОбъекты(ЗапросHttp, Тип<ДоступныеФункцииФизическихЛиц.Ссылка>, 
    новый ВебApiСервер.ИнструментыМенеджераОбъекта(
        (Объект: Сущность.Объект) -> (Объект как ДоступныеФункцииФизическихЛиц.Объект).Записать()
    ), ФорматОбмена)
;

@ВПодсистеме
метод ПолучитьЗапросИзмененияЛичныхДанных(ЗапросHttp: HttpСервисЗапрос, ФорматОбмена: Версия? = Неопределено)
    ВебApiСервер.ОтправитьОбъектПоСсылке(
        ЗапросHttp, 
        Тип<ЗапросИзмененияЛичныхДанных.Ссылка>, 
        новый СервисыHttp.ОжидаемыйПараметр("ID", Истина, СервисыHttp.ТипыПараметраUrl.Строка), 
        (Ууид) -> ЗапросИзмененияЛичныхДанных.ПолучитьСсылку(Ууид), 
        ФорматОбмена)
;

@ВПодсистеме
метод ОбновитьОграниченияДоступаКРабочимКонтактам(ЗапросHttp: HttpСервисЗапрос, ФорматОбмена: Версия? = Неопределено)
    ВебApiСервер.ЗагрузитьОбъекты(
        ЗапросHttp, 
        Тип<ОграниченияДоступаКРабочимКонтактам.Ссылка>, 
        новый ВебApiСервер.ИнструментыМенеджераОбъекта((Объект: Сущность.Объект) -> (Объект как ОграниченияДоступаКРабочимКонтактам.Объект).Записать()), 
        ФорматОбмена)
;

@ВПодсистеме
метод ЗаписатьШаблонЗаявления(ЗапросHttp: HttpСервисЗапрос)
    ШаблоныДокументовМенеджер.ЗаписатьШаблонДокумента(
        ВладельцыКабинетов.ПОДСИСТЕМА_ВЛАДЕЛЬЦЫ_КАБИНЕТОВ,
        ВладельцыКабинетов.ШАБЛОН_ЗАЯВЛЕНИЯ_НА_ИЗМЕНЕНИЕ_ЛИЧНЫХ_ДАННЫХ,
        ЗапросHttp)
;

@ВПодсистеме
метод ПолучитьQrКод(ЗапросHttp: HttpСервисЗапрос)
   попытка
        знч ПараметрыЗапроса = СервисыHttp.ПараметрыЗапроса(ЗапросHttp, [новый СервисыHttp.ОжидаемыйПараметр("ID", Истина, СервисыHttp.ТипыПараметраUrl.Ид)])
        знч ФизическоеЛицо = ФизическиеЛица.ПолучитьСсылку(ПараметрыЗапроса.Получить("ID") как Ууид)
        исп ПривилегированныйРежим: КонтекстДоступа = КонтекстДоступа.Привилегированный()
        знч Файл = ЛичныеКабинетыПользователей.ФайлQrКода(ФизическоеЛицо)
        знч ДанныеФайла = Файлы.ДвоичныеДанныеФайла(Файл)
        ПривилегированныйРежим.Закрыть()
        СервисыHttp.ЗаполнитьОтвет(ЗапросHttp, СервисыHttp.КодыСостояний.ОК_200)
        ЗапросHttp.Ответ.Заголовки.Установить("Content-Type", "image/png")
        ЗапросHttp.Ответ.Заголовки.Установить("Content-Length", ДанныеФайла.Размер.ВСтроку())
        ЗапросHttp.Ответ.УстановитьТело(ДанныеФайла.Ссылка)
    поймать Ошибка: Исключение
        СервисыHttp.ЗаполнитьОтвет(ЗапросHttp, Ошибка)
    ;
;