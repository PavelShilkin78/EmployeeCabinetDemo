импорт ИнтерфейсныеКомпонентыСтандартные
импорт Криптография
импорт ОбменДанными
импорт ОбщегоНазначения
импорт ОбщиеСтандартные
импорт ФизическиеЛица
импорт УниверсальныеМеханизмы
импорт ЛичныеКабинетыПользователей

//Область ОбработчикиСобытийФормы

@Обработчик
метод ПослеСоздания()  
    ИспользуетсяУнэп = ВладельцыКабинетов.ПараметрыСеанса().ФункциональныеОпции.ПолучениеСогласияКэдоУнэп
    если ТребуетсяСформироватьУнэп()
        Закрыть()
        ТолькоЧтение = Истина
        УведомлениеОБумажномСогласиеНаКэдо.ОткрытьВоВсплывающемОкне(
            РасположениеВсплывающегоОкна = РасположениеВсплывающегоОкна.ЦентрЭкрана,
            ОжидатьЗакрытия = Истина)
        возврат
    ;
    ДоступныеВидыПодписей = Криптография.ДоступныеВидыЭлектронныхПодписей() 
    пер ДанныеИсточника = Компоненты.СписокСогласий.Источник.Данные
    ДанныеИсточника.ДобавитьВсе(НеподписанныеЗаявленияСотрудника(ЛичныеКабинетыПользователей.ПараметрыСеанса().ТекущееФизическоеЛицо))
    
    ИнтерфейсныеКомпоненты.ИнициализироватьПодчиненныеКомпоненты(Компоненты)
;

//КонецОбласти

//Область МетодыЗаполненияФормы

метод ПамяткаОбщая(): Строка
    если Компоненты.СписокСогласий.Источник.Данные.Граница() > 1 
        возврат "Прочтите текст согласия на присоединение к кадровому электронному документообороту организаций, в которых вы работаете"    
    иначе
        возврат "Прочтите текст согласия на присоединение к кадровому электронному документообороту организации, в которой вы работаете"
    ;
;

метод СформироватьПодписьВидимость(): Булево
    возврат ТребуетсяСформироватьУнэп()
;

метод ПодписатьИзображение(): ДвоичныйОбъект.Ссылка
    если ТребуетсяСформироватьУнэп()
        возврат ИнтерфейсныеКомпоненты.ПолучитьИзображениеПоНомеру(3)
    иначе
        возврат ИнтерфейсныеКомпоненты.ПолучитьИзображениеПоНомеру(2)
    ;
;

метод СформироватьУнэпВидимость(): Булево
    возврат ДоступноФормированиеУнэп()
;

метод ДоступноФормированиеУнэп(): Булево
    возврат ТребуетсяСформироватьУнэп()
        и не (ДоступныеВидыПодписей.Размер() > 1
            или не ДоступныеВидыПодписей.Содержит(ВидыЭлектронныхПодписей.УНЭПИнформационнойСистемы))
;
метод ИспользованиеЗаписатьИЗакрыть(): Булево
    возврат не ТребуетсяСформироватьУнэп()    
;

метод ТребуетсяСформироватьУнэп(): Булево
    возврат ИспользуетсяУнэп
        и не ВладельцыКабинетов.ДоступноПодписаниеЭлектроннойПодписью(
            ЛичныеКабинетыПользователей.ПараметрыСеанса().ТекущееФизическоеЛицо)
;

//КонецОбласти

//Область ОбработчикиКомандФормы

метод ПосмотретьПриНажатии(Источник: СтандартнаяКолонкаТаблицы<Объект?>, Событие: СобытиеПриНажатии, ДанныеЭлемента: СтрокаДинамическогоСписка)
    знч Файл = ДанныеЭлемента.Данные.Получить("Файл")
    если Файл == Null 
        возврат
    ;
    УниверсальныеМеханизмы.НачатьВыгрузкуФайла(Файл как Файлы.Ссылка)    
;

метод ПриложитьПодписьПриНажатии(Источник: СтандартнаяКолонкаТаблицы<Объект?>, Событие: СобытиеПриНажатии, ДанныеЭлемента: СтрокаДинамическогоСписка)
    знч Ссылка = ДанныеЭлемента.Данные.Получить("Ссылка") как СогласиеНаПрисоединениеККэдо.Ссылка
    СогласиеНаПрисоединениеККэдоФормаДокумента.ОткрытьВМодальномОкне(КлючОбъекта = Ссылка)        
;

метод СформироватьУнэпПриНажатии(Источник: Кнопка, Событие: СобытиеПриНажатии)
    если не Криптография.ПараметрыСеанса().ПользовательИмеетСертификатЭП 
        СозданиеЦифровогоСертификата.ОткрытьВМодальномОкне(ФизическоеЛицо = ЛичныеКабинетыПользователей.ПараметрыСеанса().ТекущееФизическоеЛицо)
    ;
;

метод КомандаСохранитьПриНажатии(Источник: КоманднаяПанельФорм, Событие: СобытиеКомпонента)
    ЗаписатьИЗакрыть(Истина)
;

метод КомандаОтложитьПриНажатии(Источник: КоманднаяПанельФорм, Событие: СобытиеКомпонента)
    Закрыть()
;

метод КомандаОтказатьсяПриНажатии(Источник: КоманднаяПанельФорм, Событие: СобытиеКомпонента)
    ЗаписатьИЗакрыть(Ложь)
;

метод ГруппаКомандСохранитьОтказатьсяОшибкаПриВыполненииКоманды(Источник: КоманднаяПанельФорм, Событие: СобытиеСДанными<Исключение>)
    Криптография.СброситьПарольЗакрытогоКлючаПриОшибкеВводаПароля(Событие.Данные)
    Компоненты.КорневаяГруппа.ПоказатьОшибку(
        ОбщегоНазначения.ОписаниеОшибкиПриДействииСДокументом(Событие.Данные, ЛичныеКабинетыПользователей.ПараметрыСеанса().ТекущееФизическоеЛицо))
;

//КонецОбласти

//Область СлужебныеПроцедурыИФункции

метод ЗаписатьИЗакрыть(Согласие: Булево)
    если ИспользуетсяУнэп и Согласие 
        если не (ЗапросПароляЗакрытогоКлюча.ОткрытьВМодальномОкне() ?? Ложь)
            возврат
        ;
    ;
 
    Записать(ЛичныеКабинетыПользователей.ПараметрыСеанса().ТекущееФизическоеЛицо,
                      Согласие,
                      ВладельцыКабинетов.ПараметрыСеанса().ИнформационныеСистемы,
                      Криптография.ПараметрыСеанса().ПарольЗакрытогоКлючаПользователя,
                      ИспользуетсяУнэп)
    
    МенеджерГлобальныхКлиентскихСобытий.Оповестить(ОбщегоНазначения.СОГЛАСИЕ_КЭДО_ПОДПИСАНО)
    Закрыть() 
;

метод ПодписатьТекстПояснения(): Строка
    пер ТекстПояснения: Строка
    если ИспользуетсяУнэп
        ТекстПояснения = 
            "Подпишите согласие на присоединение к кадровому электронному документообороту вашей организации. Если вы пока не готовы - отложите на потом.
            Если вы откажетесь присоединиться к КЭДО, вы сможете использовать личный кабинет, но вам по-прежнему придется передавать в кадры и в бухгалтерию свои заявления, подписанные бумажные документы, расчетные листки и т.д."
    иначе
        ТекстПояснения = 
            "Распечатайте согласие на присоединение к кадровому электронному документообороту вашей организации, подпишите его и передайте в кадры. После чего нажмите кнопку \"Я подписал\".
            В дальнейшем вы сможете сформировать свою личную электронную подпись и использовать её для подписания документов. 
            
            Если вы пока не готовы - отложите на потом.
            
            Если вы сейчас откажетесь присоединиться к КЭДО, вы сможете использовать личный кабинет, но вам по-прежнему придется передавать в бумажном виде свои заявления и подписанные документы в кадры и в бухгалтерию.
            Вы сможете присоединиться к КЭДО своей организации позже."
    ;
    возврат ТекстПояснения
;

метод ГруппаКомандСохранитьОтказатьсяЗаголовокКнопкиЗаписатьИЗакрыть(): Строка
    пер ЗаголовокКнопки: Строка
    если ИспользуетсяУнэп
        ЗаголовокКнопки = "Подписать"
    иначе
        ЗаголовокКнопки = "Я подписал"
    ;
    возврат ЗаголовокКнопки
;
//КонецОбласти

@НаСервере @ДоступноСКлиента
статический метод Записать(ФизическоеЛицо: ФизическиеЛица.Ссылка,
                Согласие: Булево,
                ИнформационныеСистемы: Соответствие<ИнформационныеСистемы.Ссылка, ОбменДанными.НастройкиИнформационнойСистемы>,                
                ПарольЗакрытогоКлюча: Строка?,
                ИспользуетсяУнэп: Булево)
    исп Транзакции.Начать()

    пер ПараметрыЗаписи = новый СогласиеНаПрисоединениеККэдоКлиентСервер.ПараметрыЗаписи(ФизическоеЛицо)    
    ПараметрыЗаписи.Заявления = НеподписанныеЗаявленияСотрудника(ФизическоеЛицо)
    ПараметрыЗаписи.Согласие = Согласие
    ПараметрыЗаписи.ИнформационныеСистемы = ИнформационныеСистемы
    ПараметрыЗаписи.ПарольЗакрытогоКлюча = ПарольЗакрытогоКлюча
    ПараметрыЗаписи.ИспользуетсяУнэп = ИспользуетсяУнэп
    СогласиеНаПрисоединениеККэдоСервер.ЗаписатьСогласия(ПараметрыЗаписи)    
    
    исп ПривилегированныйРежим: КонтекстДоступа = КонтекстДоступа.Привилегированный()
    если Согласие и ИспользуетсяУнэп
        пер ОпцииФизическогоЛица: ДоступныеФункцииФизическихЛиц.ЗначенияОпций
        ОпцииФизическогоЛица.ИспользуетКэдо = Истина
        ДоступныеФункцииФизическихЛиц.УстановитьОпции(ФизическоеЛицо, ОпцииФизическогоЛица)   
    ;  
    ПривилегированныйРежим.Закрыть()  
;

@НаСервере @ДоступноСКлиента
статический метод НеподписанныеЗаявленияСотрудника(ФизическоеЛицо: ФизическиеЛица.Ссылка): Массив<СогласиеНаПрисоединениеККэдоКлиентСервер.ЗаявлениеРеквизиты>
    возврат СогласиеНаПрисоединениеККэдо.НеподписанныеЗаявленияСотрудника(ФизическоеЛицо)    
;

метод СформироватьПодписьТекстПояснения(): Строка
    если ДоступноФормированиеУнэп()
        возврат "Если вы готовы подписать согласие, сформируйте свою личную электронную подпись. Подпись будет храниться в сервисе и будет доступна по известному только вам паролю."
    ;
    возврат "Если вы готовы подписать согласие, перейдите в разделе \"Параметры\", выберите вид подписи и, возможно, потребуется сформировать свою личную электронную подпись. Подпись будет храниться в сервисе и будет доступна по известному только вам паролю."
;