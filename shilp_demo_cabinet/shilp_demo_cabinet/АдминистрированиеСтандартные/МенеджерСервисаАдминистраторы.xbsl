импорт ФизическиеЛица
импорт ОбменДанными
импорт ВебApi
импорт ЛичныеКабинетыПользователей

@ВПроекте
структура ДанныеАдминистратора
    обз пер ЛогинПользователя: Строка
    обз пер ЭлектроннаяПочта: Строка
    обз пер МобильныйТелефон: Строка
    обз пер АдресАктивацииПриложения: Строка
;

@ВПроекте
метод СоздатьАдминистратора(ЗапросHttp: HttpСервисЗапрос)
    пер ИдентификаторАдминистратора: Строка
    попытка
        знч ДанныеJson: ВнешнееФизическоеЛицоJson = СервисыHttp.ИзвлечьДанныеJson(ЗапросHttp, Тип<ВнешнееФизическоеЛицоJson>)
        ИдентификаторАдминистратора = ДанныеJson.personExternalID
    поймать Ошибка: Исключение
        СервисыHttp.ЗаполнитьОтвет(ЗапросHttp, Ошибка)
    ;
    попытка
        знч ИдентификаторыЛокальныхОбъектов: ЧитаемоеСоответствие<Строка, ВходящиеДанные.Ссылка> = ОбменДанными.ИдентификаторыЛокальныхОбъектов([ИдентификаторАдминистратора])
        если ИдентификаторыЛокальныхОбъектов.Пусто()
            СервисыHttp.ЗаполнитьОтвет(ЗапросHttp, СервисыHttp.КодыСостояний.НеНайдено_404)
            возврат
        ;
        знч ФизическоеЛицо: ФизическиеЛица.Ссылка? = ИдентификаторыЛокальныхОбъектов[ИдентификаторАдминистратора] как ФизическиеЛица.Ссылка?
        если ФизическоеЛицо == Неопределено или ФизическоеЛицо!.ЗагрузитьОбъект() == Неопределено
            СервисыHttp.ЗаполнитьОтвет(ЗапросHttp, СервисыHttp.КодыСостояний.НеНайдено_404)
            возврат
        ;
        
        знч ДанныеСозданияЛичногоКабинета = новый ЛичныеКабинетыПользователей.ДанныеСозданияЛичногоКабинета(ФизическоеЛицо)
        ДанныеСозданияЛичногоКабинета.ЭтоАдминистратор = Истина    
        исп ПривилегированныйРежим = КонтекстДоступа.Привилегированный()
        ЛичныеКабинетыПользователей.СоздатьЛичныйКабинетАдминистратора(ДанныеСозданияЛичногоКабинета)
        ПривилегированныйРежим.Закрыть()
        пер Настройки = ЛичныеКабинетыПользователей.ТекущиеНастройкиСозданияУчетныхЗаписей()
        знч АдресАктивации = "%{Настройки.АдресСервиса}?login_hint=%{ДанныеСозданияЛичногоКабинета.ЛогинПользователя}"
        знч ДанныеJson = новый ИнформацияОбАдминистратореJson(
            ДанныеСозданияЛичногоКабинета.ЛогинПользователя,
            ДанныеСозданияЛичногоКабинета.ДанныеФизическогоЛица.РабочаяКонтактнаяИнформация.МобильныйТелефон,
            ДанныеСозданияЛичногоКабинета.ДанныеФизическогоЛица.РабочаяКонтактнаяИнформация.ЭлектроннаяПочта,
            АдресАктивации)
            
        СервисыHttp.УпаковатьДанныеJson(ЗапросHttp, ДанныеJson)
    поймать Ошибка: Исключение
        СервисыHttp.ЗаполнитьОтвет(ЗапросHttp, Ошибка)
    ;
;

структура ВнешнееФизическоеЛицоJson
    обз пер personExternalID: Строка
;

структура ИнформацияОбАдминистратореJson
    обз пер name: Строка
    обз пер mobilePhoneNumber: Строка
    обз пер eMail: Строка
    обз пер logonUrl: Строка
;