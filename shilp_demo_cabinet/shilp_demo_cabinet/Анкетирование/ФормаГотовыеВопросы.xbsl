импорт ИнтерфейсныеКомпонентыСтандартные
импорт СтандартноеПриложение
импорт ОбщиеСтандартные


@Обработчик
метод ПослеСоздания()
    Категория = КатегорииГотовыхВопросов.Поставляемые
    НажатиеКарточкиГотовогоВопроса.ПодключитьОбработчик(&ОбработатьНажатиеГотовогоВопроса)
    ОбновитьСодержимоеПоставляемыхВопросов()
    ОбновитьКатегории() 
;   

метод ОбновитьКатегории()
    СписокКатегорий.Очистить()
    если ИспользуютсяПоставляемыеВопросы и ИдОбластиДанных != Неопределено
        ДобавитьКатегорию(КатегорииГотовыхВопросов.Поставляемые)
    ;
;

метод ДобавитьКатегорию(Категория: КатегорииГотовыхВопросов)

    пер ЗначениеСписок = новый ЭлементСпискаЗначений<КатегорииГотовыхВопросов?>(
    Значение = Категория,
    Представление = Категория.Представление())
    СписокКатегорий.Добавить(ЗначениеСписок)
;
    
метод ОбновитьСодержимоеПоставляемыхВопросов()
    
    ПоставляемыеВопросы = Анкетирование.ГотовыеВопросыПодсистемы(ИдОбластиДанных)
    
    пер КоллекцияДанных = КоллекцияИсточникаПоставляемыеВопросы()
    КоллекцияДанных.Очистить()
    КоллекцияДанных.ДобавитьВсе(ПоставляемыеВопросы)    
;

метод КоллекцияИсточникаПоставляемыеВопросы(): Массив<ДанныеВопроса>
    возврат Компоненты.ПоставляемыеВопросы.Источник.Данные
;

метод ФормаГотовыеВопросыМаксимальнаяШирина(): Число
    возврат 120 * Метрики.РАЗМЕР_ПОЛУИНТЕРВАЛА    
;

метод ФормаГотовыеВопросыМинимальнаяШирина(): Число
    возврат ОсновнойИнтерфейс.МинимальнаяШиринаФормыДокумента()  
;

метод ФормаГотовыеВопросыВысота(): Число
    возврат ОсновнойИнтерфейс.ВысотаФормыДокумента(100)    
;

метод ПоискПодсказкаВопроса(): Строка
    если Компоненты.Категории.Значение == Неопределено
        возврат "Поиск по всем категориям"
    иначе
        возврат "Поиск по " + (Компоненты.Категории.Значение.Представление()).ВНижнийРегистр()         
    ;   
;

метод ДобавитьВопросыПредставление(): Строка
    знч КоличествоВопросов = ВыбранныеВопросы.Размер()
    знч ПредставлениеКоличества = МенеджерСтрок.ТекстПоКоличеству("вопрос", КоличествоВопросов)
    возврат "Добавить %КоличествоВопросов %ПредставлениеКоличества"    
;

метод ДобавитьВопросыДоступность(): Булево
    возврат ВыбранныеВопросы.Размер() > 0
;

метод ОбработатьНажатиеГотовогоВопроса(ДанныеВыбора: Анкетирование.ДанныеВыбораГотовогоВопроса)
    если ДанныеВыбора.Добавить
        ВыбранныеВопросы.Добавить(ДанныеВыбора.ДанныеВопроса) 
    иначе
        ВыбранныеВопросы.Удалить(ДанныеВыбора.ДанныеВопроса)         
    ;  
;

метод ДобавитьВопросыОбработчик(Команда: ОбычнаяКоманда)
    Закрыть()
    ВыбраныГотовыеВопросы.Оповестить(ВыбранныеВопросы)
;