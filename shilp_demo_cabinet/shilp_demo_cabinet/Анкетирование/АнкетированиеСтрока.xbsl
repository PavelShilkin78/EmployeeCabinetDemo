импорт ИнтерфейсныеКомпонентыСтандартные

@Обработчик
метод ПослеСоздания()
    
    ИзмененТекущийРазделАнкеты.ПодключитьОбработчик((метод (ТекущийРаздел: ДанныеРаздела) ->
        ИдТекущегоРаздела = ТекущийРаздел.ИдРаздела
    ;))
    
    ВключенРежимРедактирования.ПодключитьОбработчик((метод (ИдКомпонентов: Массив<Ууид>) ->
        если ИдКомпонентов.Содержит(ДанныеСтроки.ИдЭлемента)
            ДанныеСтроки.РежимРедактирования = Истина
        иначе
            ДанныеСтроки.РежимРедактирования = Ложь
        ;
        
        ОтложеннаяИнициализация()
        
    ;))
    
    ОтложеннаяИнициализация()
;

метод ОтложеннаяИнициализация()
    ПодключитьОбработчикТаймера(&Инициализировать, 20мс, Истина)
;

метод Инициализировать()
    КомпонентКонструктора = Анкетирование.КомпонентКонструктораПоТипуОтвета(ДанныеСтроки.ДанныеВопроса.ТипОтвета)
    КомпонентКонструктора.Данные = ДанныеСтроки
    пер КонтейнерДанныеЛиста: ФиксированнаяГруппа = Компоненты.КонтейнерКомпонента
    КонтейнерДанныеЛиста.Содержимое.Очистить()
    КонтейнерДанныеЛиста.Содержимое.Добавить(КомпонентКонструктора как Компонент)
    ИнтерфейсныеКомпоненты.ИнициализироватьПодчиненныеКомпоненты([КомпонентКонструктора])  
;

метод КомандыВопросаНажатиеВверх(Источник: КомандыВопросаКомпонент, Событие: СобытиеКомпонента)
    ПеремещенВопрос.Оповестить(ИдСоставногоВопроса = ДанныеСтроки.ИдСоставногоВопроса, ПараметрыПеремещения = новый Анкетирование.ПараметрыСдвигаКомпонента(ДанныеСтроки, -1))
;

метод КомандыВопросаНажатиеВниз(Источник: КомандыВопросаКомпонент, Событие: СобытиеКомпонента)
    ПеремещенВопрос.Оповестить(ИдСоставногоВопроса = ДанныеСтроки.ИдСоставногоВопроса, ПараметрыПеремещения = новый Анкетирование.ПараметрыСдвигаКомпонента(ДанныеСтроки, 1))
;

метод КомандыВопросаНажатиеКопировать(Источник: КомандыВопросаКомпонент, Событие: СобытиеКомпонента)
    ВключенРежимРедактирования.Оповестить([])
    СкопированКомпонентКонструктора.Оповестить(ДанныеСтроки)    
;

метод КомандыВопросаНажатиеУдалить(Источник: КомандыВопросаКомпонент, Событие: СобытиеКомпонента)
    УдаленКомпонентКонструктора.Оповестить(ДанныеСтроки) 
;

метод ИзменитьРежимРедактирования(Режим: Булево)
    ДанныеСтроки.РежимРедактирования = Режим
;

метод ЦветФона(): Цвет
    если ДанныеСтроки.ИдСоставногоВопроса != Неопределено
        возврат БиблиотекаЦветов.Цвет(БиблиотекаЦветов.ВидыЦвета.ФонОкнаПриложения)
    ;
    возврат ДанныеСтроки.РежимРедактирования ? БиблиотекаЦветов.Цвет(БиблиотекаЦветов.ВидыЦвета.СветлоСерый, 0.3) : БиблиотекаЦветов.Цвет(БиблиотекаЦветов.ВидыЦвета.ФонОкнаПриложения)    
;

метод ОценкаИнформации(): ОценкаИнформации
    возврат ДанныеСтроки.РезультатПроверкиЗаполнения.Пусто() ? ОценкаИнформации.Обычная : ОценкаИнформации.Предупреждающая
;

метод НомерВопросаЗаголовок(): Строка
    возврат ДанныеСтроки.ИдСоставногоВопроса == Неопределено ? ДанныеСтроки.НомерВопроса.ВСтроку() + "." : ДанныеСтроки.НомерСоставногоВопроса.ВСтроку() + "." + ДанныеСтроки.НомерВопроса
;

метод КарточкаВопросаГиперссылка(): Булево
    
    если ДанныеСтроки.ИдСоставногоВопроса != Неопределено
        возврат Ложь
    ;
    
    возврат не ДанныеСтроки.РежимРедактирования
;

метод КарточкаВопросаПриНажатии(Источник: Карточка, СобытиеИсточника: СобытиеПриНажатии)

    если ДанныеСтроки.РежимРедактирования == Ложь
        ДанныеСтроки.РежимРедактирования = Истина
        ПодключитьОбработчикТаймера(&ПеревестиВРежимРедактирования, 100мс, Истина)
    ;
;

метод ПеревестиВРежимРедактирования()
    знч ИдКомпонентов: Массив<Ууид>
    ИдКомпонентов.Добавить(ДанныеСтроки.ИдЭлемента)
    
    если ДанныеСтроки.ДанныеВопроса.ТипОтвета == ТипыОтветов.Составной
        ИдКомпонентов.ДобавитьВсе(ИдПодвопросов(ДанныеСтроки)) 
    ;
    ВключенРежимРедактирования.Оповестить(ИдКомпонентов)   
;

метод ИдПодвопросов(ДанныеСтроки: ДанныеЭлементаКонструктора): Массив<Ууид>
    знч ИдПодвопросов: Массив<Ууид>
    для Подвопрос из ДанныеСтроки.ДанныеВопроса.СоставСтруктурногоОтвета
        ИдПодвопросов.Добавить(Подвопрос.ИдЭлемента)
    ;
    
    возврат ИдПодвопросов
;

метод ОдинИзСпискаПриНажатии(Команда: ОбычнаяКоманда)
    ДанныеСтроки.ДанныеВопроса.ТипОтвета = ТипыОтветов.ПредопределенныйВариантЕдиничный
    Инициализировать()    
;

метод НесколькоИзСпискаПриНажатии(Команда: ОбычнаяКоманда)
    ДанныеСтроки.ДанныеВопроса.ТипОтвета = ТипыОтветов.ПредопределенныйВариантМножественный
    Инициализировать()
;

метод ТекстовыйОтветПриНажатии(Команда: ОбычнаяКоманда)
    ДанныеСтроки.ДанныеВопроса.ТипОтвета = ТипыОтветов.Строка
    Инициализировать()
;

метод ЗагрузкаФайлаПриНажатии(Команда: ОбычнаяКоманда)
    ДанныеСтроки.ДанныеВопроса.ТипОтвета = ТипыОтветов.Файл
    Инициализировать()
;

метод ДатаПриНажатии(Команда: ОбычнаяКоманда)
    ДанныеСтроки.ДанныеВопроса.ТипОтвета = ТипыОтветов.Дата
    Инициализировать()
;

метод СоставнойОтветПриНажатии(Команда: ОбычнаяКоманда)
    ДанныеСтроки.ДанныеВопроса.ТипОтвета = ТипыОтветов.Составной
    Инициализировать()
;

метод ЧислоПриНажатии(Команда: ОбычнаяКоманда)
    ДанныеСтроки.ДанныеВопроса.ТипОтвета = ТипыОтветов.Число
    Инициализировать()
;

метод НастройкиВопросаОбработчик(Источник: Кнопка, СобытиеИсточника: СобытиеПриНажатии)
    НастройкаВопроса.ОткрытьВоВсплывающемОкне(РасположениеВсплывающегоОкна = Компоненты.Настройки, ЭлементыНастроек = КомпонентКонструктора.ОткрытьНастройки(), ОжидатьЗакрытия = Истина)  
    
;

метод ЗаголовокВопроса(): Строка
    возврат ДанныеСтроки.ИдСоставногоВопроса != Неопределено ? ДанныеСтроки.ДанныеВопроса.Формулировка : ДанныеСтроки.НомерВопроса.ВСтроку() + ". " + ДанныеСтроки.ДанныеВопроса.Формулировка 
;

метод ВидимостьКомандыПоИнструменту(Инструмент: ИнструментыКонструктора): Булево
    если РазрешенныйИнструментКонструктора == Неопределено
        возврат Истина
    ;
    
    возврат РазрешенныйИнструментКонструктора == Инструмент 
;