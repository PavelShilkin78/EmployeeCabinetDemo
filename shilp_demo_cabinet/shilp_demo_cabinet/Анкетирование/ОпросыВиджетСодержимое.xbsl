импорт ОбщиеСтандартные

@НаКлиенте @НаСервере
структура ДанныеОпросов
    пер КоличествоОпросов: Число
    пер ДанныеОпроса: ДанныеОпроса
;

@ВПодсистеме
@НаКлиенте @НаСервере
структура ДанныеОпроса
    пер Опрос: Опросы.Ссылка?
    пер НаименованиеОпроса: Строка
    пер КоличествоУчастниковОпроса: Число
    пер КоличествоЗавершившихОпрос: Число
;

@Обработчик
метод ПослеСоздания()
    пер ДанныеОпросов = ДанныеОпросов()
    КоличествоОпросов = ДанныеОпросов.КоличествоОпросов
    ДанныеОпроса = ДанныеОпросов.ДанныеОпроса
;

метод ОпросыПояснение(): Строка
    пер ДокументыПояснение: Строка
    если КоличествоОпросов == 0
        ДокументыПояснение = "Сейчас нет активных опросов."
    иначе если КоличествоОпросов == 1
        ДокументыПояснение = "В настоящее время проводится один опрос."
    иначе
        ДокументыПояснение ="В настоящее время проводится %КоличествоОпросов %{МенеджерСтрок.ТекстПоКоличеству("опрос", КоличествоОпросов)}."
    ;
    возврат ДокументыПояснение
;

метод СписокОпросовПриНажатииСтроки(Источник: Список<ИсточникДанныхМассив<ДанныеОпроса>>, Событие: СобытиеСДанными<СтрокаСписка<ДанныеОпроса>>)
    ОпросФормаОбъекта.ОткрытьВМодальномОкне(КлючОбъекта = (Событие.Данные.ДанныеСтроки.Опрос))
;

метод СписокОпросовДанные(): Массив<ДанныеОпроса>
    знч Данные: Массив<ДанныеОпроса>
    
    Данные.Добавить(ДанныеОпроса)

    возврат Данные    
;

@НаСервере @ДоступноСКлиента
статический метод ДанныеОпросов(): ДанныеОпросов

    пер ТекущаяДата: Дата = Дата.Сейчас()
    знч Запрос = Запрос{
            Выбрать
                Ссылка как Опрос,
                Наименование
            Из
                Опросы
            Где
                ДатаНачала <= %ТекущаяДата и ДатаОкончания > %ТекущаяДата и Статус == СтатусыОпросов.Запущен
            Упорядочить По
                ДатаНачала Убыв
        }
        
    
    исп РезультатЗапроса = Запрос.Выполнить()
    знч РезультатЗапросаСтроки = новый ЧитаемыйМассив(РезультатЗапроса)
    пер ДанныеОпроса: ДанныеОпроса    
    если РезультатЗапросаСтроки.Размер() > 0
        пер Статистика = Анкетирование.СтатистикаПрохожденияОпроса(РезультатЗапросаСтроки[0].Опрос)
        ДанныеОпроса = новый ДанныеОпроса(РезультатЗапросаСтроки[0].Опрос, РезультатЗапросаСтроки[0].Наименование, Статистика.ВсегоУчастников, Статистика.Завершили)
    ;
    
    возврат новый ДанныеОпросов(РезультатЗапросаСтроки.Размер(), ДанныеОпроса)
;