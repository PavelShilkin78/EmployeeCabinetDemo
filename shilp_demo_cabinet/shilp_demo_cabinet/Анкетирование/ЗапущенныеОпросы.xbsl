импорт ИнтерфейсныеКомпонентыСтандартные
импорт ОбщиеСтандартные
импорт СтандартноеПриложение

@НаКлиенте @НаСервере
@ВПодсистеме
структура КраткаяИнформацияОпроса
    обз пер Опрос: Опросы.Ссылка?
    обз пер Наименование: Строка
    обз пер Описание: Строка
    обз пер ПрошедшиеОпрос: Число
    обз пер РеспондентовВсего: Число
    обз пер ВсегоДнейОпроса: Число
    обз пер ПрошлоДнейОпроса: Число
    обз пер ОсталосьДнейОпроса: Число
    обз пер НачалоОпроса: Дата
    обз пер ОкончаниеОпроса: Дата
;

@ВПроекте
@Реализация
метод Инициализировать()
    ИнтерфейсныеКомпоненты.ИнициализироватьТаблицу(Таблица = Компоненты.ТаблицаОпросов, 
        ОтображатьШапку = Истина, Навигация = НавигацияВСписке.Отсутствует, РазмерСтраницы = ТаблицаОпросовРазмерСтраницы())
    ИнтерфейсныеКомпоненты.ИнициализироватьПодчиненныеКомпоненты(Компоненты)
    ОбновитьДанныеТаблицы()
;

метод РежимМалогоЭкрана(): Булево
    возврат ОсновнойИнтерфейс.РаботаНаУзкомУстройстве()
;

метод ВидимостьТаблицыОпросов(): Булево
    возврат Компоненты.ТаблицаОпросов.Источник.Данные.Размер() > 0
;

метод ОбновитьДанныеТаблицы()
    знч Данные = Компоненты.ТаблицаОпросов.Источник.Данные
    Данные.Очистить()
    Данные.ДобавитьВсе(ПолучитьСводныеДанныеОпроса())
;

метод ТаблицаОпросовРазмерСтраницы(): Число
    возврат РежимМалогоЭкрана() ? 4 : 3
;

метод ТаблицаОпросовПриНажатииСтроки(Источник: Список<ИсточникДанныхМассив<КраткаяИнформацияОпроса>>, Событие: СобытиеСДанными<СтрокаСписка<КраткаяИнформацияОпроса>>)
    ОпросФормаОбъекта.ОткрытьВМодальномОкне(КлючОбъекта = Событие.Данные.ДанныеСтроки.Опрос)
;

@НаСервере @ДоступноСКлиента
статический метод ПолучитьСводныеДанныеОпроса(): Массив<КраткаяИнформацияОпроса>
    знч ТекущаяДата = МенеджерДат.ТекущаяДатаВремя().ВДатаВремя(ЧасовойПояс.Текущий()).Дата
    знч Запрос = Запрос{
        Выбрать
            Опросы.Ссылка как Ссылка,
            Опросы.Наименование как Наименование,
            Опросы.Описание как Описание,
            Опросы.ДатаНачала как ДатаНачала,
            Опросы.ДатаОкончания как ДатаОкончания
        Поместить
            втОпросыАктуальные
        Из
            Опросы как Опросы
        Где
            Опросы.Статус == СтатусыОпросов.Запущен
            И ДатаОкончания >= %ТекущаяДата
        ;
        Выбрать
            Опросы.Ссылка как Ссылка,
            Опросы.Наименование как Наименование,
            Опросы.Описание как Описание,
            Опросы.ДатаНачала как ДатаНачала,
            Опросы.ДатаОкончания как ДатаОкончания
        Поместить
            втОпросыУпорядоченные
        Из
            втОпросыАктуальные как Опросы
        ;
        Выбрать
            Опросы.Ссылка как Опрос,
            Анкеты.Статус как СтатусАнкеты,
            КОЛИЧЕСТВО(Анкеты.Ссылка) как Количество
        Поместить 
            втАнкетыПоСтатусам
        Из
            втОпросыУпорядоченные как Опросы
            Внутреннее Соединение АнкетыОпроса как АнкетыОпроса
                По Опросы.Ссылка == АнкетыОпроса.Опрос
                Внутреннее Соединение Анкеты как Анкеты
                    По АнкетыОпроса.Анкета == Анкеты.Ссылка
        Где
            Анкеты.Статус == СтатусыАнкет.Заполняется
            Или Анкеты.Статус == СтатусыАнкет.Отправлена
            Или Анкеты.Статус == СтатусыАнкет.Заполнена
        Сгруппировать По
            Опросы.Ссылка, Анкеты.Статус
        ;
        Выбрать
            Опросы.Ссылка как Опрос,
            Опросы.Наименование как Наименование,
            Опросы.Описание как Описание,
            Опросы.ДатаНачала как ДатаНачала,
            Опросы.ДатаОкончания как ДатаОкончания,
            Анкеты.СтатусАнкеты как СтатусАнкеты,
            Анкеты.Количество как Количество
        Из
            втОпросыУпорядоченные как Опросы
            Внутреннее Соединение втАнкетыПоСтатусам как Анкеты
                По Опросы.Ссылка == Анкеты.Опрос
        Упорядочить По
            Опросы.ДатаНачала Убыв
    }
    пер РезультатПоОпросам = новый Соответствие<Строка, КраткаяИнформацияОпроса>()
    знч РезультатЗапроса = новый ЧитаемыйМассив(Запрос.Выполнить())
    если РезультатЗапроса.Размер() > 0
        для РезультатСтрока из РезультатЗапроса

            если не РезультатПоОпросам.СодержитКлюч(РезультатСтрока.Наименование)
                пер ДнейОпроса = (РезультатСтрока.ДатаОкончания.КонецДня().ДобавитьСекунды(1) - РезультатСтрока.ДатаНачала.НачалоДня()).Дни
                пер ПрошлоДней = (ТекущаяДата.НачалоДня().ДобавитьСекунды(1) - РезультатСтрока.ДатаНачала.НачалоДня()).Дни
                
                ПрошлоДней = Макс(0, ПрошлоДней)
                
                РезультатПоОпросам.Вставить(РезультатСтрока.Наименование,
                    новый КраткаяИнформацияОпроса(
                        Опрос = РезультатСтрока.Опрос,
                        Наименование = РезультатСтрока.Наименование,
                        Описание = РезультатСтрока.Описание,
                        ВсегоДнейОпроса = ДнейОпроса,
                        НачалоОпроса = РезультатСтрока.ДатаНачала,
                        ПрошедшиеОпрос = 0,
                        РеспондентовВсего = 0,
                        ПрошлоДнейОпроса = ПрошлоДней,
                        ОкончаниеОпроса = РезультатСтрока.ДатаОкончания,
                        ОсталосьДнейОпроса = ДнейОпроса - ПрошлоДней
                    )
                )
            ;
            если РезультатСтрока.СтатусАнкеты == СтатусыАнкет.Заполнена
                РезультатПоОпросам[РезультатСтрока.Наименование].ПрошедшиеОпрос += РезультатСтрока.Количество
            ;
            РезультатПоОпросам[РезультатСтрока.Наименование].РеспондентовВсего += РезультатСтрока.Количество
        ;
    ;
    пер Опросы = новый Массив<КраткаяИнформацияОпроса>()
    для ДанныеКлюч из РезультатПоОпросам.Ключи()
        пер Значение = РезультатПоОпросам[ДанныеКлюч]
        Опросы.Добавить(Значение)
    ;
    возврат Опросы
;