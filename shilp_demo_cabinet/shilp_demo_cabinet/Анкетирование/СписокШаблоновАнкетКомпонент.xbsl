импорт Анкетирование
импорт ИнтерфейсныеКомпонентыСтандартные

@Обработчик
метод ПослеСоздания()
    ОбновленСписокШаблонов.ПодключитьОбработчик((метод () ->
        Компоненты.Шаблоны.Обновить()
    ;))
;

метод ШаблоныПриНажатииСтроки(Источник: Список<ДинамическийСписок>, Событие: СобытиеСДанными<СтрокаСписка<СтрокаДинамическогоСписка>>)
    ПриНажатииСтроки(новый СобытиеСДанными<ШаблоныАнкет.Ссылка>(Событие.Данные.ДанныеСтроки.Данные["Шаблон"] как ШаблоныАнкет.Ссылка))
;

метод ОтправитьПриНажатии(Команда: ОбычнаяКоманда, ДанныеСтроки: Объект?)
    знч Шаблон = (ДанныеСтроки как СтрокаДинамическогоСписка).Данные["Шаблон"] как ШаблоныАнкет.Ссылка?
    пер НаименованиеШаблона = (ДанныеСтроки как СтрокаДинамическогоСписка).Данные["Наименование"] как Строка
    ФормаСозданияАнкет.ОткрытьВМодальномОкне(Шаблон = Шаблон, НаименованиеШаблона = НаименованиеШаблона, ВидимостьШаблона = Ложь)
;

метод КопироватьПриНажатии(Команда: ОбычнаяКоманда, ДанныеСтроки: Объект?)
    знч Шаблон = (ДанныеСтроки как СтрокаДинамическогоСписка).Данные["Шаблон"] как ШаблоныАнкет.Ссылка
    ПриКопировании(новый СобытиеСДанными<ШаблоныАнкет.Ссылка>(Шаблон))
;

метод УдалитьПриНажатии(Команда: ОбычнаяКоманда, ДанныеСтроки: Объект?)
    
    пер Шаблон = (ДанныеСтроки как СтрокаДинамическогоСписка).Данные["Шаблон"] как ШаблоныАнкет.Ссылка?
    
    если Шаблон == Неопределено
        возврат
    ;
    
    если Анкетирование.ПоШаблонуСозданыАнкеты(Шаблон!)
        СтандартныеФормы.ПоказатьУведомление("Ошибка", "Нельзя удалить шаблон, по которому были созданы анкеты")
        возврат
    ;
    
    знч Ответ = ЗапросДействия.ОткрытьВМодальномОкне(ОжидатьЗакрытия = Истина, Текст = "Вы уверены, что хотите удалить шаблон")
    если Ответ != КнопкиМодальныхДиалогов.Да
        возврат
    ;
    
    УдалитьВсеДанныеШаблона(Шаблон)
    Компоненты.Шаблоны.Обновить()
;

метод УдалитьВсеДанныеШаблона(Шаблон: ШаблоныАнкет.Ссылка)
    Анкетирование.ОчиститьВопросыПредыдущейВерсии(Шаблон)
    УдалитьШаблон(Шаблон)
;

@НаСервере @ДоступноСКлиента
статический метод УдалитьВопросыШаблона(Шаблон: ШаблоныАнкет.Ссылка)
    знч Запрос = Запрос{
        Выбрать
        Ссылка
        из ВопросыШаблоновАнкет
        где Шаблон == %Шаблон}
    исп Результат = Запрос.Выполнить()
    
    для РезультатСтрока из Результат
        пер ВопросыШаблоновАнкетОбъект = РезультатСтрока.Ссылка.ЗагрузитьОбъект()
        ВопросыШаблоновАнкетОбъект.Удалить()
    ; 
;

@НаСервере @ДоступноСКлиента
статический метод УдалитьРазделыШаблона(Шаблон: ШаблоныАнкет.Ссылка)
    знч Запрос = Запрос{
        Выбрать
        Ссылка
        из РазделыАнкет
        где Шаблон == %Шаблон}
    исп Результат = Запрос.Выполнить()
    
    для РезультатСтрока из Результат
        пер РазделыАнкетыОбъект = РезультатСтрока.Ссылка.ЗагрузитьОбъект()
        РазделыАнкетыОбъект.Удалить()
    ;
;

@НаСервере @ДоступноСКлиента
статический метод УдалитьШаблон(Шаблон: ШаблоныАнкет.Ссылка)
    Шаблон.ЗагрузитьОбъект().Удалить()
;