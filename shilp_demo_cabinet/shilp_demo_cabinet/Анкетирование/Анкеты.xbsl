импорт КонтрольДоступа
импорт ОбщиеСтандартные
импорт СтандартноеПриложение
импорт УниверсальныеМеханизмы

//КонтрольДоступа

@Обработчик
метод ВычислитьРазрешенияДоступа(): ЧитаемаяКоллекция<РазрешениеДоступа>
    возврат КонтрольДоступа.РазрешенияДоступаТаблицыБезПраваУдаления()
;

@Обработчик
метод ВычислитьРазрешенияДоступаДляОбъектов(Объекты: ЧитаемыйМассив<Анкеты.ДанныеРасчетаРазрешений>): ЧитаемоеСоответствие<Анкеты.ДанныеРасчетаРазрешений, ЧитаемаяКоллекция<РазрешениеДоступа>>
    возврат АнкетированиеКонтрольДоступаПоставщик.ПолучитьСервис().ВычислитьРазрешенияДоступаДляАнкет(Объекты)
;

//КонтрольДоступа

@ВПроекте
метод РеспондентыАнкет(Анкеты: ЧитаемаяКоллекция<Анкеты.Ссылка>): ЧитаемоеСоответствие<Анкеты.Ссылка, РеспондентАнкетирования.Ссылка?>
    знч РеспондентыАнкет: Соответствие<Анкеты.Ссылка, РеспондентАнкетирования.Ссылка?>
    
    если Анкеты.Пусто()
        возврат РеспондентыАнкет
    ;
    знч Запрос = Запрос{
        Выбрать
            Ссылка,
            Респондент
        Из
            Анкеты
        Где
            Ссылка в (%Анкеты)
    }
    
    исп Результат = Запрос.Выполнить()
    для СтрокаРезультата из Результат
        РеспондентыАнкет.Вставить(СтрокаРезультата.Ссылка, СтрокаРезультата.Респондент)
    ;
    
    возврат РеспондентыАнкет
;

@ВПроекте
метод ЗаполнитьПараметрыАнкетыВСообщении(ТекстыСообщений: Соответствие<Строка, Строка>, Анкета: Анкеты.Ссылка): Соответствие<Строка, Строка>
    знч ДанныеАнкеты = МенеджерСущности.ЗначенияРеквизитовОбъекта(Анкета, ["Наименование"])
    пер Тексты: Соответствие<Строка, Строка>
    для КлючИЗначение из ТекстыСообщений
        пер Текст = КлючИЗначение.Значение.Заменить("\%КМНаименованиеАнкеты\%", ДанныеАнкеты.Получить("Наименование"))
        Текст = Текст.Заменить("\%КМАнкета\%", СтандартноеПриложение.СсылкаПереходаКОбъекту(Анкета))
        Тексты.Вставить(КлючИЗначение.Ключ, Текст)
    ;
    возврат Тексты
;