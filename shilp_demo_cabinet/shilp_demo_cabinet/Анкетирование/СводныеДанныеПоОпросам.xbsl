импорт ИнтерфейсныеКомпонентыСтандартные
импорт СтандартноеПриложение

@НаКлиенте @НаСервере
@ВПодсистеме
структура ОпросыПоСтатусам
    обз пер КоличествоЗавершенных: Число
    обз пер КоличествоЗапущенных: Число
;

@ВПроекте
@Реализация
метод Инициализировать()
    ИнтерфейсныеКомпоненты.ИнициализироватьПодчиненныеКомпоненты(Компоненты)
;

@ВПроекте
метод ОбновитьДанныеИндикаторов(НачалоФильтра: Дата, ОкончаниеФильтра: Дата)
    этот.ИтогиПоОпросам = ПолучитьИтоговыеДанныеОпросов(НачалоФильтра, ОкончаниеФильтра)
    этот.ВсегоРеспондентов = ПолучитьКоличествоВсехРеспондентов(НачалоФильтра, ОкончаниеФильтра)
;    

метод ВыбраныЗавершенныеОпросы()
    ИнтерфейсныеКомпоненты.ОткрытьЗамещающуюФорму(
        ФормаВладелец, 
        новый ФормаРазделаОпросы(ФильтрПоСтатусу = СтатусыОпросов.Завершен)
    )
;

метод ВыбраныЗапущенныеОпросы()
    ИнтерфейсныеКомпоненты.ОткрытьЗамещающуюФорму(
        ФормаВладелец, 
        новый ФормаРазделаОпросы(ФильтрПоСтатусу = СтатусыОпросов.Запущен)
    )
;

метод КоличествоЗапущенных(): Число
    возврат ИтогиПоОпросам == Неопределено ? 0 : ИтогиПоОпросам.КоличествоЗапущенных
;

метод КоличествоЗавершенных(): Число
    возврат ИтогиПоОпросам == Неопределено ? 0 : ИтогиПоОпросам.КоличествоЗавершенных
;

@НаСервере @ДоступноСКлиента
статический метод ПолучитьКоличествоВсехРеспондентов(ДатаНачала: Дата, ДатаОкончания: Дата): Число
    знч Запрос = Запрос{
        Выбрать
            КОЛИЧЕСТВО(Анкеты.Анкета) как Количество
        Из
            Опросы как Опросы
            Внутреннее Соединение АнкетыОпроса как Анкеты
                По Опросы.Ссылка == Анкеты.Опрос
        Где 
            Опросы.ДатаНачала МЕЖДУ %ДатаНачала И %ДатаОкончания
    }
    исп ПривилегированныйРежим = КонтекстДоступа.Привилегированный() 
    знч РезультатЗапроса = новый ЧитаемыйМассив(Запрос.Выполнить())
    ПривилегированныйРежим.Закрыть()
    возврат РезультатЗапроса[0].Количество
;

@НаСервере @ДоступноСКлиента
статический метод ПолучитьИтоговыеДанныеОпросов(ДатаНачала: Дата, ДатаОкончания: Дата): ОпросыПоСтатусам
    знч Запрос = Запрос{
        Выбрать
            Опросы.Статус как Статус,
            КОЛИЧЕСТВО(Опросы.Ссылка) как Количество
        Из
            Опросы как Опросы
        Где
            Опросы.ДатаНачала МЕЖДУ %ДатаНачала И %ДатаОкончания
        Сгруппировать По
            Опросы.Статус
    }
    пер РезультатПоОпросам = новый ОпросыПоСтатусам(0,0)
    знч РезультатЗапроса = новый ЧитаемыйМассив(Запрос.Выполнить())
    если РезультатЗапроса.Размер() > 0
        для РезультатСтрока из РезультатЗапроса
            выбор РезультатСтрока.Статус
                когда Завершен
                    РезультатПоОпросам.КоличествоЗавершенных += РезультатСтрока.Количество
                когда Запущен
                    РезультатПоОпросам.КоличествоЗапущенных += РезультатСтрока.Количество
                ;
        ;
    ;
    возврат РезультатПоОпросам
;