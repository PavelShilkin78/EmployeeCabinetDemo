импорт ВебApi

@ВПроекте
метод ТипОбъектаJson(): Строка
    возврат "answersVariantsSets"
;

@ВПроекте
структура НаборВариантов_2_0
    @JsonСвойство("ID")
    обз знч Ид: Строка
    @JsonСвойство("name")
    пер Наименование: Строка?
    @JsonСвойство("predefined")
    пер Предопределенный: Булево?
    @JsonСвойство("predefinedID")
    пер ИдПредопределенногоНабора: Строка?
    @JsonСвойство("static")
    пер Статический: Булево?
;

@ВПодсистеме
метод ГруппаИзмененийВJson(Ссылки: ЧитаемаяКоллекция<ИсходящиеДанные.Ссылка>, Сервисы: ВебApiСервер.СервисыУпаковкиJson): ЧитаемоеСоответствие<Строка, Массив<Объект?>>
    возврат {"answersVariantsSets": ДанныеНаборовВариантовВJson(Ссылки, Сервисы)}
;

метод ДанныеНаборовВариантовВJson(Ссылки: ЧитаемаяКоллекция<ИсходящиеДанные.Ссылка>, Сервисы: ВебApiСервер.СервисыУпаковкиJson): Массив<Объект?>
    знч Данные: Массив<Объект?>
    для КлючИЗначение из ДанныеНаборовВариантов(Ссылки)
        Данные.Добавить(ДанныеНаборовВариантовВJson_V2_0(КлючИЗначение.Значение, Сервисы))
    ;
    возврат Данные
;

метод ДанныеНаборовВариантов(НаборыВариантовСсылки: ЧитаемаяКоллекция<ИсходящиеДанные.Ссылка>): ЧитаемоеСоответствие<НаборыВариантовОтвета.Ссылка, ДанныеНабора>
    
    знч ДанныеНаборовВариантов: Соответствие<НаборыВариантовОтвета.Ссылка, ДанныеНабора> 
    
    если НаборыВариантовСсылки.Пусто()
        возврат ДанныеНаборовВариантов
    ;
    
    знч Запрос = Запрос{
        Выбрать
            Ссылка,
            Наименование,
            Предопределенный,
            ИдПредопределенногоНабора,
            Статический
        Породить ДанныеНабора
        Из
            НаборыВариантовОтвета
        Где
            Ссылка В (%НаборыВариантовСсылки)
    }
    исп Результат = Запрос.Выполнить()
    
    для СтрокаРезультата из Результат
        ДанныеНаборовВариантов.Вставить(
            СтрокаРезультата.Ссылка,
            СтрокаРезультата
        )        
    ;
    
    возврат ДанныеНаборовВариантов   
;

метод ДанныеНаборовВариантовВJson_V2_0(ДанныеНабора: ДанныеНабора, Сервисы: ВебApiСервер.СервисыУпаковкиJson): НаборВариантов_2_0
    
    знч СериализованныйНабор = новый НаборВариантов_2_0(Сервисы.ПолучитьИдентификатор(ДанныеНабора.Ссылка))
   
    СериализованныйНабор.Наименование = ДанныеНабора.Наименование
    СериализованныйНабор.Предопределенный = ДанныеНабора.Предопределенный
    если ДанныеНабора.ИдПредопределенногоНабора != Неопределено
        СериализованныйНабор.ИдПредопределенногоНабора = (ДанныеНабора.ИдПредопределенногоНабора).ВСтроку()
    ;
    
    СериализованныйНабор.Статический = ДанныеНабора.Статический
    
    возврат СериализованныйНабор
; 

@ВПодсистеме
метод ТипДанныхJson(ВерсияФорматаОбмена: Версия?): Тип
    
    если ВерсияФорматаОбмена == Неопределено или ВерсияФорматаОбмена < Версия{2.0}
        выбросить новый ИсключениеВыполнения("Неверный формат")
    ;
    
    если ВерсияФорматаОбмена >= Версия{2.0}
        возврат Тип<НаборВариантов_2_0>
    ;
;

@ВПодсистеме
метод ЗагрузитьОбъектИзJson(СериализованныйНабор: НаборВариантов_2_0, Сервисы: ВебApiСервер.СервисыЗагрузкиJson)
    
    исп Транзакции.Начать()
    
    знч НаборВариантаОтвета = Сервисы.ПрочитатьОбъект(СериализованныйНабор.Ид, &НаборыВариантовОтвета.Объект.новый(Ууид), Тип<НаборыВариантовОтвета.Ссылка>) как НаборыВариантовОтвета.Объект
    НаборВариантаОтвета.Наименование = СериализованныйНабор.Наименование ?? ""
    НаборВариантаОтвета.Предопределенный = СериализованныйНабор.Предопределенный ?? Ложь
    если СериализованныйНабор.ИдПредопределенногоНабора != Неопределено
        НаборВариантаОтвета.ИдПредопределенногоНабора = новый Ууид(СериализованныйНабор.ИдПредопределенногоНабора)
    иначе
        НаборВариантаОтвета.ИдПредопределенногоНабора = Неопределено
    ;
    НаборВариантаОтвета.Статический = СериализованныйНабор.Статический ?? Ложь
    
    Сервисы.ЗаписатьОбъект(новый ВебApiСервер.ПараметрыЗаписиВходящихДанных(НаборВариантаОтвета, СериализованныйНабор.Ид), Сервисы.ИнструментыМенеджераОбъекта)
;

@ВПроекте
метод ВнешняяСсылка(Идентификатор: Строка): ВебApiСервер.ВнешняяСсылка
    возврат новый ВебApiСервер.ВнешняяСсылка(Идентификатор, (Ууид) -> НаборыВариантовОтвета.ПолучитьСсылку(Ууид))
;

// Конец JSON