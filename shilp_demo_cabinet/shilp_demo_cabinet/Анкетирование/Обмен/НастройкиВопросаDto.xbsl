@ВПодсистеме
структура Настройка_2_0
    @JsonСвойство("multilineResponse")
    пер МногострочныйОтвет: Булево?
    @JsonСвойство("multiple")
    пер Множественный: Булево?
    @JsonСвойство("public")
    пер Общедоступный: Булево
    @JsonСвойство("downloadDocs")
    пер ЗагружатьДокументы: Булево?
    @JsonСвойство("downloadImages")
    пер ЗагружатьКартинки: Булево?
    @JsonСвойство("selectComponentType")
    пер ВидКомпонентаВыбора: ВидыКомпонентаВыбораJson?
    @JsonСвойство("commentAvailable")
    пер ДоступенКомментарий: Булево?
;

перечисление ВидыКомпонентаВыбораJson
    list,
    dropDownList
;

конст СООТВЕТСТВИЕ_ВИДЫ_КОМПОНЕНТА_ВЫБОРА_JSON = {
    ВидыКомпонентаВыбораJson.list: ВидыКомпонентаВыбора.Список,
    ВидыКомпонентаВыбораJson.dropDownList: ВидыКомпонентаВыбора.ВыпадающийСписок     
}

@ВПодсистеме
метод СериализованнаяНастройка_2_0(Настройка: НастройкиВопроса): Настройка_2_0

    пер СериализованнаяНастройка = новый Настройка_2_0()
    СериализованнаяНастройка.МногострочныйОтвет = Настройка.МногострочныйОтвет
    СериализованнаяНастройка.Множественный = Настройка.Множественный
    СериализованнаяНастройка.Общедоступный = Настройка.Общедоступный
    СериализованнаяНастройка.ЗагружатьДокументы = Настройка.ЗагружатьДокументы
    СериализованнаяНастройка.ЗагружатьКартинки = Настройка.ЗагружатьКартинки
    СериализованнаяНастройка.ВидКомпонентаВыбора = ВидыКомпонентаВыбораВJson(Настройка.ВидКомпонентаВыбора)
    СериализованнаяНастройка.ДоступенКомментарий = Настройка.ДоступенКомментарий
    
    возврат СериализованнаяНастройка
;

метод ВидыКомпонентаВыбораВJson(ВидКомпонентаВыбора: ВидыКомпонентаВыбора): ВидыКомпонентаВыбораJson
    для КлючИЗначение из СООТВЕТСТВИЕ_ВИДЫ_КОМПОНЕНТА_ВЫБОРА_JSON
        если КлючИЗначение.Значение == ВидКомпонентаВыбора
            возврат КлючИЗначение.Ключ
        ;     
    ;    
;

метод ВидыКомпонентаВыбораИзJson(ВидКомпонентаВыбораJson: ВидыКомпонентаВыбораJson): ВидыКомпонентаВыбора
    возврат СООТВЕТСТВИЕ_ВИДЫ_КОМПОНЕНТА_ВЫБОРА_JSON[ВидКомпонентаВыбораJson]    
;

@ВПодсистеме
метод Десериализовать(СериализованнаяНастройка: Настройка_2_0): НастройкиВопроса
    знч НастрокиВопроса: НастройкиВопроса
    НастрокиВопроса.МногострочныйОтвет = СериализованнаяНастройка.МногострочныйОтвет ?? Ложь
    НастрокиВопроса.Множественный = СериализованнаяНастройка.Множественный ?? Ложь
    НастрокиВопроса.Общедоступный = СериализованнаяНастройка.Общедоступный
    НастрокиВопроса.ЗагружатьДокументы = СериализованнаяНастройка.ЗагружатьДокументы ?? Истина
    НастрокиВопроса.ЗагружатьКартинки = СериализованнаяНастройка.ЗагружатьКартинки ?? Истина
    если СериализованнаяНастройка.ВидКомпонентаВыбора != Неопределено
        НастрокиВопроса.ВидКомпонентаВыбора = ВидыКомпонентаВыбораИзJson(СериализованнаяНастройка.ВидКомпонентаВыбора) 
    иначе
        НастрокиВопроса.ВидКомпонентаВыбора = ВидыКомпонентаВыбора.Список    
    ;       
    НастрокиВопроса.ДоступенКомментарий = СериализованнаяНастройка.ДоступенКомментарий ?? Ложь
    
    возврат НастрокиВопроса
;