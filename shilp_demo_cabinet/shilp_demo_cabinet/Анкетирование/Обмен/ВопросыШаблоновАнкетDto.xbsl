импорт ВебApi

@ВПроекте
метод ТипОбъектаJson(): Строка
    возврат "questionnaireTemplateQuestions"
;

@ВПроекте
структура ВопросШаблонаАнкеты_2_0
    @JsonСвойство("ID")
    обз знч Ид: Строка
    @JsonСвойство("template")
    обз знч Шаблон: Строка
    @JsonСвойство("section")
    обз знч Раздел: Строка
    @JsonСвойство("question")
    обз знч Вопрос: Строка
    @JsonСвойство("templatePresentation")
    пер ПредставлениеШаблона: Строка?
    @JsonСвойство("priority")
    пер Приоритет: Число?
    @JsonСвойство("sectionNumber")
    пер НомерРаздела: Число?
    @JsonСвойство("required")
    пер ОтветОбязателен: Булево?
;

@ВПодсистеме
метод ГруппаИзмененийВJson(Ссылки: ЧитаемаяКоллекция<ИсходящиеДанные.Ссылка>, Сервисы: ВебApiСервер.СервисыУпаковкиJson): ЧитаемоеСоответствие<Строка, Массив<Объект?>>
    возврат {"questionnaireTemplateQuestions": ДанныеВопросовШаблоновВJson(Ссылки, Сервисы)}
;

метод ДанныеВопросовШаблоновВJson(Ссылки: ЧитаемаяКоллекция<ИсходящиеДанные.Ссылка>, Сервисы: ВебApiСервер.СервисыУпаковкиJson): Массив<Объект?>
    знч Данные: Массив<Объект?>
    для КлючИЗначение из ДанныеВопросовШаблонов(Ссылки)
        Данные.Добавить(ДанныеВопросовШаблоновВJson_V2_0(КлючИЗначение.Значение, Сервисы))
    ;
    возврат Данные
;

метод ДанныеВопросовШаблонов(ВопросыШаблоновСсылки: ЧитаемаяКоллекция<ИсходящиеДанные.Ссылка>): ЧитаемоеСоответствие<ВопросыШаблоновАнкет.Ссылка, ДанныеВопросаШаблона>
    
    знч ДанныеВопросовШаблонов: Соответствие<ВопросыШаблоновАнкет.Ссылка, ДанныеВопросаШаблона> 
    
    если ВопросыШаблоновСсылки.Пусто()
        возврат ДанныеВопросовШаблонов
    ;
    
    знч Запрос = Запрос{
        Выбрать
            Ссылка,
            ПредставлениеШаблона,
            Приоритет,
            Шаблон,
            НомерРаздела,
            Раздел,
            Вопрос,
            ОтветОбязателен
        Породить ДанныеВопросаШаблона
        Из
            ВопросыШаблоновАнкет
        Где
            Ссылка В (%ВопросыШаблоновСсылки)
    }
    исп Результат = Запрос.Выполнить()
    
    для СтрокаРезультата из Результат
        ДанныеВопросовШаблонов.Вставить(
            СтрокаРезультата.Ссылка,
            СтрокаРезультата
        )        
    ;
    
    возврат ДанныеВопросовШаблонов   
;

метод ДанныеВопросовШаблоновВJson_V2_0(ДанныеВопросаШаблона: ДанныеВопросаШаблона, Сервисы: ВебApiСервер.СервисыУпаковкиJson): ВопросШаблонаАнкеты_2_0
    
    знч СериализованныйВопросШаблона = новый ВопросШаблонаАнкеты_2_0(
        Сервисы.ПолучитьИдентификатор(ДанныеВопросаШаблона.Ссылка),
        Сервисы.ПолучитьИдентификатор(ДанныеВопросаШаблона.Шаблон),
        Сервисы.ПолучитьИдентификатор(ДанныеВопросаШаблона.Раздел),
        Сервисы.ПолучитьИдентификатор(ДанныеВопросаШаблона.Вопрос)
    )
    
    СериализованныйВопросШаблона.ПредставлениеШаблона = ДанныеВопросаШаблона.ПредставлениеШаблона
    СериализованныйВопросШаблона.Приоритет = ДанныеВопросаШаблона.Приоритет
    СериализованныйВопросШаблона.НомерРаздела = ДанныеВопросаШаблона.НомерРаздела
    СериализованныйВопросШаблона.ОтветОбязателен =  ДанныеВопросаШаблона.ОтветОбязателен

    возврат СериализованныйВопросШаблона
; 

@ВПодсистеме
метод ТипДанныхJson(ВерсияФорматаОбмена: Версия?): Тип
    
    если ВерсияФорматаОбмена == Неопределено или ВерсияФорматаОбмена < Версия{2.0}
        выбросить новый ИсключениеВыполнения("Неверный формат")
    ;
    
    если ВерсияФорматаОбмена >= Версия{2.0}
        возврат Тип<ВопросШаблонаАнкеты_2_0>
    ;
;

@ВПодсистеме
метод ЗагрузитьОбъектИзJson(СериализованныйВопросШаблона: ВопросШаблонаАнкеты_2_0, Сервисы: ВебApiСервер.СервисыЗагрузкиJson)
    
    исп Транзакции.Начать()
    
    знч ВопросШаблонаАнкеты = Сервисы.ПрочитатьОбъект(СериализованныйВопросШаблона.Ид, &ВопросыШаблоновАнкет.Объект.новый(Ууид), Тип<ВопросыШаблоновАнкет.Ссылка>) как ВопросыШаблоновАнкет.Объект
    ВопросШаблонаАнкеты.Шаблон = Сервисы.ПолучитьСсылку(ШаблоныАнкетDto.ВнешняяСсылка(СериализованныйВопросШаблона.Шаблон)) как ШаблоныАнкет.Ссылка
    ВопросШаблонаАнкеты.Раздел = Сервисы.ПолучитьСсылку(РазделыАнкетDto.ВнешняяСсылка(СериализованныйВопросШаблона.Раздел)) как РазделыАнкет.Ссылка
    ВопросШаблонаАнкеты.Вопрос = Сервисы.ПолучитьСсылку(ВопросыDto.ВнешняяСсылка(СериализованныйВопросШаблона.Вопрос)) как Вопросы.Ссылка
    ВопросШаблонаАнкеты.ПредставлениеШаблона = СериализованныйВопросШаблона.ПредставлениеШаблона ?? ""
    ВопросШаблонаАнкеты.Приоритет = СериализованныйВопросШаблона.Приоритет ?? 0
    ВопросШаблонаАнкеты.НомерРаздела = СериализованныйВопросШаблона.НомерРаздела ?? 0
    ВопросШаблонаАнкеты.ОтветОбязателен = СериализованныйВопросШаблона.ОтветОбязателен ?? Ложь
    
    Сервисы.ЗаписатьОбъект(новый ВебApiСервер.ПараметрыЗаписиВходящихДанных(ВопросШаблонаАнкеты, СериализованныйВопросШаблона.Ид), Сервисы.ИнструментыМенеджераОбъекта)        
;

@ВПроекте
метод ВнешняяСсылка(Идентификатор: Строка): ВебApiСервер.ВнешняяСсылка
    возврат новый ВебApiСервер.ВнешняяСсылка(Идентификатор, (Ууид) -> ВопросыШаблоновАнкет.ПолучитьСсылку(Ууид))
;