импорт ОбменДанными

@Подписка(Событие{Анкеты.Объект.ПослеЗаписи})
метод АнкетыПослеЗаписи(Анкета: Анкеты.Данные, До: Анкеты.Данные, ПараметрыЗаписи: Анкеты.ПараметрыЗаписи)
    исп КонтекстДоступа.Привилегированный()
    знч СервисРегистрацииИзменений = АнкетированиеРегистрацияИзменений.ПолучитьСервис()
    
    если СервисРегистрацииИзменений.АнкетыТребуетсяРегистрация(Анкета.Статус, Анкета.УровеньДоступности) 
        ОбменДанными.ЗарегистрироватьОтправляемыйДокумент(Анкета.Ссылка, Истина)
        РегистрацияИзменений.ЗарегистрироватьИзменение(Анкета.Ссылка, Неопределено)
        если СервисРегистрацииИзменений.ШаблоныАнкетТребуетсяРегистрация()
            ЗарегистрироватьШаблон(Анкета.Шаблон)
        ;
        
        ЗарегистрироватьОтветы(Анкета.Ссылка)
    ;
;

@ВПроекте
метод ЗарегистрироватьШаблон(Шаблон: ШаблоныАнкет.Ссылка)
    ОбменДанными.ЗарегистрироватьОтправляемыйДокумент(Шаблон, Истина)
    РегистрацияИзменений.ЗарегистрироватьИзменение(Шаблон, Неопределено)
    
    ЗарегистрироватьРазделы(Шаблон)
    ЗарегистрироватьВопросыШаблона(Шаблон)
;

метод ЗарегистрироватьРазделы(Шаблон: ШаблоныАнкет.Ссылка)
    знч Запрос = Запрос{
        Выбрать
            Ссылка
        Из
            РазделыАнкет
        Где
            Шаблон == %Шаблон
    }
    исп РезультатЗапроса = Запрос.Выполнить()
    для СтрокаРезультата из РезультатЗапроса
        ОбменДанными.ЗарегистрироватьОтправляемыйДокумент(СтрокаРезультата.Ссылка, Истина)
        РегистрацияИзменений.ЗарегистрироватьИзменение(СтрокаРезультата.Ссылка, Неопределено)
    ;    
;

метод ЗарегистрироватьВопросыШаблона(Шаблон: ШаблоныАнкет.Ссылка)
    
    знч ВопросыШаблона: Массив<Вопросы.Ссылка>
    
    знч ЗапросВопросыШаблоновАнкет = Запрос{
        Выбрать
            Ссылка,
            Вопрос
        Из
            ВопросыШаблоновАнкет
        Где
            Шаблон == %Шаблон
    }
    исп РезультатЗапросаВопросыШаблоновАнкет = ЗапросВопросыШаблоновАнкет.Выполнить()
    для СтрокаРезультата из РезультатЗапросаВопросыШаблоновАнкет
        ОбменДанными.ЗарегистрироватьОтправляемыйДокумент(СтрокаРезультата.Ссылка, Истина)
        РегистрацияИзменений.ЗарегистрироватьИзменение(СтрокаРезультата.Ссылка, Неопределено)
        если СтрокаРезультата.Вопрос == Неопределено
            продолжить
        ; 
        ВопросыШаблона.Добавить(СтрокаРезультата.Вопрос)
        ОбменДанными.ЗарегистрироватьОтправляемыйДокумент(СтрокаРезультата.Вопрос, Истина)
        РегистрацияИзменений.ЗарегистрироватьИзменение(СтрокаРезультата.Вопрос, Неопределено)
    ;
    
    знч Подвопросы: Массив<Вопросы.Ссылка>
    знч ЗапросПодвопросы = Запрос{
        Выбрать
            Ссылка
        Из
            Вопросы
        Где
            СоставнойВопрос В (%ВопросыШаблона)
    }
    исп РезультатЗапросаПодвопросы = ЗапросПодвопросы.Выполнить()
    для СтрокаРезультата из РезультатЗапросаПодвопросы
        Подвопросы.Добавить(СтрокаРезультата.Ссылка)
        ОбменДанными.ЗарегистрироватьОтправляемыйДокумент(СтрокаРезультата.Ссылка, Истина)
        РегистрацияИзменений.ЗарегистрироватьИзменение(СтрокаРезультата.Ссылка, Неопределено)    
    ;
    ВопросыШаблона.ДобавитьВсе(Подвопросы)
    
    знч НаборыВариантов: Массив<НаборыВариантовОтвета.Ссылка>
    знч ЗапросВопросы = Запрос{
        Выбрать
            НаборВариантов
        Из
            Вопросы
        Где
            Ссылка В (%ВопросыШаблона)
    }
    исп РезультатЗапросаВопросы = ЗапросВопросы.Выполнить()
    для СтрокаРезультата из РезультатЗапросаВопросы
        если СтрокаРезультата.НаборВариантов == Неопределено
            продолжить
        ;
        НаборыВариантов.Добавить(СтрокаРезультата.НаборВариантов)
        ОбменДанными.ЗарегистрироватьОтправляемыйДокумент(СтрокаРезультата.НаборВариантов, Истина)
        РегистрацияИзменений.ЗарегистрироватьИзменение(СтрокаРезультата.НаборВариантов, Неопределено)    
    ;
    
    знч ЗапросВариантыОтветов = Запрос{
        Выбрать
            Ссылка
        Из
            ВариантыОтветов
        Где
            НаборВариантовОтвета В (%НаборыВариантов)
    }
    исп РезультатЗапросаВариантыОтветов = ЗапросВариантыОтветов.Выполнить()
    для СтрокаРезультата из РезультатЗапросаВариантыОтветов
        ОбменДанными.ЗарегистрироватьОтправляемыйДокумент(СтрокаРезультата.Ссылка, Истина)
        РегистрацияИзменений.ЗарегистрироватьИзменение(СтрокаРезультата.Ссылка, Неопределено)    
    ;
;

@Подписка(Событие{Анкеты.Объект.ПослеУдаления})
метод АнкетыПослеУдаления(Анкета: Анкеты.Данные, ПараметрыУдаления: Анкеты.ПараметрыУдаления)
    РегистрацияИзменений.ЗарегистрироватьУдаление(Анкета.Ссылка)
;

@Подписка(Событие{ВариантыОтветов.Объект.ПослеУдаления})
метод ВариантыОтветовПослеУдаления(ВариантОтвета: ВариантыОтветов.Данные, ПараметрыУдаления: ВариантыОтветов.ПараметрыУдаления)
    РегистрацияИзменений.ЗарегистрироватьУдаление(ВариантОтвета.Ссылка)
;

@Подписка(Событие{Вопросы.Объект.ПослеУдаления})
метод ПередУдалением(Вопрос: Вопросы.Данные, ПараметрыУдаления: Вопросы.ПараметрыУдаления)
    РегистрацияИзменений.ЗарегистрироватьУдаление(Вопрос.Ссылка)
;

@Подписка(Событие{ВопросыШаблоновАнкет.Объект.ПослеУдаления})
метод ПередУдалением(ВопросШаблона: ВопросыШаблоновАнкет.Данные, ПараметрыУдаления: ВопросыШаблоновАнкет.ПараметрыУдаления)
    РегистрацияИзменений.ЗарегистрироватьУдаление(ВопросШаблона.Ссылка)
;

@Подписка(Событие{НаборыВариантовОтвета.Объект.ПослеУдаления})
метод ПередУдалением(Набор: НаборыВариантовОтвета.Данные, ПараметрыУдаления: НаборыВариантовОтвета.ПараметрыУдаления)
    РегистрацияИзменений.ЗарегистрироватьУдаление(Набор.Ссылка)
;

@Подписка(Событие{Опросы.Объект.ПослеУдаления})
метод ПередУдалением(Опрос: Опросы.Данные, ПараметрыУдаления: Опросы.ПараметрыУдаления)
    РегистрацияИзменений.ЗарегистрироватьУдаление(Опрос.Ссылка)
;

@Подписка(Событие{Ответы.Объект.ПослеУдаления})
метод ПередУдалением(Ответ: Ответы.Данные, ПараметрыУдаления: Ответы.ПараметрыУдаления)
    РегистрацияИзменений.ЗарегистрироватьУдаление(Ответ.Ссылка)
;

@Подписка(Событие{РазделыАнкет.Объект.ПослеУдаления})
метод ПередУдалением(Раздел: РазделыАнкет.Данные, ПараметрыУдаления: РазделыАнкет.ПараметрыУдаления)
    РегистрацияИзменений.ЗарегистрироватьУдаление(Раздел.Ссылка)
;

@Подписка(Событие{ШаблоныАнкет.Объект.ПослеУдаления})
метод ПередУдалением(Шаблон: ШаблоныАнкет.Данные, ПараметрыУдаления: ШаблоныАнкет.ПараметрыУдаления)
    РегистрацияИзменений.ЗарегистрироватьУдаление(Шаблон.Ссылка)
;

метод ЗарегистрироватьОтветы(Анкета: Анкеты.Ссылка)
    
    знч Запрос = Запрос{
        Выбрать
            Ссылка
        Из
            Ответы
        Где
            Анкета == %Анкета
    }
    исп РезультатЗапроса = Запрос.Выполнить()
    
    для СтрокаРезультата из РезультатЗапроса
        ОбменДанными.ЗарегистрироватьОтправляемыйДокумент(СтрокаРезультата.Ссылка, Истина)
        РегистрацияИзменений.ЗарегистрироватьИзменение(СтрокаРезультата.Ссылка, Неопределено)
    ;
;