импорт ИнтерфейсныеКомпонентыСтандартные
импорт ОбщиеСтандартные
импорт ФизическиеЛица

метод ВыборШаблонаПриИзменении(Источник: ПолеВвода<ШаблоныАнкет.Ссылка?>, Событие: СобытиеПриИзменении<неизвестно>)
    если ОпросОбъект.ШаблонАнкеты == Неопределено
        ОпросОбъект.Наименование = ""
    иначе
        ОпросОбъект.Наименование = ОпросОбъект.ШаблонАнкеты.Представление()    
    ;      
;

метод ВыборШаблонаНастройкиВводаСсылок(): НастройкиВводаСсылок
    знч Настройка = новый НастройкиВводаСсылки(
    ПсевдонимОсновнойТаблицы = "ШаблоныАнкет",
    Фильтр = новый ГруппаЭлементовФильтра(
        Элементы = [
            новый ЭлементФильтра(
                Поле = "ШаблоныАнкет.ИдПодсистемыПоставщика",
                ВидСравнения = ВидСравнения.Равно,
                Значение = "",
                Использовать = Истина
            )
        ]
    ))
    
    возврат новый НастройкиВводаСсылок({Тип<ШаблоныАнкет.Ссылка>: Настройка})
;

метод СоздатьЗаголовок(): Строка
    
    если ПериодПроведения == Неопределено
        возврат "Запустить"
    ;
    
    возврат (ПериодПроведения.От > МенеджерДат.ТекущаяДатаВремя().ВДатаВремя(ЧасовойПояс.Текущий()).Дата) ? "Запланировать" : "Запустить"
;

метод СоздатьПриНажатии(Источник: Кнопка, Событие: СобытиеПриНажатии)

    если не ПроверкаЗаполнения()
        возврат
    ;
    
    если (ПериодПроведения.От > МенеджерДат.ТекущаяДатаВремя().ВДатаВремя(ЧасовойПояс.Текущий()).Дата)
        СохранитьОпрос(новый СобытиеСДанными(СтатусыОпросов.Запланирован)) 
    иначе
        СохранитьОпрос(новый СобытиеСДанными(СтатусыОпросов.Запущен)) 
    ;
    
;

метод УчастникиУдалениеУчастника(Источник: ДанныеУчастниковКомпонент, Событие: СобытиеСДанными<ФизическиеЛица.Ссылка>)
    УдалениеУчастника(новый СобытиеСДанными(Событие.Данные))
;

метод ИзменилсяСоставУчастников(Источник: ДанныеУчастниковКомпонент, Событие: неизвестно)
    СохранитьОпрос(новый СобытиеСДанными(СтатусыОпросов.НеЗапущен))
;

метод ПроверкаЗаполнения(): Булево
    пер РезультатПроверки = Истина
    
    если ОпросОбъект.ШаблонАнкеты == Неопределено
        СтандартныеФормы.ПоказатьУведомление("Ошибка", "Не выбран шаблон опроса", ОценкаИнформации.Отрицательная)
        РезультатПроверки = Ложь
    ;
    
    если Участники.Размер() == 0
        СтандартныеФормы.ПоказатьУведомление("Ошибка", "Не заполнены респонденты опроса", ОценкаИнформации.Отрицательная)
        РезультатПроверки = Ложь
    ;
    
    если ПериодПроведения == Неопределено
        СтандартныеФормы.ПоказатьУведомление("Ошибка", "Некорректно заполнен период проведения опроса", ОценкаИнформации.Отрицательная)
        РезультатПроверки = Ложь
    иначе если ПериодПроведения.От < МенеджерДат.ТекущаяДатаВремя().ВДатаВремя(ЧасовойПояс.Текущий()).Дата
        СтандартныеФормы.ПоказатьУведомление("Ошибка", "Дата начала опроса меньше текущей", ОценкаИнформации.Отрицательная)
        РезультатПроверки = Ложь
    ;
    
    возврат РезультатПроверки
    
;

метод СохранитьЧерновикПриНажатии(Источник: Кнопка, Событие: СобытиеПриНажатии)
    СохранитьОпрос(новый СобытиеСДанными(СтатусыОпросов.НеЗапущен))
;

метод РедактироватьПриНажатии(Источник: Кнопка, Событие: СобытиеПриНажатии)
    РежимПросмотраНаименования = Истина
;

метод ПросмотрНаименованияПриНажатии(Источник: Надпись, Событие: СобытиеПриНажатии)
    РежимПросмотраНаименования = Ложь
;