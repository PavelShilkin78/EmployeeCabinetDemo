импорт ИнтерфейсныеКомпонентыСтандартные
импорт СтандартноеПриложение

@ВПроекте
@Реализация
метод Инициализировать()
    ИнтерфейсныеКомпоненты.ИнициализироватьПодчиненныеКомпоненты(Компоненты)
;

метод ПриВыделенииЭлемента(Источник: ДиаграммаГанта<Объект?, Опросы.Ссылка>, Событие: СобытиеПриВыделенииЭлементаДиаграммыГанта<Объект?, Опросы.Ссылка>)
    если Событие.Данные == Неопределено
        возврат
    ;
    
    ОпросФормаОбъекта.ОткрытьВМодальномОкне(КлючОбъекта = Событие.Данные.Данные)
    ОбновитьДиаграмму()
;

@ВПроекте
метод ОбновитьДиаграмму(НачалоДиапазона: Дата, ОкончаниеДиапазона: Дата)
    этот.НачалоДиапазона = НачалоДиапазона
    этот.ОкончаниеДиапазона = ОкончаниеДиапазона
    УстановитьДиапазон()
    ОбновитьДиаграмму()
;

метод ОбновитьДиаграмму()
    пер СписокОпросов = ПолучитьОпросыПоПромежуткамВремени(НачалоДиапазона, ОкончаниеДиапазона)
    пер ЕстьОпросыЗаПериод = СписокОпросов.Размер() > 0
    если ЕстьОпросыЗаПериод
        Компоненты.КалендарьОпросов.УстановитьЭлементы(СписокОпросов)
    ;
    Компоненты.КалендарьОпросов.Видимость = ЕстьОпросыЗаПериод
    Компоненты.КалендарьОпросов.Активировать()
;

метод УстановитьДиапазон()
    Компоненты.КалендарьОпросов.ОтображаемыйДиапазон = новый Диапазон(
        НачалоДиапазона.НачалоДня().ВМомент(ЧасовойПояс.Текущий()), 
        ОкончаниеДиапазона.КонецДня().ВМомент(ЧасовойПояс.Текущий())
    )
;

@НаСервере @ДоступноСКлиента
статический метод ПолучитьОпросыПоПромежуткамВремени(ДатаНачала: Дата, ДатаОкончания: Дата): Массив<ЭлементДиаграммыГанта<Объект?, Опросы.Ссылка>>
    пер ТекущийЧасовойПояс = ЧасовойПояс.Текущий()
    знч Запрос = Запрос{
        Выбрать
            Опросы.Ссылка как Ссылка,
            Опросы.Наименование как Наименование,
            Опросы.ДатаНачала как ДатаНачала,
            Опросы.ДатаОкончания как ДатаОкончания
        Из
            Опросы как Опросы
        Где 
            Опросы.ДатаНачала МЕЖДУ %ДатаНачала И %ДатаОкончания
            Или Опросы.ДатаОкончания МЕЖДУ %ДатаНачала И %ДатаОкончания
    }
    исп ПривилегированныйРежим = КонтекстДоступа.Привилегированный() 
    знч РезультатЗапроса = новый ЧитаемыйМассив(Запрос.Выполнить())
    ПривилегированныйРежим.Закрыть()
    
    пер РезультатПоОпросам = новый Массив<ЭлементДиаграммыГанта<Объект?, Опросы.Ссылка>>()
    если РезультатЗапроса.Размер() > 0
        для РезультатСтрока из РезультатЗапроса
            пер ДатаНачалаОпроса = РезультатСтрока.ДатаНачала
            пер ДатаОкончанияОпроса = РезультатСтрока.ДатаОкончания
            если ДатаОкончанияОпроса < ДатаНачалаОпроса
                продолжить
            ;
            РезультатПоОпросам.Добавить(
                новый ЭлементДиаграммыГанта<Объект?, Опросы.Ссылка>(
                    Наименование = "",
                    Интервалы = [
                        новый ИнтервалДиаграммыГанта<Опросы.Ссылка>(
                            Ид = РезультатСтрока.Ссылка.Ид,
                            Наименование = РезультатСтрока.Наименование,
                            Цвет = Авто,
                            Данные = РезультатСтрока.Ссылка,
                            Диапазон = новый ЗакрытыйДиапазон<Момент>(
                                новый Момент(Дата = ДатаНачалаОпроса, ЧасовойПояс = ТекущийЧасовойПояс),
                                новый Момент(Дата = ДатаОкончанияОпроса, ЧасовойПояс = ТекущийЧасовойПояс)
                            )
                        )
                    ]
                )
            )
        ;
    ;
    возврат РезультатПоОпросам
;