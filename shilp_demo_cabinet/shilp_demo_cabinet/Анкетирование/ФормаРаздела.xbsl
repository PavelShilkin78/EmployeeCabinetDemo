импорт ИнтерфейсныеКомпонентыСтандартные
импорт ОбщиеСтандартные

@ВПроекте
@Реализация
метод Заголовок(): Строка
    возврат "Анкетирование"
;

@ВПроекте
@Реализация
метод КомандыСоздания(): Команда|ГруппаКомандногоИнтерфейса<Команда>|Неопределено
    возврат Неопределено
;

@ВПроекте
@Реализация
метод ДополнительныеКоманды(): ФрагментКомандногоИнтерфейса<Команда>?
    возврат Неопределено
;

@ВПроекте
@Реализация
метод ОсновнаяКоманда(): Команда?
    возврат Неопределено
;

@ВПроекте
@Реализация
метод ОбычныеКоманды(): ФрагментКомандногоИнтерфейса<Команда>?
    возврат Неопределено
;

метод ПерейтиНаСписокОпросов(Источник: Надпись, Событие: СобытиеПриНажатии)
    ИнтерфейсныеКомпоненты.ОткрытьЗамещающуюФорму(этот, новый ФормаРазделаОпросы())
;

метод ПерейтиНаСписокШаблонов(Источник: Надпись, Событие: СобытиеПриНажатии)
    ИнтерфейсныеКомпоненты.ОткрытьЗамещающуюФорму(этот, новый ФормаСписокШаблоновАнкет())
;

@Обработчик
метод ПослеСоздания()
    ИнтерфейсныеКомпоненты.ИнициализироватьПодчиненныеКомпоненты(Компоненты)
    этот.ГодФильтра = МенеджерДат.ТекущаяДатаВремя().ВДатаВремя(ЧасовойПояс.Текущий()).Дата.Год
    этот.ВсеПериодыФильтраВнутриГода = СоздатьВсеПериодыФильтраВнутриГода()
    этот.ПериодФильтраВнутриГода = этот.ВсеПериодыФильтраВнутриГода[ЕдиницаСмещенияДатыВремени.Месяц.Представление()]
    
    Компоненты.ПредставлениеПериодаФильтраВнутриГода.Заголовок = ЕдиницаСмещенияДатыВремени.Месяц.Представление()
    Компоненты.ПредставлениеГода.Заголовок = этот.ГодФильтра.ВСтроку()
    
    СоздатьЭлементыМенюГода()
    СоздатьЭлементыМенюФильтраВнутриГода()
    РасчитатьДатыФильтра(Истина)
    ОбновитьКомпонентыДашборда()
;

метод ВыборГода(Компонент: ВсплывающееМеню, Элемент: СобытиеСДанными<ЭлементМеню>)
    пер ВыбранныйГодФильтра = Элемент.Данные.ДополнительныеПараметры как Число
    знч ЭтоТекущийГод = (ВыбранныйГодФильтра == МенеджерДат.ТекущаяДатаВремя().ВДатаВремя(ЧасовойПояс.Текущий()).Дата.Год)
    этот.ГодФильтра = ВыбранныйГодФильтра
        
    Компоненты.ОтображениеМенюПериодыФильтра.Видимость = ЭтоТекущийГод
    Компоненты.ПредставлениеГода.Заголовок = ВыбранныйГодФильтра.ВСтроку()
    
    если не ЭтоТекущийГод
        Компоненты.ПредставлениеПериодаФильтраВнутриГода.Заголовок = ЕдиницаСмещенияДатыВремени.Год.Представление()
        этот.ПериодФильтраВнутриГода = этот.ВсеПериодыФильтраВнутриГода[ЕдиницаСмещенияДатыВремени.Год.Представление()]
    ;
    РасчитатьДатыФильтра(ЭтоТекущийГод)
    ОбновитьКомпонентыДашборда()
;

метод ВыборПериодаФильтраВнутриГода(Компонент: ВсплывающееМеню, Элемент: СобытиеСДанными<ЭлементМеню>)
    знч ЕдиницаСмещения = Элемент.Данные.ДополнительныеПараметры как Строка
    этот.ПериодФильтраВнутриГода = этот.ВсеПериодыФильтраВнутриГода[ЕдиницаСмещения]
    Компоненты.ПредставлениеПериодаФильтраВнутриГода.Заголовок = ЕдиницаСмещения
    
    РасчитатьДатыФильтра(Истина)
    ОбновитьКомпонентыДашборда()
;

метод ВыбратьГодПриНажатии(Источник: Картинка, Событие: СобытиеПриНажатии)
    ВыборГода.Открыть(этот.Компоненты.ОтображениеМенюГоды)
;

метод ВыбратьГодПриНажатииНадписи(Источник: Надпись, Событие: СобытиеПриНажатии)
    ВыборГода.Открыть(этот.Компоненты.ОтображениеМенюГоды)
;

метод ВыбратьПериодПриНажатии(Источник: Картинка, Событие: СобытиеПриНажатии)
    ВыборПериодаФильтраВнутриГода.Открыть(этот.Компоненты.ОтображениеМенюПериодыФильтра)
;

метод ВыбратьПериодПриНажатииНадписи(Источник: Надпись, Событие: СобытиеПриНажатии)
    если Компоненты.ОтображениеМенюПериодыФильтра.Видимость
        ВыборПериодаФильтраВнутриГода.Открыть(этот.Компоненты.ПредставлениеПериодаФильтраВнутриГода)
    ;
;

метод ОбновитьКомпонентыДашборда()
    Компоненты.СводныеДанныеПоОпросам.ОбновитьДанныеИндикаторов(этот.ФильтрДатаНачала, этот.ФильтрДатаОкончания)
    Компоненты.СводныеДанныеПоАнкетам.ОбновитьДанныеИндикаторов(этот.ФильтрДатаНачала, этот.ФильтрДатаОкончания)
    Компоненты.КомпонентКалендарьОпросов.ОбновитьДиаграмму(этот.ФильтрДатаНачала, этот.ФильтрДатаОкончания)
;

метод СоздатьВсеПериодыФильтраВнутриГода(): Соответствие<Строка,СмещениеДатыВремени>
    пер Периоды = новый Соответствие<Строка,СмещениеДатыВремени>()
    Периоды.Вставить("Месяц", новый СмещениеДатыВремени(ЕдиницаСмещенияДатыВремени.Месяц, 1))
    Периоды.Вставить("Квартал",новый СмещениеДатыВремени(ЕдиницаСмещенияДатыВремени.Квартал, 1))
    Периоды.Вставить("Полугодие",новый СмещениеДатыВремени(ЕдиницаСмещенияДатыВремени.Квартал, 2))
    Периоды.Вставить("Год",новый СмещениеДатыВремени(ЕдиницаСмещенияДатыВремени.Год, 1))
    возврат Периоды
;

метод СоздатьЭлементыМенюГода()
    этот.ВыборГода.ПриВыбореЭлемента = &ВыборГода
    для Год = 2015 по этот.ГодФильтра
        этот.ВыборГода.Элементы.Добавить(
            новый ЭлементМеню(
                Заголовок = Год.ВСтроку(),
                ДополнительныеПараметры = Год
            )
        )
    ;
;

метод СоздатьЭлементыМенюФильтраВнутриГода()
    этот.ВыборПериодаФильтраВнутриГода.ПриВыбореЭлемента = &ВыборПериодаФильтраВнутриГода
    для Период из этот.ВсеПериодыФильтраВнутриГода.Ключи()
        этот.ВыборПериодаФильтраВнутриГода.Элементы.Добавить(
            новый ЭлементМеню(
                Заголовок = Период,
                ДополнительныеПараметры = Период
            )
        )
    ;
;

метод РасчитатьДатыФильтра(ВыбранТекущийГод: Булево)
    знч ТекущаяДата = МенеджерДат.ТекущаяДатаВремя().ВДатаВремя(ЧасовойПояс.Текущий()).Дата
    если не ВыбранТекущийГод
        знч ПрошедшийГод = новый Дата(этот.ГодФильтра, 1, 1)
        этот.ФильтрДатаНачала = ПрошедшийГод.НачалоГода()
        этот.ФильтрДатаОкончания = ПрошедшийГод.КонецГода()
    иначе
        если этот.ПериодФильтраВнутриГода.Единица == ЕдиницаСмещенияДатыВремени.Год
            этот.ФильтрДатаНачала = ТекущаяДата.НачалоГода()
            этот.ФильтрДатаОкончания = ТекущаяДата.КонецГода()
        иначе если этот.ПериодФильтраВнутриГода.Единица == ЕдиницаСмещенияДатыВремени.Квартал
            если этот.ПериодФильтраВнутриГода.Размер == 1
                если ТекущаяДата.Месяц >= 10
                    этот.ФильтрДатаНачала = новый Дата(этот.ГодФильтра, 10, 1)
                    этот.ФильтрДатаОкончания = ТекущаяДата.КонецГода()
                иначе если ТекущаяДата.Месяц >= 7
                    этот.ФильтрДатаНачала = новый Дата(этот.ГодФильтра, 7, 1)
                    этот.ФильтрДатаОкончания = новый Дата(этот.ГодФильтра, 9, 30)
                иначе если ТекущаяДата.Месяц >= 4
                    этот.ФильтрДатаНачала = новый Дата(этот.ГодФильтра, 4, 1)
                    этот.ФильтрДатаОкончания = новый Дата(этот.ГодФильтра, 6, 30)
                иначе
                    этот.ФильтрДатаНачала = новый Дата(этот.ГодФильтра, 1, 1)
                    этот.ФильтрДатаОкончания = новый Дата(этот.ГодФильтра, 3, 31)
                ;
            иначе если этот.ПериодФильтраВнутриГода.Размер == 2
                если ТекущаяДата.Месяц >= 7
                    этот.ФильтрДатаНачала = новый Дата(этот.ГодФильтра, 7, 1)
                    этот.ФильтрДатаОкончания = ТекущаяДата.КонецГода()
                иначе
                    этот.ФильтрДатаНачала = новый Дата(этот.ГодФильтра, 1, 1)
                    этот.ФильтрДатаОкончания = новый Дата(этот.ГодФильтра, 6, 30)
                ;
            ;
        иначе если этот.ПериодФильтраВнутриГода.Единица == ЕдиницаСмещенияДатыВремени.Месяц
            этот.ФильтрДатаНачала = ТекущаяДата.НачалоМесяца()
            этот.ФильтрДатаОкончания = ТекущаяДата.КонецМесяца()
        ;
                
    ;
;