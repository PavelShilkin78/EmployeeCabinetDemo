импорт ИнтерфейсныеКомпонентыСтандартные

@НаКлиенте @НаСервере
@ВПодсистеме
структура ПопулярностьШаблона
    обз пер Шаблон: ШаблоныАнкет.Ссылка?
    обз пер Наименование: Строка
    обз пер КоличествоАнкет: Число
;

@ВПроекте
@Реализация
метод Инициализировать()
    ИнтерфейсныеКомпоненты.ИнициализироватьТаблицу(Таблица = Компоненты.ТаблицаШаблоновАнкет,
        ОтображатьШапку = Истина, РазмерСтраницы = 4, Навигация = НавигацияВСписке.Отсутствует)
    ИнтерфейсныеКомпоненты.ИнициализироватьПодчиненныеКомпоненты(Компоненты)
    ОбновитьДанныеТаблицы()
;

метод ОбновитьДанныеТаблицы()
    знч Данные = Компоненты.ТаблицаШаблоновАнкет.Источник.Данные
    Данные.ДобавитьВсе(ПолучитьДанныеПопулярности())
;

метод ВидимостьТаблицыШаблоновАнкет(): Булево
    возврат Компоненты.ТаблицаШаблоновАнкет.Источник.Данные.Размер() > 0
;

метод ТаблицаШаблоновАнкетПриНажатииСтроки(Источник: Список<ИсточникДанныхМассив<ПопулярностьШаблона>>, Событие: СобытиеСДанными<СтрокаСписка<ПопулярностьШаблона>>)
    пер ФормаКонструктора = новый ШаблонАнкеты(КлючОбъекта = Событие.Данные.ДанныеСтроки.Шаблон)
    ИнтерфейсныеКомпоненты.ОткрытьЗамещающуюФорму(ФормаВладелец, ФормаКонструктора)
;

@НаСервере @ДоступноСКлиента
статический метод ПолучитьДанныеПопулярности(): Массив<ПопулярностьШаблона>
    знч Запрос = Запрос{
        Выбрать Первые 4
            Опросы.ШаблонАнкеты.ЗаменитьNull(Неопределено) как Шаблон,
            Опросы.ШаблонАнкеты.Наименование.ЗаменитьNull("") как ШаблонНаименование,
            КОЛИЧЕСТВО(Опросы.Ссылка) как Количество
        Из
            Опросы как Опросы
        Сгруппировать По
            Опросы.ШаблонАнкеты
        Упорядочить По
		    Количество Убыв
    }
    пер РезультатПоШаблонам = новый Массив<ПопулярностьШаблона>()
    исп ПривилегированныйРежим: КонтекстДоступа = КонтекстДоступа.Привилегированный()
    знч РезультатЗапроса = новый ЧитаемыйМассив(Запрос.Выполнить())
    ПривилегированныйРежим.Закрыть()
    
    если РезультатЗапроса.Размер() > 0
        для РезультатСтрока из РезультатЗапроса
            РезультатПоШаблонам.Добавить(
                новый ПопулярностьШаблона(
                    Шаблон = РезультатСтрока.Шаблон,
                    Наименование = РезультатСтрока.ШаблонНаименование,
                    КоличествоАнкет = РезультатСтрока.Количество
                )
            )
        ;
    ;
    возврат РезультатПоШаблонам
;