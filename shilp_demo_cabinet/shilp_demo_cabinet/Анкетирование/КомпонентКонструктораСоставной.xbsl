@ВПроекте
@Реализация
метод Инициализировать()
    
    УдаленКомпонентКонструктора.ПодключитьОбработчик((метод (ЭлементКонструктора) ->
        Данные.ДанныеВопроса.СоставСтруктурногоОтвета.Удалить(ЭлементКонструктора)
        ОбновитьСодержимоеСписка()
    ;))
    
    ПеремещенВопрос.ПодключитьОбработчик((метод (ИдСоставногоВопроса, ПараметрыПеремещения) ->
        если ИдСоставногоВопроса != Данные.ИдЭлемента
            возврат
        ;
        ПереместитьКомпонент(ПараметрыПеремещения) 
    ;))
    
    если Данные.ДанныеСоставногоОтвета.Размер() == 0
        Данные.ДанныеСоставногоОтвета.Добавить(новый Анкетирование.ОтветыСоставногоВопроса(ИдОтвета = новый Ууид(), Ответ = Анкетирование.КопияПодвопросов(Данные)))
    ;
    
    ОбновитьСодержимоеСписка()
    Данные.ЗаголовокЭлемента = "Составной"
    Данные.ИконкаЭлемента = Ресурс{Цепь.svg}.Ссылка
    Настройки = Компоненты.Настройки  
;

метод ПереместитьКомпонент(ПараметрыСдвига: Анкетирование.ПараметрыСдвигаКомпонента)
    
    пер ИндексЭлемента = Данные.ДанныеВопроса.СоставСтруктурногоОтвета.Найти(ПараметрыСдвига.ЭлементКонструктора)
    
    если ИндексЭлемента == Неопределено
        возврат
    ;    
    если ПараметрыСдвига.Направление == Анкетирование.НАПРАВЛЕНИЕ_ВВЕРХ и ИндексЭлемента == 0
        возврат
    ;    
    если ПараметрыСдвига.Направление == Анкетирование.НАПРАВЛЕНИЕ_ВНИЗ и ИндексЭлемента == Данные.ДанныеВопроса.СоставСтруктурногоОтвета.Граница()
        возврат    
    ;
        
    Данные.ДанныеВопроса.СоставСтруктурногоОтвета.Удалить(ПараметрыСдвига.ЭлементКонструктора)
    Данные.ДанныеВопроса.СоставСтруктурногоОтвета.Вставить(ИндексЭлемента + ПараметрыСдвига.Направление, ПараметрыСдвига.ЭлементКонструктора)
    ОбновитьСодержимоеСписка()
;

метод ДобавитьКомпонент(ЭлементКонструктора: ДанныеЭлементаКонструктора)
    ЭлементКонструктора.ИдСоставногоВопроса = Данные.ИдЭлемента
    ЭлементКонструктора.ИдРаздела = Данные.ИдРаздела
    ЭлементКонструктора.НомерВопроса = Данные.ДанныеВопроса.СоставСтруктурногоОтвета.Размер() + 1
    ЭлементКонструктора.НомерСоставногоВопроса = Данные.НомерВопроса
        
    Данные.ДанныеВопроса.СоставСтруктурногоОтвета.Добавить(ЭлементКонструктора)
    ОбновитьСодержимоеСписка()
;

метод ДобавитьОтветПриНажатии(Источник: Надпись, Событие: СобытиеПриНажатии)
    пер КопияСоставногоВопроса =  Анкетирование.КопияПодвопросов(Данные)
    Данные.ДанныеСоставногоОтвета.Добавить(новый Анкетирование.ОтветыСоставногоВопроса(ИдОтвета = новый Ууид(), Ответ = КопияСоставногоВопроса))
    ОбновитьСодержимоеСписка()
;

метод СоставВопросовПослеСозданияСтроки(Источник: Список<ИсточникДанныхМассив<ДанныеЭлементаКонструктора>>, Событие: СобытиеСДанными<СтрокаСписка<ДанныеЭлементаКонструктора>>)
    пер ДанныеСтроки = Событие.Данные как АнкетированиеСтрока
    ДанныеСтроки.ИдТекущегоРаздела = Данные.ИдРаздела
    Компоненты.ОбходОшибкиПриАктивацииСтроки.Активировать()
;

метод ОбновитьСодержимоеСписка()

    ПеренумероватьПодвопросы()
    
    пер КоллекцияДанных = КоллекцияИсточникаСоставВопросов()
    пер КоллекцияДанныхРежимаАнкеты = КоллекцияИсточникаСоставОтвета()
    
    КоллекцияДанныхРежимаАнкеты.Очистить()
    КоллекцияДанныхРежимаАнкеты.ДобавитьВсе(Данные.ДанныеСоставногоОтвета)  
    
    КоллекцияДанных.Очистить()
    КоллекцияДанных.ДобавитьВсе(Данные.ДанныеВопроса.СоставСтруктурногоОтвета)    
;

метод ПеренумероватьПодвопросы()
    пер НомерПодвопроса = 0
    для Подвопрос из Данные.ДанныеВопроса.СоставСтруктурногоОтвета
        НомерПодвопроса += 1
        Подвопрос.НомерВопроса = НомерПодвопроса     
    ;    
;

метод КоллекцияИсточникаСоставВопросов(): Массив<ДанныеЭлементаКонструктора>
    возврат Компоненты.СоставВопросов.Источник.Данные
;

метод КоллекцияИсточникаСоставОтвета(): Массив<Анкетирование.ОтветыСоставногоВопроса>
    возврат Компоненты.СоставОтвета.Источник.Данные
;

метод ДобавитьКомпонентВыборИнструмента(Источник: ДобавитьКомпонентКонструктораКнопка, Событие: СобытиеСДанными<ИнструментыКонструктора>)
    пер Подвопрос = НовыйПодвопрос(Событие.Данные)
    ДобавитьКомпонент(Подвопрос)
;

метод НовыйПодвопрос(Инструмент: ИнструментыКонструктора): ДанныеЭлементаКонструктора
    пер ДанныеВопроса: ДанныеВопроса   
    ДанныеВопроса.ТипОтвета = Анкетирование.ТипОтветаИнструмента(Инструмент)
    ДанныеВопроса = Анкетирование.ДанныеВопросаУмолчание(ДанныеВопроса.ТипОтвета)   
    возврат Анкетирование.ЗаполнитьЭлементКонструктораДанными(ДанныеВопроса, Данные.ИдРаздела, Данные.ПриоритетРаздела)
;

// Контракт КомпонентКонструктора
@ВПроекте
@Реализация
метод ОткрытьНастройки(): Компонент
    возврат Компоненты.Настройки
;

// Конец КомпонентКонструктора