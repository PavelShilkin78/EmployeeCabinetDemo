импорт СтандартноеПриложение
импорт ИнтерфейсныеКомпонентыСтандартные
импорт КонтрольДоступа
// Контракт ОсновнойИнтерфейсПоставщик

@ВПроекте
@Реализация
метод КомандыВсеФункции(): ЧитаемаяКоллекция<ОсновнойИнтерфейс.ОписательКомандыВсеФункции>
    знч Группа = новый ОсновнойИнтерфейс.ОписательГруппыКомандВсеФункции(Анкетирование.ИМЯ_РАЗДЕЛА_АНКЕТИРОВАНИЕ, 17)
    возврат [
        новый ОсновнойИнтерфейс.ОписательКомандыВсеФункции(
            Группа = Группа, 
            Представление = "Анкеты",
            СоздательФормыСписка = метод()-> возврат новый Анкеты.АвтоматическаяФормаСписка();,
            СоздательФормыОбъекта = метод()-> возврат новый Анкеты.АвтоматическаяФормаОбъекта();
        ),
        новый ОсновнойИнтерфейс.ОписательКомандыВсеФункции(
            Группа = Группа, 
            Представление = "Варианты ответов",
            СоздательФормыСписка = метод()-> возврат новый ВариантыОтветов.АвтоматическаяФормаСписка();,
            СоздательФормыОбъекта = метод()-> возврат новый ВариантыОтветов.АвтоматическаяФормаОбъекта();
        ),
        новый ОсновнойИнтерфейс.ОписательКомандыВсеФункции(
            Группа = Группа, 
            Представление = "Вопросы",
            СоздательФормыСписка = метод()-> возврат новый Вопросы.АвтоматическаяФормаСписка();,
            СоздательФормыОбъекта = метод()-> возврат новый Вопросы.АвтоматическаяФормаОбъекта();
        ),
        новый ОсновнойИнтерфейс.ОписательКомандыВсеФункции(
            Группа = Группа, 
            Представление = "Вопросы шаблонов анкет",
            СоздательФормыСписка = метод()-> возврат новый ВопросыШаблоновАнкет.АвтоматическаяФормаСписка();,
            СоздательФормыОбъекта = метод()-> возврат новый ВопросыШаблоновАнкет.АвтоматическаяФормаОбъекта();
        ),
        новый ОсновнойИнтерфейс.ОписательКомандыВсеФункции(
            Группа = Группа, 
            Представление = "Наборы вариантов ответа",
            СоздательФормыСписка = метод()-> возврат новый НаборыВариантовОтвета.АвтоматическаяФормаСписка();,
            СоздательФормыОбъекта = метод()-> возврат новый НаборыВариантовОтвета.АвтоматическаяФормаОбъекта();
        ),
        новый ОсновнойИнтерфейс.ОписательКомандыВсеФункции(
            Группа = Группа, 
            Представление = "Разделы анкет",
            СоздательФормыСписка = метод()-> возврат новый РазделыАнкет.АвтоматическаяФормаСписка();,
            СоздательФормыОбъекта = метод()-> возврат новый РазделыАнкет.АвтоматическаяФормаОбъекта();
        ),
        новый ОсновнойИнтерфейс.ОписательКомандыВсеФункции(
            Группа = Группа, 
            Представление = "Шаблоны анкет",
            СоздательФормыСписка = метод()-> возврат новый ШаблоныАнкет.АвтоматическаяФормаСписка();,
            СоздательФормыОбъекта = метод()-> возврат новый ШаблоныАнкет.АвтоматическаяФормаОбъекта();
        ),
        новый ОсновнойИнтерфейс.ОписательКомандыВсеФункции(
            Группа = Группа, 
            Представление = "Ответы",
            СоздательФормыСписка = метод()-> возврат новый Ответы.АвтоматическаяФормаСписка();,
            СоздательФормыОбъекта = метод()-> возврат новый Ответы.АвтоматическаяФормаОбъекта();
        ),
        новый ОсновнойИнтерфейс.ОписательКомандыВсеФункции(
            Группа = Группа, 
            Представление = "Опросы",
            СоздательФормыСписка = метод()-> возврат новый Опросы.АвтоматическаяФормаСписка();,
            СоздательФормыОбъекта = метод()-> возврат новый Опросы.АвтоматическаяФормаОбъекта();
        ),
        новый ОсновнойИнтерфейс.ОписательКомандыВсеФункции(
            Группа = Группа, 
            Представление = "Анкеты опроса",
            СоздательФормыСписка = метод()-> возврат новый АнкетыОпроса.АвтоматическаяФормаСписка();,
            СоздательФормыОбъекта = метод()-> возврат новый АнкетыОпроса.АвтоматическаяФормаОбъекта();
        ),
        новый ОсновнойИнтерфейс.ОписательКомандыВсеФункции(
            Группа = Группа, 
            Представление = "Используемая функциональность (Анкетирование)",
            СоздательФормыСписка = метод()-> возврат новый ИспользуемаяФункциональность.АвтоматическаяФормаСписка();,
            СоздательФормыОбъекта = метод()-> возврат новый ИспользуемаяФункциональность.АвтоматическаяФормаОбъекта();
        )
    ]
;

@ВПроекте
@Реализация
метод ДополнительныеКоманды(): ЧитаемыйМассив<Команда>
    возврат []
;

@ВПроекте
@Реализация
метод КомандыПанелиДействий(): ЧитаемыйМассив<Команда>
    возврат []
;

@ВПроекте
@Реализация
метод РазделыПриложения(): ЧитаемаяКоллекция<ОсновнойИнтерфейс.ОписаниеРаздела>
    если ИспользованиеПодсистемы() и ЕстьПолномочиеПросмотрОпросов()
        возврат [ОписаниеРаздела()]
    иначе
        возврат []
    ;
;

@ВПроекте
@Реализация
метод Виджеты(): ЧитаемаяКоллекция<ОсновнойИнтерфейс.ОписаниеВиджета>
    
    знч Виджеты: Массив<ОсновнойИнтерфейс.ОписаниеВиджета>
    если ИспользованиеПодсистемы()
        
        если Анкетирование.ЕстьНезаполненныеАнкеты()
            Виджеты.Добавить(ОписаниеВиджетаАнкет())   
        ;      
        
        если ЕстьПолномочиеПросмотрОпросов()
            Виджеты.Добавить(ОписаниеВиджетаОпросов())
        ;
    ;
    
    возврат Виджеты
;

@ВПроекте
@Реализация
метод ДополнительныеКомандыПанелиГлавное(): ЧитаемыйМассив<Команда>
    возврат []
;

// Конец ОсновнойИнтерфейсПоставщик

// Контракт НастраиваемаяПодсистема

@ВПроекте
@Реализация
метод ПанелиНастроек(): Массив<СтандартноеПриложение.ОписаниеДополнительнойСтраницы>
    знч Настройки: Массив<СтандартноеПриложение.ОписаниеДополнительнойСтраницы>
    если ЕстьДоступныеПанелиНастроек()
        Настройки.Добавить(новый СтандартноеПриложение.ОписаниеДополнительнойСтраницы(
                Анкетирование.ИД_РАЗДЕЛА_АНКЕТИРОВАНИЕ,
                Анкетирование.ИД_НАСТРОЙКИ_АНКЕТИРОВАНИЯ,
                Анкетирование.ИМЯ_РАЗДЕЛА_АНКЕТИРОВАНИЕ,
                новый ФормаСписокШаблоновАнкет()))
    ;
    возврат Настройки
;

@ВПроекте
@Реализация
метод ЕстьДоступныеПанелиНастроек(): Булево
    возврат ИспользованиеПодсистемы() и НастройкаАнкетированияДоступна()
;

// Конец НастраиваемаяПодсистема

метод НастройкаАнкетированияДоступна(): Булево
    знч ЕстьПолномочие = ЕстьПолномочиеРедактированиеШаблоновАнкет()
    возврат ЕстьПолномочие или СтандартноеПриложение.ПараметрыСеанса().ТекущийПользовательИмеетПолныеПрава
;


метод ОтображатьВиджетОпросы(): Булево
    возврат ЕстьПолномочиеПросмотрОпросов() или СтандартноеПриложение.ПараметрыСеанса().ТекущийПользовательИмеетПолныеПрава 
;

@НаСервере @ДоступноСКлиента
статический метод ЕстьПолномочиеПросмотрОпросов(): Булево
    возврат КонтрольДоступа.ЕстьПравоНаПолномочие(Анкетирование.ПолномочиеПросмотрОпросов())
;

@НаСервере @ДоступноСКлиента
статический метод ЕстьПолномочиеПросмотрШаблоновАнкет(): Булево
    возврат КонтрольДоступа.ЕстьПравоНаПолномочие(Анкетирование.ПолномочиеПросмотрШаблоновАнкет())
;

@НаСервере @ДоступноСКлиента
статический метод ЕстьПолномочиеРедактированиеШаблоновАнкет(): Булево
    возврат КонтрольДоступа.ЕстьПравоНаПолномочие(Анкетирование.ПолномочиеРедактированиеШаблоновАнкет())
;

метод ИспользованиеПодсистемы(): Булево
    возврат Анкетирование.ПараметрыСеанса().ФункциональныеОпции.ИспользованиеПодсистемы
;

метод ОписаниеВиджетаОпросов(): ОсновнойИнтерфейс.ОписаниеВиджета

    возврат новый ОсновнойИнтерфейс.ОписаниеВиджета(
        Ид = Анкетирование.ИД_РАЗДЕЛА_АНКЕТИРОВАНИЕ,
        ИдРаздела = Анкетирование.ИД_РАЗДЕЛА_АНКЕТИРОВАНИЕ,
        Имя = "Опросы",
        Компонент = новый ОпросыВиджетСодержимое(),
        Команда = Анкетирование.СформироватьКомандыСоздания(),
        Пиктограмма = Ресурс{ПиктограммаРаздела.svg}.Ссылка,
        ЦветПиктограммы = БиблиотекаЦветов.ВидыЦвета.Красный)
;

метод ОписаниеВиджетаАнкет(): ОсновнойИнтерфейс.ОписаниеВиджета
    возврат новый ОсновнойИнтерфейс.ОписаниеВиджета(
        Ид = Анкетирование.ИД_РАЗДЕЛА_АНКЕТИРОВАНИЕ,
        ИдРаздела = Анкетирование.ИД_РАЗДЕЛА_АНКЕТИРОВАНИЕ,
        Имя = "Мои анкеты",
        Компонент = новый АнкетыВиджетСодержимое(),
        Команда = Неопределено,
        Пиктограмма = Ресурс{Анкета.svg}.Ссылка,
        ЦветПиктограммы = БиблиотекаЦветов.ВидыЦвета.Зеленый,
        НавигационнаяКоманда = АнкетыНавигация
    )
;

метод ОписаниеРаздела(): ОсновнойИнтерфейс.ОписаниеРаздела
    возврат новый ОсновнойИнтерфейс.ОписаниеРаздела(
        Ид = Анкетирование.ИД_РАЗДЕЛА_АНКЕТИРОВАНИЕ,
        Имя = "Опросы, анкеты",
        РазмещениеРаздела = РазмещениеРаздела.ОсновнаяНавигация,
        Пиктограмма = Ресурс{ПиктограммаРаздела.svg}.Ссылка,
        ЦветПиктограммы = Неопределено,
        НавигационнаяКомандаРаздела = АнкетированиеНавигация,
        ШиринаДляПанелиНавигации = 160)
;