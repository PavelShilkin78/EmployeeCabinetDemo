импорт ВебApi

@ВПодсистеме
структура ДанныеИзмененияОбъектовИнформационнойСистемы
    обз знч ИзмененияОбъектовИнформационнойСистемы: ЧитаемоеСоответствие<ИсходящиеДанные.Ссылка, ОбменДанными.ПараметрыИзменения>
    обз знч ВерсияДанных: Число
;

/**
Описание: Возвращает ссылку на версию данных объекта

Параметры:
    СсылкаНаОбъект   - Любая ссылка на регистрируемые данные
Возвращает:
    Ссылку на элемент справочника ВерсииДанных
*/
@ВПодсистеме
метод ВерсияДанныхОбъекта(СсылкаНаОбъект: неизвестно): ВерсииДанныхОбъектов.Ссылка?
    
    знч Запрос = Запрос{
        Выбрать
            Ссылка
         Из
            ВерсииДанныхОбъектов
         Где
            Объект == %СсылкаНаОбъект}
    исп Результат = Запрос.Выполнить() 
    для РезультатСтрока из Результат
        возврат РезультатСтрока.Ссылка
    ;
    
    
    возврат Неопределено
    
;

/**
Описание: Возвращает массив данных ссылок на объекты с версиями данных, большими
    переданной в параметре НачинаяСВерсии.
Параметры: ИнформационнаяСистема   - ИнформационныеСистемы.Ссылка
    ПараметрыПолучения      - см. структуру ПараметрыПолученияВерсийДанныхОбъектов
Возвращает:
    Массив соответсвий с ключами:
        *Объект         - ссылка на элемент справочника, подлежащего экспорту
        *ВерсияДанных   - Число, версия данных, соответствующая объекту
*/
@ВПодсистеме
метод ИзмененияОбъектовИнформационнойСистемы(
        ИнформационнаяСистема: ИнформационныеСистемы.Ссылка, 
        ПараметрыПолучения: ОбменДанными.ПараметрыПолученияВерсийДанныхОбъектов,
        ТипыЗапрашиваемыхДанных: ВсеДанные.ОтоборПоОтправляемымТипамДокументов): ДанныеИзмененияОбъектовИнформационнойСистемы

    знч ОбъектыСистемы: Соответствие<ИсходящиеДанные.Ссылка, ОбменДанными.ПараметрыИзменения>
    знч Запрос = Запрос{
        Выбрать Первые %{ПараметрыПолучения.РазмерПорции}
            ВерсииДанныхОбъектов.Объект,
            ВерсииДанныхОбъектов.ВерсияДанных,
            ВерсииДанныхОбъектов.ХарактерИзменения,
            ОтправляемыеДокументы.Владелец
         Из
            ВерсииДанныхОбъектов Как ВерсииДанныхОбъектов
            Левое Соединение ИнформационныеСистемыРазделителейДанных Как ИнформационныеСистемыРазделителейДанных
            По ВерсииДанныхОбъектов.РазделительВыгружаемыхДанных == ИнформационныеСистемыРазделителейДанных.РазделительВыгружаемыхДанных
            И ИнформационныеСистемыРазделителейДанных.ИнформационнаяСистема == %ИнформационнаяСистема
            Левое Соединение ОтправляемыеДокументы Как ОтправляемыеДокументы
            По ВерсииДанныхОбъектов.Объект == ОтправляемыеДокументы.Владелец
        Где
            ВерсииДанныхОбъектов.ТипОбъекта В (%{ТипыЗапрашиваемыхДанных.ОтправляемыеТипыДокументов}) И
            ВерсииДанныхОбъектов.ВерсияДанных >= %{ПараметрыПолучения.НачинаяСВерсии} И
            (Не (ИнформационныеСистемыРазделителейДанных.РазделительВыгружаемыхДанных Есть Null)
                или ВерсииДанныхОбъектов.РазделительВыгружаемыхДанных == Неопределено или %{ТипыЗапрашиваемыхДанных.ФильтрТолькоПоТипам}) И
            (Не (ОтправляемыеДокументы.Владелец Есть Null) 
            или ВерсииДанныхОбъектов.ХарактерИзменения == ХарактерыИзменений.Удаление)
        Упорядочить По
            ВерсииДанныхОбъектов.ВерсияДанных}
    
    пер Результат = новый Массив(Запрос.Выполнить()) 
    для РезультатСтрока из Результат
        ОбъектыСистемы.Вставить(РезультатСтрока.Объект, 
            новый ОбменДанными.ПараметрыИзменения(
                РезультатСтрока.ВерсияДанных,
                РезультатСтрока.ХарактерИзменения))
    ;
    если не Результат.Пусто()
        ПараметрыПолучения.НачинаяСВерсии = Результат.Последний().ВерсияДанных + 1
    ;
    возврат новый ДанныеИзмененияОбъектовИнформационнойСистемы(ОбъектыСистемы, ПараметрыПолучения.НачинаяСВерсии)
;

@ВПодсистеме
метод ВерсииДанныхПоТипуОбъекта(ТипОбъекта: Тип): ЧитаемыйМассив<ВерсииДанныхОбъектов.Ссылка>
    знч ВерсииДанных: Массив<ВерсииДанныхОбъектов.Ссылка>
    знч Запрос = Запрос{
        Выбрать
            Ссылка
        Из
            ВерсииДанныхОбъектов
        Где
            ТипОбъекта == %ТипОбъекта
    }
    
    исп Результат = Запрос.Выполнить()
    для СтрокаРезультата из Результат
        ВерсииДанных.Добавить(СтрокаРезультата.Ссылка)
    ;
    
    возврат ВерсииДанных
;

@ВПодсистеме
метод РазделителиВерсийДанных(ВерсииДанных: ЧитаемыйМассив<ВерсииДанныхОбъектов.Ссылка>): ЧитаемоеСоответствие<ВерсииДанныхОбъектов.Ссылка,РазделительВыгружаемыхДанных.Ссылка?>
    знч РазделителиВерсийДанных: Соответствие<ВерсииДанныхОбъектов.Ссылка,РазделительВыгружаемыхДанных.Ссылка?>
    
    знч Запрос = Запрос{
        Выбрать
            Ссылка,
            РазделительВыгружаемыхДанных
        Из
            ВерсииДанныхОбъектов
        Где
            Ссылка в (%ВерсииДанных)
    }
    
    исп Результат = Запрос.Выполнить()
    для СтрокаРезультата из Результат
        РазделителиВерсийДанных.Вставить(СтрокаРезультата.Ссылка, СтрокаРезультата.РазделительВыгружаемыхДанных)
    ;
    возврат РазделителиВерсийДанных
;

@ВПодсистеме
метод ОбъектыВерсийДанных(ВерсииДанных: ЧитаемыйМассив<ВерсииДанныхОбъектов.Ссылка>): ЧитаемоеСоответствие<ВерсииДанныхОбъектов.Ссылка, ИсходящиеДанные.Ссылка?>
    знч ОбъектыВерсийДанных: Соответствие<ВерсииДанныхОбъектов.Ссылка, ИсходящиеДанные.Ссылка?>
    
    знч Запрос = Запрос{
        Выбрать
            Ссылка,
            Объект
        Из
            ВерсииДанныхОбъектов
        Где
            Ссылка в (%ВерсииДанных)
    }
    
    исп Результат = Запрос.Выполнить()
    для СтрокаРезультата из Результат
        ОбъектыВерсийДанных.Вставить(СтрокаРезультата.Ссылка, СтрокаРезультата.Объект)
    ;
    возврат ОбъектыВерсийДанных
;