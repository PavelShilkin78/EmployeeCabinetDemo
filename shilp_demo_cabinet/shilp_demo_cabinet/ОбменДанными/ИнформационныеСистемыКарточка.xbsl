@НаСервере @НаКлиенте
структура ДанныеИнформационнойСистемы
    обз знч Ссылка: ИнформационныеСистемы.Ссылка?
    обз знч Наименование: Строка
    обз знч Пользователь: Пользователи.Ссылка?
    обз знч ОповещатьПриОтсутствииОбмена: Булево
    обз знч ДопустимоеВремяОтсутствияОбмена: Число
;

@Обработчик
метод ПослеЧтения()
    если Объект.Пользователь != Неопределено
        Активность = Истина
    ;
;

метод СтатусЗаголовок(): Строка
    пер СтатусЗаголовок: Строка
    если Активность
        СтатусЗаголовок = "Информационная система активна. Обмен данными выполняется."
    иначе
        СтатусЗаголовок = "Информационная система не активна."
    ;
    возврат СтатусЗаголовок
;

метод СохранитьПриНажатии(Источник: Кнопка, Событие: СобытиеПриНажатии)
    если не Объект.ОповещатьПриОтсутствииОбмена
        Объект.ДопустимоеВремяОтсутствияОбмена = 0
    ;
    ЗаписатьИнформационнуюСистему(новый ДанныеИнформационнойСистемы(
        Ссылка = Объект.Ссылка,
        Наименование = Объект.Наименование,
        Пользователь = Объект.Пользователь,
        ОповещатьПриОтсутствииОбмена = Объект.ОповещатьПриОтсутствииОбмена,
        ДопустимоеВремяОтсутствияОбмена = Объект.ДопустимоеВремяОтсутствияОбмена
    ))
    СобственнаяМодифицированность = Ложь
    Закрыть()
;

@НаСервере @ДоступноСКлиента
статический метод ЗаписатьИнформационнуюСистему(ДанныеИнформационнойСистемы: ДанныеИнформационнойСистемы)
    знч ИнформационнаяСистема = ДанныеИнформационнойСистемы.Ссылка?.ЗагрузитьОбъект() ?? новый ИнформационныеСистемы.Объект()
    ИнформационнаяСистема.Наименование = ДанныеИнформационнойСистемы.Наименование
    ИнформационнаяСистема.Пользователь = ДанныеИнформационнойСистемы.Пользователь
    ИнформационнаяСистема.ОповещатьПриОтсутствииОбмена = ДанныеИнформационнойСистемы.ОповещатьПриОтсутствииОбмена
    ИнформационнаяСистема.ДопустимоеВремяОтсутствияОбмена = ДанныеИнформационнойСистемы.ДопустимоеВремяОтсутствияОбмена
    ИнформационнаяСистема.Записать()
;