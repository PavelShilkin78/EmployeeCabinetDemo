импорт ВебApi
импорт ОбщиеСтандартные

@ВПроекте
метод ТребуетсяРегистрацияИзмененийОбъекта(Объект: неизвестно, ПрежниеДанные: неизвестно): Булево
    возврат Объект.ЭтоНовый() или ОбъектИзменен(Объект, ПрежниеДанные)
;

@ВПроекте
метод ЗарегистрироватьИзменение(
        СсылкаНаОбъект: ИсходящиеДанные.Ссылка,
        РазделительВыгружаемыхДанных: РазделительВыгружаемыхДанных.Ссылка?,
        ХарактерИзменений: ХарактерыИзменений = ХарактерыИзменений.Изменение)
    
    если СоответствиеИдентификаторовОбъектов.ВнешниеИдентификаторыПоВнутреннимСсылкам([СсылкаНаОбъект как ВходящиеДанные.Ссылка]).Пусто() и 
            ХарактерИзменений == ХарактерыИзменений.Удаление
        возврат
    ;
    исп Транзакции.Начать() 
    пер ВерсияДанных = ВерсииДанныхОбмена.НоваяВерсия()
    пер ВерсияДанныхОбъекта = ВерсииДанныхОбъектов.ВерсияДанныхОбъекта(СсылкаНаОбъект)
    пер ОбъектВерсииДанныхОбъектов: ВерсииДанныхОбъектов.Объект?
    если ВерсияДанныхОбъекта == Неопределено
        ОбъектВерсииДанныхОбъектов = новый ВерсииДанныхОбъектов.Объект()
        ОбъектВерсииДанныхОбъектов.Объект = СсылкаНаОбъект
        ОбъектВерсииДанныхОбъектов.ТипОбъекта = СсылкаНаОбъект.ПолучитьТип()
    иначе
        ОбъектВерсииДанныхОбъектов = ВерсияДанныхОбъекта.ЗагрузитьОбъект(Истина)
    ;
    ОбъектВерсииДанныхОбъектов.РазделительВыгружаемыхДанных = РазделительВыгружаемыхДанных
    ОбъектВерсииДанныхОбъектов.ХарактерИзменения = ХарактерИзменений
    ОбъектВерсииДанныхОбъектов.ВерсияДанных = ВерсияДанных
    ОбъектВерсииДанныхОбъектов.Записать()
;

/**
Описание: Возвращает признак модифицированности данных объекта, сравнивая коллекцию данных объекта
    с коллекцией предыдущих данными, полученной в обработчиках ПослеЗаписи. 
Параметры: Объект          - Сохраняемый объект
    ПрежниеДанные   - Прежние данные объекта
Возвращает:
    Булево
*/
метод ОбъектИзменен(Объект: неизвестно, ПрежниеДанные: неизвестно): Булево
    пер МодифицированностьОбъекта: Булево
    пер ИменаРеквизитовОбъекта = МенеджерСущности.РеквизитыСущности(Объект.Ссылка.ПолучитьТип())
    для ИмяРеквизитаОбъекта из ИменаРеквизитовОбъекта
        если Объект[ИмяРеквизитаОбъекта] != ПрежниеДанные[ИмяРеквизитаОбъекта]
            МодифицированностьОбъекта = Истина
            прервать
        ;        
    ;
    возврат МодифицированностьОбъекта
;

@ВПроекте
метод ЗарегистрироватьУдаление(СсылкаНаОбъект: ИсходящиеДанные.Ссылка)
    ЗарегистрироватьИзменение(СсылкаНаОбъект, Неопределено, ХарактерыИзменений.Удаление)
;
