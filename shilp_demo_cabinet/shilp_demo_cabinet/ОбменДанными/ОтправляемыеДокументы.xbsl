импорт ВебApi

//Область РазрешенияДоступа

@Обработчик
метод ВычислитьРазрешенияДоступа(): Массив<РазрешениеДоступа>
    возврат ОбменДаннымиПоставщикОграничений.ПолучитьСервис().ОтправляемыеДокументыРазрешенияДоступа()
;

//КонецОбласти

//Область СлужебныйПрограммныйИнтерфейс

@ВПодсистеме
метод ЗарегистрироватьОтправляемыйДокумент(Документ: ИсходящиеДанные.Ссылка, Отправляемый: Булево)
    знч Запись = ЗаписьОтправляемогоДокумента(Документ)
    если ЗаписьОтправляемогоСуществует(Запись)
        возврат
    ;
    если Отправляемый
        исп ПривилегированныйРежим = КонтекстДоступа.Привилегированный()
        знч ОтправляемыйДокумент = новый ОтправляемыеДокументы.Объект()
        ОтправляемыйДокумент.Владелец = Документ
        ОтправляемыйДокумент.Записать()
        ПослеЗаписиДополнение(Документ)
        ПривилегированныйРежим.Закрыть()
    иначе
        СнятьРегистрациюОтправляемогоДокумента(Документ, Запись)
    ;
;

@ВПодсистеме
метод ЭтоОтправляемыйДокумент(Документ: неизвестно): Булево
    возврат ЗаписьОтправляемогоСуществует(ЗаписьОтправляемогоДокумента(Документ))
;

метод ЗаписьОтправляемогоСуществует(ЗаписьОтправляемого: ОтправляемыеДокументы.Ссылка? = Неопределено): Булево
    возврат ЗаписьОтправляемого != Неопределено
;

метод ЗаписьОтправляемогоДокумента(Документ: ИсходящиеДанные.Ссылка): ОтправляемыеДокументы.Ссылка?
    знч Запрос = Запрос{
        Выбрать Первые 1
            Ссылка
         Из
            ОтправляемыеДокументы
         Где
            Владелец == %Документ}
    исп Результат = Запрос.Выполнить() 
    знч РезультатСтроки = новый ЧитаемыйМассив(Результат)
    если (не РезультатСтроки.Пусто())
        возврат РезультатСтроки[0].Ссылка
    ;
    возврат Неопределено
;

@ВПодсистеме
метод СнятьРегистрациюОтправляемогоДокумента(Документ: неизвестно, ЗаписьОтправляемого: ОтправляемыеДокументы.Ссылка? = Неопределено) 
    
    если ЗаписьОтправляемого == Неопределено
        ЗаписьОтправляемого = ЗаписьОтправляемогоДокумента(Документ)
    ;

    если ЗаписьОтправляемогоСуществует(ЗаписьОтправляемого)
        исп ПривилегированныйРежим = КонтекстДоступа.Привилегированный()
        ЗаписьОтправляемого!.ЗагрузитьОбъект()!.Удалить()
        ПривилегированныйРежим.Закрыть()
    ;
    
;

@ВПодсистеме
метод ПослеЗаписиДополнение(Документ: ИсходящиеДанные.Ссылка?)
    СоответствиеИдентификаторовОбъектов.ОбновитьИдентификаторыЛокальныхОбъектов(Документ)
;

//КонецОбласти