импорт ОбщиеСтандартные

@Обработчик
метод ПослеСоздания()
    знч Сервисы = ОтпускДополнительныеЭлементыПоставщик.ПолучитьСервисы()
    для Сервис из Сервисы
        для Элемент из Сервис.СодержимоеИнформацияОбОтпускеКомпонент()
            Содержимое.Добавить(Элемент)    
        ;    
    ;
    НесколькоВидовОтпусков = Отпуск.ИспользуетсяНесколькоВидовОтпусков()
;

метод ЗаявленияНаОтпускДатаНачала(): Дата
    возврат Дата.Сейчас()
;

метод ОстатокНулевойВидимость(): Булево
    возврат не ОстатокИТекстВидимость() 
;

метод ОстатокНулевойЗаголовок(): Строка
    
    если не (Отпуск.ПараметрыСеанса().ФункциональныеОпции.ИнформацияОбОтпусках и Отпуск.ПараметрыСеанса().ФункциональныеОпции.ИнформацияОбОстаткахОтпусков)
        возврат "Работодатель не информирует об остатках отпусков"    
    ;
    
    пер ИнформацияОбОтпуске: Отпуск.ПараметрыСеанса = Отпуск.ПараметрыСеанса()
    если не ИнформацияОбОтпуске.ПараметрыИнициализированы
        возврат "Пока не ясно сколько дней отпуска можно взять. Информация появится позже."
    ;

    пер ТекстНадписи: Строка
    пер ОтстатокОтпуска: Число = ИнформацияОбОтпуске.ОстатокОтпуска
    ТекстНадписи = "На сегодня, " + МенеджерДат.ФорматДаты(Дата.Сейчас(), "дд ММММ")
    если ИнформацияОбОтпуске.ДнейПредыдущегоОтпуска == 0
        ТекстНадписи += " ни одного дня отпуска еще не заработано."
    иначе если ОтстатокОтпуска == 0
        ТекстНадписи += " все дни отпуска использованы."
    иначе если ОтстатокОтпуска < 0
        ТекстНадписи += " вы перерасходовали свой отпуск на %{(-ОтстатокОтпуска).ВСтроку()} %{МенеджерСтрок.ТекстПоКоличеству("день", -ОтстатокОтпуска)}."
    ;
    возврат ТекстНадписи
;

метод ОстатокИТекстВидимость(): Булево
    возврат Отпуск.ПараметрыСеанса().ФункциональныеОпции.ИнформацияОбОтпусках 
        и Отпуск.ПараметрыСеанса().ФункциональныеОпции.ИнформацияОбОстаткахОтпусков 
        и (Отпуск.ПараметрыСеанса().ОстатокОтпуска ?? 0) > 0
;

метод ОстатокИТекстКоличество(): Число
    возврат Отпуск.ПараметрыСеанса().ОстатокОтпуска
;

метод ОстатокИТекстПояснение(): Строка
    если не Отпуск.ПараметрыСеанса().ПараметрыИнициализированы
        возврат ""
    ;
    пер Сегодня: Строка = МенеджерДат.ФорматДаты(Дата.Сейчас(), "д ММММ")
    пер ОстатокОтпуска: Число = Отпуск.ПараметрыСеанса().ОстатокОтпуска
    пер КоличествоДней: Строка = (ОстатокОтпуска).ВСтроку() + " " + МенеджерСтрок.ТекстПоКоличеству("день", ОстатокОтпуска)
    пер Пояснение: Строка = "На сегодня, %Сегодня, доступно %КоличествоДней отпуска."
    пер РасшифровкаОстатка = РасшифровкаОстатка()
    если НесколькоВидовОтпусков и не РасшифровкаОстатка.Пусто()
        Пояснение = "На сегодня, %Сегодня, доступно %КоличествоДней отпуска (%РасшифровкаОстатка)."
    ;
    если КомпенсацияОтпускаВидимость()
        Пояснение += " " + КомпенсацияОтпускаЗаголовок()
    ;
    возврат Пояснение
;

метод РасшифровкаОстатка(): Строка
    пер РасшифровкаОстатка: Строка
    если не ВыводитьРасшифровку
        возврат РасшифровкаОстатка
    ;
    знч ОстаткиОтпусковСотрудников = Отпуск.ПараметрыСеанса().ОстаткиОтпусковСотрудников
    для ДанныеСотрудника из ОстаткиОтпусковСотрудников
        если ДанныеСотрудника.ОсновноеМестоРаботы
            для ДанныеВидаОтпуска из ДанныеСотрудника.ВидыОтпусков
                РасшифровкаОстатка += (РасшифровкаОстатка.Пусто() ? "" : ", ") + ДанныеВидаОтпуска.ВидОтпуска.Представление() + " %{ДанныеВидаОтпуска.ОстатокОтпуска}"
            ;
        ;
    ;
    возврат РасшифровкаОстатка
;

метод КомпенсацияОтпускаВидимость(): Булево
    если не Отпуск.ПараметрыСеанса().ФункциональныеОпции.ЗаявленияНаКомпенсациюОтпуска
        возврат Ложь
    ;
    возврат Клиент.КомпенсацияОтпускаДоступна()
;

метод КомпенсацияОтпускаЗаголовок(): Строка
    пер ИнформацияОбОтпуске: Отпуск.ПараметрыСеанса = Отпуск.ПараметрыСеанса()
    если не ИнформацияОбОтпуске.ПараметрыИнициализированы
        возврат ""
    ;
    пер Компенсация: Строка
    если ИнформацияОбОтпуске.КоличествоДнейКомпенсации < ИнформацияОбОтпуске.ОстатокОтпуска
        Компенсация = "Из них %{ИнформацияОбОтпуске.КоличествоДнейКомпенсации} %{МенеджерСтрок.ТекстПоКоличеству("день", ИнформацияОбОтпуске.КоличествоДнейКомпенсации)} можно заменить компенсацией."
    иначе
        Компенсация = "Их можно заменить компенсацией."
    ;
    возврат Компенсация
;