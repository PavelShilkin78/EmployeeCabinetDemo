импорт ВебApi
импорт КонтрольДоступа

//Область РазрешенияДоступа

@Обработчик
метод ВычислитьРазрешенияДоступа(): Массив<РазрешениеДоступа>
    возврат КонтрольДоступа.РазрешенияДоступаТаблицыСервисныхДанныхАбонентаЧтениеВсем()
;

//КонецОбласти

//Область СлужебныйПрограммныйИнтерфейс

@ВПроекте
структура НастройкиОтпусков
    @JsonСвойство(Имя = "vacationStartDateControl")
    обз знч КонтролироватьКоличествоДнейДоНачалаОтпуска: Булево?
    @JsonСвойство(Имя = "daysBeforeVacationStart")
    обз знч ДопустимоеКоличествоДнейДоНачалаОтпуска: Число?
    @JsonСвойство(Имя = "fourteenDaysVacationControl")
    обз знч КонтролироватьНаличиеОтпускаНеМенее14Дней: Булево?
    @JsonСвойство(Имя = "advanceVacationControl")
    обз знч ОграничиватьКоличествоДнейОтпускаАвансом: Булево?
    @JsonСвойство(Имя = "advanceVacationDays")
    обз знч МаксимальноеКоличествоДнейОтпускаАвансом: Число?
    @JsonСвойство(Имя = "workDaysControl")
    обз знч ПроверятьНаличиеРабочихДнейВОтпуске: Булево?
   
;

@ВПодсистеме
метод ТипДанныхJson(ВерсияФорматаОбмена: Версия?): Тип
    если ВерсияФорматаОбмена == Неопределено
        возврат Тип<НастройкиОтпусков>
    иначе если ВерсияФорматаОбмена >= Версия{1.0}
        возврат Тип<НастройкиОтпусков>
    ;
;

@ВПодсистеме
метод ТипКоллекцииДанныхJson(ВерсияФорматаОбмена: Версия?): Тип
    если ВерсияФорматаОбмена == Неопределено
        возврат Тип<Массив<НастройкиОтпусков>>
    иначе если ВерсияФорматаОбмена >= Версия{1.0}
        возврат Тип<Массив<НастройкиОтпусков>>
    ;
;

@ВПодсистеме
метод ТипОбъектаJson(): Строка
    возврат "vacationsSettings"
;

@ВПодсистеме
метод ЗагрузитьОбъектИзJson(НастройкиОтпусков: НастройкиОтпусков, Сервисы: ВебApiСервер.СервисыЗагрузкиJson)
    исп Транзакции.Начать()
    знч Запрос = Запрос{
        Выбрать 
            Ссылка
        Из 
            НастройкиОтпусков}
        
    исп РезультатЗапроса = Запрос.Выполнить() 
    пер ОбъектНастройки: НастройкиОтпусков.Объект?
    знч РезультатЗапросаСтроки = новый ЧитаемыйМассив(РезультатЗапроса)
    если (не РезультатЗапросаСтроки.Пусто())
        ОбъектНастройки = РезультатЗапросаСтроки[0].Ссылка.ЗагрузитьОбъект(Истина)
    иначе
        ОбъектНастройки = новый НастройкиОтпусков.Объект()
    ;
    
    ОбъектНастройки.КонтролироватьКоличествоДнейДоНачалаОтпуска = НастройкиОтпусков.КонтролироватьКоличествоДнейДоНачалаОтпуска ?? Ложь
    ОбъектНастройки.ДопустимоеКоличествоДнейДоНачалаОтпуска = НастройкиОтпусков.ДопустимоеКоличествоДнейДоНачалаОтпуска ?? 0
    ОбъектНастройки.КонтролироватьНаличиеОтпускаНеМенее14Дней = НастройкиОтпусков.КонтролироватьНаличиеОтпускаНеМенее14Дней ?? Ложь
    ОбъектНастройки.ОграничиватьКоличествоДнейОтпускаАвансом = НастройкиОтпусков.ОграничиватьКоличествоДнейОтпускаАвансом ?? Ложь
    ОбъектНастройки.МаксимальноеКоличествоДнейОтпускаАвансом = НастройкиОтпусков.МаксимальноеКоличествоДнейОтпускаАвансом ?? 0
    ОбъектНастройки.ПроверятьНаличиеРабочихДнейВОтпуске = НастройкиОтпусков.ПроверятьНаличиеРабочихДнейВОтпуске ?? Ложь
    
    ОбъектНастройки.Записать()
;

//КонецОбласти
