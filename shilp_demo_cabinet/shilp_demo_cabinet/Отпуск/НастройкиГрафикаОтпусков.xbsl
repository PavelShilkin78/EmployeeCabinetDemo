импорт КонтрольДоступа

//Область РазрешенияДоступа

@Обработчик
метод ВычислитьРазрешенияДоступа(): Массив<РазрешениеДоступа>
    возврат КонтрольДоступа.РазрешенияДоступаТаблицыСервисныхДанныхАбонентаЧтениеВсем()
;

//КонецОбласти

//Область СлужебныеПроцедурыИФункции

@ВПодсистеме
метод НастройкиГрафикаОтпусков(): Отпуск.НапоминанияОГрафикеОтпусков
    
    пер Настройки = новый Отпуск.НапоминанияОГрафикеОтпусков(Ложь, 0, Ложь, 0)
    
    знч Запрос = Запрос{
        Выбрать Первые 1
            НапоминатьОГрафикеОтпусков,
            НапоминаниеОГрафикеОтпусковЧислоДней,
            НапоминатьОГрафикеОтпусковПодчиненных,
            НапоминаниеОГрафикеОтпусковПодчиненныхЧислоДней
        Из 
            НастройкиГрафикаОтпусков}

    исп РезультатЗапроса = Запрос.Выполнить() 
    знч РезультатЗапросаСтроки = новый ЧитаемыйМассив(РезультатЗапроса)
    если (не РезультатЗапросаСтроки.Пусто())
        Настройки.НапоминатьОГрафикеОтпусков = РезультатЗапросаСтроки[0].НапоминатьОГрафикеОтпусков
        Настройки.НапоминаниеОГрафикеОтпусковЧислоДней = РезультатЗапросаСтроки[0].НапоминаниеОГрафикеОтпусковЧислоДней
        Настройки.НапоминатьОГрафикеОтпусковПодчиненных = РезультатЗапросаСтроки[0].НапоминатьОГрафикеОтпусковПодчиненных
        Настройки.НапоминаниеОГрафикеОтпусковПодчиненныхЧислоДней = РезультатЗапросаСтроки[0].НапоминаниеОГрафикеОтпусковПодчиненныхЧислоДней
    ;
    
    возврат Настройки
    
;

@ВПодсистеме
метод ЗаписатьНастройкиГрафикаОтпусков(НастройкиНапоминаний: Отпуск.НапоминанияОГрафикеОтпусков)
    
    пер Настройки = новый НастройкиГрафикаОтпусков.Объект()
    
    знч Запрос = Запрос{
        Выбрать Первые 1
            Ссылка
        Из 
            НастройкиГрафикаОтпусков}

    исп РезультатЗапроса = Запрос.Выполнить() 
    знч РезультатЗапросаСтроки = новый ЧитаемыйМассив(РезультатЗапроса)
    если (не РезультатЗапросаСтроки.Пусто())
        Настройки = РезультатЗапросаСтроки[0].Ссылка.ЗагрузитьОбъект()
    ;
    
    Настройки.НапоминатьОГрафикеОтпусков = НастройкиНапоминаний.НапоминатьОГрафикеОтпусков
    Настройки.НапоминаниеОГрафикеОтпусковЧислоДней = НастройкиНапоминаний.НапоминаниеОГрафикеОтпусковЧислоДней
    Настройки.НапоминатьОГрафикеОтпусковПодчиненных = НастройкиНапоминаний.НапоминатьОГрафикеОтпусковПодчиненных
    Настройки.НапоминаниеОГрафикеОтпусковПодчиненныхЧислоДней = НастройкиНапоминаний.НапоминаниеОГрафикеОтпусковПодчиненныхЧислоДней

    Настройки.Записать()
    
;

//КонецОбласти