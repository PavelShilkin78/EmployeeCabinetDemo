импорт ОбщиеСтандартные
импорт ФизическиеЛица
импорт ВебApi
импорт КонтрольДоступа
импорт ОбменДанными

//Область РазрешенияДоступа

@Обработчик
метод ВычислитьРазрешенияДоступа(): ЧитаемыйМассив<РазрешениеДоступа>
    возврат КонтрольДоступа.РазрешенияДоступаТаблицыСФизическимЛицомСПравомУдаления()
;

@Обработчик
метод ВычислитьРазрешенияДоступаДляОбъектов(Элементы: ЧитаемыйМассив<ЗаявлениеНаОтпускЗаместители.ДанныеРасчетаРазрешений>): ЧитаемоеСоответствие<ЗаявлениеНаОтпускЗаместители.ДанныеРасчетаРазрешений, ЧитаемаяКоллекция<РазрешениеДоступа>>
    знч Разрешения: Соответствие<ЗаявлениеНаОтпускЗаместители.ДанныеРасчетаРазрешений, ЧитаемаяКоллекция<РазрешениеДоступа>>
    для Элемент из Элементы
        Разрешения.Вставить(Элемент, КонтрольДоступа.РазрешенияДоступаОбъекта(Элемент.Владелец))
    ;
    возврат Разрешения
;

//КонецОбласти

//Область JSON

@ВПроекте
структура ЗаместительНаВремяОтпуска
    @JsonСвойство(Имя = "personID")
    пер ФизическеЛицо: Строка
    @JsonСвойство(Имя = "tasks")
    пер Задачи: Строка
;

@ВПодсистеме
метод ДанныеЗаместителей(ЗаявленияСсылки: ЧитаемаяКоллекция<ОснованиеОтпуска.Ссылка>):
        ЧитаемоеСоответствие<ОснованиеОтпуска.Ссылка, Массив<ЧитаемоеСоответствие<Строка, Объект?>>>

    знч Данные: Соответствие<ОснованиеОтпуска.Ссылка, Массив<ЧитаемоеСоответствие<Строка, Объект?>>>
    знч Запрос = Запрос{
        Выбрать
            Владелец,
            Заместитель,
            Комментарий
        Из
            ЗаявлениеНаОтпускЗаместители
        Где
            Владелец В (%ЗаявленияСсылки)}
    исп Результат = Запрос.Выполнить() 
    для РезультатСтрока из Результат
        если не Данные.СодержитКлюч(РезультатСтрока.Владелец)
            Данные.Вставить(РезультатСтрока.Владелец, новый Массив<ЧитаемоеСоответствие<Строка, Объект?>>())
        ;
        Данные[РезультатСтрока.Владелец].Добавить(МенеджерЗапросов.СтрокаРезультатаЗапросаВСоответствие(РезультатСтрока, Результат))
    ;        
    возврат Данные
;

@ВПодсистеме
метод ДанныеЗаместителейВJson(
        ДанныеЗаместителей: ЧитаемыйМассив<ЧитаемоеСоответствие<Строка, Объект?>>?,
        Сервисы: ВебApiСервер.СервисыУпаковкиJson): ЧитаемыйМассив<ЗаместительНаВремяОтпуска>?

    если ДанныеЗаместителей == Неопределено
        возврат Неопределено
    ;
    пер ДанныеВJson: Массив<ЗаместительНаВремяОтпуска>
    для ДанныеЗаместителя из ДанныеЗаместителей
        если ОбменДанными.СсылкаВнешнегоОбъекта(ДанныеЗаместителя["Заместитель"] как ВходящиеДанные.Ссылка)
            ДанныеВJson.Добавить(ДанныеЗаместителяВJson(ДанныеЗаместителя, Сервисы))
        ;
    ;
    если ДанныеВJson.Пусто()
        возврат Неопределено
    ;
    возврат ДанныеВJson
;

метод ДанныеЗаместителяВJson(
        ДанныеЗаместителя: ЧитаемоеСоответствие<Строка, Объект?>, 
        Сервисы: ВебApiСервер.СервисыУпаковкиJson): ЗаместительНаВремяОтпуска
        
    пер ДанныеВJson = новый ЗаместительНаВремяОтпуска()
    ДанныеВJson.ФизическеЛицо = Сервисы.ПолучитьИдентификатор(ДанныеЗаместителя["Заместитель"] как ФизическиеЛица.Ссылка)
    ДанныеВJson.Задачи = ДанныеЗаместителя["Комментарий"] как Строка
    возврат ДанныеВJson
;

//КонецОбласти