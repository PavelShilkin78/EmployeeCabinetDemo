импорт ИнтерфейсныеКомпонентыСтандартные
импорт Отпуск

@Обработчик
метод ПослеСоздания()

    знч Настройки = НастройкиНапоминанийОГрафикеОтпусков()
    НапоминатьОГрафикеОтпусков = Настройки.НапоминатьОГрафикеОтпусков
    НапоминаниеОГрафикеОтпусковЧислоДней = Настройки.НапоминаниеОГрафикеОтпусковЧислоДней
    НапоминатьОГрафикеОтпусковПодчиненных = Настройки.НапоминатьОГрафикеОтпусковПодчиненных
    НапоминаниеОГрафикеОтпусковПодчиненныхЧислоДней = Настройки.НапоминаниеОГрафикеОтпусковПодчиненныхЧислоДней
    
;

метод НапоминатьОГрафикеОтпусковПриИзмененииЗначения(Источник: ФлажокРасширенный, Событие: СобытиеПриИзменении<Булево?>)
    ЗаписатьНастройкиГрафикаОтпусков()
;

метод КоличествоДнейПередОтпускомПриИзменении(Источник: ПолеВвода<Число>, Событие: СобытиеПриИзменении<Число>)
    ЗаписатьНастройкиГрафикаОтпусков()
;

метод НапоминатьОГрафикеОтпусковПодчиненныхПриИзмененииЗначения(Источник: ФлажокРасширенный, Событие: СобытиеПриИзменении<Булево?>)
    ЗаписатьНастройкиГрафикаОтпусков()
;

метод КоличествоДнейПередОтпускомПодчиненныхПриИзменении(Источник: ПолеВвода<Число>, Событие: СобытиеПриИзменении<Число>)
    ЗаписатьНастройкиГрафикаОтпусков()
;

метод ЗаписатьНастройкиГрафикаОтпусков()

    ЗаписатьНастройкиГрафикаОтпусков(
        НапоминатьОГрафикеОтпусков, 
        НапоминаниеОГрафикеОтпусковЧислоДней,
        НапоминатьОГрафикеОтпусковПодчиненных, 
        НапоминаниеОГрафикеОтпусковПодчиненныхЧислоДней)

;

метод КоличествоДнейПередОтпускомТолькоЧтение(): Булево

    возврат не НапоминатьОГрафикеОтпусков

;

метод КоличествоДнейПередОтпускомПодчиненныхТолькоЧтение(): Булево

    возврат не НапоминатьОГрафикеОтпусковПодчиненных

;

@НаСервере @ДоступноСКлиента
статический метод НастройкиНапоминанийОГрафикеОтпусков(): Отпуск.НапоминанияОГрафикеОтпусков

    возврат НастройкиГрафикаОтпусков.НастройкиГрафикаОтпусков()

;

@НаСервере @ДоступноСКлиента
статический метод ЗаписатьНастройкиГрафикаОтпусков(НапоминатьОГрафикеОтпусков: Булево, НапоминаниеОГрафикеОтпусковЧислоДней: Число, НапоминатьОГрафикеОтпусковПодчиненных: Булево, НапоминаниеОГрафикеОтпусковПодчиненныхЧислоДней: Число)

    знч Настройки = новый Отпуск.НапоминанияОГрафикеОтпусков(НапоминатьОГрафикеОтпусков, НапоминаниеОГрафикеОтпусковЧислоДней, НапоминатьОГрафикеОтпусковПодчиненных, НапоминаниеОГрафикеОтпусковПодчиненныхЧислоДней)
    НастройкиГрафикаОтпусков.ЗаписатьНастройкиГрафикаОтпусков(Настройки)

    если Настройки.НапоминатьОГрафикеОтпусков или Настройки.НапоминатьОГрафикеОтпусковПодчиненных
        ГрафикОтпусков.ЗапланироватьНапоминанияОГрафикеОтпусков()
    ;

;
