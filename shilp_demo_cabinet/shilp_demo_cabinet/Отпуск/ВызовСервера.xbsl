импорт Компания
импорт КонтрольДоступа
импорт УниверсальныеМеханизмы
импорт ФизическиеЛица
импорт Уведомления
импорт ОбщиеСтандартные
импорт Компания::Согласования
импорт СтандартноеПриложение

@ВПодсистеме
@НаСервере @ДоступноСКлиента
метод ОстатокОтпускаНаДату(ФизическоеЛицо: ФизическиеЛица.Ссылка, ДатаНачала: Дата): Число?
    пер ОстатокОтпуска: Число? = Отпуск.ОстатокОтпускаФизическогоЛицаНаДату(ФизическоеЛицо, ДатаНачала)
    если ОстатокОтпуска != Неопределено
        возврат ОстатокОтпуска
    ;
    возврат ИнформацияОбОтпуске.ОстатокОтпускаФизическогоЛица(ФизическоеЛицо)
;

@ВПодсистеме
@НаСервере @ДоступноСКлиента
метод ЕстьРабочиеДниВПериоде(ФизическоеЛицо: ФизическиеЛица.Ссылка?, ДатаНачала: Дата?, ДатаОкончания: Дата?): Булево
    пер ГрафикРаботы: ГрафикиРаботы.Ссылка? = ОрганизационнаяСтруктура.ГрафикРаботыФизическогоЛица(ФизическоеЛицо!)
    возврат ГрафикиРаботы.ЕстьРабочиеДниВПериоде(ГрафикРаботы, ДатаНачала, ДатаОкончания)
;

@ВПодсистеме
@НаСервере @ДоступноСКлиента
метод СогласованиеДокумента(Заявление: ОснованиеОтпуска.Ссылка, Действие: PushУведомления.ДействияУведомлений, Текст: Строка): Булево
    пер ДействиеВыполнено: Булево
    пер Согласующий: ФизическиеЛица.Ссылка = ПользователиСистемы.ТекущееФизическоеЛицо()
    попытка
        знч ФизическоеЛицо = МенеджерСущности.ЗначениеРеквизитаОбъекта(Заявление, "ФизическоеЛицо")
        знч ДанныеСогласующего = Согласования.ДанныеСогласующего(
            Согласующий, Отпуск.ВидСогласуемогоЗаявленияОтпуск(), ФизическоеЛицо)
        
        если ДанныеСогласующего == Неопределено
            Журналирование.ЗарегистрироватьОшибку("ОшибкаМаршрутаСогласования", Важность = ВажностьСобытияЖурналаСобытий.Высокая, 
                Описание = "Согласующий %{Согласующий.Представление()} не найден в правилах согласования")
            возврат Ложь
        ;
        
        выбор Действие
        когда Согласовать
            СогласованияДокументов.ЗарегистрироватьСогласованиеДокумента(
                Документ = Заявление как ПодписываемыйДокумент.Ссылка,
                ДанныеСогласующего = ДанныеСогласующего)
            ДействиеВыполнено = Истина
        когда Отказать
            СогласованияДокументов.ОтклонитьДокумент(Заявление как ПодписываемыйДокумент.Ссылка, ДанныеСогласующего, Текст)
            ДействиеВыполнено = Истина
        ;
    поймать Ошибка: Исключение
    ;
    возврат ДействиеВыполнено
;