импорт Компания
импорт ОбщиеСтандартные
импорт ВладельцыКабинетов
импорт КонтрольДоступа
импорт ОбменДанными
импорт Компания::Согласования
импорт Отсутствия
импорт УниверсальныеМеханизмы
импорт Документы
импорт Криптография

//Область ОбработчикиСобытий

@Обработчик
метод ПриЗаполнении()
    ФизическоеЛицо = ПользователиСистемы.ТекущееФизическоеЛицо()
    если ФизическоеЛицо == Неопределено
        возврат
    ;
    пер ЧасовойПояс: ЧасовойПояс = ВладельцыКабинетов.ЧасовойПоясФизическогоЛица(ФизическоеЛицо)    
    МоментСоздания = Момент.Сейчас()
    Дата = Дата.Сейчас(ЧасовойПояс)
    пер ТекущаяДатаЧерезНеделю: Дата = Дата.ДобавитьДни(7)
    ДатаНачала = ТекущаяДатаЧерезНеделю.СДнемНедели(ДеньНедели.Понедельник)
;

@Обработчик
метод ПередЗаписью(ПредыдущееЗначение: ЗаявлениеНаПереносОтпуска.Данные, ПараметрыЗаписи: ЗаявлениеНаПереносОтпуска.ПараметрыЗаписи)
    ЗаполнитьПредставление()
    ЗаполнитьМесяцСоздания()
;

@Обработчик
метод ПослеЗаписи(ПредыдущееЗначение: ЗаявлениеНаПереносОтпуска.Данные, ПараметрыЗаписи: ЗаявлениеНаПереносОтпуска.ПараметрыЗаписи)
    исп КонтекстДоступа.Привилегированный()
    пер ТребуетсяРегистрацияИзменений = РегистрацияИзменений.ТребуетсяРегистрацияИзмененийОбъекта(этот, ПредыдущееЗначение)
    ЗаявлениеНаПереносОтпуска.ПослеЗаписиДополнение(Ссылка, ФизическоеЛицо, ТребуетсяРегистрацияИзменений)
;

@Обработчик
@ВыполнятьПриЗагрузкеДанных
метод ПередУдалением(ПараметрыУдаления: ЗаявлениеНаПереносОтпуска.ПараметрыУдаления)
    ЗапланированныеОтсутствия.УдалитьЗапланированноеОтсутствие(Ссылка)
    Аннулирование.УдалитьАннулирование(Ссылка)
    МенеджерСущности.УдалитьПодчиненныеОбъекты(Ссылка, Тип<СогласованияДокументов>, "Документ")
    МенеджерСущности.УдалитьПодчиненныеОбъекты(Ссылка, Тип<ПодписиДокументов>, "Документ")
    МенеджерСущности.УдалитьПодчиненныеОбъекты(Ссылка, Тип<ВерсииДанныхОбъектов>, "Объект")
    МенеджерСущности.УдалитьПодчиненныеОбъекты(Ссылка, Тип<ЗаписиОтсутствий>, "Документ")
;

//КонецОбласти

//Область СлужебныеПроцедурыИФункции

метод ЗаполнитьПредставление()
    пер ПричинаОтсутствия = ЗаСвойСчет ? ВидыОтсутствий.ОтпускЗаСвойСчет : ВидыОтсутствий.Отпуск
    ПредставлениеВидаОтсутствия = ПричинаОтсутствия.Представление()
    ПредставлениеДокумента = "Перенос ${Отсутствия.ПредставлениеОтсутствия(ДатаНачала, ДатаОкончания, ПричинаОтсутствия, ФизическоеЛицо)}"
;

метод ЗаполнитьМесяцСоздания()
    МесяцСоздания = Дата.НачалоМесяца()
;

@ВПодсистеме
метод ЗапланироватьОтсутствие(РабочиеМеста: РабочиеМеста?)
    
    пер Организации : Массив<Организации.Ссылка>
    если РабочиеМеста != Неопределено и РабочиеМеста.РабочиеМеста.Размер() > 0
        для Элемент из РабочиеМеста.РабочиеМеста
          Организации.Добавить(Элемент.Организация)
        ;
    ;
    
    пер НачалоПериода: ДатаВремя = ДатаНачала.НачалоДня()
    пер ОкончаниеПериода: ДатаВремя = ДатаОкончания.КонецДня()
    знч ВидОтсутствия =  ЗаСвойСчет ? ВидыОтсутствий.ОтпускЗаСвойСчет : ВидыОтсутствий.Отпуск
  
    ЗапланированныеОтсутствия.ЗаписатьЗапланированныеОтсутствия(
        Отсутствие = Ссылка,
        ВидОтсутствия =  ВидОтсутствия,
        НачалоПериода = НачалоПериода,
        ОкончаниеПериода = ОкончаниеПериода,
        ФизическоеЛицо = ФизическоеЛицо,
        Организации = Организации,
        Внутрисменное = Ложь 
    ) 
    
;

@ВПодсистеме
метод ЗаписатьАннулирование(АннулируемыйДокумент: АннулируемыйДокумент.Ссылка)
    Аннулирование.ЗаписатьАннулирование(ФизическоеЛицо, Ссылка, АннулируемыйДокумент)
;

//КонецОбласти