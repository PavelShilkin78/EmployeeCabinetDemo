импорт ИнтерфейсныеКомпонентыСтандартные
импорт Компания
импорт Отсутствия
импорт ОбщиеСтандартные
импорт ФизическиеЛица
импорт ВладельцыКабинетов
импорт ЛичныеКабинетыПользователей

@Обработчик
метод ПослеСоздания()
    ИнтерфейсныеКомпоненты.ИнициализироватьТаблицу(Таблица = Компоненты.ГрафикОтпусков)
    МенеджерГлобальныхКлиентскихСобытий.ПодключитьОбработчик(метод (Источник, Параметры) ->
        если Источник != ФизЛица.ТРЕБУЕТСЯ_ОБНОВЛЕНИЕ_ДАННЫХ_ФИЗИЧЕСКОГО_ЛИЦА
            возврат
        ;
        ПриИзмененииДанныхФизическогоЛица()
    ;)

;

метод ПриИзмененииДанныхФизическогоЛица()
    Компоненты.ГрафикОтпусков.Обновить()
;

метод ГрафикОтпусковИсточникДанные(): Массив<ГрафикОтпусковКомпонентДанных>
    пер Данные: Массив<ГрафикОтпусковКомпонентДанных>
    Данные.ДобавитьВсе(ГрафикОтпусков(ЛичныеКабинетыПользователей.ПараметрыСеанса().ТекущееФизическоеЛицо, СтрокаПоиска))
    возврат Данные
;

метод КоллекцияИсточникаГрафикОтпусков(): Массив<ГрафикОтпусковКомпонентДанных>
    возврат Компоненты.ГрафикОтпусков.Источник.Данные
;

метод ГрафикОтпусковКоллегКомпонентВидимость(): Булево
    возврат не КоллекцияИсточникаГрафикОтпусков().Пусто()
;

метод СтрокаСпискаСМиниатюройКомпонентДанных(ДанныеЭлемента: ГрафикОтпусковКомпонентДанных): СтрокаСпискаСМиниатюройКомпонентДанных
    возврат ДанныеЭлемента.ДанныеДляПредставления
;

@НаСервере @ДоступноСКлиента
статический метод ГрафикОтпусков(ФизическоеЛицо: ФизическиеЛица.Ссылка, СтрокаПоиска: Строка): Массив<ГрафикОтпусковКомпонентДанных>
    пер Данные = новый Массив<ГрафикОтпусковКомпонентДанных>()
    пер Коллеги = ОрганизационнаяСтруктура.КоллегиФизическогоЛица(ФизическоеЛицо, СтрокаПоиска)
    пер Руководитель = Коллеги.Руководитель
    пер СотрудникиНаДолжности = Коллеги.СотрудникиНаДолжности
    пер СотрудникиПодразделения = Коллеги.СотрудникиПодразделения
    пер СписокФизическихЛиц: Массив<ФизическиеЛица.Ссылка>
    СписокФизическихЛиц.ДобавитьВсе(СотрудникиПодразделения)
    СписокФизическихЛиц.ДобавитьВсе(СотрудникиНаДолжности)
    если Руководитель != Неопределено
        СписокФизическихЛиц.Добавить(Руководитель)
    ;
    пер ЧасовойПояс: ЧасовойПояс = ВладельцыКабинетов.ЧасовойПоясФизическогоЛица(ФизическоеЛицо)
    пер ТекущаяДата = Дата.Сейчас(ЧасовойПояс)
    знч Запрос = Запрос{
        Выбрать
            ГрафикОтпусков.ФизическоеЛицо Как ФизическоеЛицо,
            ГрафикОтпусков.ДатаНачала.ЗаменитьNull(Неопределено) Как ДатаНачала,
            ГрафикОтпусков.ДатаОкончания.ЗаменитьNull(Неопределено) Как ДатаОкончания,
            ГрафикОтпусков.ФизическоеЛицо.Представление.ЗаменитьNull("") Как ФизическоеЛицоПредставление,
            ГрафикОтпусков.ФизическоеЛицо.Миниатюра.ЗаменитьNull(Неопределено) Как Миниатюра,
            Выбор
                Когда ГрафикОтпусков.ФизическоеЛицо == %Руководитель
                    Тогда 1
                Когда ГрафикОтпусков.ФизическоеЛицо В(%СотрудникиНаДолжности)
                    Тогда 2
                Иначе 3
            Конец Как Приоритет
        Из
            ГрафикОтпусков Как ГрафикОтпусков
        Где
            ГрафикОтпусков.ДатаНачала >| %ТекущаяДата
            И ГрафикОтпусков.ФизическоеЛицо В(%СписокФизическихЛиц)
        Упорядочить По
            Приоритет,
            ГрафикОтпусков.ФизическоеЛицо,
            ГрафикОтпусков.ДатаНачала,
            ГрафикОтпусков.ДатаОкончания}
    исп Результат = Запрос.Выполнить()
    для РезультатСтрока из Результат
        пер Элемент = новый ГрафикОтпусковКомпонентДанных()
        Элемент.ФизическоеЛицо = РезультатСтрока.ФизическоеЛицо
        Элемент.ДатаНачала = РезультатСтрока.ДатаНачала
        Элемент.ДатаОкончания = РезультатСтрока.ДатаОкончания
        пер ДанныеПредставления = новый СтрокаСпискаСМиниатюройКомпонентДанных()
        ДанныеПредставления.Представление = РезультатСтрока.ФизическоеЛицоПредставление
        ДанныеПредставления.ДополнительноеПредставление =
            Отсутствия.ПредставлениеОтсутствия(РезультатСтрока.ДатаНачала, РезультатСтрока.ДатаОкончания, Неопределено)
        ДанныеПредставления.Изображение = ФизическиеЛица.ФотографияИзФайла(РезультатСтрока.Миниатюра)
        Элемент.ДанныеДляПредставления = ДанныеПредставления
        Данные.Добавить(Элемент)
    ;
    возврат Данные
;