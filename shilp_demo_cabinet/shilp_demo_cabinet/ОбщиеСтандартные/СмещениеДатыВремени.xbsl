@ВПроекте
@НаСервере @НаКлиенте
метод ПредставлениеСмещения(): Строка
    возврат "$Размер ${Единица.ПредставлениеПоКоличеству(Размер, СЗаглавнойБуквы = Ложь)}"
;

@ВПроекте
@НаСервере @НаКлиенте
метод От(От: ДатаВремя): ДатаВремя
    выбор Единица
    когда Час
         возврат От + 1ч * Размер    
    когда День
        возврат От + 1д * Размер
    когда Неделя
        возврат От + 7д * Размер
    когда Месяц
        возврат От.ДобавитьМесяцы(Размер)
    когда Квартал
        возврат От.ДобавитьМесяцы(3 * Размер)
    когда Год
        возврат От.ДобавитьГоды(Размер)
    ;
    возврат От     
;

@ВПроекте
метод ПримернаяДлительность(): Длительность
    возврат Единица.ПримернаяДлительность() * Размер    
;

@ВПроекте
метод СравнитьПримерно(Смещение: СмещениеДатыВремени): Число
    возврат этот.ПримернаяДлительность().Сравнить(Смещение.ПримернаяДлительность())    
;

// JSON
@ВПроекте
структура ДанныеJson_V2_0
    обз пер unit: ОбщиеСтандартные.ЕдиницаСмещенияДатыВремениJson
    обз пер size: Число
;

@ВПроекте
метод ЗаполнитьПоJson(ДанныеJson: ДанныеJson_V2_0)
    Единица = ОбщиеСтандартные.ЕдиницаСмещенияДатыВремениИзJson(ДанныеJson.unit)
    Размер = ДанныеJson.size
;

@ВПроекте
метод ПолучитьJson_V2_0(): ДанныеJson_V2_0 
    возврат новый ДанныеJson_V2_0(
        ОбщиеСтандартные.ЕдиницаСмещенияДатыВремениВJson(Единица),
        Размер
    ) 
;
// Конец JSON