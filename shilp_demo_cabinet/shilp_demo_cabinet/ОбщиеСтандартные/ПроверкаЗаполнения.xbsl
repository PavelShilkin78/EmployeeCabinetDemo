
@ВПроекте
@НаКлиенте
конст СООБЩЕНИЕ_ОБЯЗАТЕЛЬНОЕ_ПОЛЕ = "Поле обязателено для заполнения"

@ВПроекте
@НаСервере @НаКлиенте
исключение ИсключениеПроверкиЗаполнения
    обз знч Ошибки: ЧитаемоеМножество<Перечисление> 
;

@ВПроекте
@НаСервере
метод ПроверитьЗаполнение(ОшибкиИОбработчикиПроверки: Соответствие<Перечисление, ()-> Булево>)
    знч НайденныеОшибки: Множество<Перечисление>
    для Проверка из ОшибкиИОбработчикиПроверки
        если Проверка.Значение()
            НайденныеОшибки.Добавить(Проверка.Ключ)
        ;
    ;
    
    если НайденныеОшибки.Размер() > 0
        пер Описание = ["При проверке заполнения выявлены ошибки."]
        для Ошибка из НайденныеОшибки
           Описание.Добавить(Ошибка.Представление())
        ;
        выбросить новый ИсключениеПроверкиЗаполнения(Описание.Соединить(Символы.НОВАЯ_СТРОКА), НайденныеОшибки)
    ;
;

@ВПроекте
@НаКлиенте
метод ОбработатьОшибкиЗаполнения(Исключение: ИсключениеПроверкиЗаполнения, ОбработкаОшибок: Соответствие<Перечисление, ()-> ничто>) 
    знч НеОбработанныеОшибки: Множество<Перечисление>
    для Ошибка из Исключение.Ошибки
        знч ОбработкаОшибки = ОбработкаОшибок.ПолучитьИлиУмолчание(Ошибка)
        если ОбработкаОшибки == Неопределено
            НеОбработанныеОшибки.Добавить(Ошибка)
        иначе
            ОбработкаОшибки()
        ;
    ;
    
    пер ТекстУведомления: Строка
    если НеОбработанныеОшибки.Размер() == 1
        ТекстУведомления = "При проверке заполнения обнаружена ошибка: %{НеОбработанныеОшибки.ПервыйИлиУмолчание().Представление()}"
    иначе если НеОбработанныеОшибки.Размер() > 1
        ТекстУведомления = "При проверке заполнения обнаружены ошибки:"
        для Ошибка из НеОбработанныеОшибки
            ТекстУведомления += 
            "
            %{Ошибка.Представление()}"
        ;
    ;
    если не ТекстУведомления.Пусто()
        пер Уведомление = новый Уведомление("Проверка заполнения", ТекстУведомления)
        Уведомление.Важность = ВажностьКлиентскогоУведомления.Обычная
        Уведомление.Таймаут = 15с
        Уведомление.ОценкаИнформации = ОценкаИнформации.Предупреждающая
        Уведомление.Показать()    
    ;
;