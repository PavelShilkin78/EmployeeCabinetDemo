// Область ПрограммныйИнтерфейс

@ВПроекте
структура ДанныеФайлаJson
    обз знч ID: Строка?
    обз знч name: Строка
    знч hash: Строка?
    знч fileID: Строка?
    пер extension: Строка?
    пер size: Число?
    пер electronicDocumentID: Строка?
;

@ВПроекте
структура ОбработкаИзображения
    пер Уменьшать: Булево = Ложь
    пер МаксимальнаяШирина: Число
    пер МаксимальнаяВысота: Число
    пер СохранятьСоотношениеСторон: Булево = Истина
    пер ИзменятьФормат: Булево = Ложь
    пер Формат: ФорматИзображения?
    пер ВырезатьКруг: Булево
;

@ВПроекте
исключение ИсключениеОбработкиИзображения
;

@ВПроекте
метод ОбработатьИзображение(ДанныеИзображения: ДвоичныйОбъект, Настройки: ОбработкаИзображения): ДвоичныйОбъект
    
    если не ОбрабатыватьИзображение(Настройки)
        возврат ДанныеИзображения
    ;
    исп Поток: ПотокЧтения = ДанныеИзображения.ОткрытьПотокЧтения()
    пер Изображение = новый ОбрабатываемоеИзображение(Поток)
    если Настройки.Уменьшать
        Изображение.УстановитьРазмер(Настройки.МаксимальнаяШирина, Настройки.МаксимальнаяВысота, Настройки.СохранятьСоотношениеСторон)
    ;
    если Настройки.ИзменятьФормат
        Изображение.УстановитьФормат(Настройки.Формат)
    ;
    если Настройки.ВырезатьКруг
        Изображение.ВырезатьКруг()
    ;
    попытка 
        возврат ОбъектноеХранилище.ЗагрузитьИзБайт(Изображение.ПолучитьИзображение())
    поймать Ошибка: ИсключениеВводаВывода
        выбросить новый ИсключениеОбработкиИзображения("Не удалось обработать изображение по причине %{Ошибка.Описание}", Ошибка)
    ;
;

@ВПроекте
метод ОтмасштабированноеИзображение(Хранилище: ДвоичныйОбъект.Ссылка, КоэффициентМасштабирования: Число): ДвоичныйОбъект 
    исп Поток: ПотокЧтения = Хранилище.Загрузить().ОткрытьПотокЧтения()
    пер Изображение = новый ОбрабатываемоеИзображение(Поток)
    Изображение.Масштабировать(КоэффициентМасштабирования ** 0.5)
    возврат ОбъектноеХранилище.ЗагрузитьИзБайт(Изображение.ПолучитьИзображение())
;

@ВПроекте
метод МаксимальноДопустимыйРазмерФайлаВМегабайтах(): Число
    пер Размер = 30
    знч СервисНастроек = НастройкиРаботыСФайлами.ПолучитьСервис()
    если СервисНастроек != Неопределено
        Размер = СервисНастроек.МаксимальныйРазмерЗагружаемыхФайловВМегабайтах()
    ;
    возврат Размер
;
//КонецОбласти

// Область СлужебныеПроцедурыИФункции

метод ОбрабатыватьИзображение(Обработка: ОбработкаИзображения): Булево
    возврат Обработка.Уменьшать или Обработка.ИзменятьФормат
;

//КонецОбласти