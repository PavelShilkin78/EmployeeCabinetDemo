@ВПроекте
исключение ДублиЗначенийСоответствия
    обз пер Значение: неизвестно
    обз пер Ключ: неизвестно
;

@ВПроекте
структура СравнениеКоллекций
    
    обз знч ПрежняяКоллекция: ЧитаемаяКоллекция<Сущность.Ключ|Перечисление|Неопределено>
    обз знч НоваяКоллекция: ЧитаемаяКоллекция<Сущность.Ключ|Перечисление|Неопределено>
    
    пер Добавленные: ЧитаемоеМножество<Сущность.Ключ|Перечисление|Неопределено> = {}
    пер Удаленные: ЧитаемоеМножество<Сущность.Ключ|Перечисление|Неопределено> = {}
    пер Различия: ЧитаемоеМножество<Сущность.Ключ|Перечисление|Неопределено> = {}
    пер Объединение: ЧитаемоеМножество<Сущность.Ключ|Перечисление|Неопределено> = {}
;

@ВПроекте
метод Пересечение(Обходимое1: Обходимое<неизвестно>, Обходимое2: Обходимое<неизвестно>): ЧитаемоеМножество<неизвестно>
    возврат новый ЧитаемоеМножество<Объект?>(Обходимое1).Пересечение(новый ЧитаемоеМножество<Объект?>(Обходимое2))
;

@ВПроекте
метод УдалитьВхожденияВсех(ИсходныйМассив: Коллекция<неизвестно>, МассивУдаляемыхЗначений: ЧитаемаяКоллекция<неизвестно>)
    ИсходныйМассив.УдалитьВсе(МассивУдаляемыхЗначений)
;

@ВПроекте
метод УдалитьДубли(ИсходныйМассив: Коллекция<неизвестно>): ЧитаемаяКоллекция<неизвестно>
    пер МассивБезДублей: Коллекция<неизвестно> = <Объект?>{}
    для ЭлементМассива из ИсходныйМассив
        если не МассивБезДублей.Содержит(ЭлементМассива)
            МассивБезДублей.Добавить(ЭлементМассива)
        ;    
    ;    
    возврат МассивБезДублей
;

@ВПроекте
метод СравнитьКоллекции(
    ПредыдущееСостояние: ЧитаемаяКоллекция<Сущность.Ключ|Перечисление|Неопределено>, 
    НовоеСостояние: ЧитаемаяКоллекция<Сущность.Ключ|Перечисление|Неопределено>): СравнениеКоллекций

    знч Сравнение = новый СравнениеКоллекций(ПредыдущееСостояние, НовоеСостояние)
    
    знч НовоеМножество = новый Множество<Сущность.Ключ|Перечисление|Неопределено>(Сравнение.НоваяКоллекция)
    знч ПрежнееМножество = новый Множество<Сущность.Ключ|Перечисление|Неопределено>(Сравнение.ПрежняяКоллекция)
    
    Сравнение.Добавленные = новый ЧитаемоеМножество<Сущность.Ключ|Перечисление|Неопределено>(НовоеМножество.Разность(ПрежнееМножество))
    Сравнение.Удаленные = новый ЧитаемоеМножество<Сущность.Ключ|Перечисление|Неопределено>(ПрежнееМножество.Разность(НовоеМножество))
    
    Сравнение.Различия = новый ЧитаемоеМножество<Сущность.Ключ|Перечисление|Неопределено>(ПрежнееМножество.СимметрическаяРазность(НовоеМножество))
    Сравнение.Объединение = новый ЧитаемоеМножество<Сущность.Ключ|Перечисление|Неопределено>(ПрежнееМножество.Объединение(НовоеМножество))
    
    возврат Сравнение
;