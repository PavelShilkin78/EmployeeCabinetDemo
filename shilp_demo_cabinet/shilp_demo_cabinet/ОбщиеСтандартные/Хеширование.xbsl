@ВПроекте
@ПроверятьИспользованиеЗначения
метод РассчитатьХешСтрокуРеквизитаКоллекции(РеквизитКоллекция: ЧитаемыйМассив<Справочник.Ссылка|Документ.Ссылка|ПланОбмена.Ссылка|Хешируемое|Число|Строка>): Строка
    возврат РассчитатьХешКоллекции(РеквизитКоллекция).ВСтроку()    
;

@ВПроекте
@ПроверятьИспользованиеЗначения
метод РассчитатьХешКоллекции(Коллекция: ЧитаемаяКоллекция<Справочник.Ссылка|Документ.Ссылка|ПланОбмена.Ссылка|Хешируемое|Число|Строка>): Байты
    знч Хеш = новый ХешированиеДанных(АлгоритмХеширования.Sha256)
    для ЭлементКоллекции из Коллекция
        Хеш.ДобавитьДанные(РассчитатьХеш(ЭлементКоллекции))
    ;
    возврат Хеш.Результат        
;

@ВПроекте
@ПроверятьИспользованиеЗначения
метод РассчитатьХеш(Данные: Справочник.Ссылка|Документ.Ссылка|ПланОбмена.Ссылка|Хешируемое|Число|Строка): Байты
    если Данные это Хешируемое
        возврат (Данные как Хешируемое).Хеш()
    ;
    знч Хеш = новый ХешированиеДанных(АлгоритмХеширования.Sha256)
    выбор Данные
    когда это Справочник.Ссылка|Документ.Ссылка|ПланОбмена.Ссылка
        знч ИдСсылки = ИдСсылки(Данные как Справочник.Ссылка|Документ.Ссылка|ПланОбмена.Ссылка)
        Хеш.ДобавитьДанные(ИдСсылки)
    иначе
        Хеш.ДобавитьДанные(Данные)
    ;
    возврат Хеш.Результат         
;

метод ИдСсылки(Ссылка: Справочник.Ссылка|Документ.Ссылка|ПланОбмена.Ссылка): Ууид
    выбор Ссылка
    когда это Справочник.Ссылка
        возврат (Ссылка как Справочник.Ссылка).Ид
    когда это Документ.Ссылка
        возврат (Ссылка как Документ.Ссылка).Ид
    когда это ПланОбмена.Ссылка
        возврат (Ссылка как ПланОбмена.Ссылка).Ид        
    ;
;