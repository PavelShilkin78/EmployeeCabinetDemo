импорт ОбщиеСтандартные
импорт ФизическиеЛица
импорт ВладельцыКабинетов
импорт Компания
импорт КонтрольДоступа
импорт ОбменДанными
импорт Компания::Согласования
импорт Криптография

//Область ОбработчикиСобытий

@Обработчик
@ВыполнятьПриЗагрузкеДанных
метод ПриЗаполнении()
    ФизическоеЛицо = ПользователиСистемы.ТекущееФизическоеЛицо()
    если ФизическоеЛицо == Неопределено
        возврат
    ;
    пер ЧасовойПояс: ЧасовойПояс = ВладельцыКабинетов.ЧасовойПоясФизическогоЛица(ФизическоеЛицо)    
    Дата = Дата.Сейчас(ЧасовойПояс)
    МоментСоздания = Момент.Сейчас()
    знч ВключаяУволенных = ФизЛица.СотрудникиФизическогоЛицаУволены(ФизическоеЛицо)
    МестоРаботы = Сотрудники.ОрганизацияФизическогоЛицаПоОсновномуМестуРаботы(
        ФизическоеЛицо = ФизическоеЛицо, ВключаяУволенных = ВключаяУволенных)
    ВариантПолученияДокумента = Сервер.ВариантПолученияДокументаПоУмолчанию()    
    знч ЗапрашиваемыеДанные: ВладельцыКабинетов.ЗапрашиваемыеДанныеВладельцевКабинетов
    ЗапрашиваемыеДанные.ЗапрашиваемыеДанныеФизическихЛиц.РабочаяКонтактнаяИнформация = Истина
    ЭлектроннаяПочта = ВладельцыКабинетов.ДанныеВладельцаКабинета(ФизическоеЛицо, ЗапрашиваемыеДанные).ДанныеФизлица.РабочаяКонтактнаяИнформация.ЭлектроннаяПочта
;

@Обработчик
@ВыполнятьПриЗагрузкеДанных
метод ПередЗаписью(ПредыдущееЗначение: ЗапросСправкиСМестаРаботы.Данные, ПараметрыЗаписи: ЗапросСправкиСМестаРаботы.ПараметрыЗаписи)
    ЗаполнитьПредставление()
    ЗаполнитьМесяцСоздания()
    ПроверитьЗаполнение()
;

метод ЗаполнитьПредставление()
    ПредставлениеДокумента = ЗапросСправкиСМестаРаботыКлиентСервер.ЗаголовокНовогоДокумента(
        ВидСправки == Неопределено ? "" : ВидСправки.Представление()) 
;

метод ЗаполнитьМесяцСоздания()
    МесяцСоздания = Дата.НачалоМесяца()
;

метод ПроверитьЗаполнение()
    МенеджерСущности.ПроверитьЗаполнениеРеквизита(ФизическоеЛицо, "Физическое лицо")
    МенеджерСущности.ПроверитьЗаполнениеРеквизита(МестоРаботы, "Место работы")    
;

@Обработчик
@ВыполнятьПриЗагрузкеДанных
метод ПослеЗаписи(ПредыдущееЗначение: ЗапросСправкиСМестаРаботы.Данные, ПараметрыЗаписи: ЗапросСправкиСМестаРаботы.ПараметрыЗаписи)
    исп КонтекстДоступа.Привилегированный()
    пер ТребуетсяРегистрацияИзменений = РегистрацияИзменений.ТребуетсяРегистрацияИзмененийОбъекта(этот, ПредыдущееЗначение)
    ЗапросСправкиСМестаРаботы.ПослеЗаписиДополнение(Ссылка, ФизическоеЛицо, ТребуетсяРегистрацияИзменений)
;

@Обработчик
@ВыполнятьПриЗагрузкеДанных
метод ПередУдалением(ПараметрыУдаления: ЗапросСправкиСМестаРаботы.ПараметрыУдаления)
    МенеджерСущности.УдалитьПодчиненныеОбъекты(Ссылка, Тип<СогласованияДокументов>, "Документ")
    МенеджерСущности.УдалитьПодчиненныеОбъекты(Ссылка, Тип<ПодписиДокументов>, "Документ")
    МенеджерСущности.УдалитьПодчиненныеОбъекты(Ссылка, Тип<ВерсииДанныхОбъектов>, "Объект")
;

//КонецОбласти