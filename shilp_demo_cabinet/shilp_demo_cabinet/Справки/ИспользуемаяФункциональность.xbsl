импорт КонтрольДоступа

//Область РазрешенияДоступа

@Обработчик
метод ВычислитьРазрешенияДоступа(): Массив<РазрешениеДоступа>
    возврат КонтрольДоступа.РегистрСведенийРедактированиеАдминистратору()
;

//КонецОбласти

@ВПодсистеме
метод ФункциональныеОпции(): Справки.ФункциональныеОпции
    пер ФункциональныеОпции: Справки.ФункциональныеОпции
    знч Запрос = Запрос{
        Выбрать 
            Функциональность, 
            Использование
        Из ИспользуемаяФункциональность}
    исп Результат = Запрос.Выполнить()
    для РезультатСтрока из Результат
        УстановитьЗначение(ФункциональныеОпции, РезультатСтрока.Функциональность, РезультатСтрока.Использование)
    ;
    возврат ФункциональныеОпции
;

метод УстановитьЗначение(ФункциональныеОпции: Справки.ФункциональныеОпции, 
    Функциональность: Функциональность, Значение: Булево)
    
    выбор Функциональность
    когда Справка2Ндфл
        ФункциональныеОпции.Справка2Ндфл = Значение
    когда СправкаСМестаРаботы
        ФункциональныеОпции.СправкаСМестаРаботы = Значение
    когда СправкаПоОстаткамОтпуска
        ФункциональныеОпции.СправкаПоОстаткамОтпуска = Значение
    когда ИнформацияОСправках
        ФункциональныеОпции.ИнформацияОСправках = Значение    
    ;
;

@ВПодсистеме
метод ЗаписатьФункциональныеОпции(ФункциональныеОпции: Справки.ФункциональныеОпции) 
    ЗаписатьЗначение(Функциональность.Справка2Ндфл, ФункциональныеОпции.Справка2Ндфл)
    ЗаписатьЗначение(Функциональность.СправкаСМестаРаботы, ФункциональныеОпции.СправкаСМестаРаботы)
    ЗаписатьЗначение(Функциональность.СправкаПоОстаткамОтпуска, ФункциональныеОпции.СправкаПоОстаткамОтпуска)  
    ЗаписатьЗначение(Функциональность.ИнформацияОСправках, ФункциональныеОпции.ИнформацияОСправках)
;  

метод ЗаписатьЗначение(Функциональность: Функциональность, Значение: Булево?)
    если Значение == Неопределено
        возврат
    ;
    
    знч НаборЗаписей = новый ИспользуемаяФункциональность.НаборЗаписей()
		НаборЗаписей.Фильтр.Функциональность.Установить(Функциональность)
		НаборЗаписей.ДобавитьЗапись(
            Функциональность = Функциональность,
            Использование = Значение
        )
    НаборЗаписей.Записать()     
;