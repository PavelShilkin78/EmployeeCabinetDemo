импорт ИнтерфейсныеКомпонентыСтандартные
импорт Компания
импорт ОбщиеСтандартные
импорт ФизическиеЛица
импорт УниверсальныеМеханизмы

@ВПроекте
структура ПараметрыКомпонента
    обз пер Справка2Ндфл: Справка2Ндфл.Ссылка
;

@ВПодсистеме
метод УстановитьПараметры(Параметры: ПараметрыКомпонента)
    пер ДанныеДокумента = ДополнительныеДанныеДокумента(Параметры.Справка2Ндфл)
    пер ДанныеСправки = ДанныеДокумента.ДанныеСправки
    Ссылка = ДанныеСправки.Получить("Ссылка") как Справка2Ндфл.Ссылка
    ЗапросСправки = ДанныеСправки.Получить("ЗапросСправки") как ЗапросСправки2Ндфл.Ссылка
    Организация = ДанныеСправки.Получить("Организация") как Организации.Ссылка
    ФизическоеЛицо = ДанныеСправки.Получить("ФизическоеЛицо") как ФизическиеЛица.Ссылка
    Год = ДанныеСправки.Получить("Год") как Число
    Доход = ДанныеСправки.Получить("Доход") как Число
    Налог = ДанныеСправки.Получить("Налог") как Число
    Комментарий = ДанныеСправки.Получить("Комментарий") как Строка
    ТаблицаФайлы = ДанныеДокумента.ТабличныеЧасти.ТаблицаФайлы
    ИнтерфейсныеКомпоненты.ИнициализироватьПодчиненныеКомпоненты(Компоненты)
;

метод ПрисоединенныеФайлыИсточникДанные(): Массив<ПрисоединенныйФайл>
    пер Данные: Массив<ПрисоединенныйФайл>
    для ФайлСправка из ТаблицаФайлы.Файлы
        если не ФайлСправка.Удален
            Данные.Добавить(ФайлСправка)
        ;
    ;
    возврат Данные
;


метод ОрганизацияЗаголовок(): Строка
    возврат МенеджерСущности.ПредставлениеСсылки(Организация)
;

метод ГодСтрокой(): Строка
    возврат (Год).ВСтроку()
;

метод ДоходЗначение(): Строка
    возврат МенеджерСтрок.ФорматЧисла(Доход, 0, Истина, 2)
;

метод НалогЗначение(): Строка
    возврат МенеджерСтрок.ФорматЧисла(Налог, 0, Истина, 2)
;

метод КомментарийВидимость(): Булево
    возврат не Комментарий.Пусто()
;

метод ПослеСозданияСтроки(Источник: Список<ИсточникДанныхМассив<ПрисоединенныйФайл>>, Событие: СобытиеСДанными<СтрокаСписка<ПрисоединенныйФайл>>)
    знч ЭлементФайлСправка = Событие.Данные как ПрисоединенныйФайлСправки
    ЭлементФайлСправка.ВыбранаСправка = &ВыбранаСправка
;

метод ВыбранаСправка(Элемент: ПрисоединенныйФайлСправки, Событие: СобытиеСДанными<ПрисоединенныйФайл>)
    ПросмотрСправки.ОткрытьВМодальномОкне(
        ЗаголовокСправки = Событие.Данные.Наименование,
        СправкаДанные = Событие.Данные.Хранилище
    )
;

@НаСервере @НаКлиенте
структура ДополнительныеДанныеДокумента
    пер ДанныеСправки: Соответствие<Строка, Объект?>
    пер ТабличныеЧасти: ТабличныеЧасти
;

@НаСервере @НаКлиенте
структура ТабличныеЧасти
    пер ТаблицаФайлы: ПрисоединенныеФайлы
;

@НаСервере @ДоступноСКлиента
статический метод ДополнительныеДанныеДокумента(Справка: Справка2Ндфл.Ссылка): ДополнительныеДанныеДокумента
    
    пер Данные: ДополнительныеДанныеДокумента
    Данные.ДанныеСправки.ВставитьВсе(Справка2Ндфл.ДанныеСправки(Справка))
    Данные.ТабличныеЧасти = ДанныеТабличныхЧастей(Справка)
    
    возврат Данные
;

 
@НаСервере
статический метод ДанныеТабличныхЧастей(Справка: Справка2Ндфл.Ссылка): ТабличныеЧасти
    
    пер ДанныеТабличныхЧастей: ТабличныеЧасти
    ДанныеТабличныхЧастей.ТаблицаФайлы =
        ПрисоединенныеФайлыМенеджер.ПрисоединенныеФайлы(
            Справка, [ВидыФайла.ЭлектронныйДокумент, ВидыФайла.ДополнительноеПредставлениеЭлектронногоДокумента])
    возврат ДанныеТабличныхЧастей

;