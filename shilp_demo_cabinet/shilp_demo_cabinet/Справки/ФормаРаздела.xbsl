импорт ОбщиеСтандартные
импорт СтандартноеПриложение
импорт ИнтерфейсныеКомпонентыСтандартные
импорт УниверсальныеМеханизмы
импорт Справки
импорт ФизическиеЛица
импорт Зарплата
импорт Отпуск
импорт ЛичныеКабинетыПользователей
импорт Документы

@Обработчик
метод ПослеСоздания()
    КомандыСоздания = Клиент.СформироватьКомандыСоздания()
;

метод ФормаРазделаМаксимальнаяШирина(): Число
    возврат Содержимое.МаксимальнаяШирина
;

метод ФормаРазделаМинимальнаяШирина(): Число
    возврат Содержимое.МинимальнаяШирина
;

метод ПанельМинимальнаяШирина(): Число
    возврат ОсновнойИнтерфейс.МинимальнаяШиринаПанелейПодразделов()
;

метод ПанельМаксимальнаяШирина(): Число
    возврат ОсновнойИнтерфейс.МаксимальнаяШиринаПанелейПодразделов()
;

метод СодержимоеОтсутствуетВидимость(): Булево
    возврат не СодержимоеВидмисть() 
;

метод СодержимоеВидмисть(): Булево
    возврат Компоненты.ЗапросыСправок.Видимость
;

метод СправкаЗаглушкаВидимость(): Булево
    возврат ТекущаяСправка == Неопределено
;

метод СправкаИКнопкаВидимость(): Булево
    возврат не СправкаЗаглушкаВидимость()
;
 
метод ПраваяПанельВидимость(): Булево
    возврат не ОсновнойИнтерфейс.РаботаНаУзкомУстройстве()
;

метод ПраваяПанельВертикальноеПоложениеДочерних(): ВыравниваниеПоВертикали | Авто
    возврат СправкаЗаглушкаВидимость() ? ВыравниваниеПоВертикали.Центр : ВыравниваниеПоВертикали.Верх
;

метод ЗапросыСправокФизическоеЛицо(): ФизическиеЛица.Ссылка
    возврат ЛичныеКабинетыПользователей.ПараметрыСеанса().ТекущееФизическоеЛицо! 
;

метод ЗапросыСправокПриАктивацииСтроки(Источник: СостояниеДокументовКомпонент, Событие: СобытиеСДанными<ДокументИмеющийСостояние.Ссылка?>)
    знч Документ: ЗапросСправки2Ндфл.Ссылка|ЗапросСправкиСМестаРаботы.Ссылка|ЗапросСправкиПоОстаткамОтпуска.Ссылка = 
        Событие.Данные как ЗапросСправки2Ндфл.Ссылка|ЗапросСправкиСМестаРаботы.Ссылка|ЗапросСправкиПоОстаткамОтпуска.Ссылка
        
    пер СправкаИзменилась: Булево = (ТекущаяСправка != Документ)
    ТекущаяСправка = Документ
    если ОсновнойИнтерфейс.РаботаНаУзкомУстройстве()
        ОткрытьФормуСправки()
    иначе если СправкаИзменилась
        УстановитьТекущуюСправку()
    ;
;

метод УстановитьТекущуюСправку()
    пер Объект = МенеджерСущности.ОбъектПоСсылке(ТекущаяСправка)
    знч СправкаИКнопкаСодержимое = Компоненты.СправкаИКнопка.Содержимое
    пер Компонент: ДокументСотрудникаКомпонент?
    знч Сервисы = ДокументыСотрудниковПоставщик.ПолучитьСервисы()
    для Сервис из Сервисы
        Компонент = Сервис.КомпонентДокумента(ТекущаяСправка)
        если Компонент != Неопределено
            прервать
        ;
    ;
    Компонент.УстановитьТекущееЗаявление(Объект)
    Компонент.РежимПросмотра = Истина
    если СправкаИКнопкаСодержимое.Размер() == 2
        СправкаИКнопкаСодержимое.УдалитьПоИндексу(0)
    ;
    СправкаИКнопкаСодержимое.Вставить(0, Компонент как Компонент)
    ТекущееСостояние = СостояниеДокумента(ТекущаяСправка)
;

метод ОткрытьФормуСправки(): Булево
    пер Действие: СтандартныеФормы.ДействиеВФорме?
    если ТекущаяСправка это ЗапросСправки2Ндфл.Ссылка
        Действие = ЗапросСправки2НдфлФормаДокумента.ОткрытьВМодальномОкне(
            КлючОбъекта = ТекущаяСправка как ЗапросСправки2Ндфл.Ссылка)
    иначе если ТекущаяСправка это ЗапросСправкиСМестаРаботы.Ссылка
        Действие = ЗапросСправкиСМестаРаботыФормаДокумента.ОткрытьВМодальномОкне(
            КлючОбъекта = ТекущаяСправка как ЗапросСправкиСМестаРаботы.Ссылка)
    иначе если ТекущаяСправка это ЗапросСправкиПоОстаткамОтпуска.Ссылка
        Действие = ЗапросСправкиПоОстаткамОтпускаФормаДокумента.ОткрытьВМодальномОкне(
            КлючОбъекта = ТекущаяСправка как ЗапросСправкиПоОстаткамОтпуска.Ссылка)
    ;
    если Действие == СтандартныеФормы.ДействиеВФорме.Удален
        ТекущаяСправка = Неопределено
    ;
    возврат (Действие == СтандартныеФормы.ДействиеВФорме.Изменен)
;

метод ИзменитьПриНажатии(Источник: Кнопка, Событие: СобытиеПриНажатии)
    если ОткрытьФормуСправки()
        УстановитьТекущуюСправку()
    ;
;

метод ИзменитьЗаголовок(): Строка
    возврат (ТекущееСостояние == Неопределено или ТекущееСостояние == СостоянияДокумента.Черновик) ? "Изменить" : "Открыть"
;

@НаСервере @ДоступноСКлиента
статический метод СостояниеДокумента(Ссылка: ЗапросСправки2Ндфл.Ссылка|ЗапросСправкиСМестаРаботы.Ссылка|ЗапросСправкиПоОстаткамОтпуска.Ссылка): СостоянияДокумента
    возврат СостДокументов.СостояниеДокумента(Ссылка)
;

// Контракт ВладелецСостояниеДокументовКомпонент

@ВПроекте
@Реализация
метод ПредставленияДокументов(): ЧитаемаяКоллекция<СостояниеДокументаКомпонентДанных>
    возврат Справки.ПредставленияСправокФизлица(ЛичныеКабинетыПользователей.ПараметрыСеанса().ТекущееФизическоеЛицо)
;

@ВПроекте
@Реализация
метод ПриНажатии(Документ: ДокументИмеющийСостояние.Ссылка, ИдКомпонента: Строка = "")
    Клиент.ОткрытьФормуДокумента(Документ как ЗапросСправки2Ндфл.Ссылка|ЗапросСправкиСМестаРаботы.Ссылка|ЗапросСправкиПоОстаткамОтпуска.Ссылка)
;

@ВПроекте
@Реализация
метод ТипыОтображаемыхДокументов(): Массив<Тип>
    возврат []
;

// Конец ВладелецСостояниеДокументовКомпонент