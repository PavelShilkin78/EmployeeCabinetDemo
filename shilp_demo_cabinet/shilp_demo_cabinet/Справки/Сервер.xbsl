импорт ОбщегоНазначения

@ВПодсистеме
перечисление ВариантыПолученияДокументаJson
    fileForPrinterOutput,
    scanWithSignatureAndStamp,
    fileWithElectronicSignature,
    paperForm
;

@ВПодсистеме
метод ЗаполнитьПолучениеДокументаJson(ДанныеДокументаJson: Объект?, ДанныеОбъекта: ЧитаемоеСоответствие<Строка, Объект?>)
    (ДанныеДокументаJson как неизвестно).obtainingMode = ВариантПолученияВJson(ДанныеОбъекта["ВариантПолученияДокумента"] как ВариантыПолученияДокумента)
    если ДанныеОбъекта["ВариантПолученияДокумента"] == ВариантыПолученияДокумента.ВБумажномВиде
        (ДанныеДокументаJson как неизвестно).copiesNumber = ДанныеОбъекта["КоличествоЭкземпляров"] как Число
    иначе
        (ДанныеДокументаJson как неизвестно).sendEmail = ДанныеОбъекта["ОтправитьНаЭлектроннуюПочту"]
        если ДанныеОбъекта["ОтправитьНаЭлектроннуюПочту"] == Истина
            (ДанныеДокументаJson как неизвестно).addressEmail = ДанныеОбъекта["ЭлектроннаяПочта"]
        ;
    ;
;

@ВПодсистеме
метод ВариантПолученияВJson(Вариант: ВариантыПолученияДокумента): ВариантыПолученияДокументаJson?
    пер ВариантJson: ВариантыПолученияДокументаJson?
    выбор Вариант
    когда ФайлДляВыводаНаПринтер
        ВариантJson = ВариантыПолученияДокументаJson.fileForPrinterOutput
    когда СканСПодписьюПечатью
        ВариантJson = ВариантыПолученияДокументаJson.scanWithSignatureAndStamp
    когда ФайлСЭлектроннойПодписью
        ВариантJson = ВариантыПолученияДокументаJson.fileWithElectronicSignature
    когда ВБумажномВиде
        ВариантJson = ВариантыПолученияДокументаJson.paperForm
    ;
    возврат ВариантJson
;

@ВПодсистеме
метод ВариантПолученияДокументаПоУмолчанию(
    ИсключаемыеВарианты: ЧитаемыйМассив<ВариантыПолученияДокумента>? = Неопределено): ВариантыПолученияДокумента?
    
    если ИсключаемыеВарианты == Неопределено
        ИсключаемыеВарианты = новый Массив<ВариантыПолученияДокумента>()
    ;
    пер ФункциональныеОпции = ОбщегоНазначения.ПолучитьФункциональныеОпции()
    если ФункциональныеОпции.ФайлДляВыводаНаПринтер
        и не ИсключаемыеВарианты.Содержит(ВариантыПолученияДокумента.ФайлДляВыводаНаПринтер)
        возврат ВариантыПолученияДокумента.ФайлДляВыводаНаПринтер
    иначе если ФункциональныеОпции.СканДокумента
        и не ИсключаемыеВарианты.Содержит(ВариантыПолученияДокумента.СканСПодписьюПечатью)
        возврат ВариантыПолученияДокумента.СканСПодписьюПечатью
    иначе если ФункциональныеОпции.ФайлСЭлектроннойПодписью
        и не ИсключаемыеВарианты.Содержит(ВариантыПолученияДокумента.ФайлСЭлектроннойПодписью)
        возврат ВариантыПолученияДокумента.ФайлСЭлектроннойПодписью
    иначе если ФункциональныеОпции.ВБумажномВиде
        и не ИсключаемыеВарианты.Содержит(ВариантыПолученияДокумента.ВБумажномВиде)
        возврат ВариантыПолученияДокумента.ВБумажномВиде
    ;
    возврат Неопределено
;
