импорт СтандартноеПриложение
импорт ИнтерфейсныеКомпонентыСтандартные
импорт ОбменДанными
импорт ОбщегоНазначения
импорт УниверсальныеМеханизмы
импорт ФизическиеЛица
импорт ВладельцыКабинетов
импорт УниверсальныеМеханизмы::ШаблоныДокументов

@Обработчик
метод ПослеЧтения()
    ИнтерфейсныеКомпоненты.ИнициализироватьПодчиненныеКомпоненты(Компоненты)
    Компоненты.ГлавнаяГруппа.ЧтениеДанныхЗавершено = Истина 
;

метод ПанельОсновнаяЗаголовокКнопкиЗаписатьИЗакрыть(): Строка
    возврат Компоненты.Содержимое.ВозможноРедактирование() ? "Заказать" : "Закрыть" 
;

метод ПанельОсновнаяЗаписатьИЗакрыть(Источник: КоманднаяПанельФорм, Событие: СобытиеКомпонента)
    если не Компоненты.Содержимое.ВозможноРедактирование()
        Закрыть(СтандартныеФормы.ДействиеВФорме.НеИзменен)
        возврат
    ;
    если не Компоненты.Содержимое.ПроверкаЗаполнения()
        возврат
    ;
    // Признак ЭтоНовый получаем до записи 
    пер ЭтоНовый: Булево = ЭтоНовый()
    пер ТабличныеЧасти: ЗапросСправкиПоОстаткамОтпускаКомпонент.ТабличныеЧасти = Компоненты.Содержимое.МодифицированныеТабличныеЧасти()
    
    пер ДанныеДокумента = новый ДанныеДокумента()

    ДанныеДокумента.Ссылка = Объект.Ссылка
    ДанныеДокумента.Дата = Объект.Дата
    ДанныеДокумента.ФизическоеЛицо = Объект.ФизическоеЛицо
    ДанныеДокумента.ВариантПолученияДокумента = Объект.ВариантПолученияДокумента
    ДанныеДокумента.КоличествоЭкземпляров = Объект.КоличествоЭкземпляров
    ДанныеДокумента.ОтправитьНаЭлектроннуюПочту = Объект.ОтправитьНаЭлектроннуюПочту
    ДанныеДокумента.ЭлектроннаяПочта = Объект.ЭлектроннаяПочта

    пер СостояниеОбмена: ОбменДанными.СостояниеОбмена? = Записать(
            ЭтоНовый(), 
            ДанныеДокумента, 
            ТабличныеЧасти,
            ВладельцыКабинетов.ПараметрыСеанса().ИнформационныеСистемы)
    если РассчитаннаяМодифицированность
        СобственнаяМодифицированность = Ложь
    ;
    // Опровещение выполянем перед закрытием - когда уже записаны все данные, сопровождающие запись документа
    знч ДанныеСостояния = СостояниеДокументаКомпонентДанных.Создать()
    ДанныеСостояния.Документ = ДанныеДокумента.Ссылка
    ДанныеСостояния.ДатаДокумента = ДанныеДокумента.Дата
    ДанныеСостояния.Состояние = СостоянияДокумента.Подготовлен
    ДанныеСостояния.ДополнительноеПредставление = СостДокументов.ПредставлениеПериодаДокумента(ДатаДокумента = ДанныеДокумента.Дата)
    ИзмененоСостояниеДокумента.Оповестить(ДанныеСостояния, ЭтоНовый)
    ЗапросыСправокКлиент.ОповеститьОВыполненномДействии(СостояниеОбмена)
    Закрыть(СтандартныеФормы.ДействиеВФорме.Изменен)
;

метод ЗапросСправкиПоОстаткамОтпускаФормаДокументаВысота(): Число
    возврат ОсновнойИнтерфейс.ВысотаФормыДокумента(50)
;

метод ОшибкаПриВыполненииКоманды(Источник: КоманднаяПанельФорм, Событие: СобытиеСДанными<Исключение>)
    Компоненты.ГлавнаяГруппа.ПоказатьОшибку(
        ОбщегоНазначения.ОписаниеОшибкиПриДействииСДокументом(Событие.Данные, Объект.ФизическоеЛицо))
;

@НаСервере @НаКлиенте
структура ДанныеДокумента
    пер Ссылка: ЗапросСправкиПоОстаткамОтпуска.Ссылка?
    пер Дата: Дата?
    пер ФизическоеЛицо: ФизическиеЛица.Ссылка?
    пер ВариантПолученияДокумента: ВариантыПолученияДокумента
    пер КоличествоЭкземпляров: Число
    пер ОтправитьНаЭлектроннуюПочту: Булево
    пер ЭлектроннаяПочта: Строка
;

@НаСервере @ДоступноСКлиента
статический метод Записать(ЭтоНовый: Булево, 
                ДанныеДокумента: ДанныеДокумента, 
                ТабличныеЧасти: ЗапросСправкиПоОстаткамОтпускаКомпонент.ТабличныеЧасти,
                ИнформационныеСистемы: Соответствие<ИнформационныеСистемы.Ссылка, ОбменДанными.НастройкиИнформационнойСистемы>): ОбменДанными.СостояниеОбмена
    пер Заявление: ЗапросСправкиПоОстаткамОтпуска.Объект?
    пер ЗаявлениеСсылка: ЗапросСправкиПоОстаткамОтпуска.Ссылка? = ДанныеДокумента.Ссылка
    если ЭтоНовый
        Заявление = новый ЗапросСправкиПоОстаткамОтпуска.Объект(ЗаявлениеСсылка.Ид).Заполнить()
    иначе
        Заявление = ЗаявлениеСсылка.ЗагрузитьОбъект()
    ;
    
    Заявление!.Дата = ДанныеДокумента.Дата
    Заявление!.ФизическоеЛицо = ДанныеДокумента.ФизическоеЛицо
    Заявление!.ВариантПолученияДокумента = ДанныеДокумента.ВариантПолученияДокумента
    Заявление!.КоличествоЭкземпляров = ДанныеДокумента.КоличествоЭкземпляров
    Заявление!.ОтправитьНаЭлектроннуюПочту = ДанныеДокумента.ОтправитьНаЭлектроннуюПочту
    Заявление!.ЭлектроннаяПочта = ДанныеДокумента.ЭлектроннаяПочта
    
    исп Транзакции.Начать()
    Заявление.Записать()
    ШаблоныДокументовМенеджер.СохранитьРеквизиты(ТабличныеЧасти.ДополнительныеРеквизиты)
    ПредставленияДокументов.СформироватьПредставленияДокумента(ЗаявлениеСсылка, Истина)
    СостДокументов.УстановитьСостояниеДокумента(ЗаявлениеСсылка, СостоянияДокумента.Подготовлен)
    возврат ВладельцыКабинетов.ПроверитьСостояниеОбмена(ДанныеДокумента.ФизическоеЛицо)
;

метод ОсновнойКомпонентПанели(): Компонент
    возврат Компоненты.Содержимое
;