импорт ИнтерфейсныеКомпонентыСтандартные

@Обработчик
метод ПослеСоздания()
    если Зарплата.ПараметрыСеанса().ФункциональныеОпции.ЗаявленияНаНалоговыеВычеты
        Компоненты.ВыборВидаЗаявленияНаВычеты.Элементы.Добавить(новый ЭлементМеню(
            Заголовок = "Дети",
            Изображение = Ресурс{ВидыЗаявленийНаВычеты/НаДетей.svg}.Ссылка,
            ДополнительныеПараметры = ВидыЗаявленийНаВычеты.Дети
        ))
        Компоненты.ВыборВидаЗаявленияНаВычеты.Элементы.Добавить(новый ЭлементМеню(
            Заголовок = "Лечение",
            Изображение = Ресурс{ВидыЗаявленийНаВычеты/Лечение.svg}.Ссылка,
            ДополнительныеПараметры = ВидыЗаявленийНаВычеты.Лечение
        ))
        Компоненты.ВыборВидаЗаявленияНаВычеты.Элементы.Добавить(новый ЭлементМеню(
            Заголовок = "Обучение",
            Изображение = Ресурс{Образование.svg}.Ссылка,
            ДополнительныеПараметры = ВидыЗаявленийНаВычеты.Обучение
        ))
        Компоненты.ВыборВидаЗаявленияНаВычеты.Элементы.Добавить(новый ЭлементМеню(
            Заголовок = "Недвижимость",
            Изображение = Ресурс{ВидыЗаявленийНаВычеты/Недвижимость.svg}.Ссылка,
            ДополнительныеПараметры = ВидыЗаявленийНаВычеты.Недвижимость
        ))
        Компоненты.ВыборВидаЗаявленияНаВычеты.Элементы.Добавить(новый ЭлементМеню(
            Заголовок = "На себя",
            Изображение = Ресурс{ВидыЗаявленийНаВычеты/Личный.svg}.Ссылка,
            ДополнительныеПараметры = ВидыЗаявленийНаВычеты.НаСебя
        ))
    ;
    если Зарплата.ПараметрыСеанса().ФункциональныеОпции.ЗаявленияНаУдержаниеДсвВПфр
        пер ДСВ: ЭлементМеню
        ДСВ.Заголовок = "Добровольные страховые взносы"
        ДСВ.Изображение = КартинкаДСВ
        ДСВ.ДополнительныеПараметры = ВидыУдержаний.ДСВ_ПФР
        Компоненты.ВыборВидаЗаявленияНаВычеты.Элементы.Добавить(ДСВ)
    ;
;

метод ЗаявлениеНаВычетыКнопкаПриНажатии(Источник: Кнопка, Событие: СобытиеПриНажатии)
    ВыборВидаЗаявленияНаВычеты.Открыть(этот)
;

метод ВыборВидаЗаявленияНаВычетыПриВыбореЭлемента(Компонент: ВсплывающееМеню, Элемент: СобытиеСДанными<ЭлементМеню>)
    пер ДополнительныйПараметр = Элемент.Данные.ДополнительныеПараметры
    если ДополнительныйПараметр это ВидыЗаявленийНаВычеты
        если ДополнительныйПараметр == ВидыЗаявленийНаВычеты.Дети
            ЗаявлениеНаНалоговыеВычетыНаДетейФормаДокумента.ОткрытьВМодальномОкне()
        иначе если ДополнительныйПараметр == ВидыЗаявленийНаВычеты.НаСебя
            ЗаявлениеНаНалоговыеВычетыФормаДокумента.ОткрытьВМодальномОкне(
                ЛичныйВычет = Истина
            )
        иначе
            ЗаявлениеНаНалоговыеВычетыФормаДокумента.ОткрытьВМодальномОкне(
                ПредставлениеВычетовПоУведомлениям = Элемент.Данные.Заголовок,
                ВычетыНаНедвижимость = ДополнительныйПараметр == ВидыЗаявленийНаВычеты.Недвижимость,
                ВычетыНаЛечение = ДополнительныйПараметр == ВидыЗаявленийНаВычеты.Лечение,
                ВычетыНаОбучение = ДополнительныйПараметр == ВидыЗаявленийНаВычеты.Обучение
            )
        ;
    иначе если ДополнительныйПараметр это ВидыУдержаний
        если ДополнительныйПараметр == ВидыУдержаний.ДСВ_ПФР
            ЗаявлениеНаУдержаниеДсвВПфрФормаДокумента.ОткрытьВМодальномОкне()
        ;
    ;
;

метод ДобровольныеСтраховыеВзносыВидимость(): Булево
    возврат Зарплата.ПараметрыСеанса().ФункциональныеОпции.ЗаявленияНаУдержаниеДсвВПфр
;