импорт ИнтерфейсныеКомпонентыСтандартные
импорт Компания::Согласования
импорт УниверсальныеМеханизмы
импорт Документы
импорт УниверсальныеМеханизмы::ШаблоныДокументов
импорт ОбщегоНазначения

конст ПОЯСНЕНИЕ: Строка = ""

// Контракт ДокументСотрудникаКомпонент

@ВПроекте
@Реализация
метод УстановитьТекущееЗаявление(Заявление: Объект)
    если Заявление это ЗаявлениеНаНалоговыеВычеты.Объект
        Объект = Заявление как ЗаявлениеНаНалоговыеВычеты.Объект
        ПрочитатьОбъект()
    ;
;

// Конец ДокументСотрудникаКомпонент

// Методы заполнения формы

метод ЗаявлениеНаНалоговыеВычетыКомпонентЗаголовок(): Строка
    пер ЗаголовокФормы: Строка
    если РежимПросмотра
        ЗаголовокФормы = ЗаголовокФормы()
    ;
    возврат ЗаголовокФормы
;

метод ЗаявлениеНаНалоговыеВычетыКомпонентОтступПоВертикали(): РазмерОтступа | Авто
    возврат РежимПросмотра ? РазмерОтступа.Двойной : РазмерОтступа.Отсутствует
;

метод РежимПросмотра(): Булево
    возврат РежимПросмотра или не ВозможноРедактирование()
;

метод Пояснение(): Строка
    пер ВидВычета: Строка
    пер Документы: Строка
    пер ПояснениеРезультат: Строка
    знч ВычетыНедвижимость = Объект.ВычетыНаНедвижимость
    знч ВычетыЛечение = Объект.ВычетыНаЛечение
    знч ВычетыОбучение = Объект.ВычетыНаОбучение
    если ВычетыНедвижимость или ВычетыЛечение или ВычетыОбучение
        если ВычетыНедвижимость
            ВидВычета = "недвижимость"
            Документы = "документы о приобретении недвижимости"
        иначе если ВычетыЛечение
            ВидВычета = "лечение"
            Документы = "документы об оплате лечения"
        иначе если ВычетыОбучение
            ВидВычета = "обучение"
            Документы = "документы об оплате обучения"
        ;
        ПояснениеРезультат = "Для получения вычета на %ВидВычета следует сначала предоставить в свою налоговую инспекцию %Документы, получить там уведомление о праве на вычет и приложить к этому документу его скан или фото."
    иначе
        ПояснениеРезультат = "Приложите копии документов или опишите в поле Комментарий причину обращения с этим заявлением."
    ;
    возврат ПояснениеРезультат
;

метод ТолькоПросмотр(): Булево
    возврат не ВозможноРедактирование()
;

// Конец

// Обработчик событий формы

метод НачатьЗагрузкуФайлов(Источник: ПрисоединенныеФайлыКомпонент, Событие: неизвестно)
    ПрисоединенныеФайлыНачатаЗагрузкаФайлов(Событие)
;

метод ЗавершитьЗагрузкуФайлов(Источник: ПрисоединенныеФайлыКомпонент, Событие: неизвестно)
    ПрисоединенныеФайлыЗавершенаЗагрузкаФайлов(Событие)    
;

// Конец

// Заполнение из родительской формы

метод ПрочитатьОбъект()
    знч ДанныеСостояния = СостДокументов.ДанныеСостоянияДокумента(Объект.Ссылка)
    СостояниеЗаявления = ДанныеСостояния.Состояние
    пер Ссылка = Объект.Ссылка
    пер ТабличныеЧасти = ДанныеТабличныхЧастей(Объект.ЭтоНовый(), Ссылка)
    ДополнительныеРеквизиты = ТабличныеЧасти.ДополнительныеРеквизиты
    Компоненты.ДополнительныеРеквизиты.СобственнаяМодифицированность = Ложь
    если Объект.ЭтоНовый()
        Объект.ЛичныйВычет = ЛичныйВычет
        Объект.ВычетыНаНедвижимость = ВычетыНаНедвижимость
        Объект.ВычетыНаЛечение = ВычетыНаЛечение
        Объект.ВычетыНаОбучение = ВычетыНаОбучение
        если ЛичныйВычет и не (ВычетыНаНедвижимость или ВычетыНаЛечение или ВычетыНаОбучение)
            Объект.ПредставлениеДокумента = "На себя"
        иначе если не ЛичныйВычет и (ВычетыНаНедвижимость или ВычетыНаЛечение или ВычетыНаОбучение)
            Объект.ПредставлениеДокумента = ПредставлениеВычетовПоУведомлениям
        ;
        ТаблицаФайлы.Владелец = Ссылка
    иначе
        ТаблицаФайлы = ТабличныеЧасти.ТаблицаФайлы!
        ТаблицаСогласующие = ТабличныеЧасти.ТаблицаСогласующие
    ;
    ОтслеживатьИзменениеДанных(Компоненты.ПрисоединенныеФайлы, Истина)
    ОтслеживатьИзменениеДанных(Компоненты.ДополнительныеРеквизиты, Истина)
    ЗавершеноЧтениеДанных(новый СобытиеКомпонента())
    ИнтерфейсныеКомпоненты.ИнициализироватьПодчиненныеКомпоненты(Компоненты)
    СобственнаяМодифицированность = Ложь
    Пояснение = "Получить вычет можно до окончания налогового периода при обращении к работодателю. %{Пояснение()}"
;


@ВПодсистеме
@НаСервере @НаКлиенте
структура ТабличныеЧасти
    пер ТаблицаФайлы: ПрисоединенныеФайлы?
    пер ТаблицаСогласующие: Согласующие?
    пер ДополнительныеРеквизиты: РеквизитыШаблона?
;

@ВПодсистеме
метод МодифицированныеТабличныеЧасти(): ТабличныеЧасти
    пер ТабличныеЧасти: ТабличныеЧасти
    если Компоненты.ПрисоединенныеФайлы.СобственнаяМодифицированность
        ОбщегоНазначения.ОчиститьКлиентскиеДанные(ТаблицаФайлы)    
        ТабличныеЧасти.ТаблицаФайлы = ТаблицаФайлы
    ;
    если Компоненты.ДополнительныеРеквизиты.СобственнаяМодифицированность
        ТабличныеЧасти.ДополнительныеРеквизиты = ДополнительныеРеквизиты
    ;
    возврат ТабличныеЧасти
;

@НаСервере @ДоступноСКлиента
статический метод ДанныеТабличныхЧастей(ЭтоНовый: Булево, Владелец: ЗаявлениеНаНалоговыеВычеты.Ссылка): ТабличныеЧасти
    
    пер ДанныеТабличныхЧастей: ТабличныеЧасти
    если ЭтоНовый
        ДанныеТабличныхЧастей.ДополнительныеРеквизиты = ШаблоныДокументовМенеджер.РеквизитыШаблона(
            Неопределено, УниверсальныеМеханизмы.Ид(Зарплата.ПОДСИСТЕМА_ЗАРПЛАТА, Зарплата.ШАБЛОН_ЗАЯВЛЕНИЯ_НА_НАЛОГОВЫЕ_ВЫЧЕТЫ))
        ДанныеТабличныхЧастей.ДополнительныеРеквизиты.Владелец = Владелец
    иначе
        ДанныеТабличныхЧастей.ТаблицаФайлы = ПрисоединенныеФайлыМенеджер.ПрисоединенныеФайлы(Владелец)
        ДанныеТабличныхЧастей.ТаблицаСогласующие = Согласования.Согласующие(Владелец)
        ДанныеТабличныхЧастей.ДополнительныеРеквизиты = ШаблоныДокументовМенеджер.РеквизитыШаблона(
            Владелец, УниверсальныеМеханизмы.Ид(Зарплата.ПОДСИСТЕМА_ЗАРПЛАТА, Зарплата.ШАБЛОН_ЗАЯВЛЕНИЯ_НА_НАЛОГОВЫЕ_ВЫЧЕТЫ))
    ;
    возврат ДанныеТабличныхЧастей

;

// Конец

// Прочее

@ВПодсистеме
метод ВозможноРедактирование(): Булево
    возврат СостояниеЗаявления == СостоянияДокумента.Черновик или СостояниеЗаявления == Неопределено
;

@ВПодсистеме
метод ПрисоединенныеФайлы(): ПрисоединенныеФайлыКомпонент 
    возврат Компоненты.ПрисоединенныеФайлы
;

@ВПодсистеме 
метод ЗаголовокФормы(): Строка
    возврат Объект?.ПредставлениеДокумента ?? ""
;

// Конец