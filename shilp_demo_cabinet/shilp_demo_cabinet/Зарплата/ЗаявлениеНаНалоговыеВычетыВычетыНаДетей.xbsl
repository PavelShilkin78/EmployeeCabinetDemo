импорт ОбщиеСтандартные
импорт КонтрольДоступа

//Область РазрешенияДоступа

@Обработчик
метод ВычислитьРазрешенияДоступа(): Массив<РазрешениеДоступа>
    возврат КонтрольДоступа.РазрешенияДоступаТаблицыСФизическимЛицомСПравомУдаления()
;

@Обработчик
метод ВычислитьРазрешенияДоступаДляОбъектов(Элементы: ЧитаемыйМассив<ЗаявлениеНаНалоговыеВычетыВычетыНаДетей.ДанныеРасчетаРазрешений>): ЧитаемоеСоответствие<ЗаявлениеНаНалоговыеВычетыВычетыНаДетей.ДанныеРасчетаРазрешений, ЧитаемаяКоллекция<РазрешениеДоступа>>
    знч Разрешения: Соответствие<ЗаявлениеНаНалоговыеВычетыВычетыНаДетей.ДанныеРасчетаРазрешений, ЧитаемаяКоллекция<РазрешениеДоступа>>
    для Элемент из Элементы
        Разрешения.Вставить(Элемент, КонтрольДоступа.РазрешенияДоступаОбъекта(Элемент.Владелец))
    ;
    возврат Разрешения
;

//КонецОбласти

//Область JSON
@ВПроекте
структура ДанныеJson
    пер childSeniority: Число
    пер childDisability: Булево
    пер singleParent: Булево
    пер guardian: Булево
    пер endDate: Момент?
    пер note: Строка
;

@ВПодсистеме
метод ДанныеВычетовНаДетей(Заявления: ЧитаемаяКоллекция<ЗаявлениеНаНалоговыеВычеты.Ссылка>):
        ЧитаемоеСоответствие<ЗаявлениеНаНалоговыеВычеты.Ссылка, Массив<ЧитаемоеСоответствие<Строка, Объект?>>>

    знч ДанныеВычетов: Соответствие<ЗаявлениеНаНалоговыеВычеты.Ссылка, Массив<ЧитаемоеСоответствие<Строка, Объект?>>>

    знч Запрос = Запрос{
        Выбрать
            Владелец,
            ФИО,
            ДатаРождения,
            Ссылка,
            СтаршинствоРебенка,
            РебенокИнвалид,
            ЕдинственныйРодитель,
            Опекун,
            ДатаОкончанияПрименения,
            Комментарий
         Из
            ЗаявлениеНаНалоговыеВычетыВычетыНаДетей
         Где
            Владелец В (%Заявления)}
    исп Результат = Запрос.Выполнить() 
    для РезультатСтрока из Результат
        если не ДанныеВычетов.СодержитКлюч(РезультатСтрока.Владелец)
            ДанныеВычетов.Вставить(РезультатСтрока.Владелец, новый Массив<ЧитаемоеСоответствие<Строка, Объект?>>())
        ;
        ДанныеВычетов[РезультатСтрока.Владелец].Добавить(МенеджерЗапросов.СтрокаРезультатаЗапросаВСоответствие(РезультатСтрока, Результат))            
    ; 

    возврат ДанныеВычетов
;

@ВПодсистеме
метод ДанныеВычетовНаДетейЗаявления(Заявление: ЗаявлениеНаНалоговыеВычеты.Ссылка): Массив<ЧитаемоеСоответствие<Строка, Объект?>>?
    если ДанныеВычетовНаДетей([Заявление]).СодержитКлюч(Заявление)
        возврат ДанныеВычетовНаДетей([Заявление])[Заявление]
    ;
    возврат Неопределено
;

@ВПодсистеме
метод ДанныеВычетовНаДетейВJson(ДанныеВычетов: ЧитаемыйМассив<ЧитаемоеСоответствие<Строка, Объект?>>): ЧитаемыйМассив<ДанныеJson>
    знч ДанныеВычетовJson: Массив<ДанныеJson>
    для ДанныеВычета из ДанныеВычетов
        ДанныеВычетовJson.Добавить(ДанныеВычетаНаРебенкаВJson(ДанныеВычета))
    ;
    возврат ДанныеВычетовJson    
;    

метод ДанныеВычетаНаРебенкаВJson(ДанныеОбъекта: ЧитаемоеСоответствие<Строка, Объект?>): ДанныеJson
    знч ДанныеВJson = новый ДанныеJson()
    ДанныеВJson.childSeniority = СтаршинствоРебенкаВJSON(ДанныеОбъекта["СтаршинствоРебенка"] как СтаршинствоРебенка)
    ДанныеВJson.childDisability = ДанныеОбъекта["РебенокИнвалид"] как Булево
    ДанныеВJson.singleParent = ДанныеОбъекта["ЕдинственныйРодитель"] как Булево
    ДанныеВJson.guardian = ДанныеОбъекта["Опекун"] как Булево
    пер МоментДатыОкончания: Момент =
        новый Момент(ДанныеОбъекта["ДатаОкончанияПрименения"] как Дата,Время{23:59:59.999},ЧасовойПояс.Текущий())
    ДанныеВJson.endDate = МоментДатыОкончания
    ДанныеВJson.note = ДанныеОбъекта["Комментарий"] как Строка

    возврат ДанныеВJson    
;

метод СтаршинствоРебенкаВJSON(Старшинство: СтаршинствоРебенка): Число
    возврат {
        СтаршинствоРебенка.Первый: 1,
        СтаршинствоРебенка.Второй: 2,
        СтаршинствоРебенка.ТретийИПоследующие: 3}[Старшинство]
;

//КонецОбласти