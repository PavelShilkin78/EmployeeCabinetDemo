импорт КонтрольДоступа

//Область РазрешенияДоступа

@Обработчик
метод ВычислитьРазрешенияДоступа(): Массив<РазрешениеДоступа>
    возврат КонтрольДоступа.РегистрСведенийРедактированиеАдминистратору()
;

//КонецОбласти

@ВПодсистеме
метод ФункциональныеОпции(): Зарплата.ФункциональныеОпции
    пер ФункциональныеОпции: Зарплата.ФункциональныеОпции
    знч Запрос = Запрос{
        Выбрать 
            Функциональность, 
            Использование
        Из ИспользуемаяФункциональность}
    исп Результат = Запрос.Выполнить()
    для РезультатСтрока из Результат
        УстановитьЗначение(ФункциональныеОпции, РезультатСтрока.Функциональность, РезультатСтрока.Использование)
    ;
    возврат ФункциональныеОпции
;

@ВПодсистеме
метод ЗаписатьФункциональныеОпции(ФункциональныеОпции: Зарплата.ФункциональныеОпции) 
    ЗаписатьЗначение(Функциональность.ИнформацияОЗарплате, ФункциональныеОпции.ИнформацияОЗарплате)
    ЗаписатьЗначение(Функциональность.ЗаявленияНаНалоговыеВычеты, ФункциональныеОпции.ЗаявленияНаНалоговыеВычеты)  
    ЗаписатьЗначение(Функциональность.ЗаявленияНаУдержаниеДсвВПфр, ФункциональныеОпции.ЗаявленияНаУдержаниеДсвВПфр)  
    ЗаписатьЗначение(Функциональность.СпособРасчетаУдержанияПроцентом, ФункциональныеОпции.СпособРасчетаУдержанияПроцентом)  
    ЗаписатьЗначение(Функциональность.СпособРасчетаУдержанияСуммой, ФункциональныеОпции.СпособРасчетаУдержанияСуммой)  
    ЗаписатьЗначение(Функциональность.СпособРасчетаУдержанияПроцентомНеБолееЗаданнойСуммы, ФункциональныеОпции.СпособРасчетаУдержанияПроцентомНеБолееЗаданнойСуммы)              
;

метод УстановитьЗначение(ФункциональныеОпции: Зарплата.ФункциональныеОпции, 
    Функциональность: Функциональность, Значение: Булево)
    
    выбор Функциональность
    когда ИнформацияОЗарплате
        ФункциональныеОпции.ИнформацияОЗарплате = Значение
    когда ЗаявленияНаНалоговыеВычеты
        ФункциональныеОпции.ЗаявленияНаНалоговыеВычеты = Значение
    когда ЗаявленияНаУдержаниеДсвВПфр
        ФункциональныеОпции.ЗаявленияНаУдержаниеДсвВПфр = Значение
    когда СпособРасчетаУдержанияПроцентом
        ФункциональныеОпции.СпособРасчетаУдержанияПроцентом = Значение
    когда СпособРасчетаУдержанияСуммой
        ФункциональныеОпции.СпособРасчетаУдержанияСуммой = Значение
    когда СпособРасчетаУдержанияПроцентомНеБолееЗаданнойСуммы
        ФункциональныеОпции.СпособРасчетаУдержанияПроцентомНеБолееЗаданнойСуммы = Значение    
    ;
;

метод ЗаписатьЗначение(Функциональность: Функциональность, Значение: Булево?)
    если Значение == Неопределено
        возврат
    ;

    знч НаборЗаписей = новый ИспользуемаяФункциональность.НаборЗаписей()
		НаборЗаписей.Фильтр.Функциональность.Установить(Функциональность)
		НаборЗаписей.ДобавитьЗапись(
            Функциональность = Функциональность,
            Использование = Значение
        )
    НаборЗаписей.Записать()
    
;