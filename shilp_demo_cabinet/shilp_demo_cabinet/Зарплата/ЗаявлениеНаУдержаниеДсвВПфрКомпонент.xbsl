импорт ИнтерфейсныеКомпонентыСтандартные
импорт КонтрольДоступа
импорт ОбменДанными
импорт ОбщегоНазначения
импорт Компания::Согласования
импорт УниверсальныеМеханизмы
импорт ОбщиеСтандартные
импорт ФизическиеЛица
импорт ЛичныеКабинетыПользователей
импорт УниверсальныеМеханизмы::ШаблоныДокументов

// Контракт ДокументСотрудникаКомпонент

@ВПроекте
@Реализация
метод УстановитьТекущееЗаявление(Заявление: Объект)
    если Заявление это ЗаявлениеНаУдержаниеДсвВПфр.Объект
        Объект = Заявление как ЗаявлениеНаУдержаниеДсвВПфр.Объект
        ПрочитатьОбъект()
    ;
;

// Конец ДокументСотрудникаКомпонент

@ВПодсистеме 
метод ЗаголовокФормы(): Строка
    возврат "Добровольные страховые взносы в ПФР"
;

метод ВозможноРедактирование(): Булево
    возврат Состояние == СостоянияДокумента.Черновик или Состояние == Неопределено
;

@ВПодсистеме
метод МодифицированныеТабличныеЧасти(): ЗаявлениеНаУдержаниеДсвВПфрКлиентСервер.ТабличныеЧасти
    пер ТабличныеЧасти: ЗаявлениеНаУдержаниеДсвВПфрКлиентСервер.ТабличныеЧасти
    если Компоненты.ПрисоединенныеФайлы.СобственнаяМодифицированность
        ОбщегоНазначения.ОчиститьКлиентскиеДанные(ТаблицаФайлы)    
        ТабличныеЧасти.ТаблицаФайлы = ТаблицаФайлы
    ;
    если Компоненты.ДополнительныеРеквизиты.СобственнаяМодифицированность
        ТабличныеЧасти.ДополнительныеРеквизиты = ДополнительныеРеквизиты
    ;
    возврат ТабличныеЧасти
;

@ВПодсистеме 
метод СброситьМодифицированностьТабличныхЧастей()
    Компоненты.ПрисоединенныеФайлы.СобственнаяМодифицированность = Ложь
    Компоненты.ДополнительныеРеквизиты.СобственнаяМодифицированность = Ложь
;

@ВПодсистеме
метод ПроверитьЗаполнение(): Булево
    пер РезультатПроверки: Булево
    если не ОбщегоНазначения.ЭтоЧерновик(Состояние)
        РезультатПроверки = Истина
    иначе
        РезультатПроверки = Компоненты.ДеталиУдержания.ПроверкаЗаполнения()
    ;
    возврат РезультатПроверки
;

метод ОсновноеСодержаниеФормыТолькоЧтение(): Булево
    возврат РедактированиеЗапрещено 
;

метод ПрочитатьОбъект()
    пер ФизическоеЛицо: ФизическиеЛица.Ссылка = ЛичныеКабинетыПользователей.ПараметрыСеанса().ТекущееФизическоеЛицо
    пер ДанныеДокумента: ДополнительныеДанныеДокумента =
        ДополнительныеДанныеДокумента(
            Объект.ЭтоНовый(),
            Объект.Ссылка,
            Объект.ФизическоеЛицо,
            ФизическоеЛицо)
    
    Состояние = ДанныеДокумента.Состояние
    ДатаСостояния = ДанныеДокумента.ДатаСостояния
    Отправлен = ДанныеДокумента.Отправлен
    ФизическоеЛицоЗаголовок = ДанныеДокумента.ФизическоеЛицоЗаголовок
    ПодписанТекущимФизическимЛицом = ДанныеДокумента.ПодписанТекущимФизическимЛицом
    РедактированиеЗапрещено = ОбщегоНазначения.РедактированиеЗапрещено(
        Состояние, 
        Объект.ФизическоеЛицо,
        ФизическоеЛицо)
        
    пер Ссылка = Объект.Ссылка
    если Объект.ЭтоНовый()
        ТаблицаФайлы.Владелец = Ссылка
    иначе
        ТаблицаФайлы = ДанныеДокумента.ТабличныеЧасти.ТаблицаФайлы!
        ТаблицаСогласующие = ДанныеДокумента.ТабличныеЧасти.ТаблицаСогласующие
        ОтслеживатьИзменениеДанных(Компоненты.ПрисоединенныеФайлы, Истина)
    ;
    ДополнительныеРеквизиты = ДанныеДокумента.ТабличныеЧасти.ДополнительныеРеквизиты
    ОтслеживатьИзменениеДанных(Компоненты.ДополнительныеРеквизиты, Истина)
    ИнтерфейсныеКомпоненты.ИнициализироватьПодчиненныеКомпоненты(Компоненты)
    если не РежимПросмотра
        ЗавершеноЧтениеДанных(новый СобытиеКомпонента())
    ;
    СброситьМодифицированностьТабличныхЧастей()
    СобственнаяМодифицированность = Ложь
;

метод ПояснениеЗначение(): Строка
    пер Пояснение: Строка
    если ВозможноРедактирование()
        пер ДоступныеСпособыУдержания: Массив<СпособыРасчетаУдержания> = Клиент.ДоступныеСпособыУдержания()
        если ДоступныеСпособыУдержания.Размер() == 1 
            если ДоступныеСпособыУдержания[0] == СпособыРасчетаУдержания.Процентом
                Пояснение = "Вы можете из своего заработка ежемесячно перечислять в Пенсионный фонд добровольные страховые взносы. Размер перечисляемой суммы будет вычисляться как процент от вашего заработка. Укажите в заявлении процент удержания."
            иначе если ДоступныеСпособыУдержания[0] == СпособыРасчетаУдержания.Суммой
                Пояснение = "Вы можете из своего заработка ежемесячно перечислять в Пенсионный фонд добровольные страховые взносы. Укажите в заявлении размер перечисляемой ежемесячно суммы."
            иначе если ДоступныеСпособыУдержания[0] == СпособыРасчетаУдержания.ПроцентомНеБолееЗаданнойСуммы
                Пояснение = "Вы можете из своего заработка ежемесячно перечислять в Пенсионный фонд добровольные страховые взносы. Размер перечисляемой суммы будет вычисляться как процент от вашего заработка, но за месяц не будет превышать указанную вами максимальную сумму. Укажите в заявлении процент удержания и его максимальный размер."
            ;
        иначе
            Пояснение = "Вы можете из своего заработка ежемесячно перечислять в Пенсионный фонд добровольные страховые взносы. Выберите как именно следует рассчитывать перечисляемую сумму и укажите в заявлении размер удержания."
        ;
    иначе
        пер ДатаОтправки = Объект.МоментСоздания
        если Состояние != Неопределено 
            пер ВремяОтправки: Строка
            если МенеджерДат.НачалоДня(ДатаОтправки) == МенеджерДат.НачалоДня(МенеджерДат.ТекущаяДатаВремя())
                ВремяОтправки = "сегодня, в " + МенеджерДат.ФорматМомента(ДатаОтправки, "ЧЧ:мм")
            иначе если МенеджерДат.НачалоДня(МенеджерДат.ДобавитьДни(ДатаОтправки, 1)) == МенеджерДат.НачалоДня(МенеджерДат.ТекущаяДатаВремя())
                ВремяОтправки = "вчера, в " + МенеджерДат.ФорматМомента(ДатаОтправки, "ЧЧ:мм")
            иначе
                ВремяОтправки = МенеджерДат.ФорматМомента(ДатаОтправки, "дд ММММ гггг")
            ;
            пер СтатусЗапроса: Строка
            если Состояние == СостоянияДокумента.Выполнен
                СтатусЗапроса = " и уже согласовано. Взносы ежемесячно удерживаются и перечисляются в ПФР при расчете зарплаты."
            иначе если Состояние == СостоянияДокумента.Подготовлен
                СтатусЗапроса = ", но еще находится в обработке."
            иначе если Состояние == СостоянияДокумента.ПолученОтказ
                СтатусЗапроса = ", но по нему получен отказ. Причины можно уточнить в бухгалтерии."
            ;
            Пояснение = "Заявление на удержание взносов было отправлено %ВремяОтправки%СтатусЗапроса"
        ;
    ;
    возврат Пояснение
;

метод ЗаявлениеНаУдержаниеДсвВПфрКомпонентОтступПоВертикали(): РазмерОтступа | Авто
    возврат РежимПросмотра ? РазмерОтступа.Двойной : РазмерОтступа.Отсутствует
;

метод ЗаявлениеНаУдержаниеДсвВПфрКомпонентЗаголовок(): Строка
    пер ЗаявлениеНаУдержаниеДсвВПфрКомпонентЗаголовок: Строка
    если РежимПросмотра
        ЗаявлениеНаУдержаниеДсвВПфрКомпонентЗаголовок = "Добровольные страховые взносы в ПФР"
    ;
    возврат ЗаявлениеНаУдержаниеДсвВПфрКомпонентЗаголовок
;

метод ФизическоеЛицоВидимость(): Булево
    возврат Объект.ФизическоеЛицо != ЛичныеКабинетыПользователей.ПараметрыСеанса().ТекущееФизическоеЛицо
;

метод ФизическоеЛицоЗаголовок(): Строка
    возврат ФизическоеЛицоЗаголовок
;

метод РедактированиеРазрешено(): Булево
    возврат не РедактированиеЗапрещено
;

// Контракт ВладелецПрисоединенныеФайлыКомпонент
@Реализация
@ВПроекте
метод ПрисоединенныеФайлыНачатаЗагрузкаФайлов(Источник: ПрисоединенныеФайлыКомпонент, Событие: неизвестно, ИдКомпонента: Строка = "")
    ИзменениеСостоянияЗагрузкиФайлов(новый СобытиеСДанными(Истина))
;

@Реализация
@ВПроекте
метод ПрисоединенныеФайлыЗавершенаЗагрузкаФайлов(Источник: ПрисоединенныеФайлыКомпонент, Событие: неизвестно, ИдКомпонента: Строка = "")
    ИзменениеСостоянияЗагрузкиФайлов(новый СобытиеСДанными(Ложь))
;
// Конец ВладелецПрисоединенныеФайлыКомпонент


метод ПрисоединенныеФайлыДобавлятьФайлы(): Булево
    возврат ВозможноРедактирование()
;

метод ПрисоединенныеФайлыВидимость(): Булево
    возврат РежимПросмотра и ТаблицаФайлы.Файлы.Размер() > 0 или не РежимПросмотра  
;

@НаСервере @НаКлиенте
структура ДополнительныеДанныеДокумента
    пер Состояние: СостоянияДокумента?
    пер Отправлен: Булево
    пер ФизическоеЛицоЗаголовок: Строка
    пер ФизическоеЛицо: ФизическиеЛица.Ссылка?
    пер ПодписанТекущимФизическимЛицом: Булево
    пер ДатаСостояния: Момент?
    пер ТабличныеЧасти: ЗаявлениеНаУдержаниеДсвВПфрКлиентСервер.ТабличныеЧасти
;

@НаСервере @ДоступноСКлиента
статический метод ДополнительныеДанныеДокумента(ЭтоНовый: Булево, Ссылка: ЗаявлениеНаУдержаниеДсвВПфр.Ссылка, ФизическоеЛицо: ФизическиеЛица.Ссылка?, ТекущееФизическоеЛицо: ФизическиеЛица.Ссылка?): ДополнительныеДанныеДокумента
    если ТекущееФизическоеЛицо == Неопределено
        ТекущееФизическоеЛицо = ПользователиСистемы.ТекущееФизическоеЛицо()
    ;
    пер ДополнительныеДанныеДокумента: ДополнительныеДанныеДокумента
    ДополнительныеДанныеДокумента.ФизическоеЛицо = ТекущееФизическоеЛицо 
    ДополнительныеДанныеДокумента.ФизическоеЛицоЗаголовок = МенеджерСущности.ПредставлениеСсылки(ФизическоеЛицо)
    если не ЭтоНовый
        ДополнительныеДанныеДокумента.Отправлен = ЭтоОтправляемыйДокумент(Ссылка)
        ДополнительныеДанныеДокумента.ПодписанТекущимФизическимЛицом = ПодписанТекущимФизическимЛицом(Ссылка, ТекущееФизическоеЛицо)
    ;
    ЗаполнитьДанныеСостояния(Ссылка, ДополнительныеДанныеДокумента)
    ДополнительныеДанныеДокумента.ТабличныеЧасти = ДанныеТабличныхЧастей(ЭтоНовый, Ссылка)
    возврат ДополнительныеДанныеДокумента
;

@НаСервере
статический метод ДанныеТабличныхЧастей(ЭтоНовый: Булево, Владелец: ЗаявлениеНаУдержаниеДсвВПфр.Ссылка): ЗаявлениеНаУдержаниеДсвВПфрКлиентСервер.ТабличныеЧасти
    пер ДанныеТабличныхЧастей: ЗаявлениеНаУдержаниеДсвВПфрКлиентСервер.ТабличныеЧасти
    ДанныеТабличныхЧастей.ТаблицаФайлы = ПрисоединенныеФайлыМенеджер.ПрисоединенныеФайлы(Владелец)
    ДанныеТабличныхЧастей.ТаблицаСогласующие = Согласования.Согласующие(Владелец)
    если ЭтоНовый
        ДанныеТабличныхЧастей.ДополнительныеРеквизиты = ШаблоныДокументовМенеджер.РеквизитыШаблона(
            Неопределено, УниверсальныеМеханизмы.Ид(Зарплата.ПОДСИСТЕМА_ЗАРПЛАТА, Зарплата.ШАБЛОН_ЗАЯВЛЕНИЯ_НА_УДЕРЖАНИЕ_ДСВ_В_ПФР))
        ДанныеТабличныхЧастей.ДополнительныеРеквизиты.Владелец = Владелец
    иначе
        ДанныеТабличныхЧастей.ДополнительныеРеквизиты = ШаблоныДокументовМенеджер.РеквизитыШаблона(
            Владелец, УниверсальныеМеханизмы.Ид(Зарплата.ПОДСИСТЕМА_ЗАРПЛАТА, Зарплата.ШАБЛОН_ЗАЯВЛЕНИЯ_НА_УДЕРЖАНИЕ_ДСВ_В_ПФР))
    ;
    возврат ДанныеТабличныхЧастей
;

@НаСервере
статический метод ЗаполнитьДанныеСостояния(Ссылка: ЗаявлениеНаУдержаниеДсвВПфр.Ссылка, ДополнительныеДанныеДокумента: ДополнительныеДанныеДокумента)

    пер ДанныеСостояния = СостДокументов.ДанныеСостоянияДокумента(Ссылка)
    ДополнительныеДанныеДокумента.Состояние = ДанныеСостояния.Состояние
    ДополнительныеДанныеДокумента.ДатаСостояния = ДанныеСостояния.Дата
;

@НаСервере
статический метод ЭтоОтправляемыйДокумент(Ссылка: ЗаявлениеНаУдержаниеДсвВПфр.Ссылка): Булево
    возврат ОбменДанными.ЭтоОтправляемыйДокумент(Ссылка)
;

@НаСервере
статический метод ПодписанТекущимФизическимЛицом(Ссылка: ЗаявлениеНаУдержаниеДсвВПфр.Ссылка, ТекущееФизическоеЛицо: ФизическиеЛица.Ссылка): Булево
    возврат СогласованияДокументов.ПодписьДокумента(Ссылка, ТекущееФизическоеЛицо) != Неопределено
;

@НаСервере @ДоступноСКлиента
статический метод ЗагрузитьЗаявление(Заявление: ЗаявлениеНаУдержаниеДсвВПфр.Ссылка): ЗаявлениеНаУдержаниеДсвВПфр.Объект
    возврат Заявление.ЗагрузитьОбъект()
;