импорт ОбщиеСтандартные
импорт КонтрольДоступа

//Область РазрешенияДоступа

@Обработчик
метод ВычислитьРазрешенияДоступа(): Массив<РазрешениеДоступа>
    возврат КонтрольДоступа.РазрешенияДоступаТаблицыСФизическимЛицомСПравомУдаления()
;

@Обработчик
метод ВычислитьРазрешенияДоступаДляОбъектов(Элементы: ЧитаемыйМассив<ЗаявлениеНаНалоговыеВычетыУведомленияНалоговой.ДанныеРасчетаРазрешений>): ЧитаемоеСоответствие<ЗаявлениеНаНалоговыеВычетыУведомленияНалоговой.ДанныеРасчетаРазрешений, ЧитаемаяКоллекция<РазрешениеДоступа>>
    знч Разрешения: Соответствие<ЗаявлениеНаНалоговыеВычетыУведомленияНалоговой.ДанныеРасчетаРазрешений, ЧитаемаяКоллекция<РазрешениеДоступа>>
    для Элемент из Элементы
        Разрешения.Вставить(Элемент, КонтрольДоступа.РазрешенияДоступаОбъекта(Элемент.Владелец))
    ;
    возврат Разрешения
;

//КонецОбласти РазрешенияДоступа

//Область JSON

@ВПроекте
структура ДанныеJson
    пер amount: Число
    пер note: Строка
;

@ВПодсистеме
метод ДанныеУведомленийНалоговой(Заявления: ЧитаемаяКоллекция<ЗаявлениеНаНалоговыеВычеты.Ссылка>):
        ЧитаемоеСоответствие<ЗаявлениеНаНалоговыеВычеты.Ссылка, Массив<ЧитаемоеСоответствие<Строка, Объект?>>>

    знч ДанныеУведомленийНалоговой: Соответствие<ЗаявлениеНаНалоговыеВычеты.Ссылка, Массив<ЧитаемоеСоответствие<Строка, Объект?>>>
    знч Запрос = Запрос{
        Выбрать
            Владелец,
            Ссылка,
            Размер,
            Комментарий
         Из
            ЗаявлениеНаНалоговыеВычетыУведомленияНалоговой
         Где
            Владелец В (%Заявления)}
    исп Результат = Запрос.Выполнить() 
    для РезультатСтрока из Результат
        если не ДанныеУведомленийНалоговой.СодержитКлюч(РезультатСтрока.Владелец)
            ДанныеУведомленийНалоговой.Вставить(РезультатСтрока.Владелец, новый Массив<ЧитаемоеСоответствие<Строка, Объект?>>())
        ;
        ДанныеУведомленийНалоговой[РезультатСтрока.Владелец].Добавить(МенеджерЗапросов.СтрокаРезультатаЗапросаВСоответствие(РезультатСтрока, Результат))            
    ;

    возврат ДанныеУведомленийНалоговой
;

@ВПодсистеме
метод ДанныеУведомленийНалоговойВJson(ДанныеВычетов: ЧитаемыйМассив<ЧитаемоеСоответствие<Строка, Объект?>>): ЧитаемыйМассив<ДанныеJson>
    знч ДанныеВычетовJson: Массив<ДанныеJson>
    для ДанныеВычета из ДанныеВычетов
        ДанныеВычетовJson.Добавить(ДанныеУведомленияНалоговойВJson(ДанныеВычета))
    ;
    возврат ДанныеВычетовJson    
;    

метод ДанныеУведомленияНалоговойВJson(ДанныеОбъекта: ЧитаемоеСоответствие<Строка, Объект?>): ДанныеJson
    знч ДанныеВJson = новый ДанныеJson()
    ДанныеВJson.amount = ДанныеОбъекта["Размер"] как Число
    ДанныеВJson.note = ДанныеОбъекта["Комментарий"] как Строка

    возврат ДанныеВJson    
;

//КонецОбласти JSON