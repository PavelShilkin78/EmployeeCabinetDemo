@ВПодсистеме
@НаСервере @ДоступноСКлиента
метод ЗаявлениеДети(Владелец: ЗаявлениеНаНалоговыеВычеты.Ссылка): ЗаявлениеДети
    пер ЗаявлениеДети: ЗаявлениеДети
    ЗаявлениеДети.Владелец = Владелец
    знч Запрос = Запрос{
        Выбрать
            Ссылка,
            ФИО,
            ДатаРождения,
            СтаршинствоРебенка,
            РебенокИнвалид,
            ЕдинственныйРодитель,
            Опекун,
            ДатаОкончанияПрименения
         Из
            ЗаявлениеНаНалоговыеВычетыВычетыНаДетей
         Где
            Владелец == %Владелец
         Упорядочить По
            СтаршинствоРебенка}
    исп Результат = Запрос.Выполнить() 
    для РезультатСтрока из Результат
        пер ЗаявлениеНаДетей = новый ЗаявлениеДетиСтрока()
        ЗаявлениеНаДетей.Ссылка = РезультатСтрока.Ссылка
        ЗаявлениеНаДетей.ФИО = РезультатСтрока.ФИО
        ЗаявлениеНаДетей.ДатаРождения = РезультатСтрока.ДатаРождения
        ЗаявлениеНаДетей.СтаршинствоРебенка = РезультатСтрока.СтаршинствоРебенка
        ЗаявлениеНаДетей.РебенокИнвалид = РезультатСтрока.РебенокИнвалид
        ЗаявлениеНаДетей.ЕдинственныйРодитель = РезультатСтрока.ЕдинственныйРодитель
        ЗаявлениеНаДетей.Опекун = РезультатСтрока.Опекун
        ЗаявлениеНаДетей.ДатаОкончанияПрименения = РезультатСтрока.ДатаОкончанияПрименения
        ЗаявлениеДети.Дети.Добавить(ЗаявлениеНаДетей)
    ;
    возврат ЗаявлениеДети
;

@ВПодсистеме 
@НаСервере @ДоступноСКлиента
метод Записать(ЗаявлениеДети: ЗаявлениеДети)
    для ЗаявлениеНаДетей из ЗаявлениеДети.Дети
        если не ЗаявлениеНаДетей.Удалено и не ЗаявлениеНаДетей.Модифицированность 
            продолжить
        ;
        если ЗаявлениеНаДетей.Удалено
            если ЗаявлениеНаДетей.Ссылка != Неопределено
                пер ЗаявлениеОбъект = ЗаявлениеНаДетей.Ссылка.ЗагрузитьОбъект(Истина)
                ЗаявлениеОбъект.Удалить()
            ;
        иначе если ЗаявлениеНаДетей.Модифицированность
            пер ЗаявлениеОбъект: ЗаявлениеНаНалоговыеВычетыВычетыНаДетей.Объект?
            если ЗаявлениеНаДетей.Ссылка == Неопределено
                ЗаявлениеОбъект = новый ЗаявлениеНаНалоговыеВычетыВычетыНаДетей.Объект()
                ЗаявлениеОбъект.Владелец = ЗаявлениеДети.Владелец
            иначе
                ЗаявлениеОбъект = ЗаявлениеНаДетей.Ссылка.ЗагрузитьОбъект(Истина)
            ;
            ЗаявлениеОбъект!.ФИО = ЗаявлениеНаДетей.ФИО
            ЗаявлениеОбъект!.ДатаРождения = ЗаявлениеНаДетей.ДатаРождения
            ЗаявлениеОбъект!.СтаршинствоРебенка = ЗаявлениеНаДетей.СтаршинствоРебенка
            ЗаявлениеОбъект!.РебенокИнвалид = ЗаявлениеНаДетей.РебенокИнвалид
            ЗаявлениеОбъект!.ЕдинственныйРодитель = ЗаявлениеНаДетей.ЕдинственныйРодитель
            ЗаявлениеОбъект!.Опекун = ЗаявлениеНаДетей.Опекун
            ЗаявлениеОбъект!.ДатаОкончанияПрименения = ЗаявлениеНаДетей.ДатаОкончанияПрименения
            ЗаявлениеОбъект!.Владелец = ЗаявлениеДети.Владелец
            ЗаявлениеОбъект!.Записать()
        ;
    ;
;