импорт ОбщиеСтандартные
импорт Документы
импорт ФизическиеЛица
импорт ЛичныеКабинетыПользователей
импорт Компания::Согласования

@Обработчик
метод ПослеСоздания()
    ПрочитатьДанныеПредупреждения()
    МенеджерГлобальныхКлиентскихСобытий.ПодключитьОбработчик(метод (Источник, Параметры) ->
        если Источник != Зарплата.ПОДПИСАН_РАСЧЕТНЫЙ_ЛИСТ
            и Источник != Согласования.ДОКУМЕНТ_СОГЛАСОВАН
            
            возврат
        ;
        ПрочитатьДанныеПредупреждения()
    ;)
;

метод ПрочитатьДанныеПредупреждения()
    пер ДанныеПредупреждения = ДанныеПредупреждения(ЛичныеКабинетыПользователей.ПараметрыСеанса().ТекущееФизическоеЛицо)
    КоличествоДней = ДанныеПредупреждения.КоличествоДней
    Месяц = ДанныеПредупреждения.Месяц
;

метод ПредупреждениеВидимость(): Булево
    возврат КоличествоДней != 0
;

метод ТекстПредупрежденияЗаголовок(): Строка
    пер ТекстПредупреждения: Строка
    пер КоличествоСтрокой = КоличествоДней.ВСтроку()
    пер КоличествоДнейСтрокой = КоличествоСтрокой + " " + МенеджерСтрок.ТекстПоКоличеству("день", КоличествоДней)
    пер МесяцСтрокой = МенеджерДат.ФорматДаты(Месяц, "ММММ гггг")
    пер ПрошлоПрошел = (КоличествоСтрокой.ЗаканчиваетсяНа("1") и не КоличествоСтрокой.ЗаканчиваетсяНа("11")) ? "Прошел" : "Прошло"
    ТекстПредупреждения = "%ПрошлоПрошел уже %КоличествоДнейСтрокой. Ознакомьтесь с расчетным листом за %МесяцСтрокой."
    возврат ТекстПредупреждения
;

@НаСервере @НаКлиенте
структура ОписаниеПредупреждения
    пер КоличествоДней: Число
    пер Месяц: Дата?
; 

@НаСервере @ДоступноСКлиента
статический метод ДанныеПредупреждения(ФизическоеЛицо: ФизическиеЛица.Ссылка?): ОписаниеПредупреждения
    
    пер Предупреждение: ОписаниеПредупреждения
    
    если ФизическоеЛицо == Неопределено
        возврат Предупреждение
    ;
    
    пер Настройки = ПолученныеДокументы.НастройкиПолученныхДокументов()
    если не Настройки.НапоминатьОПолученныхДокументах
        возврат Предупреждение
    ;
    
    пер Период = ПолученныеДокументы.ПериодНаОзнакомлениеСПолученнымиДокументами()
    пер ДатаПубликации = Дата.Сейчас().ДобавитьДни(-Период)
    пер СписокФизическихЛиц = новый ЧитаемоеМножество<ФизическиеЛица.Ссылка>([ФизическоеЛицо])
    
    пер Запрос = новый ПроизвольныйЗапрос() // текст запроса использует временные таблицы
    Запрос.Текст = 
        "Выбрать
            НеподписанныеРасчетныеЛисты.Месяц Как Месяц,
            НеподписанныеРасчетныеЛисты.ДатаПубликации Как ДатаПубликации
        Из  
            РасчетныеЛисты.ВТНеподписанныеРасчетныеЛисты Как НеподписанныеРасчетныеЛисты
        Упорядочить По
            ДатаПубликации"
    исп ОбластьВидимостиВременныхТаблиц.Открыть(Ложь)
    РасчетныеЛисты.ОбластьВТНеподписанныеРасчетныеЛисты(СписокФизическихЛиц, ДатаПубликации)
    исп Результат = Запрос.Выполнить() 
    знч РезультатСтроки = новый ЧитаемыйМассив(Результат)
    если (не РезультатСтроки.Пусто())
        пер Длительность = Дата.Сейчас() - РезультатСтроки[0].ДатаПубликации
        Предупреждение.КоличествоДней = Длительность.ВДнях()
        Предупреждение.Месяц = РезультатСтроки[0].Месяц
    ;
    возврат Предупреждение
;