импорт УниверсальныеМеханизмы
импорт ОбщиеСтандартные
импорт ИнтерфейсныеКомпонентыСтандартные
импорт ОбменДанными

@ВПодсистеме
метод ОповеститьОВыполненномДействии(Подготовлено: Булево, СостояниеОбмена: ОбменДанными.СостояниеОбмена?)
    если Подготовлено
        пер ЗаголовокУведомления = "Заявление отправлено"
        пер ТекстУведомления = "Заявление на вычеты отправлено в кадровую службу. После его принятия бухгалтерией, при расчете вашей зарплаты будут учитываться налоговые вычеты"
        пер КоннотацияУведомления: ОценкаИнформации = ОценкаИнформации.Обычная
        если СостояниеОбмена.ОповеститьОбОтсутствииОбмена
            ЗаголовокУведомления = "Приостановлена отправка заявления"
            ТекстУведомления = "Заявление на вычеты сейчас не может быть отправлено. Заявление будет отправлено в кадровую службу, как только появится такая возможность." 
            если СостояниеОбмена.ОповеститьСотрудникаОВозобновленииОбмена
                ТекстУведомления += " Вы получите уведомление после его отправки."
            ;
            КоннотацияУведомления = ОценкаИнформации.Предупреждающая
        ;
        СтандартныеФормы.ПоказатьУведомление(ЗаголовокУведомления, ТекстУведомления, КоннотацияУведомления)
    ;
;

@ВПодсистеме
метод ПояснениеЗаявленияПослеОтправки(ДатаОтправки: Момент?, СостояниеЗаявления: СостоянияДокумента?): Строка
    пер ПояснениеЗаголовок: Строка
    пер ВремяОтправки: Строка
    если ДатаОтправки == Неопределено
        ВремяОтправки = ""
    иначе если МенеджерДат.НачалоДня(ДатаОтправки) == МенеджерДат.НачалоДня(МенеджерДат.ТекущаяДатаВремя())
        ВремяОтправки = "сегодня, в " + МенеджерДат.ФорматМомента(ДатаОтправки, "ЧЧ:мм")
    иначе если МенеджерДат.НачалоДня(МенеджерДат.ДобавитьДни(ДатаОтправки, 1)) == МенеджерДат.НачалоДня(МенеджерДат.ТекущаяДатаВремя())
        ВремяОтправки = "вчера, в " + МенеджерДат.ФорматМомента(ДатаОтправки, "ЧЧ:мм")
    иначе
        ВремяОтправки = МенеджерДат.ФорматМомента(ДатаОтправки, "дд ММММ гггг")
    ;
    пер СтатусЗапроса: Строка
    если СостояниеЗаявления == СостоянияДокумента.Выполнен
        СтатусЗапроса = " и уже согласовано. Вычеты применяются при расчете зарплаты и уменьшают налог на доходы."
    иначе если СостояниеЗаявления == СостоянияДокумента.Подготовлен
        СтатусЗапроса = ", но еще находится в обработке."
    иначе если СостояниеЗаявления == СостоянияДокумента.ПолученОтказ
        СтатусЗапроса = ", но по нему получен отказ. Причины можно уточнить в бухгалтерии."
    ;
    ПояснениеЗаголовок = "Заявление на вычеты было отправлено %ВремяОтправки%СтатусЗапроса"
    возврат ПояснениеЗаголовок
;