импорт КонтрольДоступа

@ВПроекте
структура РезультатОтправкиОбязательнойСтатистики
    пер ОтправленаСтатистикаЛичныхКабинетов: Булево
    пер ДатаОтправкиОбязательнойСтатистики: Момент?
;

@ВПроекте
структура РезультатОтправкиДополнительнойСтатистики
    пер ОтправленаСтатистикаСправочников: Булево
    пер ОтправленаСтатистикаДокументов: Булево
    пер ОтправленаСтатистикаРасчетныхЛистков: Булево
    пер ОтправленаСтатистикаВложений: Булево
    пер ДатаОтправкиДополнительнойСтатистики: Момент?
;

@ВПроекте
структура РезультатОтправкиОтчетовОбОшибках
    пер ОтправленоОтчетовОбОшибках: Число
    пер ДатаОтправкиОтчетовОбОшибках: Момент?
;

//Область РазрешенияДоступа

@Обработчик
метод ВычислитьРазрешенияДоступа(): Массив<РазрешениеДоступа>
    возврат КонтрольДоступа.РазрешенияДоступаТаблицыСервисныхДанныхАбонента()
;

//КонецОбласти

@ВПодсистеме 
метод РезультатОтправки(): Статистика.РезультатОтправки
    пер РезультатОтправки = новый Статистика.РезультатОтправки()
    исп РезультатЗапроса = РезультатЗапросаКСправочнику()
    для СтрокаЗапроса из РезультатЗапроса
        РезультатОтправки.ОтправленаСтатистикаЛичныхКабинетов = СтрокаЗапроса.ОтправленаСтатистикаЛичныхКабинетов
        РезультатОтправки.ОтправленаСтатистикаСправочников = СтрокаЗапроса.ОтправленаСтатистикаСправочников
        РезультатОтправки.ОтправленаСтатистикаДокументов = СтрокаЗапроса.ОтправленаСтатистикаДокументов
        РезультатОтправки.ОтправленаСтатистикаРасчетныхЛистков = СтрокаЗапроса.ОтправленаСтатистикаРасчетныхЛистков
        РезультатОтправки.ОтправленаСтатистикаВложений = СтрокаЗапроса.ОтправленаСтатистикаВложений
        РезультатОтправки.ОтправленоОтчетовОбОшибках = СтрокаЗапроса.ОтправленоОтчетовОбОшибках
        РезультатОтправки.ДатаОтправкиОбязательнойСтатистики = СтрокаЗапроса.ДатаОтправкиОбязательнойСтатистики
        РезультатОтправки.ДатаОтправкиДополнительнойСтатистики = СтрокаЗапроса.ДатаОтправкиДополнительнойСтатистики
        РезультатОтправки.ДатаОтправкиОтчетовОбОшибках = СтрокаЗапроса.ДатаОтправкиОтчетовОбОшибках
        прервать
    ;    
    возврат РезультатОтправки
;

@ВПодсистеме 
метод УстановитьРезультатОтправки(РезультатОтправки: РезультатОтправкиОбязательнойСтатистики)
    исп ПривилегированныйРежим: КонтекстДоступа = КонтекстДоступа.Привилегированный()
    исп Транзакции.Начать()
    пер ОбновляемыйЭлемент: РезультатОтправкиСтатистики.Объект = ОбновляемыйЭлементСправочника()
    ОбновляемыйЭлемент.ОтправленаСтатистикаЛичныхКабинетов = РезультатОтправки.ОтправленаСтатистикаЛичныхКабинетов 
    ОбновляемыйЭлемент.ДатаОтправкиОбязательнойСтатистики = РезультатОтправки.ДатаОтправкиОбязательнойСтатистики
    ОбновляемыйЭлемент.Записать()
    ПривилегированныйРежим.Закрыть()
;

@ВПодсистеме 
метод УстановитьРезультатОтправки(РезультатОтправки: РезультатОтправкиДополнительнойСтатистики)
    исп ПривилегированныйРежим: КонтекстДоступа = КонтекстДоступа.Привилегированный()
    исп Транзакции.Начать()
    пер ОбновляемыйЭлемент: РезультатОтправкиСтатистики.Объект = ОбновляемыйЭлементСправочника()
    ОбновляемыйЭлемент.ОтправленаСтатистикаСправочников = РезультатОтправки.ОтправленаСтатистикаСправочников 
    ОбновляемыйЭлемент.ОтправленаСтатистикаДокументов = РезультатОтправки.ОтправленаСтатистикаДокументов 
    ОбновляемыйЭлемент.ОтправленаСтатистикаРасчетныхЛистков = РезультатОтправки.ОтправленаСтатистикаРасчетныхЛистков 
    ОбновляемыйЭлемент.ОтправленаСтатистикаВложений = РезультатОтправки.ОтправленаСтатистикаВложений 
    ОбновляемыйЭлемент.ДатаОтправкиДополнительнойСтатистики = РезультатОтправки.ДатаОтправкиДополнительнойСтатистики 
    ОбновляемыйЭлемент.Записать()
    ПривилегированныйРежим.Закрыть()
;

@ВПодсистеме 
метод УстановитьРезультатОтправки(РезультатОтправки: РезультатОтправкиОтчетовОбОшибках)
    исп ПривилегированныйРежим: КонтекстДоступа = КонтекстДоступа.Привилегированный()
    исп Транзакции.Начать()
    пер ОбновляемыйЭлемент: РезультатОтправкиСтатистики.Объект = ОбновляемыйЭлементСправочника()
    ОбновляемыйЭлемент.ОтправленоОтчетовОбОшибках = РезультатОтправки.ОтправленоОтчетовОбОшибках 
    ОбновляемыйЭлемент.ДатаОтправкиОтчетовОбОшибках = РезультатОтправки.ДатаОтправкиОтчетовОбОшибках 
    ОбновляемыйЭлемент.Записать()
    ПривилегированныйРежим.Закрыть()
;


метод РезультатЗапросаКСправочнику(): РезультатЗапроса<неизвестно>
    знч Запрос = Запрос{
        Выбрать Первые 1 
            Ссылка как Ссылка,
            ОтправленаСтатистикаЛичныхКабинетов как ОтправленаСтатистикаЛичныхКабинетов,
            ОтправленаСтатистикаСправочников как ОтправленаСтатистикаСправочников,
            ОтправленаСтатистикаДокументов как ОтправленаСтатистикаДокументов,
            ОтправленаСтатистикаРасчетныхЛистков как ОтправленаСтатистикаРасчетныхЛистков,
            ОтправленаСтатистикаВложений как ОтправленаСтатистикаВложений,
            ОтправленоОтчетовОбОшибках как ОтправленоОтчетовОбОшибках,
            ДатаОтправкиОбязательнойСтатистики как ДатаОтправкиОбязательнойСтатистики,   
            ДатаОтправкиДополнительнойСтатистики как ДатаОтправкиДополнительнойСтатистики,
            ДатаОтправкиОтчетовОбОшибках как ДатаОтправкиОтчетовОбОшибках
        Из 
            РезультатОтправкиСтатистики}
    
    возврат Запрос.Выполнить()
;

метод ОбновляемыйЭлементСправочника(Блокировать: Булево = Истина): РезультатОтправкиСтатистики.Объект
    исп РезультатЗапроса = РезультатЗапросаКСправочнику()
    для СтрокаЗапроса из РезультатЗапроса
        возврат СтрокаЗапроса.Ссылка.ЗагрузитьОбъект(Блокировать) 
    ;
    возврат новый РезультатОтправкиСтатистики.Объект()
;