импорт ПоказателиСтатистики
импорт УниверсальныеМеханизмы

конст Килобайт: Число = 1024
конст Мегабайт: Число = Килобайт * Килобайт 

@ВПодсистеме
метод СправочникиСтатистика(): Массив<СтатистикаДанных.Справочник>
    пер СправочникиСтатистика = новый Массив<СтатистикаДанных.Справочник>()
    знч Сервисы = СтатистикаПоставщик.ПолучитьСервисы()
    для Сервис из Сервисы
        СправочникиСтатистика.ДобавитьВсе(Сервис.СтатистикаСправочников())
    ;
    возврат СправочникиСтатистика
;

@ВПодсистеме 
метод ДокументыСтатистика(): Массив<СтатистикаДанных.Документ>
    пер ДокументыСтатистика = новый Массив<СтатистикаДанных.Документ>()
    знч Сервисы = СтатистикаПоставщик.ПолучитьСервисы()
    для Сервис из Сервисы
        ДокументыСтатистика.ДобавитьВсе(Сервис.СтатистикаДокументов())
    ;
    возврат ДокументыСтатистика
; 

@ВПодсистеме 
метод ВложенияСтатистика(): Массив<ПоказателиСтатистики.Вложения>
    пер ВложенияСтатистика = новый Массив<ПоказателиСтатистики.Вложения>()
    пер Запрос = новый ПроизвольныйЗапрос() // текст запроса динамический
    пер КилобайтСтрокой: Строка = (Килобайт).ВСтроку()
    пер МегабайтСтрокой: Строка = (Мегабайт).ВСтроку()
    Запрос.Текст = 
    "Выбрать
        Выбор 
            Когда Размер < %КилобайтСтрокой * 10    Тогда РазмерВложения.Крошечный 
            Когда Размер < %КилобайтСтрокой * 100   Тогда РазмерВложения.Маленький 
            Когда Размер < %МегабайтСтрокой         Тогда РазмерВложения.Средний 
            Когда Размер < %МегабайтСтрокой * 16    Тогда РазмерВложения.Большой
            Иначе                           РазмерВложения.Огромный
        Конец Как Размер,
        Файлы.Владелец.ПолучитьТип() Как ТипДокумента, 
        Количество(*) Как Количество,
        Сумма(Размер) Как Объем 
    Из Файлы Как Файлы
    Сгруппировать По 
        Выбор 
            Когда Размер < %КилобайтСтрокой * 10    Тогда РазмерВложения.Крошечный 
            Когда Размер < %КилобайтСтрокой * 100   Тогда РазмерВложения.Маленький 
            Когда Размер < %МегабайтСтрокой         Тогда РазмерВложения.Средний 
            Когда Размер < %МегабайтСтрокой * 16    Тогда РазмерВложения.Большой
            Иначе                           РазмерВложения.Огромный
        Конец,
        Файлы.Владелец.ПолучитьТип()"
    исп РезультатЗапроса = Запрос.Выполнить()
    для РезультатЗапросаСтрока из РезультатЗапроса
        ВложенияСтатистика.Добавить(
            новый ПоказателиСтатистики.Вложения(
                РезультатЗапросаСтрока.Размер, 
                РезультатЗапросаСтрока.ТипДокумента.ВСтроку(), 
                РезультатЗапросаСтрока.ТипДокумента.ВСтроку(), 
                РезультатЗапросаСтрока.Объем,
                РезультатЗапросаСтрока.Количество
            )
        )
    ;
    возврат ВложенияСтатистика
; 

@ВПодсистеме 
метод ОшибкиПриложения(ДатаНачалаАнализа: Момент?): Массив<ПоказателиСтатистики.ОшибкаПриложения> 
    пер Ошибки = новый Массив<ПоказателиСтатистики.ОшибкаПриложения>() 
    исп РезультатПоиска = ЖурналСобытий.Найти(ДатаНачала = ДатаНачалаАнализа)
    
    пока РезультатПоиска.Следующий()
        если РезультатПоиска.Событие.Важность == ВажностьСобытияЖурналаСобытий.Максимальная
            или РезультатПоиска.Событие.ВидСобытия == ВидСобытияЖурналаСобытий.Ошибка
            
            Ошибки.Добавить(ОшибкаПриложенияИзСобытия(РезультатПоиска.Событие)) 
        ;   
    ; 
    возврат Ошибки 
;

метод ОшибкаПриложенияИзСобытия(СобытиеПриложения: СобытиеЖурналаСобытий): ПоказателиСтатистики.ОшибкаПриложения
    пер СвойстваОшибки: Соответствие<Строка, Объект?>
    для СвойствоСобытияКлюч из СобытиеПриложения.Свойства.Ключи()
        пер СвойствоСобытияЗначение = СобытиеПриложения.Свойства[СвойствоСобытияКлюч]
        если СвойствоСобытияЗначение != Неопределено
            СвойстваОшибки.Вставить(СвойствоСобытияКлюч, СвойствоСобытияЗначение.ВСтроку()) 
        ;
    ;
    возврат новый ПоказателиСтатистики.ОшибкаПриложения(
        СобытиеПриложения.Момент,
        СобытиеПриложения.Имя,
        СвойстваОшибки
    ) 
; 